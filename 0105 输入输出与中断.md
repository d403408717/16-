[TOC]

通常用汇编语言编写与硬件关系密切的程序。==输入输出和中断与硬件关系密切，所以相关的程序往往用汇编语言编写。==本章介绍输入输出与中断的基本概念，结合实例说明如何用汇编语言编写输入输出程序和中断处理程序。

# 5.1 输人和输出的基本概念

每种输入输出设备都要通过一个硬件接口或控制器和CPU相连。例如，打印机通过打印接口与系统相连；显示器通过显示控制器和系统相连。==从程序设计的角度看，接口由一组寄存器组成，是完成输入输出的桥梁。程序利用I/O指令，存取接口上的寄存器，获得外部设备的状态信息，控制外部设备的动作，从而实现输入输出。==

本章所说的==输入和输出是站在处理器或主机立场上而言的，也即输入是指输入到处理器或主机，输出是指从处理器或主机输出。==

## 5.1.1 I/O端口地址和I/O指令

### 1. I/O端口地址

为了==存取接口上的寄存器，系统给这些寄存器分配专门的存取地址，这样的地址称为I/O端口地址==。

==在某些微型机上，I/O端口地址和存储单元地址统一编址。这相当于把I/O接口（设备）视为一个或几个存储单元，利用存取内存单元的指令就可存取接口上的寄存器。==但这会减少原本就有限的一部分存储空间，同时由于访问内存的指令一般超过2字节，从而延长了外部设备与处理器进行数据交换的时间。

在以Intel的80x86家族处理器为CPU的系统中，I/O端口地址和存储单元的地址是各自独立的，分占两个不同的地址空间。8086/8088提供的I/O端口地址空间达64K,因而可接64K个8位端口，或可接32K个16位端口。但实际上，==PC及其兼容机一般只使用0到3FFH之间的I/O端口地址==，只占整个I/O端口地址空间的很小一部分。

### 2. I/O指令

由于8086/8088的I/O端口地址和内存单元地址是独立的，所以要用专门的I/O指令来存取端口上的寄存器，也就是说要用专门的I/O指令进行输入输出。

==I/O指令属于数据传送指令组==。

#### (1) 输入指令

输入指令的一般格式如下：

```assembly
IN		累加器，端口地址
```

==输入指令从一个输入端口读取一个字节或一个字，传送至AL(若是一个字节）或AX(若是一个字）==。端口地址可采用直接方式表示，也可采用间接方式表示。==当采用直接方式表示端口地址时，端口地址仅为8位，即0~255;当采用间接方式表示端口地址时，端口地址存放在DX寄存器中，端口地址可为16位。==所以输入指令有如下四种具体格式：

```assembly
IN AL,PORT	;AL <- (PORT)
IN AX,PORT	;AX <- (PORT+1:PORT)
IN AL,DX	;AL <- (DX)
IN AX,DX	;AX <- (DX+1:DX)
```

前两种格式是直接端口寻址，端口地址PORT是一个8位的立即数，例如：

```assembly
IN	AL,21H
```

后两种是间接端口寻址，端口地址在寄存器DX中。==当端口地址超过255时，只能采用DX间接端口寻址==。例如：

```assembly
MOV	DX,2FCH
IN 	AX,DX
```

注意：==当从端口n输入一个字时，相当于同时从端口n和n+1分别读取一个字节。如果上述两条指令连续执行，相当于从端口2FCH输入一个字节送AL,从2FDH输入一个字节送AH。==

#### (2) 输出指令

输出指令的一般格式如下：

```assembly
OUT		端口地址,累加器
```

==输出指令将AL中的一个字节，或在AX中的一个字，输出到指定端口==。像输入指令一样，端口地址可采用直接方式表示，也可采用间接方式表示。当采用直接方式表示端口地址时，端口地址仅为8位，即0~255;当采用间接方式表示端口地址时，端口地址存放在DX寄存器中，端口地址可为16位。所以输出指令也有如下四种具体格式：

```assembly
OUT 	PORT,AL		;(PORT)<-AI
OUT 	PORT,AX		;(PORT+1:PORT)<-AX
OUT 	DX,AL		;(DX)<-AL
OUT 	DX,AX		;(DX+1:DX)<-AX
```

注意：==当向端口n输出一个字时，相当于向端口n输出AL中的内容和向端口n+1输出AH中的内容。例如，下面的程序片段向2FCH端口输出23H,向2FDH端口输出45H:==

```assembly
MOV		AX,4523H
MOV		DX,2FCH
OUT 	DX,AX
```

## 5.1.2 数据传送方式

### 1.CPU与外设之间交换的信息

==CPU与外设之间交换的信息包括数据、控制和状态信息。尽管这三种信息具有不同性质，但它们都通过IN和OUT指令在数据总线上进行传送，所以通常采用分配不同口的方法将它们加以区别。==

> 数据线
>
> 控制线
>
> 状态线

==数据是CPU和外设真正要交换的信息。==数据通常为8位或16位，可分为各种不同类型。不同的外设要传送的数据类型也是不同的。

==控制信息输出到I/O接口，告诉接口和设备要做什么工作。==

==从接口输入的状态信息表示I/O设备当前的状态。==在输入数据前，通常要先取得表示设备是否已准备好的状态信息；在输出数据前，往往要先取得表示设备是否忙的状态信息。

### 2.数据传送方式

系统中数据传送的方式主要有：

#### (1) 无条件传送方式

==在不需要查询外设的状态，即已知外设已准备好或不忙时，可以直接使用`IN OUT`指令实现数据传送==。这种方式软件实现简单，只要在指令中指明端口地址，就可选通指定外设进行输入输出。无条件传送方式是方便的，==但要求外设工作速度能与CPU同步，否则就可能出错==。例如，在外设还没有准备好的情况下，就用IN指令得到的数据就可能是不正确的数据。

#### (2) 查询方式

==轮询查询，直至查询到不忙==

==查询传送方式适用于CPU与外设不同步的情况==。输入之前，查询外设数据是否已准备好，若数据已准备好，则输入；否则继续查询，直到数据准备好。输出之前，查询外设是否“忙”，若不“忙”，则输出；否则继续查询，直到不“忙”。也就是说，要等待到外设准备好时才能输入或输出数据，而通常外设速度远远慢于CPU速度，于是查询过程就将花费大量的时间。

#### (3) 中断方式

为了提高CPU的效率，可采用中断方式。==当外设准备好时，外设向CPU发出中断请求，CPU转入中断处理程序，完成输入输出工作。==

#### (4) 直接存储器传送（DMA)方式

==由于高速I/O设备（如磁盘机等）准备数据的时间短，要求传送速度快等特点，所一般采用直接存储器传送方式，即高速设备与内存储器直接交换数据。==这种方式传送数据是成组进行的。其过程是：

1. ==先把数据在高速外设中存放的起始位置、数据在内存储器中存放的起始地址、传送数据长度等参数输出到连接高速外设的接口（控制器）,==
2. 然后启动高速外设，设备准备开始直接传送数据。
3. 当高速外设直接传送准备好后，向处理机发送一个直接传送的请求信号，处理机以最短时间批准进行直接传送，并让出总线控制权，高速外设在其控制器控制下交换数据。
4. 数据交换完毕后，由高速外设发出“完成中断请求”，并交总线控制权。
5. 处理机响应上述中断，由对应的中断处理程序对高速外设进行控制或对已经传送的数据进行处理，中断返回后，原程序继续运行。

## 5.1.3 存取RT/CMOS RAM

### 1.关于RT/CMOS RAM

在IBM PC/AT及其兼容机上均安装有一个RT/COMS RAM芯片，它是互补金属氧化物半导体随机存取存储器，不仅可长期保存系统配置状况，而且记录包括世纪、年、月、日和时分秒在内的实时钟（Real_Time_Clock)信息。

==RT/CMOS RAM作为一个I/O接口芯片，系统分配的I/O端口地址区为70H至7FH,通过IN和OUT指令可对其进行存取。它共提供64个字节RAM单元，分配使用情况如表5.1所示。前14个字节用于实时钟，剩下的50个字节用于系统配置。==

==CPU实时钟==

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/root/image.2dohhmj9y5g.webp)

### 2.存取RT/CMOS RAM

在存取`RT/CMOS RAM`芯片内部的64个字节内容时，往往要分两步进行。即先把要存取单元的地址送端口70H,然后再存取端口71H。还需注意，14个记录实时钟信息的单元（位移0置0DH)的地址就是表中位移，其他单元的地址是表5.1所示位移上加80H。

#### (1) 读操作代码片段如下：

```assembly
MOV 	AL,n		;n是要访问单元地址
OUT		70H,AL		;把要访问单元的地址送地址端门
JMP		$+2			;延时
IN 		AL,71H		;从数据端口取访问单元的内容
```



#### (2) 写操作代码片段如下：

```assembly
MOV 	AL,n		;n是要访问单元地址
OUT 	70H,AL		;把要访问单元的地址送地址端口
JMP 	$+2			;延时
MOV 	AL,m		;m是要输出数据
OUT 	71H,AL		;把数据从数据端口输出
```

需要指出，==在对同一个I/O设备或端口相继发送I/O指令时，为确保I/O设备或端口有足够的电路恢复时间，一般在I/O指令之间使用一条转移指令`JMP $+2`。该指令的动作是转移到下一条指令执行，其意义是延时，以满足I/O端口的需要。==

### 3.CMOS RAM 累加和检查

在对系统配置时，要对`CMOS RAM`的位移`10H~2DH`的系统配置信息按字节求累加和，其值存放在位移2E~2FH的单元中。==低字节存放在2EH单元中，高字节存放在2FH单元中==。在系统加电自检时，将对CMO SRAM的位移10H~2DH单元的内容求和，并与保存的累加和比较。若两者不等，则置诊断状态字节的第6位，表明累加和错。这往往会导致要求用户重新进行系统配置。

下面的程序片段进行累加和检查，在累加和错时设置诊断状态字节的位6。

```assembly
CMOS_PORT 	EQU 70H		;CMOS端口地址
CMOS_BEGIN 	EQU 90H		;求和开始地址
CMOS_END 	EQU ADH		;求和结束地址
CHECK_SUM 	EQU AEH		;累加和存放开始地址
DIAG_STATUS EQU 8EH		;诊断状态字节地址
BAD_CKSUM	EQU 40H		;累加和检查错标志位
;
		SUB 	BX,BX	;累加和清0
		MOV 	CL,CMOS_BEGIN	;指向开始地址
		MOV 	CH,CMOS_END+1	;指向结束地址后一单元
		SUB 	AH,AH
CMOS2: 	MOV 	AL,CI
		OUT 	CMOS_PORT,AL
		JMP		$+2
		IN 		AL,CMOS_PORT+1	;取一字节
		ADD 	BX,AX			;求累加和
		INC		CL				;指向下一字节
		CMP 	CH,CL			;是否到结束地址
		JNZ 	CMOS2			;未到，继续求和
		MOV 	AL,CHECK_SUM+1
		OUT 	CMOS_PORT,AL
		JMP		$+2
		IN 		AL,CMOS_PORT+1	;取原累加和高字节
		MOV 	AH,AL			;保存到AH
		MOV		AL,CHECK_SUM
		OUT 	CMOS_PORT,AL
		JMP 	$+2
		IN 		AL,CMOS_PORT+1	;取原累加和低字节
		CMP 	AX,BX			;比较
		JZ 		CMOS4			;等，转
		;
		MOV 	AL,DIAG_STATU
		OUT 	CMOS_PORT,AL
		JMP 	$+2
		IN 		AL,CMOS_PORT+1	;取诊断状态字节
		MOV 	AH,AL			;送AH
		OR 		AH,BAD_CKSUM	;或上累加和检查错标志
		MOV 	AL,DIAG_STATUS
		OUT 	CMOS_PORT,AL
		JMP 	$+2
		MOV 	AL,AH
		OUT 	CMOS_PORT+1,AL	;再送回诊断字节
CMOS4:	....
```

# 5.2查询方式传送数据

本节在介绍查询传送方式概念后，举例说明查询传送方式的实现。

## 5.2.1 查询传送方式

查询方式的基本思想是由CPU主动地通过输入输出指令查询指定的外部设备的当前状态，若设备就绪，则立即与设备进行数据交换，否则循环查询。具体地说，在输入之前要查询外设的数据是否已准备好，直到外设把数据准备好后才输入；在输出之前，要查询外设是否“忙”，直到外设不“忙”后才输出。查询传送方式适用于CPU与外设不同步的情况。查询方式输入输出的示意流程如图5.1所示。

为了采用查询方式输入或输出，相应的外设（或接口）不仅要有数据寄存器，而且还有状态寄存器，有些外设还有控制寄存器。数据寄存器用来存放要传送的数据，状态寄存器用来存放表示设备所处状态的信息。通常，在状态寄存器中有一个“就绪（Ready)”位或一个“忙（Busy)”位来反映外设是否已准备好。

在实际应用中，为防止设备因某种原因发生故障而无法就绪或空闲，从而导致CPU在无限循环之中，通常都设计一个等待超时值，其值随设备而定。一旦设备在规定时间内还无法就绪或空闲，也中止循环查询过程。如此，图5.1所示的流程图修改为图5.2所示的流程图。大多数情况下，等待超时值用查询次数表示，每查询一次，查询次数减一，如果查询次数减到0,那么查询等待也就结束。

有时系统中同时有几个设备要求输入输出数据，那么对每个设备都可编写一段执行输入输出数据的程序，然后轮流查询这些设备的状态寄存器中的就绪位，当某一设备准备好允许输入或输出数据时，就调用这个设备的I/O程序完成数据传送，否则依次查询下一设备是否准备好。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/root/image.2wyrpcxjaqg0.webp)

==查询方式的优点是：软硬件实现比较简单；当同时查询多个外设时，可以由程序安排查询的先后次序。缺点是浪费了CPU原本可执行大量指令的时间。==

## 5.2.2 读实时钟

RT/CMOS RAM的状态寄存器A的位7是计时更新标志位，为1表示实时钟正在计时，为0表示实时钟信息可用于读出。所以，在读实时钟前，要判别该标志位是否为0。

下面的程序片段读实时种，并把读到的秒、分和时数保存到相应的变量中。

```assembly
CMOS_PORT	EQU 70H		;CMOS端口地址
CMOS_REGA	EQU OAH		;状态寄存器A地址
UPDATE_F	EQU 80H		;更新标志位
CMOS_SEC	EQU 00H		;秒单元地址
CMOS_MIN	EQU	02H		;分单元地址
CMOS_HOUR 	EQU 04H		;时单元地址
;
	......
SECOND		DB	?		;秒数保存单元
MINUTE 		DB 	?		;分数保存单元
HOUR		DB 	?		;时数保存单元
	......
	;
	MOV 	AX,SEG SECOND	;设置数据段寄存器值
	MOV 	DS,AX
	;
UIP:
	MOV		AL,CMOS_REGA	;判是否可读实时钟
	OUT		CMOS_PORT,AL	;准备读状态寄存器A
	JMP		$+2
	IN		AL,CMOS_PORT+1	;读状态寄存器A
	TEST 	AL,UPDATE_F		;测更新标志
	JNZ		UIP				;如不可读则继续测试
	;
	MOV		AL,CMOS_SEC
	OUT 	CMOS_PORT,AL
	JMP		$+2
	IN		AL,CMOS_PORT+1	;读秒值
	MOV		SECOND,AL		;保存之
	MOV		AL,CMOS_MIN
	OUT		CMOS_PORT,AL
	JMP		$+2
	IN		AL,CMOS_PORT+1	;读分值
	MOV		MINUTE,AL		;保存之
	MOV		AL,CMOS_HOUR
	OUT 	CMOS_PORT,AL
	JMP		$+2
	IN		AL,CMOS_PORT+1	;读时值
	MOV		HOUR,AL			;保存之
	...
```

把更新标志位理解为状态寄存器中的“就绪”位，上面的程序片段采用查询方式检测是否就绪，但没有限制检测次数，符合如图5.1所示的流程。

## 5.2.3 查询方式打印输出

在IBMPC系列及其兼容机上，打印机通过打印接口（打印适配卡）连入系统。打印接口的功能是传递打印命令和数据到打印机并返回打印机状态，为此它包含数据寄存器、状态寄存器和控制寄存器。打印接口的状态寄存器和控制寄存器各位的定义如图5.3所示。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/root/image.3qgsvsvsyss0.webp)

上述三个寄存器有各自的端口地址，并且三个端口地址是连续的。==设数据寄存器端口地址是378H,那么，状态寄存器端口地址是379H,控制寄存器端口地址是37AH==。在系统加电初始化期间，数据寄存器端口地址被保存到BIOS数据区。

在确定查询次数和取得数据寄存器端口地址后，利用查询方式打印一个字符的流程如图5.4所示。首先输出打印数据，但此时打印机并未接受，随后读取状态信息，判打印机是否忙碌，一直等到打印机不忙碌，才向打印机发出选通命令。下面的子程序实现了上述流程。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/root/image.refftgkh334.webp)

```assembly
;子程序名：PRINT
;功能：打印一个字符
;入门参数：DX=数据寄存器端口地址
;		  BL=超时参数
;AL=打印字符的代码
;出口参数：AH=打印机状态，各位意义如下：
;位0:1表示超时，即超过规定的查询次数
;位1和位2:不用
;位3:1表示出错
;位4:1表示联机
;位5:1表示无纸
;位6:1表示应答
;位7:0表示忙碌
PRINT 	PROC
		PUSH 	DX
		PUSH	AX
		OUT		DX,AL		;输出打印数据
		INC		DX			;使DX含状态寄存器端口地址
WAIT:	XOR		CX,CX		;1个超时参数单位表示查询65536次
WAIT1:	IN		AL,DX		;读取状态信息
		MOV		AH,AL		;保存到AH
		TEST	AL,80H		;测是否忙碌
		JNZ		NEXT		;不忙碌，则转
		LOOP	WAIT1		;继续查询
		DEC 	BL			;超时参数减1
		JNZ		WAIT		;未超时，继续查询
		AND		AH,0F8H		;已超时，去掉状态信息中的无用位
		OR		AH,1		;置超时标志
		JMP 	EXIT		;转结束
NEXT:	INC		DX			;不忙碌，使DX含控制寄存器端口地址
		MOV		AL,ODH		;准备选通命令
		OUT		DX,AL		;选通
		MOV		AL,OCH		;准备复位选通命令
		JMP		$+2
		OUT		DX,AL		;复位选通位
		AND		AH,0F8H		;去掉状态信息中的不用位
EXIT:	XOR		AH,48H		;使返回的状态消息中有关位符合要求
		POP		DX
		MOV		AL,DL		;恢复AL寄存器值
		POP 	DX
		RET
PRINT 	ENDP
```

在上面的子程序中，使用了一个超时参数，一个超时参数单位表示查询65536次，超时参数应根据系统具体情况而定。在发出选通命令和复位选通之间使用了一条转移指令，该指令起延时作用，保证有足够的选通时间。另外，为了使AH寄存器含有规定的出口参数，要取反应答位和错误位的值，所以在子程序结束前，用异或指令取反这两位。

# 5.3 中断

本节以PC系统为背景介绍中断的基本概念和8086/8088响应中断的过程。

## 5.3.1 中断和中断传送方式

### 1. 中断和中断源

==中断是一种使CPU挂起正在执行的程序而转去处理特殊事件的操作。这些引起中断的事件称为中断源==。它们可能是来自外设的输入输出请求，==例如，由按键引起的键盘中断==，由串行口接收到信息引起的串行口中断等；也可能是计算机的一些异常事件或其他的内部原因，==例如：除数为0==。

### 2. 中断传送方式

中断传送方式的具体过程是：当CPU需要输入或输出数据时，先作一些必要的准备工作（有时包括启动外部设备）,然后继续执行程序；==当外设完成一个数据的输入或输出后，则向CPU发出中断请求，CPU就挂起正在执行的程序，转去执行输入或输出操作，在完成输入或输出操作后，返回原程序继续执行==。

中断传送方式是CPU和外部设备进行输入输出的有效方式，一直被大多数计算机所采用，它可以避免因反复查询外部设备的状态而浪费时间，从而提高CPU的效率。==不过，每中断一次，只传送一次数据，数据传送的效率并不高，所以，中断传送方式一般用于低速外设。==另外，与查询方式相比，中断传送方式实现比较复杂，对硬件的条件也较多。

## 5.3.2中断向量表

### ==1 .中断向量表==

IBM PC系列及其兼容机共能支持256种类型的中断，系统给每一种中断都安排一个中断类型号（简称为中断号）,中断类型号依次为0~0FFH。例如，==属于外部中断的定时器中断类型号为08和键盘中断类型号为09,属于内部中断的除法出错中断类型号为0等等。==

==每种类型的中断都由相应的中断处理程序来处理，为了使系统在响应中断后，CPU能快速地转入对应的中断处理程序，系统用一张表来保存这些中断处理程序的入口地址，该表就称为中断向量表。中断向量表的每一项保存一个中断处理程序的入口地址，它相当于一个指向中断处理程序的指针，所以就称它为中断向量。==

> 中断向量也依次编号为0~0FFH,n号中断向量就保存处理中断类型为n的中断处理程序的入口地址。

所以，一般不再区分中断类型号和中断向量号。

中断向量表如图5.5所示，它被安排在内存最低端的1K字节空间中。其中每个中断向量占用四个字节，==前（低地址）两字节保存中断处理程序入口地址的偏移，后（高地址）两字节保存中断处理程序入口地址的段值，所以含有256个中断向量的中断向量表需要占用1K字节内存空间。==

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/root/image.3m701zy2mda0.webp)

按照上述中断向量表的结构和存放位置，根据中断向量号（或类型号）可方便地计算出中断向量所在单元的地址。==设中断向量号为n,则中断向量所在单元的开始地址是4*n==

### 2. 中断向量号的分配

在系统中，某个中断类型号分配给哪个中断，即某个中断向量含有哪个中断处理程序的入口地址有一些规定和约定，应用程序不能违反规定，不宜不遵守约定。

在IBM PC系列及其兼容机上，除保留给用户使用的60H~68H和F1H~FFH中断向量号外，可以认为其他中断向量号已被分配。表5.2列出了部分中断向量号的分配情况。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/root/image.qwiax04pqsg.webp)

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/root/image.27as782dwf40.webp)

顺便说一下，==中断向量不一定非要指向中断处理程序，也可作为指向一组数据的指针==。例如，1DH号中断向量就指向显示器参数，1EH号中断向量指向软盘基数。当然，如果中断向量m没有指向中断处理程序，那么就不应发生类型为m的中断。

### 3. ==设置和获取中断向量==

在系统程序或应用程序由于某种需要而提供新的中断处理程序时，就要设置对应的中断向量，使其指向新的中断处理程序。

下面的程序片段直接设置n号中断向量，假设对应中断处理程序的入口标号是:

```assembly
INTHAND:
	......
	MOV		AX,0
	MOV 	DS,AX
	MOV	 	BX,n*4		;准备设置n号中断向量
	CLI
	;关中断
	MOV		WORD PTR[BX],OFFSET INTHAND;置偏移
	MOV		WORD PTR[BX+2],SEG	INTHAND;置段值
	STI
......
```

==在上面的程序片段中，使用了关中断指令CLI，目的是保证真正用于设置中断向量的两条传送指令能够连续执行。在执行完前一条传送指令后，n号中断向量就暂时被破坏既不指向原中断处理程序，也不指向新的中断处理程序，如果此时发生类型为n的中断，那么就不能正确地转到中断处理程序执行，这是最糟糕的事了。如果能确定当前是关中状态，当然就不再需要使用该关中断指令，也不需要随后的开中断指令。==另外，如果能肯定在设置n号中断向量的过程中不发生类型为n的中断，那么可不考虑是否为关中断状有这种情况只有在对应的中断处理程序仅供应用程序自己使用时才有可能。

实际上，总是尽量避免采用上述直接设置中断向量的方法（有时是必须的）,而是利用DOS提供的25H号系统功能调用来设置中断向量，这可避免考虑许多细节。

==25H号系统功能调用是设置中断向量==，其入口参数如下：

> AL=中断向量（类型）号
>
> DS=中断处理程序入口地址的段值
>
> DX=中断处理程序入口地址的偏移

==设置自己的中断：==

下面的程序片段设置n号中断向量，假设对应中断处理程序入口标号是

```assembly
INTHAND:
......
MOV AX,SEG INTHAND 		;自己定义的中断的段值
MOV DS,AX				;设置自己定义的中断的段值到DS中
MOV DX,OFFSET INTHAND	;将自己定义的中断的偏移设置到DX中
MOV AH,25H				;调用25号中断
MOV AL,n				;设置25号中断的参数，n表示自己定义的中断的中断号
INT 21H					;执行25号中断
......
```

有时需要取得中断向量。例如：==在应用程序要用自己的中断处理程序代替系统中原有的某个中断处理程序时，先要保存原中断向量，待应用程序结束时再恢复原中断向量。==
下面的程序片段直接从中断向量表中取得n号中断向量，并且保存到双字变量

```assembly
OLDVECTOR:
......
XOR AX,AX						;清空AX
MOV ES,AX						;设置中断向量表首地址到附加段
MOV AX,ES:[n*4]					;自己定义的中断的中断号乘以4后寻址
MOV WORD PTR OLDVECTOR,AX		;高16位为段值
MOV AX,ES:[n*4+2]
MOV WORD PTR OLDVECTOR+2,AX		;低16位为偏移
......
```

与利用DOS功能调用设置中断向量一样，==实际上一般都利用DOS提供的35H号系统功能调用取得中断向量==。35H号系统调用的功能是获取中断向量，其出入口参数如下：

> 入口参数：
> 	AL = 中断向量（类型）号
> 出口参数：
> 	ES = 中断处理程序入口地址的段值
> 	BX = 中断处理程序入口地址的偏移

下面的程序片段取得n号中断向量，并将其保存到双字变量OLDVECTOR中:

```assembly
...
MOV		AH,35H					;准备调用DOS的35号中断
MOV 	AL,N					;设置35号中断的参数
INT		21H						;调用35号中断
MOV 	WORD PTR OLDVECTOR,ES	;段值保存在附加段
MOV 	WORD PTR OLDVECTOR,BX	;偏移保存在BX中
...
```

## 5.3.3中断响应过程

### 1.中断响应过程

通常CPU在执行完每一条指令后均要检测是否有中断请求，在有中断请求且满足一定条件时就响应中断，这个过程如图5.6所示。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/root/image.dfdjnh2wqww.webp)

在中断响应的过程中，由硬件自动完成如下工作：

1. 取得中断类型号；
2. 把标志寄存器内容压入堆栈；
3. 禁止外部中断和单步中断（使IF和TF标志位为0);
4. 把下一条要执行指令的地址（中断返回地址）压入堆栈（CS和IP内容压入堆栈）;
5. 根据中断类型号从中断向量表中取中断处理程序入口地址；
6. 转入中断处理程序。

在CPU响应中断转入中断处理程序时，堆栈如图5.7所示。中断处理程序在最后从堆栈中弹出返回地址和原标志值结束中断，返回被中断程序。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/root/image.4bfby8watcq0.webp)

### 2.中断返回指令

中断处理程序利用中断返回指令从堆栈中弹出返回地址和原标志值。中断返回指令的格式如下：

```assembly
IRET
```

该指令的功能是从中断返回。具体操作如下所示：

```assembly
IP <- --[SP]
SP <- SP+2
CS <- [SP]
SP <- SP+2
FLAGS <- [SP]
```

在执行中断返回指令IRET时的堆栈变化如图5.7(b) 到 (a)。

## 5.3.4 外部中断

==由发生在CPU外部的某个事件引起的中断称为外部中断==。如输入输出设备和协处理器等引起的中断就是外部中断。外部中断以完全随机的方式中断现行程序。8086/8088有两条外部中断请求线：==INTR接受可屏蔽中断请求，NMI接受非屏蔽中断请求。==

#### 1. 可屏蔽中断

在IBMPC系列及其兼容机中，==键盘和硬盘等外设的中断请求都通过中断控制器8259A传给可屏蔽中断请求线INTR==，如图5.8所示，中断控制器8259A共能接收8个独立的中断请求信号IRQ0至IRQ7。在AT机上，有两个中断控制器8259A,一主一从，从8259A连接到主8259A的IRQ2上，这样AT系统就可接收15个独立的中断请求信号。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/root/image.tmha7wg0epc.webp)

==中断控制器8259A在控制外设中断方面起着重要的作用==。==如果接收到一个中断请求信号，并且满足一定的条件，那么它就把中断请求信号传到CPU的可屏蔽中断请求线INTR,使CPU感知到有外部中断请求；同时也把相应的中断类型号送给CPU,使CPU在响应中断时可根据中断类型号取得中断向量，转相应的中断处理程序==。

中断控制器8259A是可编程的，也就是说可由程序设置它如何控制中断。在机器系统加电初始化期间，已对8259A进行过初始化。在初始化时规定了在传出中断请求IRQ0至IRQ7时，送出的对应中断类型号分别是08H~0FH,请参见表5.2。例如，设传出中断请求IRQ1，即传出键盘中断请求，那么送出的中断类型号为9,所以键盘中断的中断类型号为9,键盘中断处理程序的入口地址存放在9号中断向量中。

从普通汇编语言程序设计者的角度看，中断控制器8259A包含两个寄存器：中断屏蔽寄存器和中断命令寄存器，它们决定了传出一个中断请求信号的条件。中断屏蔽寄存器的I/O端口地址是21H,它的8位对应控制8个外部设备，通过设置这个寄存器的某位为0或为1来允许或禁止相应外部设备中断。当第位为0时，表示允许传出来自IRQi的中断请求信号，当第i位为1时，表示禁止传出来自IRQi的中断请求信号。中断屏蔽寄存器的内容称为中断屏蔽字。在PC系列及其兼容机上，中断屏蔽寄存器各位与对应外设的关系如图5.9所示。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/root/image.t96qygo9n0g.webp)

例如：为了使中断控制器8259A只传出来自键盘的中断请求信号，可设置中断屏蔽字11111101B,程序片段如下：

```assembly
MOV	AL,11111101B
OUT 21H,AL
```

例如：下面的程序片段使中断屏蔽寄存器的位4为0,从而允许传出来自串行通信口1的中断请求信号：

```assembly
IN 	AL,21H
AND	AL,11101111B
OUT 21H,AI.
```

从图5.6可知，尽管中断控制器把外设的中断请求信号由INTR传给CPU,但CPU是否响应还取决于中断允许标志IF。如果IF为0,则CPU仍不响应由INTR传入的中断请求；只有在IF为1时，CPU才响应由INTR传入的中断请求。所以，由INTR传入的外部中断请求称为可屏蔽的外部中断请求，由此引起的中断称为可屏蔽中断。由于外设的中断请求均由INTR传给CPU,所以，当IF为0时，CPU不响应所有外设中断请求，当IF为1时，才响应外设中断请求。CPU响应外设中断请求称为开中断（IF=1),反之称为关中断（IF=0)。CPU在响应中断时会自动关中断，从而避免在中断过程中再响应其他外设中断。当然，程序员也可根据需要在程序中安排关中断指令CLI和开中断指令STI。

综上所述，==在IBM PC系列及其兼容机中，所有的外设中断均是可屏蔽中断==；==CPU响应某个外设中断请求的两个必要条件是：中断屏蔽寄存器中的相应位为0和处于开中断状态。==通过对这两个必要条件的控制，可使CPU响应某些外设中断请求，而不响应另外一些外设中断请求。

#### 2.非屏蔽中断

中断从图5.6可知，==当收到从NMI传来的中断请求信号时，不论是否处于开中断状态CPU总会响应。所以，由NMI传入的外部中断请求称为非屏蔽外部中断请求，由此而引起的中断称为非屏蔽中断==。不可屏蔽中断请求由电源掉电、存储器出错或者总线奇偶校验错等紧急故障产生，要求CPU及时处理。在IBM PC系列及其兼容机上，非屏蔽中断的中断类型号规定为2.CPU在响应非屏蔽中断请求时，总是转入由2号中断向量所指定的中断处理程序。

## 5.3.5 内部中断

==由发生在CPU内部的某个事件引起的中断称为内部中断。由于内部中断是CPU在执行某些指令时产生，所以也称为软件中断==。

> 其特点是：不需要外部硬件的支持；不受中断允许标志IF的控制。

### 1.中断指令INT引起的中断

中断指令的一般格式如下：

```assembly
INT n
```

==其中，n是一个0~0FFH的立即数。CPU在执行上面的中断指令后，便产生一个类型号为n的中断，从而转入对应的中断处理程序。==

例如，为了调用DOS系统功能，就在程序中安排如下的中断指令：

```assembly
INT 21H
```

当CPU执行该指令后，就产生一个类型为21H的中断，从而转入对应的中断处理程F,也即转入DOS系统功能服务程序。执行中断指令后的堆栈如图5.7所示。

值得指出的是，程序员根据需要在程序中安排中断指令，所以它不会真正随机产生，而完全受程序控制。

### 2.CPU遇到特殊情况引起的中断

#### (1) 除法错中断

在执行除法指令时，如果CPU发现除数为0或者商超过了规定的范围，那么就产生一个除法错中断，中断类型号规定为0。

例如，在执行下面的程序片段时，会产生一个0号类型的中断：

```assembly
MOV	AX,1234
MOV CL,3
DIV CL		;商超过255(AL容纳不下）
```

为了避免产生0号类型的中断，可改写上述程序片段如下：

```assembly
MOV	AX,1234
MOV	CL,3
XOR	DX,DX
XOR CH,CH
DIV CX
```



#### (2) 溢出中断

8086/8088提供一条专门检测运算溢出的指令，该指令的格式如下：

```assembly
INTO
```

在溢出标志OF置1时，如果执行该指令，则产生溢出中断。溢出中断的类型号规定为4。如果溢出标志OF为0,则执行该指令后并不产生溢出中断。

### 3.用于程序调试的中断

#### (1) ==单步中断==

如单步标志TF为1,则在每条指令执行后产生一个单步中断，中断类型号规定为1。产生单步中断后，CPU就执行单步中断处理程序。由于CPU在响应中断时，已把TF置为0,所以，不会以单步方式执行单步中断处理程序。通常，由调试工具（如DEBUG等）把TF置1,在执行完一条被调试程序的指令后，就转入单步中断处理程序，一般情况下，单步中断处理程序报告各寄存器的当前内容，程序员可据此调试程序。

#### (2) ==断点中断==

8086/8088提供一条特殊的中断指令`INT3`，调试工具（如DEBUG等）可用它替换断点处的代码，当CPU执行这条中断指令后，就产生类型号为3的中断。这种中断称为断点中断。通常情况下，断点中断处理程序恢复被替换的代码，并报告各寄存器的当前内容，程序员可据此调试程序。==所以说中断指令`INT3`特殊是因为它只有一个字节长，其他的中断指令长2字节。==

## 5.3.6 中断优先级和中断嵌套

### 1. 中断优先级

系统中有多个中断源，当多个中断源同时向CPU请求中断时，CPU按系统设计时规定的优先级响应中断请求。在IBM PC系列及其兼容机系统中，如图5.6所示，规定的优先级如下：

> 优先级最高	  内部中断（除法错,INTO,INT)
>
> ​								非屏蔽中断（NM1)
>
> ​								可屏蔽中断（INTR)
>
> 最低					  单步中断

如图5.8所示，外设的中断请求都通过中断控制器8259A传给CPU的INTR引线。在对8259A初始化时规定了8个优先级，在正常的优先级方式下，优先级次序如下：

> IRQ0,IRQ1,IRQ2,IRQ3,IRQ4,IRQ5,IRQ6,IRQ7

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/root/image.f25qfm2yyc0.webp)

但在必要的情况下，通过设置中断控制器8259A中的中断命令寄存器的有关位可改变上述优先级次序。中断命令寄存器的I/O端口地址是20H,其各位的定义如图5.10所示。其中的L2~L0三位指定IRQ0~IRQ7中具有最低优先级的中断请求，R位和SL位控制IRQ0~IRQ7的中断优先级的次序。当R位和SL位全为0时，表示采用正常优先级方式，即上述优先级次序。在一般情况下，总采用这种正常优先级方式。在以后的章节中，如无特别说明，外设中断的优先级次序是上述正常优先级次序。

中断命令寄存器中EOI位是中断结束位，当把它置为1时，表示当前中断处理结束。在对中断控制器8259A初始化时规定，在CPU响应某个外设的中断请求后，中断控制器8259A不再传出中断级相同或较低的外设中断请求，直到8259A接收到中断结束命令为止。例如：CPU在响应来自IRQ1的9号键盘中断后，8259A就不再传出来自IRQ1~IRQ7的外设中断请求，直到通知8259A键盘中断已结束为止。所以，在外设中断处理程序结束时，要通知8259A中断已结束，以便使8259A传出中断级相同或较低的外设中断请求，从而使CPU响应它们。下面程序片段通知8259A当前中断结束：

```assembly
MOV	AL,20H
OUT 20H,AL
```

注意，通知中断控制器8259A当前中断结束，并非中断返回。只有在执行了中断返回指令后，才返回被中断程序。

在对8259A初始化时作上述规定的理由是，使CPU在响应外设中断请求后，只要中断，那么就可响应优先级高的外设中断请求，而不会响应优先级相同或低的外设中断请求。

### 2. 中断嵌套

==CPU在执行中断处理程序时，又发生中断，这种情况称为中断嵌套==。

在中断处理过程中，发生内部中断，引起中断联套是经常的事。例如：==CPU在执行中断处理程序时，遇到软中断指令，就会引起中断嵌套。==

==在中断处理过程中，发生非屏蔽中断，也会引起中断嵌套。==

==由于CPU在响应中断的过程中，已自动关中断，所以，CPU也就不会再自动响应可屏蔽中断。如果需要在中断处理过程的某些时候响应可屏蔽中断，那么可在中断处理程序中安排开中断指令，CPU在执行开中断指令后，就处于开中断状态，也就可以响应可屏蔽中断了，直到再关中断。所以，如果在中断处理程序中使用了开中断指令，也就可能会发生可屏蔽中断引起的中断联套。==

8086/8088没有限制中断嵌套的深度（层次）,但客观上受到堆栈容量的限制。

## 5.3.7 中断处理程序的设计

CPU在响应中断后，自动根据中断类型，取中断向量，并转入中断处理程序，所以，具体的处理工作由中断处理程序完成。不同的中断处理，由不同的中断处理程序完成。对应外设中断的外设中断处理程序和对应指令中断的软中断处理程序有些区别，下面对它们的设计分别作些原则性的介绍。

### 1. ==外设中断处理程序==

在开中断的情况下，外设中断的发生是随机的，在设计外设中断处理程序时必须充分注意到这一点。外设中断处理程序的主要步骤如下：

1. ==必须保护现场。==这里的现场可理解为中断发生时CPU各内部寄存器的内容。CPU在响应中断时，已把各标志和返回地址压入堆栈，所以要保护的现场主要是指通用寄存器的内容和除代码段寄存器外的其他三个段寄存器的内容。因为中断的发生是随机的，所以凡是中断处理程序中要重新赋值的各寄存器的原有内容必须先预保护。保护的一般方法是把它们压入堆栈。
2. ==尽快完成中断处理。==外设中断处理必须尽快完成，所以外设中断处理必须追求速度上的高效率。因为在进行外设中断处理时，往往不再响应其它外设的中断请求，因此必须快，以免影响对其他外设的中断请求。
3. ==恢复现场。==在中断处理完成后，依次恢复被保护寄存器的原有内容。
4. 通知中断控制器中断已结束。如果应用需要，也可==提早通知中断控制器中断结束，这样做必须考虑到外设中断的联套==。
5. ==利用IRET指令实现中断返回==。

此外，应及时开中断。==除非必要，中断处理程序应尽早开中断，以便CPU响应具有更高优先级的中断请求==。

### 2. 软中断处理程序

由中断指令引起的软件中断尽管是不可屏蔽的，但它不会随机发生，只有在CPU执行了中断指令后，才会发生。所以，中断指令类似于子程序调用指令，相应的==软中断处理程序在很大程度上类似于子程序，但并不等同于子程序。==软中断处理程序的主要步骤如下：

1. ==考虑切换堆栈。==由于软中断处理程序往往在开中断状态下执行，并且可能较复杂(要占用大量的堆栈空间）,所以应该考虑切换堆栈。切换堆栈对实现中断嵌套等均较为有利。
2. ==及时开中断。==开中断后，CPU就可响应可屏蔽的外设中断请求，或者说使外设中断请求可及时得到处理。但要注意，如果该软中断程序要被外设中断处理程序“调用”，则是否要开中断或者何时开中断应另外考虑。
3. ==应该保护现场。==应该保护中断处理程序要重新赋值的寄存器原有内容，这样在使用软中断指令时，可不必考虑有关寄存器内容的保护问题。
4. ==完成中断处理。==但不必过分追求速度上的高效率，除非它是被外设中断处理程序“调用”的。
5. ==恢复现场。==依次恢复被保护寄存器的原内容。
6. ==堆栈切换。==如果在开始时切换了堆栈，那么也要再重新切换回原堆栈。
7. 一般利用IRET指令实现中断返回。

# 5.4 基本输入输出系统BIOS

本节在介绍BIOS基本概念的基础上，介绍键盘输入、显示和打印输出三方面的BIOS及部分细节。

## 5.4.1 基本输入输出系统BIOS概述

==固化在ROM中的基本输入输出系统BIOS(Basic Input/Output System)包含了主要I/O设备的处理程序和许多常用例行程序，它们一般以中断处理程序的形式存在。==例如：负责显示输出的显示I/O程序作为10H号中断处理程序存在，负责打印输出的打印I/O程序作为17H号中断处理程序存在，而负责键盘输入的键盘I/O程序作为16H号中断处理程序存在。再如，获取内存容量的例行程序就作为12H号中断处理程序存在。==BIOS直接建立在硬件基础上。==

==磁盘操作系统DOS(Disk Operating System)建立在BIOS的基础上，通过BIOS操纵控制硬件。==例如，DOS调用BIOS显示I/O程序完成显示输出，调用打印I/O程序完成打印输出，调用键盘I/O程序完成键盘输入。尽管DOS和BIOS都提供某些相同的功能，但它们之间的层次关系是明显的。

应用程序、DOS、BIOS和外设接口之间的关系如图5.11所示。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/root/image.3z41oyey92a0.webp)

通常应用程序应该调用DOS提供的系统功能完成输入输出或其他操作。这样做不仅实现容易，而且对硬件的依赖性最少。但有时DOS不提供某种服务，例如，取打印机状态信息，那么就不能调用DOS实现了。

应用程序可以通过BIOS进行输入输出或完成其他功能。在下列三种场合可考虑调用BIOS：一是需要利用BIOS提供而DOS不提供的某个功能的场合；二是不能利用DOS功能调用的场合；==三是出于某种原因需要绕过DOS的场合。==由于BIOS提供的设备处理程序和常用例行程序都以中断处理程序的形式存在，所以应用程序调用BIOS较为方便。但BIOS毕竟比DOS更靠近硬件。

应用程序也可以直接操纵外设接口来控制外设，从而获得速度上最高的效率，但这样的应用程序不仅复杂而且与硬件关系十分密切，此外，还需要程序员对硬件性能比较了解熟悉。所以，应用程序一般不直接与硬件发生关系。

值得指出的是，有时应用程序需要扩充或替换ROM BIOS中的某些处理程序或例行程序，那么这些新的BIOS程序原则上不能调用DOS提供的功能。

## 5.4.2 键盘输入

### 1. 键盘中断处理程序

==当用户按键时，键盘接口会得到一个代表被按键的键盘扫描码，同时产生一个中断请求==。从图5.8和图5.9可知，==如果键盘中断是允许的（中断屏蔽字中的位1为0),并且CPU处于开中断状态（IF=1),那么CPU通常就会响应中断请求==。由于键盘中断的中断类型号安排为9,所以CPU响应键盘中断，就是转入9号中断处理程序。我们把9号中断处理程序称为键盘中断处理程序，它属于外设中断处理程序这一类。

键盘中断处理程序首先从键盘接口取得代表被按键的扫描码，然后根据扫描码判定用户所按的键并作相应的处理，最后通知中断控制器中断结束并实现中断返回。我们把键盘上的键简单地分成五种类型：字符键（字母、数字和符号等）,功能键（如F1和PgUp等）,控制键（Ctrl、Alt和左右Shift),双态键（如Num Lock和Caps Lock等）,特殊请求键(如Print screen等）。键盘中断处理程序对五种键的基本处理方法如下：

如果用户按的是双态键，那么就设置有关标志，在AT以上档次的系统上还要改变LED指示器状态。如果用户按的是控制键，那么就设置有关标志。如果用户按的是功能,键，那么就根据键盘扫描码和是否按下某些控制键（如Alt)确定系统扫描码，把系统扫描码和一个全0字节一起存入键盘缓冲区。如果用户按的是字符键，那么就根据键盘扫描码和是否按下某些控制键（如Ctrl)确定系统扫描码，并且得出对应的ASCII码，把系统扫描码和ASCII码一起存入键盘缓冲区。如果用户按的是特殊请求键，那么就产生一个相对应的动作，例如用户按Print screen键，那么就调用5H号中断处理程序打印屏幕。

### 2. 键盘缓冲区

==键盘缓冲区是一个先进先出的环形队列、结构和占用的内存区域如下==：

```assembly
BUFF_HEAD	DW	?			;0040:001AH
BUFF_TAIL 	DW 	?			;0040:001CH
KB_BUFFER 	DW 	16 DUP (?) 	;0040:001EH~003DH
```

BUFF_HEAD和BUFF_TAIL是缓冲区的头指针和尾指针，当这两个指针相等时，表示缓冲区为空。==由于缓冲区本身长16个字，而存放一个键的扫描码和对应的ASCII码需要占用一个字，所以键盘缓冲区可实际存放15个键的扫描码和ASCII码。==键盘中断处理程序把所按字符键或功能键的扫描码和对应的ASCII码（如为功能键，对应的ASC码理解为0)依次存入键盘缓冲区。如缓冲区已满，则不再存入，而是发出“嘟”的一声。

顺便说一下，键盘中断处理程序根据控制键和双态键建立的标志在内存单元0040:0017H字单元中。

### 3.键盘I/O程序

尽管系统程序和应用程序可从键盘缓冲区中取得用户所按键的代码，但除非特殊情况，==一般不宜直接存取键盘缓冲区，而应调用BIOS提供的键盘I/O程序。==

==键盘I/O程序以16H号中断处理程序的形式存在，它属于软中断处理程序这一类它的主要功能是进行键盘输入。==一般情况下，系统程序和应用程序的键盘输入最后都是调用它完成的。简单的键盘I/O程序从键盘缓冲区中取出所按键的ASCII码和扫描码返回给调用者。键盘中断程序、键盘缓冲区和键盘I/O程序之间的关系如图5.12所示。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/root/image.5cyjxcl2wwo0.webp)

### 4. 键盘I/O程序的功能和调用方法

键盘I/O程序提供的主要功能列于表5.3,每一个功能有一个编号。在调用键盘I/O程序时，把功能编号置入AH寄存器，然后发出中断指令`INT 16H`。调用返回后，从有关寄存器中取得出口参数。除保存出口参数的寄存器外，其他寄存器内容保持不变。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/root/image.5lepimois800.webp)

我们把控制键和双态键统称为变换键，调用键盘I/O程序的2号功能可获得各变换键的状态。变换键状态字节各位的定义如图5.13所示，其中高四位记录双态键的变换情况，每按一下双态键，则对应的位值取反；==低四位反映控制键是否正被按下，按着某个控制键时，对应的位为1。==

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/root/image.36eynntgmzu0.webp)

下面的程序片段从键盘读一个字符：

```assembly
MOV AH,0
INT 16H
```

如果键盘缓冲区中有字符，那么中断处理就会极快结束，即调用就会极快返回，读到的字符是调用发出之前用户按下的字符。如果键盘缓冲区空，那么要等待用户按键后调用才会返回，读到的字符是调用发出之后按下的字符。==如果程序员出于某种理由，要从键盘取得在调用发出之后用户按下的字符，那么就要先清除键盘缓冲区。下面的程序片段先清除键盘缓冲区，然后再从键盘读一个字符==：

```assembly
...
AGAIN:	MOV		AH,1
		INT		16H		;判缓冲区空？
		JZ 		NEXT	;空，转
		MOV		AH,0
		INT		16H		;从键盘缓冲区取走一字符
		JMP 	AGAIN	;继续
NEXT:	MOV		AH,0
		INT 	16H		;等待键盘输入
...
```

当然，==程序员也可通过直接修改键盘缓冲区头指针的方法清除键盘缓冲区==，但我们不鼓励这样做。

#### 例1:写一个程序完成如下功能：读键盘，并把所按键盘显示出来，在检测到按下SHIFT键后，就结束运行。

调用键盘I/O程序的2号功能取得变换键状态字节，进而判断是否按下了SHIFT键。在调用0号功能读键盘之前，先调用1号功能判键盘是否有键可读，否则会导致不能及时检测到用户按下的SHIFT。源程序如下：

```assembly
;程序名：T5-1.ASM
;功能：(略）
;常量定义
L_SHIFT=00000010B
R_SHIFT=00000001B
;代码段
CSEG 	SEGMENT
		ASSUME CS:CSEG
START: 	MOV 	AH,2				;取变换键状态字节
		INT		16H
		TEST 	AL,L_SHIFT+R_SHIFT	;判是否按下SHIFT
		JNZ 	OVER				;按下，转
		MOV		AH,1
		INT		16H					;是否有键可读
		JZ 		START				;没有，转
		MOV 	AH,0				;读键
		INT		16H
		MOV 	DL,AL				;显示所读键
		MOV		AH,6
		INT		21H
		JMP 	START				;继续
OVER:	MOV		AH,4CH
		INT		21H					;结束
CSEG 	ENDS
		END 	START
```

## 5.4.3 显示输出

显示器通过显示适配卡与系统相连，显示适配卡是显示输出的接口。早先的显示适配卡是CGA和EGA等，目前常用的显示适配卡是VGA和TVGA等。它们都支持两类显示方式：文本显示方式和图形显示方式，每一类显示方式还含有多种显示模式。

### 1.文本显示方式

所谓文本显示方式是指以字符为单位显示的方式。字符通常是指字母、数字、普通符号（如运算符号）和一些特殊符号（如菱形块和矩形块）。

通常0~3号显示模式为文本显示方式，它们之间的区别是每屏可显示的字符数和可使用的颜色数目不同。用得最普遍的是3号显示模式，我们就以3号显示模式为代表作介绍。

==在3号文本显示模式下，显示器的屏幕被划分成80列25行，所以每一屏最多可显示2000(80×25)个字符。==我们用行号和位号组成的坐标来定位屏幕上的每个可显示位置，左上角的坐标规定为(0,0)，向右增加列号，向下增加行号，这样右下角的坐标便是（79,24)。如图5.15所示。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/root/image.35kqflae35w0.webp)

### 2. 显示属性

屏幕上显示的字符取决于字符代码及字符属性。这里的属性是指显示属性，它规定字符显示时的特性。在单色显示时，属性定义了闪烁、反相和高亮度等显示特性。在彩色显示时，属性还定义了前景色和背景色。图5.14给出了彩色显示时属性字节各位的定义。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/root/image.y8zdmnquusw.webp)

在属性字节中，==RGB分别表示红、绿、蓝，I表示亮度，BL表示闪烁。位0~位3组合16种前景颜色，位4~位6组合8种背景颜色。亮度和闪烁只能用于前景。当I位为1时，表示高亮度，当I位为0时，表示普通亮度。当BL位为1时，表示闪烁，当BI位为0时，表示不闪烁。==表5.4给出了彩色文本模式下IRGB组合成的通常颜色。前景颜色和背景颜色一起确定字符的显示效果，表5.5列出了几种典型的属性值。当前景和背景相同时，字符就看不出了。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/root/image.6c9tlnrq1bg0.webp)

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/root/image.1xxjl7a5i1gg.webp)

### 3. 显示存储区

显示适配卡带有显示存储器，用于存放屏幕上显示文本的代码及属性或图形信息。显示存储器作为系统存储器的一部分，可用访问普通内存的方法访问显示存储器。==通常，为显示存储器安排的存储地址空间的段值是B800H或B000H,对应的内存区域就称为显示存储区。我们假设段值是B800H。==

在3号文本显示模式下，屏幕上的每一个显示位置依次对应显示存储区中的两个字节单元，这种对应关系如图5.15所示。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/root/image.35kqflae35w0.webp)

为了在屏幕上某个位置显示字符，只需把要显示字符的代码及其属性填到显示存储区中的对应存储单元即可。下面的程序片段实现在屏幕的左上角以黑底白字显示字符"A":

```assembly
......
MOV AX,0B800H
MOV DS,AX
MOV BX,0
MOV AL,'A'
MOV AH,07H
MOV [BX],AX
......
```

为了了解屏幕上某个显示位置所显示的字符是什么，或显示的颜色是什么，那么只要从显示存储区中的对应存储单元中取出字符的代码和属性即可。下面的程序片段取得屏幕右下角所显示字符的代码及属性：

```assembly
......
MOV AX,0B800H
MOV DS,AX
MOV BX,(80*24+79)*2		;386模式下可以这样写
MOV AX,[BX]
......
```

这种直接存取显示存储器进行显示的方法称为直接写屏。

#### 例2: 采用直接写屏法在屏幕上用多种属性显示字符串“HELLO”。

先用一种属性在屏幕上显示指定信息，然后在用户按一键后再换一种属性显示，如按ESC键，则结束。其中的显示子程序ECHO采用了直接写屏方法实现显示。源程序如下：

```assembly
;源程序名：T5-2.ASM
;功能：(略）
;常量定义
ROW = 5			;显示信息的行号
COLUM = 10		;列号
ESCKEY = 1BH		;ESC键的ASCII码值
;数据段
DSEG 	SEGMENT
MESS 	DB	'HELLO'				;显示信息
MESS_LEN = $-OFFSET MESS		;显示信息长度
COLORB 	DB 07H,17H,0FH,70H,74H	;颜色
COLORE 	LABEL BYTE
DSEG ENDS
;代码段
CSEG SEGMENT
	ASSUME	CS:CSEG,DS:DSEG
START:	
		MOV		AX.DSEG
		MOV 	DS,AX				;设置数据段段值
		MOV 	DI,OFFSET COLORB-1	;颜色指针初值
NEXTC:
		INC 	DI					;调整颜色指针
		CMP 	DI,OFFSET COLORE	;是否超过指定的最后一种颜色
		JNZ 	NEXTE				;否
		MOV 	DI,OFFSET COLORB	;是，重新指定第一种颜色
NEXTE:
		MOV		BL,[D1]				;取颜色
		MOV 	SI,OFFSET MESS		;取显示信息指针
		MOV 	CX,MESS_LEN			;取显示信息长度
		MOV 	DH,ROW				;置显示开始行号
		MOV 	DL,COLUM			;置显示开始列号
		CALL 	ECHO				;显示
		MOV 	AH,0
		INT		16H
		CMP 	AL,ESCKEY			;是否为ESC键
		JNZ 	NEXTC				;不是，继续
		
		MOV		AX,4C00H			;结束
		INT		21H
;
;子程序名：ECHO
;功能：直接写屏显示字符串
;入口参数：DS:S1=字符串首地址
;CX=字符串长度
;BL=属性,DH=显示开始行号
;DL=显示开始列号
;出口参数：无
ECHO 	PROC
		MOV 	AX,0B800H
		MOV 	ES,AX		;设置显示段段值
		MOV 	AL,80		;计算显示开始位置偏移
		MUL 	DH			;偏移=(行号*80+列号）*2
		XOR 	DH,DH
		ADD 	AX,DX
		ADD 	AX,AX
		XCHG 	AX,BX
		MOV 	AH,AL		;属性值保存到AH寄存器
		JCXZ 	ECHO2		;显示信息长度是否为0
ECHO1:
		MOV		AL,[SI]		;取一要显示字符代码
		INC 	SI			;调整指针
		MOV 	ES:[BX],AX	;送显示存储区，即显示
		INC 	BX			;准备显示下一个字符
		INC 	BX
		LOOP 	ECHO1		;循环显示
ECHO2:	RET					;返回
ECHO 	ENDP
CSEG	ENDS
		END 	START
```

### 4. 显示I/O程序的功能和调用方法

利用直接写屏方法，程序可实现快速显示。但为了实现直接写屏，必须了解显示存储器占用存储空间的具体细节和显示存储区与屏幕显示位置的对应关系，并且最终的程序也与显示适配卡相关。所以，除非追求显示速度，一般不采用直接写屏方法，而是调用BIOS提供的显示I/O程序。

==BIOS提供的显示I/O程序作为10H号中断处理程序存在。==

显示I/O程序的主要功能列于表5.6,每一个功能有一个编号。在调用显示I/O程序的某个功能时，应根据要求设置好入口参数，把功能编号置入AH寄存器，然后发出中断指令`INT 10H`。调用返回后，从有关寄存器中取得出口参数。除保存出口参数的寄存器外，其他寄存器内容保持不变。

现就显示页号作些说明。为了支持屏幕上显示2000个字符，需要的显示存储器容量约为4KB。如果显示存储器的容量为32KB,那么显示存储器可存放8屏显示内容。为此，把显示存储器再分成若干段，称为显示页。调用显示I/O程序的5号功能，可选择当前显示页。通常，总是使用第0页。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/root/image.cc37isbodp4.webp)

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/root/image.4wkuoyex5a00.webp)

### 5. 举例

调用显示I/O程序的5号功能，可选择当前显示页。下面的程序片段选择第0页作为当前显示页：

```assembly
...
MOV	AL,0
MOV AH,5
INT 10H
...
```

如果要知道当前显示页号，则可调用显示I/O程序的0FH号功能，同时可知道当前显示模式和该模式下的最大显示列数，下面的程序片段调用OFH号功能：

```assembly
...
MOV AH,OFH
INT 10H
...
```

下面的程序片段调用显示I/O程序的9号功能在当前光标位置处显示指定属性的字符，但不移动光标：

```assembly
...
MOV BH,0		;第0页
MOV BL,47H		;红底白字
MOV CX,1		;1个
MOV AL,'A'		;字符为A
MOV AH,9
INT 10H
...
```

在窗口滚屏时，如果滚屏行数为0,就表示清除整个窗口。如果把整个屏幕作为窗口，那么就可实现清屏。下面的程序假设屏幕为80列，它先清除屏幕，然后把光标定到左上角：

```assembly
...
MOV CH,0		;置左上角坐标
MOV CL,0
MOV	DH,24		;置右下角坐标
MOV	DL,79
MOV BH,07		;清除区的填充属性（黑底白字）
MOV AL,0		;清整个窗口
MOV	AH,6
INT 10H			;实现清屏
MOV BH,0		;设第0页
MOV DH,0		;置光标定位坐标
MOV DL.0
MOV AH,2
INT 10H			;定位光标
...
```

#### 例3:写一个程序完成如下功能：在屏幕中间部位开出一个窗口，随后接收用户按键，并把按键字符显示在窗口的最底行，当窗口底行显示满时，窗口内容就向上滚动一行；用户按Ctrl+C键时，结束运行。

调用显示I/O程序可方便地实现该程序。源程序如下：

```assembly
;程序名：T5-3.ASM
;功能：(略）
;常量定义
WINWIDTH 	= 	40		;窗口宽度
WINTOP		=	8		;窗口左上角行号
WINLEFT		=	20		;窗口左上角列号
WINBOTTOM	=	17		;窗口右下角行号
WINRIGHT	= 	WINLEFT+WINWIDTH-1	;窗口右下角列号
COLOR		=	74H		;属性值
PAGEN		=	0		;显示页号
CTRL_C		=	03H		;结束符ASCII码
;代码段
CSEG 	SEGMENT
		ASSUME CS : CSEG
START: 	
		MOV 	AL,PAGEN		;选择显示页
		MOV 	AH,5
		INT 	10H
		MOV 	CH,WINTOP		;清规定窗口
		MOV 	CL,WINLEFT
		MOV 	DH,WINBOTTOM
		MOV		DL,WINRIGHT
		MOV 	BH,COLOR
		MOV 	AL,0
		MOV 	AH,6
		INT 	10H
		MOV 	BH,PAGEN		;定位光标到窗口左下角
		MOV 	DH,WINBOTTOM
		MOV 	DL,WINLEFT
		MOV 	AH,2
		INT		10H
NEXT:
		MOV		AH,0			;接受一个键
		INT		16H
		CMP 	AL,CTRL_C		;判是否是结束键
		JZ 		EXIT			;是，转
		MOV 	BH,PAGEN		;在当前光标位置显示所按键
		MOV 	CX,1			;但没有移动光标
		MOV 	AH,OAH
		INT		10H				
		;
		INC		DL				;光标列数加1,准备向右移动光标
		CMP 	DL,WINRIGHT+1	;判是否越出窗口右边界
		JNZ 	SETCUR			;不，转
		MOV 	CH,WINTOP		;是，窗口内容上滚一行
		MOV 	CL,WINLEFT		;空出窗山的最底行
		MOV 	DH,WINBOTTOM
		MOV 	DL,WINRIGHT
		MOV 	BH,COLOR
		MOV		AL,1
		MOV		AH,6
		INT 	10H
		MOV 	DL,WINLEFT		;光标要回到最左面
SETCUR:
		MOV 	BH,PAGEN		;置光标（光标后移）
		MOV		AH,2
		INT 	10H
		JMP		NEXT			;继续
EXIT:	
		MOV 	AX,4C00H
		INT 	21H				;结束
CSEG	ENDS
		END 	START
```

#### 例4: 调用显示I/O程序来实现程序T5-2.ASM中的显示子程序ECHO。

##### 方法之一：调用I/O程序中的13H号功能直接显示字符串。源程序代码如下：

```assembly
;子程序名：ECHOA
;功能：调用显示I/O程序的13H号功能显示字符串
;入日参数：DS:SI=字符串首地址CX=字符串长度
;BL=属性
;DH=显示开始行号
;DL=显示开始列号
;出门参数：无
ECHOA 	PROC
		PUSH 	ES
		PUSH 	BP			;保护有关寄存器内容
		PUSH 	DS
		POP 	ES		
		MOV 	BP,SI		;满足13H号功能入口参数要求
		MOV		BH,0		;指定显示页
		MOV		AL,0		;采用0号显示方式（不移动光标
		MOV		AH,13H		;字符串中不含属性）
		INT 	10H
		POP		BP
		POP		ES			;恢复有关寄存器内容
		RET
ECHOA 	ENDP
```

该子程序与ECHO稍有不同：第一是13H号功能解释回车和换行等控制码，所以如果要显示的字符串中含有这样的控制码，那么显示效果就会有差异；第二是当显示超出最后一行的最后一列时，13H功能要引起滚屏。

##### 方法之二：先调用显示I/O程序的2号功能把光标定到指定位置，然后利用显示I/O程序的TTY显示功能逐个显示字符串中的字符。

但由于TTY方式显示不含属性，所以先调用.9号功能把指定属性写到显示字符串的位置处。源程序代码如下：

```assembly
;子程序名：ECHOB
;其他说明信息略
ECHO 	PROC
		JCXZ	ECHO2		;如果字符串长度为0,则结束
		MOV 	BH,0
		MOV		AH,2		;设置光标位置
		INT 	10H
		MOV		AL,20H		;用指定属性写一串空格
		MOV		AH,9
		INT		10H
		MOV		AH,OEH
ECHOI:
		MOV		AL,[SI]
		INC 	SI
		INT 	10H 		;逐个显示字符
		LOOP	ECHO1
ECHO2:	RET
ECHO 	ENDP
```

该子程序于ECHO也稍有不同：第一TTY方式显示也解释控制符，所以如果要显示的字符串中含有控制符，则显示效果就不一样；第二是当TTY方式在屏幕的右下角显示一个字符后要上滚屏幕；第三是字符串显示完后，光标定位在字符串之后。

方法之三：先读取当前光标位置且保存；定位光标到指定位置；调用显示I/O程序的9号功能逐个显示字符，每显示一个字符后，把光标向后移一个位置；最后把光标回到原位。注意，在把光标向后移一个位置时，要判别是否超越屏幕右边界和是否已到达屏幕的右下角。请读者作为练习完成子程序ECHOC。

## 5.4.4打印输出

### 1.打印I/O程序的功能和调用方法

BIOS提供的打印I/O程序作为17H号中断处理程序存在。

打印1/0程序的主要功能列于表5.7,每一个功能有一个编号。系统可连接多台打印机.用打印机号选择打印机，打印机号为0、1和2。调用返回的出口参数只是打印机状态字节。打印机状态字节各位的定义类似与图5.3给出的打印接口中状态寄存器的定义，有两点区别：(1)第0位定义成超时标志位，当打印机在规定时间内仍处于忙，即不能接受打印数据时，就置超时标志。（2)第6位（应答位）和第3位（错误位）都规定状态有效为1(与状态寄存器的定义相反）。

在调用打印1/0程序的某个功能时，应根据要求设置好入口参数，把功能编号置入AH寄存器，然后发出中断指令`INT 17H`。除保存出口参数的寄存器外，其他寄存器内容保持不变。通常系统只连接一台打印机，打印机号为0。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/root/image.5w025n292jw0.webp)

### 2.举例

#### 例5:写一个在0号打印机上打印屏幕内容的程序

程序流程如图5.16所示，现结合流程作些说明。为把屏幕上的内容打印出来，先要获得屏幕上的显示内容这可利用显示I/O程序提供的8号功能实现。以行为单位从左到右打印屏幕上的内容，从顶行开始到底行结束，这样就形成一个二重循环。在把一行屏幕内容输出到打印机言，追加输出一个回车符和一个换行符，使打印机实施日印动作。在输出一个字符到打印机后，还判打印机是否正常工作，判打印机是否正常工作的依据是测打印机状态字节中的超时标志、IO错误标志和纸尽标志。由于要移动光标，所以在移动前先保存光标位置，最后恢复原光标位置。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/root/image.55un8nf661s0.webp)

```assembly
;程序名：T5-4.ASM
;功能：打印屏幕内容
;常量定义
TIME_OUT	=	00000001B
IO_ERROR 	= 	00001000B
OUT_OFP		=	00100000B
F1_AG 		= 	TIME_OUT+IO_ERROR+OUT_OFP	
;代码段
CSEG 	SEGMENT
		MOV 	AH,OFH
		INT 	10H			;取当前显示页号和最大列数
		MOV		CL,AH		;最大列数保存到CL
		MOV 	CH,25		;行数送CH
		PUSH	CX
		MOV 	AH,3		;取当前光标位置
		INT 	10H
		POP 	CX
		PUSH	DX			;保存当前光标
		XOR 	DX,DX		;准备从左上角开始
PRI1:
		MOV		AH,2		;置光标
		INT 	10H
		MOV 	AH,8		;取当前光标处字符
		INT 	10H
		OR 		AL,AL		;字符有效？
		JNZ		PR12		;是，转
		MOV 	AL,"		;否，作为空格处理
PRI2:
		PUSH	DX
		XOR 	DX,DX
		XOR 	AH,AH		;所取字符送打印机
		INT 	17H
		POP 	DX
		TEST	AH,FLAG		;打印机OK?
		JNZ 	ERR1 		;否，转
		INC		DL			;是，光标列加1
		CMP 	CL,DL 		;到右边界？
		JNZ 	PRI1		;否，继续下一列
		PUSH 	DX
		XOR 	DX,DX		;是，发出回车和换行控制符
		MOV 	AX,ODH
		INT 	17H
		MOV 	AX,OAH
		INT 	17H
		POP 	DX
		XOR 	DL,DL		;光标准备到下一行左边
		INC 	DH			;为此，行号加1
		CMP 	CH,DH 		;最后一行完？
		JNZ 	PRI1		;否，继续下一行
		POP 	DX			;恢复保存的原光标位置
		MOV 	AH,2		;把光标置回原处
		INT 	10H
		JMP 	SHORT EXIT 	;转结束
		;
ERR1:	
		POP		DX			;恢复保存的原光标位置
		MOV 	AH,2
		INT 	10H			;把光标置回原处
ERR2:
		MOV		AL,7
		MOV 	AH,OEH
		INT 	10H			;打印机故障时，发出三声“嘟”
		INT 	10H
		INT 	10H
		;
EXIT:	
		MOV		AH,4CH;结束
		INT 	21H
CSEG 	ENDS
		END START
```

BIOS提供的屏幕打印程序的实现方法和上述程序是相同的，只是作为5H号中断处理程序而存在。

# 5.5软中断处理程序举例

本节以打印I/O程序和时钟显示程序的实现为例介绍软中断处理程序的设计。

## 5.5.1打印I/O程序

我们在5.4.4节介绍了由BIOS提供的打印I/O程序的功能和调用方法，现在介绍打印I/O程序的源程序，把它作为BIOS中断处理程序设计的例子。

每个打印接口有三个寄存器：数据寄存器、状态寄存器和控制寄存器，它们的端口地址都是连续的，在系统加电初始化期间，数据寄存器端口地址被依次保存到BIOS数据区，从40H段的偏移8处开始。

尽管通过中断控制器8259A可实现中断方式打印输出，但BIOS提供的打印I/O程序却采用在5.2.3节中介绍的查询方式实现打印输出。查询时使用的超时参数也在BIOS数据区中，从40H段的偏移78H开始。

BIOS提供的打印I/O程序作为17H号中断处理程序存在。实现流程如图5.17所示。作为BIOS软中断处理程序，没有自己的堆栈，而是使用主程序的堆栈。首先开中断，以便使CPU及时响应外设中断请求。然后保护要使用到的各寄存器内容。再从BIOS数据区中取出指定打印机的数据寄存器端口地址，如端口地址值为0,表示没有安装指定的打印接口。接下来根据功能号，分情况处理。源程序如下所示：

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/root/image.571dn3wb9r80.webp)

```assembly
CODE 	SEGMENT PUBLIC
ASSUME	CS:CODE,DS:DATA
;常量说明
PRINT_TIM_OUT = 78H		;超时参数存放单元开始偏移
PRINTER_BASE  = 84		;端口地址存放单元开始地址偏移
BIOS_DATA_SEG = 40H		;BIOS数据段的段值
;代码部分
PRINTER_IO PROC FAR
		STI					;开中断
		PUSH 	DS
		PUSH 	DX			;保护现场
		PUSH 	SI
		PUSH 	CX
		PUSH 	BX
		MOV 	BX,BIOS_DATA_SEG
		MOV 	DS,BX					;置BIOS数据段段值
		MOV 	SI,DX					;DX=0,0号打印机
		MOV 	BL,PRINT_TIM_OUT[SI]	 ;取超时测试基本单位
		SHL		SI,1
		MOV 	DX,PRINTER_BASE[SI]		;取数据寄存器端口地
		OR 		DX,DX					;判系统是否有对应接
		JZ 		B1						;转结束
		OR 		AH,AH					;0号功能？
		JZ 		B2						;是，转
		DEC 	AH						;1号功能？
		JZ		B8						;是，转
		DEC 	AH						;2号功能？
		JZ 		B5						;是，转
B1:
		POP		BX
		POP 	CX						;恢复现场
		POP 	SI
		POP		DX
		POP 	DS
		IRET							;中断返回
;0号功能处理
B2: 
		PUSH 	AX
		OUT		DX,AL					;输出打印数据
		INC	 	DX						;DX=状态寄存器端口地址
		PUSH 	BX
		SUB		BH,BH
		RCL 	BX,1					;确定超时参数单位数
		RCL 	BX,1
B3:
		SUB		CX,CX					;每一单位测65536次
B3_1:
		IN		AL,DX					;读状态寄存器
		MOV		AH,AL
		TEST	AL,80H					;是否“忙碌”？
		JNZ		B4						;否，转
		LOOP 	B3_1					;是，继续测
		DEC 	BX						;单位数完？
		JNZ		B3						;否，转
		POP		BX						;是
		OR 		AH,1					;置超时标志位
		AND		AH,OF9H					;去掉无定义位
		JMP 	SHORT B7				;转，中断返回
B4:
		POP 	BX
		MOV 	AL,ODH					;选通，即真正输出到打印机
		INC		DX
		OUT 	DX,AL
		MOV		AL,OCH
		JMP 	$+2
		OUT 	DX,AL
		POP		AX						;2号功能处理
B5:
		PUSH	AX
B6: 
		MOV		DX,PRINTER_BASE[SI]
		INC 	DX						;DX=状态寄存器端口地址
		IN 		AL,DX					;读状态寄存器
		MOV 	AH,AL
		AND		AH,OF8H					;去掉无定义位和清超时标志位
B7:
		POP		DX
		MOV 	AL,DL
		XOR		AH,48H					;第6和第3位取反（符合出口约定）
		JMP		B1						;转，中断返回1号功能处理
B8: 
		PUSH 	AX
		INC		DX
		INC		DX						;DX=控制寄存器端口地址
		MOV		AL,8
		OUT		DX,AL					;输出初始化命令
		MOV 	AX,10000
B9:
		DEC		AX						;等待一段时间
		JNZ		B9
		MOV 	AL,OCH					;输出正常控制命令
		OUT		DX,AL
		JMP		B6						;转取状态字节和中断返回
PRINTER_IO		ENDP
		CODE 	ENDSEND
```

上述17H号中断处理程序直接操纵控制打印接口，没有再调用其他程序。

## 5.5.2 时钟显示程序

在系统加电初始化其间，把系统定时器初始化为每隔约55毫秒发出一次中断请求。据图5.9，CPU在响应定时中断请求后转入8H号中断处理程序。BIOS提供的8H号中断处理程序中有一条中断指令`INT 1CH`，所以每秒要调用到约18.2次1CH号中断处理程序。而BIOS的1CH号中断处理程序实际上并没有做任何工作，只有一条中断返回指令。这样安排的目的是为应用程序留下一个软接口，应用程序只要提供新的1CH号中断处理程序，就可能实现某些周期性的工作。

下面介绍的时钟显示程序就是利用这个软接口，实现时钟显示。

在新的1CH号中断处理程序中安排一个计数器，记录调用它的次数，当计数满18次后，就在屏幕的右上角显示当前的时间（时分秒）,清计数器。这样约每秒显示一次当前时间。当前时间的获取是调用1AH号中断处理程序的2号功能完成的，该功能在CH、CL和DH寄存器中返回时、分和秒的BCD码。在把BCD码转换为对应十进制数的ASCII码后，调用显示I/O程序完成显示。

主程序首先保存原1CH号中断向量，然后设置新的1CH号中断向量，在完成主程序的其他工作后，再恢复原1CH号中断向量。在主程序设置新的1CH号中断向量后，时钟就开始工作。作为例子，这里的主程序实际上并没有进行实质性的工作。

```assembly
;程序名：T5-5.ASM
;功能：(略）
;中断处理程序常量定义
COUNT_VAL  	=  	18	;间隔“嘀答”数
DPAGE		=	0	;显示页号
ROW			=	0	;显示时钟的行号
COLUMN		=	80-BUFF_LEN ;显示时钟的开始列号
COLOR		=	07H			;显示时钟的属性值
;代码
CSEG SEGMENT
	ASSUME CS:CSEG,DS:CSEG
;1CH号中断处理程序使用的变量
COUNT 	DW 	COUNT_VAL		;“嘀答”计数
HHHH 	DB 	?,?,':'			;时
MMMM 	DB 	?,?,':'			;分
SSSS 	DB 	?,?				;秒
BUFF_LEN = 	$-OFFSET HHHH	;BUFF_LEN为显示信息长度
CURSOR 	DW 	?				;原光标位置
;1CH号中断处理程序代码
NEWICH:
		CMP		CS:COUNT,0	;是否已到显示时候？
		JZ		NEXT		;是，转
		DEC 	CS:COUNT	;否
		IRET				;中断返回
NEXT:
		MOV 	CS:COUNT,COUNT_VAL	;重置间隔数初值
		STI							;开中断
		PUSH 	DS
		PUSH 	ES
		PUSH 	AX
		PUSH 	BX
		PUSH 	CX					;保护现场
		PUSH 	DX
		PUSH 	SI
		PUSH 	BP
		PUSH 	CS
		SPOP 	DS					;置数据段寄存器
		PUSH 	DS
		POP 	ES					;置代码段寄存器
		CALL 	GET_T				;取时间
		MOV 	BH,DPAGE
		MOV 	AH,3				;取原光标位置
		INT 	10H
		MOV 	CURSOR,DX			;保存原光标位置
		MOV 	BP,OFFSET HHHH
		MOV 	BH,DPAGE
		MOV 	DH,ROW
		MOV 	DL,COLUMN
		MOV 	BL,COLOR
		MOV 	CX,MESS_LEN
		MOV 	AL,0
		MOV		AH,13H				;显示时钟
		INT 	10H
		MOV 	BH,DPAGE			;核复原光标
		MOV 	DX,CURSOR
		MOV		AH,2
		INT		10H
		POP 	BP
		POP 	SI
		POP 	DX					;恢复现场
		POP 	CX
		POP		BX
		POP 	AX
		POP 	ES
		POP 	DS
		IRET						;中断返回
		;
;子程序说明信息略
GET_T 	PROC
		MOV 	AH,2				;取时间信息
		INT 	1AH
		MOV 	AL,CH				;把时数转为可显示形式
		CALL 	TTASC
		XCHG 	AH,AL
		MOV 	WORD PTR HHHH,AX 	;保存
		MOV 	AL,CL				;把分数转为可显示形式
		CALL 	TTASC
		XCHG 	AH,AL		
		MOV 	WORD PTR MMMM,AX	;保存
		MOV 	AL,DH				;把秒数转为可显示形式
		CALL 	TTASC
		XCHG 	AH,AL
		MOV 	WORD PTR SSSS,AX 	;保存
		RET
GET_T 	ENDP
;子程序名：TTASC
;功能：把两位压缩的BCD码转换为对应的ASCII码
;入口参数：AL=压缩BCD码
;出口参数：AH=高位BCD码所对应的ASCII码
; 		  AL=低位BCD码所对应的ASCII码
TTASC 	PROC
		MOV 	AH,AL
		AND 	AL,OFH
		SHR 	AH,1
		SHR 	AH,1
		SHR 	AH,1
		SHR 	AH,1
		ADD 	AX,3030H
		RET
TTASC ENDP
;=====================
;初始化部分代码和变量
OLD1CH 	DD 		?			;原中断向量保存单元
START:
		PUSH	CS
		POP 	DS
		MOV 	AX,351CH	;取1CH号中断向量
		INT		21H
		MOV 	WORD PTR OLD1CH,BX	;保存
		MOV 	WORD PTR OLD1CH+2,ES
		MOV 	DX,OFFSET NEW1CH
		MOV 	AX,251CH			;置新的1CH号中断向量
		INT21H
		;
		;....
		;....				;其它工作
		;····
		MOV		AH,0		;假设其它工作是等待按键
		INT		16H
		LDS 	DX,OLD1CH	;取保存的原1CH号中断向量
		MOV 	AX,251CH
		INT		21H			;恢复原1CH号中断向量
		MOV		AH,4CH		;结束
		INT		21H
CSEG 	ENDS
		END 	START
```

上述1CH号中断处理程序没有调用DOS系统功能显示时间，而是调用显示I/O程序显示时间。另外，它除了管理计数器外，还使用了若干变量。