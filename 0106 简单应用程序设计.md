[toc]

8086/8088指令集含有专门的字符串操作指令，利用它们可有效地进行字符串操作。指令集还有十进制数算术运算调整指令，利用它们可快速地实现十进制数算术运算。本章先介绍这些指令，然后结合若干特殊情况处理程序说明简单应用程序的设计方法。

# 6.1字符串处理

字符串是字符的一个序列。对字符串的操作处理包括复制、检索、插入、删除和替换等。为了便于对字符申进行有效的处理，8086/8088提供专门用于处理字符串的指令，我们称之为字符串操作指令，简称为串操作指令。本节先介绍串操作指令及与串操作指令密切相关的重复前缀，然后举例说明如何利用它们进行字符串处理。

## 6.1.1字符串操作指令

### 1.一般说明

8086/8088共有五种基本的串操作指令。每种基本的串操作指令包括两条指令，一条适用于以字节为单元的字符串，另一条适用于以字为单元的字符串。

在字符串操作指令中，由变址寄存器SI指向源操作数（串）,由变址寄存器DI指向目的操作数（串）。规定源串存放在当前数据段中，目的串存放在当前附加段中，也即在涉及源操作数时，引用数据段寄存器DS,在涉及目的操作数时，引用附加段寄存器ES。换句话说，DS:SI指向源串，ES:DI指向目的串。

串操作指令执行时会自动调整作为指针使用的寄存器SI或DI之值。如串操作的单元是字节，则调整值为1;如串操作的单元是字，则调整值为2。此外，字符串操作的方向(处理字符串中单元的次序）由标志寄存器中的方向标志DF控制。当方向标志DF复位(为0)时，按递增方式调整寄存器SI或DI值；当方向标志DF置位（为1)时，按递减方式调整寄存器SI或DI之值。

### 2.字符串装入指令（LOAD String)

字符串装入指令的格式如下：

```assembly
LODSB	;装入字节（Byte)
LODSW	;装入字（Word)
```

字符串装入指令只是把字符串中的一个字符装入到累加器中。字节装入指令LODSB把寄存器SI所指向的一个字节数据装入到累加器AL中，然后根据方向标志DF复位或置位使SI之值增1或减1。它类似下面的两条指令：

```assembly
MOV		AL,[SI]
INC		SI	或	DEC		SI
```

字装入指令LODSW把寄存器SI所指向的一个字数据装入到累加器AX中，然后根据方向标志DF复位或置位使SI之值增2或减2。类似于如下的两条指令：

```assembly
MOV 	AX,[SI]
ADD 	SI,2	或	SUB 	SI,2
```

字符串装入指令的源操作是存储操作数，所以引用数据段寄存器DS。

字符串装入指令不影响标志。

下面的子程序使用了LODSB指令。此外，该子程序算法也较好，所以它的效率较高。请与3.6节中的例4作比较。

```assembly
;子程序名：STRLWR
;功能：把字符串中的大写字母转化为小写（字符串以0结尾）
;入口参数：DS:SI=字符串首地址的段值：偏移
;出口参数：无
STRLWR 	PROC
		PUSH 	SI
		CLD			;清方向标志（以便按增值方式调整指针）
		JMP 	SHORT STRLWR2
STRLWR1:
		SUB 	AL,'A'
		CMP 	AL,'Z'-'A'
		JA 		STRLWR2
		ADD 	AL,'a'
		MOV		[SI--1],AL		;注意指针已被调整
STRLWR2:
		LODSB					;取一字符，同时调整指针
		AND		AL,AL
		JNZ 	STRLWR1
		POP 	SI
		RET
STRLWR 	ENDP
```

在汇编语言中，两条字符串装入指令的格式可统一为如下一种格式：

```assembly
LODS 	OPRD
```

汇编程序根据操作数的类型决定使用字节装入指令还是字装入指令。也即，如果操作数的类型为字节，则采用LODSB指令；如果操作数的类型为字，则采用LODSW指令。请注意，操作数OPRD不影响指针寄存器SI之值，所以在使用上述格式的串装入指令时，仍必须先给SI赋合适的值。例如：

```assembly
....
MESS 	DB	'HELLO*.0
TAB 	DW	123,43,332,44,-1
....
MOV		SI,OFFSET MESS
LODS	MESS			;LODSB
....
MOV		SI,OFFSET TAB
LODS	TAB				;LODSW......
```

### 3.字符串存储指令（STOre String)

字符串存储指令的格式如下：

```assembly
STOSB		;存储字节
STOSW		;存储字
```

字符串存储指令只是把累加器的值存到字符串中，即替换字符串中一个字符。

字节存储指令STOSB把累加器AL的内容送到寄存器DI所指向的存储单元中，然后根据方向标志DF复位或置位使DI之值增1或减1。它类似下面的两条指令：

```assembly
MOV 	ES:[DI],AL
INC		DI		或		DEC		DI
```

字装入指令STOSW把累加器AX的内容送到寄存器DI所指向的存储单元中，然后根据方向标志DF复位或置位使DI之值增2或减2。类似于如下的两条指令：

```assembly
MOV 	ES:[DI],AX
ADD		DI,2		或	SUB		DI,2
```

字符串存储指令的源操作是累加器AL或AX,目的操作是存储操作数，所以引用当前附加段寄存器ES。

字符串存储指令不影响标志。

在汇编语言中，两条字符串存储指令的格式可统一为如下一种格式

```assembly
STOS OPRD
```

汇编程序根据操作数OPRD的类型决定使用字节存储指令还是字存储指令。操作数OPRD 不影响指针寄存器DI之值。

例如：如下程序片段把当前数据段中偏移1000H开始的100个字节的数据传送到从偏移2000H开始的单元中。

```assembly
	CLD						;清方向标志（以便按增值方式调整指针）
	PUSH 		DS			;由于在当前数据段中传送数据
	POP 		ES 			;所以使ES等于DS
	MOV			SI,1000H	;置源串指针初值
	MOV			DI,2000H	;置目的串指针初值
	MOV 		CX,100		;置循环次数
NEXT:
	LODSB					;取一字节数据
	STOSB					;存一字节数据
	LOOP 		NEXT		;循环CX次
```

如果方向标志已清，则清方向标志的指令可省；如果当前附加段和当前数据已是重叠的，则也就无需再给ES赋值。

### 4.字符串传送指令（MOVe String)

字符串传送指令的格式如下：

```
MOVSB字节传送MOVSW;字传送
```

字节传送指令MOVSB把寄存器SI所指向的一个字节数据传送到由寄存器DI所指向的存储单元中，然后根据方向标志DF复位或置位使SI和DI之值分别增1或减1。字传送指令MOVSW把寄存器SI所指向的一个字数据传送到由寄存器DI所指向的存储单元中，然后根据方向标志DF复位或置位使SI和DI之值分别增2或减2。注意，根据DS和SI计算源操作数地址，根据ES和DI计算目的操作数地址。

字符串传送指令不影响标志。

该指令的源操作数和目的操作均在存储器中，它与下面的字符串比较指令一起属于特殊情况。

在汇编语言中，两条字符串传送指令的格式可统一为如下一种格式：

```assembly
MOVS OPRD1,OPRD2
```

两个操作数的类型应该一致。汇编程序根据操作数的类型决定使用字节传送指令还是字传送指令。也即，如果操作数的类型为字节，则采用MOVSB指令；如果操作数的类型为字，则采用MOVSW指令。请注意，操作数OPRD1或OPRD2可起到方便阅读程序的作用，但不影响寄存器SI和DI之值，所以在使用上述格式的串传送指令时，仍必须先给SI和DI赋合适的值。

上面我们利用了字符串装入指令和字符串存储指令的结合实现数据块的移动，现在利用字符串传送指令实现数据块的移动。假设要求同上，程序片段如下，请作比较。

```assembly
		CLD				;清方向标志
		....			;其他指令同上
		MOV		CX,100	;置循环次数
NEXT:
		MOVSB			;每次传送一字节数据
		LOOP 	NEXT
```

现在循环体中只有一条串传送指令，执行速度可明显提高。在这个程序片段中，把100个字节的数据当作以字节为单元的字符串，所以利用了字节传送指令。如果把这10个字节的数据当作以字为单元的字符串，那么这个字符串也就只有50个单元了，于是循环次数可减少一半，执行速度还会提高。改写后的程序片段如下：

```assembly
		CLD			;清方向标志
		....		;其他指令同上
		MOV			CX,100/2	;置循环次数
NEXT:
		MOVSW		;每次传送一字节数据
		LOOP 	NEXT
```

### 5.字符串扫描指令（SCAn String)

字符串比较指令的格式如下：

```assembly
SCASB		;串字节扫描
SCASW		;串字扫描
```

串字节扫描指令SCASB把累加器AL的内容与由寄存器DI所指向一个字节数据采用相减方式比较，相减结果反映到各有关标志位（AF,CF,OF,PF,SF和ZF),但不影两个操作数，然后根据方向标志DF复位或置位使D1之值增1或减1。串字扫描指令SCASW把累加器AX的内容与由寄存器DI所指向的一个字数据比较，结果影响标志，然后DI之值增2或减2。

下面的程序片段判断AL中的字符是否为十六进制数符：

```assembly
....
STRING 		DB 	'0123456789ABCDEFabcdef'
STRINGL 	EQU $ - STRING
....					;把要判断的字符送AL
		CLD
		MOV 	DX,SEG STRING
		MOV 	ES,DX
		MOV 	CX,STRINGL
		MOV 	DI,OFFSET STRING
NEXT:
		SCASB
		LOOPNZ 	NEXT
		JNZ 	NOT_FOUND
FOUND:
		....
		....
NOT FOUND:
		....
```

在汇编语言中，两条字符串比较指令的格式可统一为如下一种格式：

```assembly
SCAS OPRD
```

汇编程序根据操作数的类型决定使用串字节扫描指令还是串字扫描指令。

### 6.字符串比较指令（CoMPare String)

字符串比较指令的格式如下：

```assembly
CMPSB	;串字节比较
CMPSW	;串字比较
```

串字节比较指令CMPS把寄存器SI所指向的一个字节数据与由寄存器DI所指向一个字节数据采用相减方式比较，相减结果反映到各有关标志位（AF,CF,OF,PF,SF和ZF),但不影响两个操作数，然后根据方向标志DF复位或置位使SI和DI之值分别增1或减1。串字比较指令CMPSW把寄存器SI所指向的一个字数据与由寄存器DI所指向的一个字数据比较，结果影响标志，然后按调整值2调整SI和DI之值。在汇编语言中，两条字符串比较指令的格式可统一为如下一种格式：

```assembly
CMPS OPRD1,OPRD2
```

两个操作数的类型应该一致。汇编程序根据操作数的类型决定使用串字节比较指令还是串字比较指令。请注意，OPRD1或OPRD2不影响寄存器SI和DI之值和段寄存器DS和ES之值。

## 6.1.2重复前缀

由于串操作指令每次只能对字符串中的一个字符进行处理，所以使用了一个循环，以便完成对整个字符串的处理。为了进一步提高效率，8086/8088还提供了重复指令前缀。重复前缀可加在串操作指令之前，达到重复执行其后的串操作指令的目的。

### 1.量复前级REP

REP用作为一个串操作指令的前缀，它重复其后的串操作指令动作。每一次重复都先判断CX是否为0,如为0就结束重复，否则CX的值减1,重复其后的串操作指令。所以当CX值为0时，就不执行其后的字符串操作指令。

它类似于LOOP指令，但LOOP指令是先把CX的值减1,后再判是否为0。

注意，在重复过程中的CX减1操作，不影响各标志。

重复前级REP主要用在串传送指令MOVS和串存储指令STOS之前。值得指出的是，一般不在LODSB或LODSW指令之前使用任何重复前缀。

使用重复前缀REP,可进一步改写前面的移动数据块的程序片段如下，请作比较：

```assembly
CLD		;如果已清方向标志，则这条指令可省
....	;其他指令同上
MOV 	CX,50
REP 	MOVSW		;重复执行（CX)次
```

在下面的子程序中，重复前级REP与串存储操作指令配合，实现用指定的字符填充指定的缓冲区

```assembly
;子程序名：FILLB
;功能：用指定字符充填指定缓冲区
;入口参数：ES:DI=缓冲区首地址
;		CX=缓冲区长度，AL=充填字符
;出口参数：无
FILLB 	PROC
		PUSH 	AX
		PUSH 	DI
		JCXZ 	FILLB_1		;CX值为0时直接跳过（可省）
		CLD
		SHR 	CX,1		;字节数转成字数
		MOV 	AH,AL		;使AH与AL相同
		REP 	STOSW		;按字充填
		JNC 	FILLB_1		;如果缓冲区长度为偶数，则转
		STOSB				;补缓冲区长度为奇数时的一字节
FILLB_1:
		POP		DI
		POP		AX
		RET
FILLB 	ENDP
```

在上面的子程序中，先按字充填缓冲区，然后再处理可能出现的“零头”，这与重复CX次字节充填相比，可获得更高的效率。注意，字符串存储指令STOSW不影响标志。

### 2.重复前缀REPZ/REPE

REPZ与REPE是一个前缀的两个助记符，下面的介绍以REPZ为代表。

REPZ用作为一个申操作指令的前缀，它重复其后的串操作指令动作。每重复一次，CX的值减1,重复一直进行到CX为0或串操作指令使零标志ZF为0时止。重复结束条件的检查是在重复开始之前进行的。

注意，在重复过程中的CX值减1操作，不影响标志。

重复前缀REPZ主要用在字符串比较指令CMPS和字符串扫描指令SCAS之前。于串传送指令MOVS和串存储指令STOS都不影响标志，所以在这些串操作指令前使用前缀REP和前级REPZ的效果一样。

在下面的子程序中，重复前缴REPZ与串比较指令CMPSB配合，实现两个字符串的比较。重复前级REPZ与CMPSB的配合表示当相同时继续比较。

```assembly
;子程序名：STRCMP
;功能：比较字符串是否相同
;入口参数：DS:SI=字符串1首地址的段值：偏移
;		ES:DI=字符串2首地址的段值：偏移
;出口参数：AX=0表示两字符串相同，否则表示字符串不同
;说明：设字符串均以0为结束标志
STRCMP 	PROC
		CLD
		PUSH	DI
		XOR		AL,AL		;先测一个字符串的长度
		MOV 	CX,OFFFFH
NEXT:
		SCASB
		JNZ 	NEXT
		NOT 	CX			;至此CX含字符串2的长度（包括结束标志）
		POP		DI
		REPZ	CMPSB		;两个串比较（包括结束标志在内）
		MOV 	AL,[SI-1]
		MOV 	BL,ES:[DI-1]
		XOR 	AH,AH		;如两个字符串相同，则AL应等于BL
		MOV 	BH,AH
		SUB 	AX,BX
		RET
STRCMP 	ENDP
```

在上面子程序中最后使用的比较方法稍繁，但CF反映了两个字符串的字典序。

### 3.重复前级REPNZ/REPNE

REPNZ与REPNE是一个前缀的两个助记符，下面的介绍以REPNZ为代表。

REPNZ用作为一个串操作指令的前缀。与REPZ类似，所不同的是重复一直进行到CX为0或串操作指令使零标志ZF为1时止。

重复前缀REPNZ主要用在字符串扫描指令SCAS之前。重复前缀REPNZ与SCASB指令配合，表示当不等时继续扫描，一直搜索到字符串结束。如果搜索到，则ZF标志为1,CX之值可能为0;如果没有搜索到，则ZF标志为0,CX之值一定为0。

上述的判断AL中的字符是否为十六进制数符的程序片段改写如下：

```assembly
....		;同上
NEXT:
		REPNZ 	SCASB
		JNZ 	NOT_FOUND
....		;同上
NOT_FOUND:
....
```

下面的子程序测字符串的长度，设字符串以0结尾。它巧妙地利用了重复前缀REPNZ和字符串扫描指令SCASB

```assembly
;子程序名：STRLEN
;功能：测字符串长度
;入口参数：ES:DI=字符串首地址的段值：偏移
;出口参数：AX=字符串长度
;说明：字符串以0结尾；字符串长度不包括结尾标志
STRLEN 	PROC
		PUSH 	CX
		PUSH 	DI
		CLD
		XOR		AL,AL		;使AL含结束标志值
		MOV 	CX,OFFFFH	;取字符串长度极值
		REPNZ 	SCASB		;搜索结束标志0
		MOV 	AX,CX
		NOT		AX			;得字符串包括结束标志在内的长度
		DEC 	AX			;减结束标志1字节
		POP		DI
		POP		CX
		RET
STRLEN 	ENDP
```

如果重复前缀REPZ与SCASB相配合，则表示当相等时继续搜索，直到第一个不等时为止（当然CX之值决定了最终搜索的次数）。

### 4.说明

重复的字符串处理操作过程可被中断。CPU在处理字符串的下一个字符之前识别中断。如果发生中断，那么在中断处理返回以后，重复过程再从中断点继续执行下去。但应注意，如指令前还有其他前缀（段超越前级或锁定前缀）的话，中断返回时其他的前缀就不再有效。因为CPU在中断时，只能“记住”一个前缀，即字符串操作指令前的重复前缀。如字符串操作指令必须使用一个以上的前缀，则可在此之前禁止中断。

## 6.1.3字符串操作举例

下面再举几例来说明字符串操作指令和重复前缀的使用，同时说明如何进行字符串操作。

#### 例1:写一个判别字符是否在字符串中出现的子程序。设字符串以0结尾。

串扫描指令可用于在字符串中搜索指定的字符，从而判别字符是否属于字符串。下面的子程序并没有利用串扫描指令，代码虽长，自有其独到之处，请注意。

```assembly
;子程序名：STRCHR
;功能：判字符是否属于字符串
;人口参数：DS:SI=搜索字符串首地址的段值，偏移
;		AL=字符代码
;出口参数：CF=0表示字符在字符串中，AX=字符首次出现处的偏移,
;		CF=1表示字符不在字符串中
STRCHR 	PROC
		PUSH 	BX
		PUSH 	SI
		CLD		
		MOV 	BL,AL		;字符复制到BL寄存器
		TEST 	SI,1		;判地址是否为偶
		JZ 		STRCHR1		;是，转
		LODSB				;取第一个字符，比较之
		CMP 	AL,BL
		JZ 		STRCHR3
		AND 	AL,AL
		JZ 		STRCHR2
STRCHR1:
		LODSW				;取一个字
		CMP		AL,BL		;比较低字节
		JZ 		STRCHR4
		AND 	AL,AL
		JZ 		STRCHR2
		CMP 	AH,BL		;比较高字节
		JZ 		STRCHR3
		AND 	AH,AH
		JNZ 	STRCHR1
STRCHR2:
		STC
		JMP 	SHORT STRCHR5
STRCHR3:
		INC 	SI
STRCHR4:
		LEA 	AX,[SI-2]
STRCHR5:
		POP SI
		POP BX
		RET
STRCHR 	ENDP
```

上面的子程序对从奇地址开始存放的字符串的第一个字符作了特别处理。在随后的循环处理中，字符串便总从偶地址开始，每次取一个字，即两个字符，再逐个字符比较。为什么要从偶地址开始取一个字？较好的理由留给读者思考。

#### 例2:写一个在字符串1后追加字符串2的子程序。

设字符串均以0结尾。该子程序的实现流程如图6.1所示。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/root/image.5pcasvj0dmo0.webp)

现再作几点说明：

1. 要传送的字符串2包括其结束标志；
2. 字符串2的传送以字传送为主，考虑了从偶地址开始进行字的传送；
3. 最后处理可能遗留的一字节

```assembly
;子程序名：STRCAT
;功能：在字符串1末追加字符串2
;入口参数：DS:SI=字符串1起始地址的段值：偏移
;		 DS:DI=字符串2起始地址的段值，偏移
;出口参数：无
;说明：不考虑在字符串1后是否留有足够的空间
STRCAT 	PROC
		PUSH 	ES
		PUSH 	AX
		PUSH 	CX
		PUSH 	SI
		PUSH 	DI
		CLD
		PUSH 	DS
		POP		ES				;使ES同DS
		PUSH 	DI
		MOV 	DI,SI
		XOR 	AL,AL
		MOV 	CX,OFFFFH
		REPNZ 	SCASB			;确定字符串1的尾
		LEA 	SI,[DI-1]		;SI指向字符串1的结束标志
		POP 	DI
		MOV 	CX,OFFFFH
		REPNZ 	SCASB			;测字符串2的长度
		NOT 	CX				;CX为字符串2包括结束标志的长度
		SUB		DI,CX			;DI再次指向字符串2的首
		XCHG 	SI,DI			;为拼接作准备
		TEST 	SI,1			;字符串2是否从奇地址开始？
		JZ 		STRCAT1
		MOVSB					;特别处理第一字节
		DEC 	CX
STRCAT1:
		SHR		CX,1			;移动数据块长度除2
		REPZ 	MOVSW			;字移动
		JNC 	STRCAT2
		MOVSB					;补字移动时遗留的一字节
STRCAT2:
		POP		DI
		POP 	SI
		POP 	CX
		POP 	AX
		POP 	ES
		RET
STRCAT 	ENDP
```

#### 例3:写一个程序，它先接收一个字符串，然后抽去其中的空格，最后按相反的顺序显示它。

程序如下所示，请注意删除空格的方法和方向标志的变化。

```assembly
;程序名：T6-1.ASM
;功能：接收一个字符串，去掉其中的空格后按相反的顺序显示它符号
;常量的定义
MAXLEN	=	64	;字符串最大长度
SPACE 	= 	''	;空格
CR 		= 	ODH	;回车符
LF 		= 	OAH	;换行符
;数据段的定义
DSEG 	SEGMENT
BUFFER 	DB 	MAXLEN+1,0,MAXLEN+1 DUP (0)
STRING 	DB 	MAXLEN+3 DUP (0)
DSEG 	ENDS
;代码
CSEG 	SEGMENT
		ASSUME CS:CSEG,DS:DSEG,ES:DSEG
START: 
		MOV 	AX,DSEG
		MOV 	DS,AX
		MOV 	ES,AX
		MOV 	DX,OFFSET BUFFER
		MOV 	AH,10
		INT 	21H				;接收一个字符串
		XOR 	CH,CH
		MOV 	CL,BUFFER+1
		JCXZ	OK				;字符串长度为0.则结束
		CLD		MOV SI,OFFSET BUFFER +2
		MOV 	DI,OFFSET STRING
		XOR 	AL,AL
		STOSB					;先存入结束符
		MOV		AL,SPACE
PP1:
		XCHG	SI,DI
		REPZ 	SCASB			;去掉空格
		XCHG 	SI,DI
		JCXZ	PP3				;如已到尾，转
		DEC 	SI				;恢复被REPZ SCASB指令扫过的字符
		INC 	CX				;及计数
PP2:
		CMP		BYTEPTR[SI],SPACE	;欲传字符为空格？
		JZPP1					;是，去掉空格
		MOVSB					;传一字符
		LOOP	PP2				;下一个
PP3: 
		MOV 	AL,CR
		STOSB					;存入回车符
		MOV 	AL,LF
		MOV		[DI],AL			;存入换行符
		STD						;置方向标志
		MOV		SI,DI
PP4:
		LODSB					;按相反顺序取字符
		OR		AL,AL
		JZ 		OK				;结束，转
		MOV 	DL,AL
		MOV		AH,2
		INT		21H				;显示字符
		JMP		PP4				;
		;		
OK:		
		MOV		AH,4CH
		INT		21H
CSEG 	ENDS
		END		START
```

#### 例4:写一个判字符串2是否为字符串1子串的子程序。

具体要求如下：

1. 子程序是一个远过程；
2. 指向字符串的指针是远指针（即包括段值）;
3. 通过堆栈传递两个分别指向字符串1和字符串2的远指针；
4. 由DX:AX返回指向字符串2在字符串1中首次出现处的指针，如字符串2不是字符串1的子串，则返回空指针；
5. 字符串均以0为结束符。

第3章中的程序T3-16.ASM完成类似的工作，现在利用串操作指令来实现它，图6.2是实现的流程图，请比较。图6.3是调用进入该子程序后的堆栈。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/root/image.q8vko9s1qkw.webp)

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/root/image.6sf92vbilhk0.webp)

```assembly
;子程序名：STRSTR
;功能：判字符串2是否为字符串1的子串
;入口参数：指向字符串的远指针（见调用方法）
;出口参数：DX:AX返回指向字符串2在字符串1中首次出现处的指针
;说明：调用方法如下：
;(1)压入字符串2的远指针
;(2)压入字符串1的远指针
;(3)CALL FAR PTR STRSTR
STRSTR 	PROC 	FAR
		PUSH 	BP				;此前的堆找如图6.3(a)所示
		MOV 	BP,SP
		PUSH 	DS
		PUSH 	ES
		PUSH 	BX				;保护有关寄存器
		PUSH 	CX
		PUSH 	SI
		PUSH	DI				;此时的堆栈如图6.3(b)所示
		LES 	BX,[BP+10]		;取STR2指针
		CMP 	BYTE PTR ES:[BX],0	;判STR2是否为空串
		JNZ 	STRSTR1			;否
		MOV 	DX,[BP+8]+STR2	;为空串时
		MOV 	AX,[BP+6]		;返回STR1指针
		JMP 	SHORT STRSTR6
STRSTR1:
		CLD
		LES 	D1,[BP+6]		;取STR1指针
		PUSH 	ES
		MOV 	BX,DI			;STR1偏移送BX寄存器
		XOR 	AX,AX
		MOV 	CX,OFFFFH
		REPNZ 	SCASB			;测STR1长度
		NOT 	CX
		MOV 	DX,CX			;使DX含STR1长度（含结束标志）
		LES 	DI,[BP+10]		;取STR2指针
		PUSH 	ES
		MOV 	BP,DI			;STR2偏移送BP寄存器
		XOR 	AX,AX
		MOV 	CX,OFFFFH
		REPNZ 	SCASB			;测STR2长度
		NOT 	CX
		DEC 	CX				;CX为STR2长度
		POP 	DS				;此时，DS:BP指向STR2
		POP 	ES				;ES:BX指向STR1
STRSTR2: 
		MOV 	SI,BP			;DS:SI指向STR2
		LODSB					;取STR2的第一个字符
		MOV 	DI,BX
		XCHG 	CX,DX			;使CX含STR1长度，DX含STR2长度
		REPNZ 	SCASB			;在STR1中搜索STR2的第一个字符
		MOV 	BX,DI
		JNZ 	STRSTR3			;找到？
		CMP 	CX,DX			;找到，STR1剩下的字符数比STR2长？
		JNB 	STRSTR4			;是转
STRSTR3: 
		XOR 	BX,BX			;找不到处理
		MOV 	ES,BX
		MOV 	BX,1
		JMP 	SHORT STRSTR5
STRSTR4:
		XCHG	CX,DX			;使CX含STR2长度，DX含STR1长度
		MOV 	AX,CX
		DEC 	CX
		REPZ 	CMPSB			;判STR1中是否有STR2后的其它字符
		MOV 	CX,AX
		JNZ 	STRSTR2			;没有，转继续找
		;
STRSTR5: 
		MOV 	AX, BX			;找到！
		DEC 	AX				;准备返回值
		MOV 	DX,ES			
		;
STRSTR6:
		POP		DI
		POP 	SI
		POP 	CX
		POP		BX				;恢复有关寄存器
		POP		ES
		POP 	DS
		POP 	BP
		RET						;RETF
STRSTR 	ENDP
```

# 6.2 十进制数算术运算调整指令及应用

在2.4节中介绍的算术运算指令都是对二进制数进行操作，为了方便地进行十进制数的算术运算，8086/8088提供了各种调整指令。本节介绍这些调整指令和举例说明它们的应用。

8086/8088的十进制数算术运算调整指令所认可的十进制数是以8421BCD码（见表1.1)表示的，它分为未组合（或非压缩）的和组合（或压缩）的两种。组合的BCD码是指一字节含两位BCD码；未组合的BCD码是指一字节含一位BCD码，字节的高四位无意义。

数字的ASCII码是一种非组合的BCD码。因为数字的ASCII码的低四位是对应的8421BCD码。

## 6.2.1组合BCD码的算术运算调整指令

### 1.组合的BCD码加法调整指令DAA(Decimal Adjust for Addition)

组合的BCD码加法调整指令的格式如下：

```assembly
DAA
```

这条指令对在AL中的和（由两个组合的BCD码相加后的结果）进行调整，产生一个组合的BCD码。调整方法如下：

1. 如AL中的低4位在A~F之间，或AF为1,则AL+(AL)+6,且AF位置1;
2. 如AL中的高4位在A~F之间，或CF为1,则AL+(AL)+60H,且CF位置1。

该指令影响标志AF,CF,PF,SF和ZF,但不影响标志OF。

下面是为了说明该指令而写的一个程序片段，每条指令执行后的结果作为注释给出。第一条指令使AL含表示两位十进制数34的组合BCD码；第二条指令进行加操作，因ADD是二进制数相加，所以结果为7BH,但作为十进制数34加47的结果应为81。第三条指令进行调整，得正确结果81。第五条指令又把由第四条指令相加的结果进行调整，得结果68(百位进入CF)。第七条指令把由第六条指令相加的结果进行调整，得结果48(百位进入CF)。

```assembly
MOV		AL,34H
ADD		AL,47H		;AL=7BH,AF=0,CF=0
DAA					;AL=81H,AF=1,CF=0
ADC		AL,87H		;AL=08H,AF=0,CF=1
DAA					;AL=68H,AF=0,CF=1
ADC		AL,79H		;AL=E2H,AF=1,CF=0
DAA					;AL=48H,AF=1,CF=1
```

### 2.组合的BCD码减法调整指令DAS(Decimal Adjust for Subtraction)

组合的BCD码减法调整指令的格式如下：

```assembly
DAS
```

这条指令对在AL中的差（由两个组合的BCD码相减后的结果）进行调整，产生一个组合的BCD码。调整方法如下：

1. 如AL中的低4位在A~F之间，或AF为1,则AL-(AL)-6,且AF位置1;
2. 如AL中的高4位在A~F之间，或CF为1,则AL-(AL)-60H,且CF位置1。

该指令影响标志AF,CF,PF,SF和ZF,但不影响标志OF。

下面是为了说明该指令而写的一个程序片段，每条指令执行后的结果作为注释给出第一条指令使AI.含表示两位十进制数35的组合BCD码；第二条指令进行减操作，因SUB是二进制数相减，所以结果为1EH,但作为十进制数45减27的结果应为18。第三条指令进行调整，得正确结果18。第五条指令又把由第四条指令相减的结果进行调整，得结果69(百位上的借位在CF中）。

```assembly
MOV		AL,45H
SUB		AL,27H		;AL=1EH,AF=1,CF=0
DAS					;AL=18H,AF=1,CF=0
SBB		AL,49H		;AL=CFH,AF=1,CF=1
DAS					;AL=69H,AF=1,CF=1
```

## 6.2.2未组合BCD码的算术运算调整指令

### 1.未组合的BCD码加法调整指令AAA(ASCII Adjust for Addition)

未组合的BCD码加法调整指令的格式如下：

```assembly
AAA
```

这条指令对在AL中的和（由两个未组合的BCD码相加后的结果）进行调整，产生一个未组合的BCD码。调整方法如下：

1. 如AL中的低4位在0~9之间，且AF为0,则转（3);
2. 如AL中的低4位在A~F之间，或AF为1,则AL-(AL)+6,AH-(AH)+1,且AF位置1;
3. 清除AL的高4位；
4. AF位的值送CF位。

该指令影响标志AF和CF,对其他标志均无定义。

下面是为了说明该指令而写的一个程序片段，每条指令执行后的结果作为注释给出，请注意比较：

```assembly
MOV		AX,7
ADD		AL,6		;AL=ODH,AH=00H,AF=0,CF=0
AAA					;AL=03H,AH=01H,AF=1,CF=1
ADC		AL,5		;AL=09H,AH=01H,AF=0,CF=0
AAA					;AL=09H,AH=01H,AF=0.CF=0
ADD		AL,39H		;AL=42H,AH=01H,AF=1,CF=0
AAA					;AL=08H,AH=02H,AF=1,CF-1
```

### 2.未组合的BCD码减法调整指令AAS(ASCII Adjust for Subtraction)

未组合的BCD码减法调整指令的格式如下：

```assembly
DAS
```

这条指令对在AL中的差（由两个未组合的BCD码相减后的结果）进行调整，产生一个未组合的BCD码。调整方法如下：

1. 如AL中的低4位在0~9之间，且AF为0,则转（3);
2. 如AI.中的低4位在A~F之间，或AF为1,则AL-(AL)-6,AH+-(AH)-1,且AF位置1;
3. 清除AL的高4位；
4. AF位的值送CF位。

该指令影响标志AF和CF,对其他标志均无定义。

下面是为了说明该指令而写的一个程序片段，每条指令执行后的结果作为注释给出，请注意比较：

```assembly
MOV		AL,34H
SUB		AL,09H		;AL=2BH,AF=1,CF=0
AAS					;AL=05H,AF=1,CF=1
```

### 3.未组合的BCD码乘法调整指令AAM(ASCII Adjust for Multiplication)

未组合的BCD码乘法调整指令的格式如下：

```assembly
AAM
```

这条指令对在AL中的积（由两个组合的BCD码相乘的结果）进行调整，产生两个未组合的BCD码。调整方法如下：

把AL中的值除以10,商放在AH中，余数放在AL中。

该指令影响标志SF,ZF和PF,对其他标志无影响。

下面是为了说明该指令而写的一个程序片段，每条指令执行后的结果作为注释给出，请注意比较：

```assembly
MOV	AL,03H
MOV BL,04H
MUL BL		;AL=OCH,AH=00H
AAM			;AL=02H,AH=01H
```

### 4.未组合的BCD码除法调整指令AAD(ASCII Adjust for Division)

未组合的BCD码除法调整指令的格式如下：

```assembly
AAD
```

该指令和其他调整指令的使用次序上不同，其他调整指令均安排在有关算术运算指令后，而这条指令应安排在除运算指令之前。它的功能是：把存放在寄存器AH(高位十进制数）及存放在寄存器AL中的两位非组合BCD码，调整为一个二进制数，存放在寄存器AL中。调整的方法如下：

```assembly
AL	<-	AH*10+(AL)
AH	<-	-0
```

由于采用上述调整方法，存放在AL和AH中的非组合BCD的高四位应为0。

该指令影响标志SF,ZF和PF,对其他标志无影响。

下面是为了说明该指令而写的一个程序片段，每条指令执行后的结果作为注释给出，请注意比较：

```assembly
MOV 	AH,04H
MOV 	AL,03H
MOV 	BL,08H
AAD				;AL=2BH,AH=00H
DIV 	BL		;AL=05H,AH=03H
```

## 6.2.3应用举例

#### 例1:设在缓冲区DATA中存放着12个组合的BCD码，求它们的和，把结果存放到缓冲区SUM中。

有关的程序片段如下：

```assembly
....
NUMI 	DB 	23H,45H,67H,89H,32H,93H,36H,12H,66H,78H,43H,99H
RESULT 	DB 	2 DUP (0)
....
		MOV 	AX,SEG NUM1
		MOV 	DS,AX
		MOV 	BX,OFFSET DATA
		MOV 	CX,10			;准备循环
		XOR 	AL,AL
		XOR 	AH,AH
NEXT:
		ADD		AL,[BX]			;加DAA调整
		ADC		AH,0			;考虑进位
		XCHG 	AH,AL
		DAA						;调整
		XCHG 	AH,AL
		INC		BX				;修改指针
		LOOP 	NEXT			;下一个
		XCHG 	AH,AL			;准备高位低地址存放
		MOV		WORD PTR RESULT,AX
....
```

#### 例2:利用DAA指令改写把一位十六进制数转换为对应的ASCII码符的子程序HTOASC

下面的子程序巧妙地利用了加法调整指令DAA,使得在子程序中没有条件转移指令。

```assembly
;子程序名：HTOASC
;功能：把一位十六进制数转换为对应的ASCII码
;入口参数，AL的低4位为要转换的十六进制数
;出口参数：AL含对应的ASCII码
HTOASC 	PROC
		AND 	AL,OFH
		ADD		AL,90H
		DAA		
		ADC		AL,40H
		DAA
		RET
HTOASC 	ENDP
```

请读者仔细考虑上述子程序。选几个十六进制数试试。

#### 例3:写一个能实现两个十进制数的加法运算处理的程序。设每个十进制数最多10位。

如果不采用十进制数算术运算调整指令，那么在接收了以ASCII码串表示的十进制数后，要把它转换为二进制数。在对二进制数进行运算后，还要把结果转换为十进制数的ASCII码。当要处理的十进制数位数较多时，这种转换较麻烦。现采用十进制数算术运算调整指令完成它。

该程序分为如下四步：

1. 接收按十进制表示的被加数，并作适当的处理；
2. 接收按十进制表示的加数，也作适当的处理；
3. 进行加法处理；
4. 显示结果。为此，设计三个子程序。它们分别是：子程序GETNUM接收按十进制数表示的数串并作适当的处理；子程序ADDITION进行加法处理；子程序DISPNUM显示结果。

在子程序ADDITION中，使用非组合BCD码加调整指令AAA,所以十位的被加数和加数均保持非组合BCD码串形式，产生的11位和也是非组合BCD码串。子程序GETNUM通过DOS的0AH号系统功能调用，接收一个字符串，然后检查用户输入是否确实输入了一个十进制数，最后形成一个十位的非组合BCD码串（不足用0补足）。子程序DISPNUM比较容易，先跳过结果中可能存在的前导的0,然后把非组合的BCD码转换为ASCII码后显示之。

```assembly
;程序名：T6-2.ASM
;功能：完成两个由用户输入的10位十进制数的加法运算
;常数定义
MAXLEN 	= 	10			;最多位数
BUFFLEN = 	MAXLEN+1	;缓冲区长度
;数据段
DSEG 	SEGMENT
BUFF1 	DB 		BUFFLEN,O,BUFFLEN DUP (?)	;存放被加数
NUM1 	EQU 	BUFF1 + 2
BUFF2	DB		BUFFLEN,0,BUFFLEN DUP (?)	;存放加数
NUM2 	EQU 	BUFF2+2
RESULT 	DB 		BUFFLEN DUP (?),24H			;存放和
DIGITL 	DB 		'0123456789'				;有效的十进制数字符
DIGLEN 	EQU 	$ - DIGITL
MESS	DB 		'Invalid number!',0DH,0AH,24H
DSEG 	ENDS
;代码段
CSEG 	SEGMENT
		ASSUME CS:CSEG,DS:CSEG,ES:CSEG
START:
		MOV 	AX,DSEG
		MOV 	DS,AX				;置DS和ES
		MOV 	ES,AX
		MOV 	DX,OFFSET BUFF1
		CALL 	GETNUM				;(1)接收被加数
		JC 		OVER				;不合法时，处理
		MOV 	DX,OFFSET BUFF2
		CALL 	GETNUM				;(2)接收加数
		JC 		OVER				;不合法时，处理
		MOV 	SI,OFFSET NUM1
		MOV 	DI,OFFSET NUM2
		MOV 	BX,OFFSET RESULT
		MOV 	CX,MAXLEN
		CALL 	ADDITION			;(3)加运算
		MOV 	DX,OFFSET RESULT
		CALL 	DISPNUM				;(4)显示结果
		JMP 	SHORT OK
OVER:
		MOV		DX,OFFSET MESS		;出错处理
		MOV 	AH,9
		INT		21H
OK:
		MOV 	AH,4CH
		INT		21H
		;
;子程序名：GETNUM
;功能：接收一个十进制数字串，且扩展成10位
;入口参数：DX=缓冲区偏移
;出口参数：CF=0,表示成功
;		CF=1,表示不成功
GETNUM 	PROC
		MOV		AH,10				;接收一个字符串
		INT		21H
		CALL 	NEWLINE				;产生回车和换行
		CALL 	ISDNUM				;判是否为十进制数字串
		JC 		GETNUM2				;不是，转
		MOV 	SI,DX
		INC		SI
		MOV 	CL,[SI]				;取输入的数字串长度
		XOR 	CH,CH
		MOV 	AX,MAXLEN
		STD		
		MOV 	DI,SI
		ADD		DI,AX
		ADD 	SI,CX
		SUB		AX,CX
		REP 	MOVSB				;数字串向高地址移让出低地址
		MOV 	CX,AX
		JCX		ZGETNUM1
		XOR 	AL,AL				;低地址的高位用0补足
		REP 	STOSB
GETNUM1:
		CLD
		CLC
GETNUM2:
		RET
GETNUM 	ENDP
;
;子程序名：ADDITION
;功能：多位非组合BCD码数加
;入口参数：SI=代表被加数的非组合BCD码串开始地址偏移
;		DI=代表加数的非组合BCD码串开始地址偏移
;		CX=BCD码串长度（字节数）
;		BX=存放结果的缓冲区开始地址偏移
;出门参数：结果缓冲区含结果
;说明：在非组合的BCD码中，十进制数的高位在低地址
ADDITION	PROC
			STD						;准备从在高地址的低位开始处理
			ADD		BX,CX			;BX指向结果缓冲区最后一字节
			ADD 	SI,CX
			ADD		DI,CX
			DEC 	SI				;S1指向被加数串最后一字节
			DEC		DI				;DI指向加数串最后一字节
			XCHG 	DI,BX			;BX指向加数串，DI指向结果串
			INC 	BX				;循环前的指针预调整
			CLC
ADDP1:
			DEC		BX
			LODSB					;取一字节被加数
			ADC 	AL,[BX]			;加上加数（带上低位的进位）
			AAA						;调整
			STOSB					;保存结果
			LOOP 	ADDP1			;循环处理下一个
			MOV		AL,0
			ADC		AL,0			;考虑最后一次进位
			STOSB					;保存之
			CLD
			RET
ADDITION 	ENDP
;
;子程序名：DISPNUM
;功能：显示结果
;入口参数：DX=结果缓冲区开始地址偏移
;出口参数：无
DISPNUM 	PROC
			MOV		DI,DX
			MOV 	AL,0
			MOV 	CX,MAXLEN
			REPZ 	SCASB			;跳过前导的0
			DEC 	DI
			MOV 	DX,DI
			MOV 	SI,DI
			INC 	CX
DISPNU2:
			LODSB					;把非组合BCD码串转换成ASCII码串
			ADD 	AL,30H
			STOSB
			LOOP 	DISPNU2
			MOV 	AH,9			;显示结果
			INT		21H
			RET
DISPNUM 	ENDP
;
;子程序名：ISDNUM
;功能：判一个利用DOS的0AH号功能调用输入的字符串是否为数字符串
;人口参数：DX=缓冲区开始地址偏移
;出口参数：CF=0,表示是；CF=1,表示否
ISDNUM 		PROC
			MOV 	SI,DX
			LODSB
			LODSB					;AL=字符串长度
			MOV 	CL,AL
			XOR 	CH,CH
			JCXZ 	ISDNUM2			;空串认为非数字串
ISDNUM1:
			LODSB					;取一个字符
			CALL 	ISDECM			;判该字符是否为数字符
			JNZ 	ISDNUM2			;不是，转
			LOOP 	ISDNUM1			;是，下一个
			RET
ISDNUM2:
			STC
			RET
ISDNUM 		ENDP
;
;子程序名：ISDECM
;功能：判一个字符是否为十进制数字符
;入口参数：AL=字符
;出口参数：ZF=1,表示是,ZF=0.表示否
ISDECM 		PROC
			PUSH 	CX
			MOV 	DI,OFFSET DIGITL
			MOV 	CX,DIGLEN
			REPNZ 	SCASB
			POP 	CX
			RET
ISDECM 		ENDP
;
;子程序说明信息略
NEWLINE		PROC
;该子程序的代码同T4-3.ASM中同名子程序
NEWLINE		ENDP
CSEG 		ENDS
			END 	START
```

# 6.3 DOS程序段前缀和特殊情况处理

程序本节把几个特殊情况处理程序作为简单应用程序的实例作介绍，其中也说明了软中断处理程序的设计方法。

## 6.3.1 DOS程序段前缀PSP

### 1.程序段前绿PSP

程序段前缀（Program Segment Prefix)是DOS加载一个外部命令或应用程序（EXE或COM类型）时，在程序段之前设置的一个具有256字节的信息区。PSP含有许多可用信息，其中常用信息的安排如表6.1所示。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/root/image.51j2lvovjx00.webp)

当DOS把控制权转给外部命令或应用程序之时，数据段寄存器DS和附加段寄存器ES均指向其PSP,即均含有PSP的段值，并不指向程序的数据段和附加段。这样应用程序可方便地使用到PSP中的有关信息。

### 2.终止程序的另一途径

利用DOS的4CH号系统功能调用能终止程序，把控制权转交回DOS,这是我们现在常用的方法。但早先常利用DOS提供的20H号中断处理程序来终止程序。

通过20H号中断处理程序终止程序有一个条件，即进入20H号中断处理程序之前，代码段寄存器CS必须含有PSP的段值。由于对EXE类型的应用程序而言，其代码段与PSP不是同一个段，所以不能简单地直接利用指令“INT 20H”来终止程序。DOS注意到了这一点，在PSP的偏移0处，安排了一条“INT20H”指令（其机器码为CD20)。于是，应用程序只要设法转到PSP的偏移0处，就能实现程序的终止。下面是一个例子，它是程序T3-1.ASM的改写。

```assembly
;程序名：T6-3.ASM
;功能：显示信息“HELLO”
SSEG 	SEGMENT PARA STACK	;堆栈段
		DW 256 DUP(?)
SSEG 	ENDS
DSEG 	SEGMENT		;数据段
MESS 	DB 'HELLO',0DH,0AH,'$'
DSEG 	ENDS
CSEG 	SEGMENT		;代码段
		ASSUME CS:CSEG,DS:DSEG
MAIN 	PROC 	FAR
START:	
		PUSH	DS				;把PSP的段值压入堆栈
		XOR 	AX,AX
		PUSH 	AX				;把0000H(偏移）压入堆栈
		MOV 	AX,DSEG
		MOV 	DS,AX
		MOV 	DX,OFFSET MESS
		MOV 	AH,9
		INT		21H
		RET						;转PSP的偏移0处执行
MAIN 	ENDP
CSEG 	ENDS
		END 	START
```

在标号START开始处的三条指令把PSP的段值和值0压入堆栈，最后的返回指令RET从堆栈弹出程序开始时压入堆栈的PSP段值和偏移0到CS和IP,随后CPU就执行位于PSP首的指令“INT20H”，此时的CS含PSP段值，程序终止，控制转回DOS。细心的读者也许发现，T6-3.ASM与T6-1.ASM相比，还多出一对过程定义伪指令语句，即定义了一个名为MAIN的远过程，这样做的目的是告诉汇编程序，把为了终止程序返回DOS而设的RET指令汇编成远返回指令。如果利用TASM汇编程序，则完全可不定义过程MAIN,而使用远返回指令的助记符RETF。但我们不鼓励采用上述方法终止程序，而是推荐通过DOS系统功能调用4CH终止程序。

### 3.应用程序取得命令行参数

DOS加载一个外部命令或应用程序时，允许在被加载的程序名之后，输入多达127个字符（包括最后的回车符）的参数，并把这些参数送到PSP的非格式化参数区，即PSP中从偏移80H开始的区域。

应用程序可从PSP中获得命令行参数。PSP的偏移80H处含命令行参数的长度（字节数）,从PSP的偏移81H开始存放命令行参数。命令行参数通常以空格符引导，至回车符（ODH)结束。注意，命令行中的重定向符和管道符及有关信息不作为命令行参数送到PSP。

#### 例1:写一个显示命令行参数的程序。

先从PSP中把命令行参数传到程序定义的缓冲区中，然后再显示。源程序如下所示，数据段和代码段相重。

```assembly
;程序名：T6-4,ASM
;功能：显示命令行参数
CSEG 	SEGMENT
		ASSUME CS:CSEG,DS:CSEG
BUFFER 	DB 128 DUP (?)			;用于存放命令行参数的缓冲区
START:
		CLD
		MOV 	SI,80H
		LODSB					;取得命令行参数长度
		MOV 	CL,AL
		XOR 	CH,CH			;CX含命令行参数字节数
		PUSH	CS				;该程序中数据和代码在一个段中
		POP		ES
		MOV 	DI,OFFSET BUFFER
		PUSH 	CX
		REP 	MOVSB			;传命令行参数
		POP		CX
		PUSH 	ES
		POP		DS				;置数据段寄存器
		MOV 	SI,OFFSET BUFFER
		MOV		AH,2
		JCXZ	OVER
NEXT:
		LODSB					;显示命令行参数
		MOV 	DL,AL
		INT		21H
		LOOP 	NEXT
OVER:
		MOV		AX,4C00H		;程序结束
		INT 	21H
		CSEG 	ENDS
END 	START
```

#### 例2:写一个显示文本文件内容的程序。文件名作为命令行参数给出。

程序T4-6.ASM显示当前盘当前目录下的文本文件TEST.TXT的内容，只要对它稍作修改，即把由用户输入的命令行参数传送到文件名缓冲区就可。此外，当没有命令行参数，或命令行参数仅是空格等符号时，给出有关提示信息；为了简单，没有具体分析命令行参数，仅仅去掉前导空格符后，就把余下的参数作为文件标识符处理。源程序如下：

```assembly
;程序名：T6-5.ASM
;功能：显示文本文件的内容
;符号常量定义
LENOFID 	= 	128		;文件标识符最大长度
SPACE 		= 	''
TAB			=	09H
EOF 		= 	1AH		;文件结束符的ASCII码
;数据段
DSEG 	SEGMENT
FNAME	DB	LENOFID	DUP	(?)		;准备存放文件名串
ERROR 	DB 'Required parameter missing',0
...								;同T4-6.ASM对应部分
DSEG 	ENDS
;代码段
CSEG 	SEGMENT
		ASSUME CS:CSEG,DS:DSEG
START:
		CLD
		MOV 	SI,80H
		LODSB					;取命令行参数长度
		OR 		AL,AL			;是否有命令行参数
		JNZ		GETFID1			;有
FIDERR: 
		MOV 	AX,SEG ERRORO	;没有命令行参数处理
		MOV 	DS,AX
		MOV 	SI,OFFSET ERRORO
		CALL 	DMESS
		JMP 	OVER
GETFID1:
		MOV		CL,AL
		XOR 	CH,CH			;CX含命令行参数长度取参数一字节
GETFID2:
		LODSB
		CMP 	AL,SPACE		;为空格？
		JZ 		GETFID3			;是，跳过
		CMP 	AL,TAB			;为制表符？
		JNZ 	GETFID4			;不是，表示已去掉前导空格
GETFID3:
		LOOP	GETFID2			;跳过前导的空格和制表符
		JMP 	FIDERR			;命令行参数没有其他符号，转
GETFID4:
		DEC 	SI
		MOV 	AX,SEG FNAME
		MOV 	ES,AX
		MOV 	DI,OFFSET FNAME	;把剩下的命令行参数送
		REP 	MOVSB			;文件标识符区
		XOR 	AL,AL			;再补一个0.形成ASCIIZ串
		STOSB
		;
		MOV 	AX,DSEG
		MOV 	DS,AX			;置数据段寄存器
		;
		......					;同T4-6.ASM对应部分
CSEG	ENDS
		END 	START
```

## 6.3.2对Ctrl+C键和Ctrl+Break键的处理

### 1.Ctrl+C键的处理

程序先看下面的程序T6-6.ASM。它的功能是在屏幕上显示用户所按字符，直到用户按ESC键为止。

```assembly
;程序名：T6-6.ASM
;功能：(略）
CR  = 	ODH 	;常量定义
LF	=	0AH
ESCAPE = 1BH
CSEG 	SEGMENT		;代码段
		ASSUME CS:CSEG,DS:CSEG
START:
		PUSH 	CS
		POP		DS
CONT: 
		MOV 	AH,8		;用不回显方式，接收一个字符
		INT		21H
		CMP 	AL,ESCAPE	;是否为ESC键？
		JZ 		SHORT XIT	;是，结束
		MOV 	DL,AL
		MOV 	AH,2		;否，显示所按字符
		INT 	21H
		CMP 	DL,CR		;是否为回车键？
		JNZ	 	CONT		;否，继续
		MOV 	AH,2
		INT 	21H
		MOV 	DL,LF
		MOV 	AH,2		;是，再显示换行符（形成回车和换行）
		INT 	21H
		JMP 	CONT		;继续
XIT: 
		MOV 	AH,4CH		;结束
		INT 	21H
CSEG 	ENDS
		END 	START
```

就上述程序而言，按ESC键能终止程序运行，这是符合程序功能要求的。但按Ctrl+C键却能中止程序的运行。

当应用程序利用DOS系统功能调用进行字符输入输出（如键盘输入和显示输出等）时，DOS通常要检测Ctrl+C键（对应ASCII码为03H),请参见4.3节中给出的常用DOS 功能调用。如检测，那么DOS在遇到Ctrl+C键后，就先显示符号“C”，并产生INT 23H。缺省的23H号中断处理程序是中止程序的运行。DOS提供的这一功能便于用户随机地中止一个执行错误或不必继续执行的程序。上述程序调用DOS的8号功能输入和2号功能输出，其间DOS要检测Ctrl+C键，所以按Ctrl+C键能中止上述程序的运行。

DOS为应用程序改变这种处理方法作了准备。应用程序只要改变23H号中断处理程序，就可基本控制住对Ctrl+C键的处理。为了改变23H号中断处理程序，应用程序得提供一个新的23H号中断处理程序，然后修改23H号中断向量，使其指向新的23H号中断处理程序。由于DOS在设置PSP时，已把当时的23H号中断向量（即Ctrl+C键处理中断向量）保存到PSP中，且在程序终止时再自动从PSP中取出并恢复。所以，应用程序在修改23H号中断向量后，可不必恢复它。

下面的程序T6-6A.ASM就增加了23H号中断处理程序，该处理程序极其简单，只有一条中断返回指令IRET,也即不作任何处理。所以在运行程序T6-6A时，按Ctrl+C键就不能再中止程序的运行。只要应用程序需要，可把按Ctrl+C键后要进行的工作安排在23H号中断处理程序中。

```assembly
;程序名：T6-6A.ASM
;功能：(略）
......	;常量定义周T6-6.ASM
CSEG 	SEGMENT		;代码段
		ASSUME CS:CSEG,DS:CSEG
;新的23H号中断处理程序
NEW23HIRET			;中断返回
;......
;主程序
START:	
		PUSH	CS
		POP		DS
		MOV		DX,OFFSET NEW23H		;置23H号中断向量
		MOV 	AX,2523H				;使其指向新的处理程序
		INT 	21H
		;
CONT:
		....							;其他代码同T6-6.ASM
		....
		CSEG 	ENDS
				END		START
```

尽管按`Ctrl+C`键不再能中止T6-6A的运行，但屏幕上却会显示出符号`C`。如果应用程序不在乎由于按`Ctrl+C`键带来的符号，那么上述处理就可接受。如果应用程序“讨厌”由`Ctrl+C`键带来的符号，那么如下几种处理方法也许可满足需要：(1)应用程序使用不检测`Ctrl+C`键的DOS功能调用进行字符输入输出；(2)应用程序不利用DOS功能调用进行字符输入输出。就上述程序T6-6.ASM这样的应用程序而言，这两种方法都是可行的。对一般应用程序而言，这两种方法不完全有效。首先，应用程序不利用DOS功能调用进行字符输入输出，就要利用BIOS进行字符输入输出或直接进行输入输出，有时这是麻烦的。其次，在大多数DOS系统功能调用（不仅仅是输入输出功能调用）其间，DOS要查看`Ctrl+Break`键是否被按下，如发现`Ctrl+Break`键被按，则也会显示符号“^C”和产生`INT 23H`。下面先介绍对`Ctrl+Break`键的处理，然后再看一个基本解决问题的例子。

### 2.对Ctrl+Break键的处理

键盘中断处理程序（9H号中断处理程序）发现`Ctrl+Break`键被按时，将产生`INT 1BH`。在DOS自举时，由DOS提供的1BH号中断处理程序将在约定的内存单元中设置一个标志，然后结束。DOS通过该标志检测`Ctrl+Break`键是否被按下，如果发现被按下过，则象处理`Ctrl+C`那样显示符号“C”和产生`INT 23H`。

如果应用程序要自己处理`Ctrl+Break`键，则可通过提供新的1BH号中断处理程序的方法来实现。所以，如果应用程序要使得`Ctrl+Break`键不干扰程序的运行，只要使1BH号中断处理程序不设置与DOS约定的内存单元。但要注意，DOS并不自动保存和恢复1BH号中断向量，所以如果应用程序要提供新的1BH号中断处理程序，那么在修改1BH号中断向量之前，先要保存原1BH号中断向量，在程序结束前恢复它。下面的程序T6-6B.ASM是对T6-6A.ASM的修改，提供了新的1BH号中断处理程序。作为例子，新的1BH号中断处理程序只显示信息`**BREAK**`，然后就返回，所以在运行T6-6B.ASM时，按`Ctrl+Break`键不会出现符号`C`。

```assembly
;程序名：T6-6B.ASM
......			;常量定义同T6-6.ASM
CSEG	SEGMENT		;代码段
		ASSUME CS:CSEG,DS:CSEG
;变量和数据定义
OLDIBH	DD	?			;保存原1BH号中断向量
VPAGE	DB	?			;当前显示页
MESS	DB 	'**BREAK**',0	
;
;新的1BH号中断处理程序
NEW1BH:	PUSH	DS
		PUSH 	AX		;保护现场
		PUSH 	BX
		PUSH 	SI
		PUSH 	CS
		POP		DS
		CLD
		MOV 	SI,OFFSET MESS
		MOV 	BH,VPAGE	;准备显示信息**BREAK**
		MOV 	AH,OEH
BRRNEXT:
		LODSB				;取一字节信息
		OR 		AL,AL		;结束？
		JZ 		SHORT BRKEXIT;是
		INT 	10H			;否则，显示
		JMP 	BRKNEXT		;继续
BRKEXIT:
		POP		SI
		POP 	BX
		POP 	AX			;恢复现场
		POP 	DS
		IRET				;中断返回
;新的23H号中断处理程序
NEW23H:	
		IRET				;中断返回
;主程序
START:
		PUSH	CS
		POP 	DS
		MOV 	AH,OFH		;取显示状态信息
		INT 	10H
		MOV 	VPAGE,BH	;保存当前显示页号
		MOV 	AX,351BH
		INT 	21H			;取原1BH中断向量并保存
		MOV 	WORD PTR OLD1BH,BX
		MOV 	WORD PTR OLD1BH+2,ES
		MOV 	DX,OFFSET NEW23H	;置23H号中断向量
		MOV 	AX,2523H	;使其指向新的处理程序
		INT 	21H
		MOV 	DX,OFFSET NEW1BH	;置1BH号中断向量
		MOV 	AX,251BH			;使其指向新的处理程序
		INT 	21H
CONT:
		....						;其他代码同T6-6.ASM
		....
XIT:
		LDS		DX,OLD1BH
		MOV		AX,251BH			;恢复原1BH号中断向量
		INT		21H
		MOV		AH,4CH				;程序正常终止
		INT		21H
CSEG	ENDS
		END 	START
```

有一点值得指出，如果在T6-6B.ASM中不提供新的23H号中断处理程序，可能会导致麻烦。因为如果按Ctrl+C键将中止程序的运行，而原1BH号中断处理程序得不到恢复。

### 3.一个能控制住Ctrl+C键和Ctrl+Break键的例子

在上述程序T6-6B中，控制住了`Ctrl+Break`键。在其运行时，按`Ctrl+C`键也不中止程序的运行，但仍会出现符号`C`。现在我们修改键盘管理程序（16H号中断处理程序）,使其不返回`Ctrl+C`键（即ASCII码03H)。

在下面的程序中，提供了新的`16H`号中断处理程序，它“吃掉”了`Ctrl+C`键，同时也滤掉了`Ctrl+2`键（它类似于Ctrl+C,其扫描码为`03H`)。这样，DOS就不可能检测到`Ctrl+C`键了。新的`1BH`号中断处理程序仅是一条中断返回指令。所以，我们就不再提供新的`23H`号中断处理程序。

```assembly
;程序名：T6-6C.ASM
......				;常量定义同T6-6.ASM
CSEG 	SEGMENT		;代码段
		ASSUME CS:CSEG,DS:CSEG		;变量和数据定义
OLDIBH	DD	?						;保存原1BH号中断向量
OLD16H	DD	?						;保存原16H号中断向量
NEWKEY	PROC	FAR
;新的16H号中断处理程序
NEW16H:
		CMP		AH,10H
		JZ 		PKEY
		CMP 	AH,11H
		JZ 		PKEY2
		CMP		AH,1
		JZ 		PKEY2
		OR 		AH,AH
		JZ 		PKEY
		JMP		DWORD PTR CS:OLD16H	;转原16H号中断处理程序
		;
PKEY:
		PUSH	AX					;0号和10H号功能
PKEY1:
		POP		AX
		PUSH	AX
		PUSHF
		CALL	DWORD PTR CS:OLD16H	;调原16H号中断处理程序
		CMP		AL,3
		JZ		PKEY1				;如是Ctrl+C,则"吃掉"
		CMP 	AX,0300H			;如是Ctrl+2,也“吃掉”
		JZ 		PKEY1
		ADD 	SP,2				;堆栈平衡
		IRET
		;
PKEY2: 
		PUSH 	AX					;01H和11H号功能
PKEY3:
		POP		AX
		PUSH	AX
		PUSHF	
		CALL	DWORD PTR CS:OLD16H	;调原16H号中断处理程序
		JZ 		PKEY6				;没有字符，转
		CMP		AL,3
		JZ 		PKEY4				;有字符Ctrl+2,转
		CMP 	AX,0300H
		JNZ 	PKEY5
PKEY4: 
		XOR 	AH,AH				;如遇字符Ctrl+C和Ctrl+2键
		PUSHF						;则读出它，然后继续
		CALL	DWORD PTR CS:OLD16H
		JMP 	PKEY3
PKEY5:
		ADD		SP,2				;有字符时返回，平衡堆栈
		CMP 	AX,0300H			;使ZF为0
		RET		2					;带标志返回
;
PKEY6:
		ADD		SP,2				;无字符时返回，平衡堆栈
		CMP 	AX,AX				;置ZF为1
		RET		2					;带标志返回
NEWKEY 	ENDP						;新的1BH号中断处理程序
NEWIBH:
		IRET						;中断返回

;主程序
START:	
		PUSH	CS
		POP 	DS
		MOV 	AX,3516H
		INT 	21H					;取16H号中断向量保存
		MOV		WORD PTR OLD16H,BX
		MOV		WORD PTR OLD16H+2,ES
		;
		MOV 	DX,OFFSET NEW16H	;置新的16H号中断向量
		MOV 	AX,2516H
		INT		21H
		MOV 	DX,OFFSET NEW1BH	;置新的1BH号中断向量
		MOV 	AX,251BH
		INT		21H
;
CONT:
		....						;同T6-6.ASM
		;
XIT:
		LDS		DX,OLD1BH
		MOV 	AX,251BH			;恢复1BH号原中断向量
		INT		21H
		LDS 	DX,CS:OLD16H
		MOV 	AX,2516H			;恢复16H号原中断向量
		INT		21H
		MOV 	AH,4CH				;程序正常终止
		INT		21H
CSEG 	ENDS
		END 	START
```

# 6.4 TSR程序设计举例

TSR(Terminate and Stay Resident)意为结束并驻留。TSR程序是一种特殊的DOS应用程序，不同于结束即退出的一般DOS应用程序。TSR程序装入内存并初次运行后，程序的大部分仍驻留内存，被某种条件激活后又投入运行。它能及时地处理许多暂驻程序不能处理的事件，并可为单任务操作系统DOS增添一定的多任务处理能力。

编写TSR程序时要考虑许多方面的问题，诸如TSR的激活条件和资源使用的冲突等，对这些问题的讨论超出了本教程的范围，所以本节仅举两个例子说明如何编写简单的TSR程序。

## 6.4.1驻留的时钟显示程序

通常TSR程序由驻留内存部分和初始化部分组成。把TSR程序装入内存时，初次运行的是初始化部分。初始化程序的主要功能是，对驻留部分完成必要的初始化工作；使驻留部分保留在内存中。

下面的时钟显示程序是对在5.5.2节中介绍的时钟显示程序的扩充。初始化后驻留在内存的程序由时钟中断激活。程序名：T6-7.ASM

```assembly
;程序名：T6-7.ASM
;功能：在内存中驻留显示时钟的程序
;1CH号中断处理程序使用的常数说明与T5-5.ASM对应部分相同
CSEG 	SEGMENT
		ASSUME CS:CSEG,DS:CSEG
;1CH号中断处理程序使用的变量说明与T5-5.ASM对应部分相同
......
OLDICH	DD	?		;保存原中断向量的变量
;1CH号中断处理程序代码
NEWICH:
		CMP		CS:COUNT,0		;计数为0?
		JZ 		NEXT			;为0转，显示时钟
		DEC 	CS:COUNT		;计数减1,转原1CH中断处理程序
		JMP 	DWORD PTR CS:OLDICH
		;
NEXT:
		MOV		CS:COUNT,COUNT_VAL	;重置计数值
		STI							;开中断
		;
		PUSH 	DS
		......						;(与T5-5.ASM对应部分租同）
		POP		DS
		JMP 	DWORD PTR CS:OLDICH ;代替IRET
;子程序GET_T
		......						;(与T5-5.ASM对应部分相同）
;子程序TTASC
		......						;(与T5-5.ASM对应部分相同）
;-------------		
;初始化部分代码和变量
START:
		PUSH	CS
		POP 	DS
		MOV 	AX,351CH			;(与T5-5.ASM对应部分相同）
		INT		21H
		MOV 	WORD PTR OLDICH,BX
		MOV 	WORD PTR OLD1CH+2,ES
		MOV 	DX,OFFSET NEWICH
		MOV 	AX,251CH
		INT		21H					;计算驻留节数并驻留退出
		MOV 	DX,OFFSET START		;取欲驻留部分代码和数据的字节数
		ADD		DX,15				;考虑字节数不是16倍数的情况
		MOV 	CL,4
		SHR		DX,CL				;转换成节数
		ADD		DX,10H				;加上PSP的长度
		MOV 	AH,31H
		INT		21H					;结束并驻留
CSEG	ENDS
		END 	START
```

把上面的程序与T5-5.ASM相比较可知，其中的1CH号中断处理程序基本相同；初始化部分包含了驻留退出的代码，把1CH号中断处理程序驻留在内存中，此外还把保存原1CH号中断向量的双字变量OLDIC移到驻留区。

通过DOS的31H号功能调用进行驻留退出。该功能调用的主要入口参数是含在DX中的驻留节数（1节等于16字节）,驻留的内容从程序段前级开始计算，所以在计算驻留节数时，除了计算要驻留的数据和代码的长度外，还需要加上PSP的10H节。

把DOS的31H号功能调用与4CH号功能调用相比，所不同的是它在交出控制权时没有全部交出占用的内存资源，而是根据要求（由入口参数规定）保留了部分。

## 6.4.2热键激活的TSR程序

有多种方式或方法激活驻留的程序，键盘激活是常见的一种方法。例如可驻留一个保存屏幕画面的程序，然后当屏幕上出现需要保存的画面时，按一个约定的激活键来激活它。这样的键称为热键。

下面的程序T6-8.ASM是一个简单的热键激活TSR程序的例子。热键设定为CTRL+F8,在驻留后，每按一次CTRL+F8键，就在屏幕约定的位置显示一字符串。

```assembly
;程序名：T6-8.ASM
;功能：简单的热键激活TSR程序
;常量说明
BUFF_HEAD 	= 	1AH				;键盘缓冲区头指针保存单元偏移
BUFF_TAIL	=	1CH				;键盘缓冲区尾指针保存单元偏移
BUFF_START	= 	1EH				;键盘缓冲区开始偏移
BUFF_END 	= 	3EH				;键盘缓冲区结束偏移
CTRL_F8		=	6500H			;激活键扫描码
ROW			=	3				;行号
COLUMN		=	0				;列号
PAGEN		=	0				;显示页号
;代码
CSEG 	SEGMENI
		ASSUME 	CS:CSEG,DS:CSEG
;新键盘中断处理程序
OLD9H	DD	?					;原9号中断向量保存单元
MESS 	DB 	'Hello!'			;显示信息
MESSLEN	EQU	$-MESS
NEW9H:
		PUSHF
		CALL		CS:OLD9H	;调用原中断处理程序
		;
		STI						;开中断
		PUSH 		DS
		PUSH 		AX			;保护现场
		PUSH 		BX
		MOV 		AX.40H
		MOV 		DS,AX
		MOV 		BX,DS:[BUFF_HEAD]
		CMP 		BX,DS:[BUFF_TAIL]	;键盘缓冲区空？
		JZ 			IOVER				;是，结束
		MOV 		AX,DS:[BX]			;取所按键的代码
		CMP			AX,CTRL_F8			;是否为激活键？,是，转处理
		JZ 			YES
		;
IOVER:
		POP			BX					;结束处理
		POP			AX					;恢复现场
		POP			DS					;中断返回
		IRET
		;
YES:
		INC			BX					;调整键盘缓冲区头指针（取走激活键）
		INC 		BX
		CMP 		BX,BUFF_END			;指针到缓冲区未？
		JNZ			YES1				;否，转
		MOV 		BX,BUFF_START		;是，指向头
YES1: 
		MOV 		DS:[BUFF_HEAD],BX 	;保存
		PUSH		CX					;再保护部分现场
		PUSH 		DX
		PUSH 		BP
		PUSH 		ES
		MOV 		AX,CS				;数据段同代码段
		MOV 		ES,AX
		MOV 		BP,OFFSET MESS
		MOV 		CX,MESSLEN
		MOV 		DH,ROW
		MOV 		DL,COLUMN
		MOV 		BH,PAGEN
		MOV 		BL,07H				;显示后不移动光标，串中不含属性
		MOV			AL,0
		MOV			AH,13H
		INT 		10H
		POP 		ES
		POP			BP					;恢复部分现场
		POP			DX
		POP 		CX
		JMP 		IOVER				;转结束
		;------------------
		;初始化代码
INIT:
		PUSH		CS
		POP			DS
		MOV 		AX,3509H			;取9号中断向量
		INT			21H					;并保存
		MOV 		WORD PTR OLD9H,BX
		MOV 		WORD PTR OLD9H+2,ES
		MOV			DX,OFFSET NEW9H		;置新的9号中断向量
		MOV 		AX,2509H
		INT 		21H
		;
		MOV 		DX,OFFSET INIT+15
		MOV 		CL,4				;计算驻留节数
		SHR 		DX,CL
		ADD			DX,10H				;加上PSP的节数
		MOV			AL,0
		MOV 		AH,31H				;驻留退出
		INT 		21H
CSEG 	ENDS
		END			INIT
```

上面程序的初始化部分先保存了9号中断向量（原键盘中断处理程序的入口地址）,然后设置新的9号中断向量，使其指向新的键盘中断处理程序，最后驻留结束。这样每当按键，就会运行新的键盘中断处理程序。新的键盘中断处理程序先调用老的键盘中断处理程序完成按键处理工作，然后通过检查键盘缓冲区，判断是否按了约定的热键CTRL+F8,当判断出按了CTRL+F8后，就显示一预定的提示信息。
