[TOC]

尽管汇编语言作为低级语言不可能象高级语言那样具有丰富的数据类型和方便灵活的表达方式，但汇编语言仍力求提供这方面的功能。本章介绍汇编语言的一些高级技术：结构、宏和条件语句，利用它们可编写出更具适应性的汇编语言源程序。

# 7.1结构和记录

为了使程序员能更方便、更有效地对数据进行组织和描述，宏汇编语言除了提供定义简单数据变量的伪指令（如DB和DW等）外，还提供了用于说明复杂数据类型的伪指令，利用这些伪指令能够说明复杂的数据类型，从而定义复杂的数据变量。本节描述结构和记录并说明如何使用它们。

## 7.1.1结构

### 1.结构类型的说明

在描述结构型数据或使用结构型变量之前，需要说明结构类型。用伪指令STRUC和ENDS把一系列数据定义语句括起来就说明了一个结构类型，一般格式如下：

```assembly
结构名		STRUC
			数据定义语句序列
结构名		ENDS
;例如，下列语句说明了一个名为PERSON的结构类型：
PERSON	STRUC
ID		DW 	?
SCORE	DB	0
PNAME	DB	'ABCDEFGH'
PERSO	NENDS
```

组成结构的变量称为结构的字段，相应的变量名称为字段名。一个结构中可以含有任意数目的字段，并且各字段可以有不同的长度（基本单位是字节）,还可以独立地存取。结构中的字段也可以没有字段名。

结构中的字段名代表了从结构的开始到相应字段的偏移。在上面定义的结构PERSON中，字段ID、SCORE和FNAME分别有偏移值0、2和3。

在说明结构类型时，可以给字段赋初值，也可以不赋初值。在上面定义的结构PERSON中，给字段SCORE赋缺省初值0,没有给字段ID赋初值。如果字段是一个字符串，那么要确保其初值有足够的长度以适应可能最长的字符串。

再如，下列语句说明了一个名为MESST的结构类型：

```assembly
MESST 	STRUC
MBUFF 	DB 	100 DUP	(?)
CRLF	DB 	ODH,0AH
ENDMARK	DB	24H
MESST	ENDS
```

结构MESST中的字段MBUFF和CRLF均含有多个值，字段MBUFF,CRLF和ENDMARK分别有偏移值0、100和102。

在说明结构类型时，结构名必须是唯一的，各字段名也应该是唯一的。

注意，在说明结构类型时不进行任何存储分配，只有在定义结构变量时才进行存储分配。这与高级语言中的数据类型定义相似。

还请注意，标记一个结构类型结束的伪指令与标记一个段结束的伪指令有相同的助记符ENDS,汇编程序通过上下文理解ENDS的含义，所以要确保每一SEGMENT伪指令和每一STRUC伪指令有各自对应的ENDS伪指令。

### 2.结构变量的定义

在说明了结构类型后，就可定义相应的结构变量。结构变量定义的一般格式如下：

> [变量名]	结构名	<[字段值表]>

其中，变量名就是当前定义的结构变量的名称，结构变量名也可以省略，如果省略，那么就不能直接通过符号名访问该结构变量。结构名是在说明结构类型时所用的名字。字段值表用来给结构变量的各字段赋初始值，其中各字段值的排列顺序及类型应与结构定义时的各字段相一致，中间以逗号分隔。如果某个字段采用在说明结构时所给定的缺省初值，那么可简单地用逗号表示；如果结构变量的所有字段均如此，那么可省去字段值表，但仍必须保留一对尖括号。

例如，设已说明了上述结构PERSON,那么可定义如下结构变量：

```assembly
STUDENT1 	PERSON 	<103.88,'WANG'>	;三个字段都重赋初值
STUDENT2 	PERSON 	<104,,'LIMING'>	;字段SCORE仍用缺省初值
STUDENT		PERSON 	< >				;三个字段均用缺省初值
			PERSON 	99 DUP (< >)	;定义99个结构变量，初值不变
```

注意，对宏汇编程序MASM而言，如果某个字段有多值，那么在定义结构变量时，就不能给该字段重赋初值。如上面说明的结构MESST,不能给其MBUFF字段和CRLF字段重赋初值。下面定义结构变量的语句是正确的：

```assembly
MESS1	MESST< >
MESS2	MESST< , , 0>
```

### 3.结构变量及其字段的访问

通过结构变量名可直接存取结构变量。若要存取结构变量中的某一字段，则可采用如下形式：

```assembly
结构变量命名.结构字段名
```

在上述形式中，结构变量名与结构字段名之间用点号分隔，并且结构字段名所代表的字段必须是对应结构所具有的字段。这种形式表示的变量的地址偏移值是结构变量地址(起始地址）的偏移值与相应字段偏移值之和。

下面的程序片段说明了对结构字段的访问：

```assembly
	......
DATE 	STRUC					;说明结构类型
YEAR 	DW
MONTH 	DB	?
DAY 	DB	?
DATE 	ENDS
	......
YESTERDAY	DATE <1995,7,17>	;定义结构变量
TODAY		DATE <1995,7,18>	
TOMORROW	DATE <1995,7,19>
	......
	MOV		AL,YESTERDAY,DAY	;访间结构变量
	MOV		AH,TODAY.MONTH
	MOV		TOMORROW.YEAR,DX
	.......
```

上述形式所对应的寻址方式是直接寻址。另一种存取结构变量中某一字段的方法是把结构变量地址的偏移先存入某个基址或变址寄存器，然后用“[寄存器名]”代替结构变量名。这种方法所对应的寻址方式是相对基址或变址寻址。例如：

```assembly
......
MOV		BX,OFFSET YESTERDAY
MOV		AL,[BX].MONTH
......
```

#### 例1:数据文件SCORE.DAT中依次存放着30个学生的成绩记录，文件（成绩）记录具有如下字段：

> 学号			整数		2字节
>
> 姓名			字符串	8字节
>
> 语文成绩	整数		1字节
>
> 数学成绩	整数		1字节
>
> 外语成绩	整数		1字节

写一个程序计算三门课程的总分，把学号和总分依次写到文件SCORE.SUM中。SCORE.SUM文件的记录有两个字段，第一个字段是学号，第二个字段是总分（用2字节表示）。实现流程是：

1. 打开文件SCORE.DAT;
2. 循环处理每个学生的成绩，把学号和总分依次存入预定义的缓冲区（称为总分表）中；
3. 关闭文件SCORE.DAT;
4. 建立文件SCORE.SUM;
5. 把缓冲区中的内容写入文件SCORE.SUM;
6. 关闭文件SCORE.SUM。

为了突出结构的应用，下面列出的程序没有考虑磁盘文件读写出错等情况。

```assembly
;程序名：T7-1.ASM
;功能：(略）
;常量定义
COUNT	=	30			;设学生数为30
;对应原始成绩的结构SCORE的定义
SCORE 	STRUC
NO 		DW 		?				;学号
SNAME 	DB 		8 DUP ('')		;姓名
CHN		DB		0				;语文成绩
MATH 	DB 		0				;数学成绩
ENG 	DB 		0				;外语成绩
SCORE 	ENDS
;对应学号和总分的结构ITEM的定义
ITEM 	STRUC
NOS		DW		0				;学号
SUM		DW		0				;总分
ITEM 	ENDS					;数据段
DSEG 	SEGMENT
BUFFER 	SCORE<>					;存放原始成绩的缓冲区
STABLE 	ITEM COUNT DUP (< >)	;预留总分表
FNAME1	DB		'SCORE.DAT',0	;文件名
FNAME2	DB		'SCORE.SUM',0
DSEG 	ENDS
;代码段
CSEG 	SEGMENT
		ASSUME CS:CSEG,DS:DSEG
START:
		MOV		AX,DSEG
		MOV 	DS,AX
		MOV 	DX,OFFSET FNAME1
		MOV		AX,3D00H		;打开文件SCORE.DAT
		INT		21H
		MOV 	BX,AX
		MOV 	DI,COUNT		;置循环计数器初值
		MOV 	SI,OFFSET STABLE;置学号总分缓冲区指针初值
READ:
		MOV		DX,OFFSE TBUFFER;读一个学生的原始成绩
		MOV 	CX,TYPE SCORE
		MOV 	AH,3FH
		INT		21H
		MOV 	AL,BUFFER.CHN	;统计总分
		XOR 	AH,AH
		ADD 	AL,BUFFER.MATH
		ADC 	AH,0
		ADD 	AL,BUFFER.ENG
		ADC 	AH,0
		MOV		[SI].SUM,AX		;把总分保存到总分表的当前项
		MOV 	AX,BUFFER.NOMOV[SI].NOS.AX	;把学号保存到总分表的当前项
		ADD 	SI,TYPE ITEM	;调整当前总分表当前项
		DEC 	DI				;循环计数控制
		JNZ 	READ
		MOV 	AH,3EH			;关闭文件SCORE.DAT
		INT 	21H
		MOV 	DX,OFFSET FNAME2
		MOV 	CX,0
		MOV 	AH,3CH			;建立文件SCORE.SUM
		INT 	21H
		MOV 	BX,AX
		MOV		DX,OFFSET STABLE;写出学号和总分
		MOV 	CX,(TYPE ITEM)*COUNT
		MOV 	AH,40H
		INT 	21H
		MOV 	AH,3EH			;关闭文件SCORE.SUM
		INT 	21H
		MOV 	AX.4C00H
		INT 	21H
CSEC 	ENDS
		END 	START
```

在程序T7-1.ASM中，我们定义两个结构SCORE和ITEM,用来描述文件记录的字段组成。利用SCORE定义了存放一个原始成绩记录的缓冲区，这样可方便地引用各字段。还利用ITEM定义了一张总分表，然后借助指向总分表当前项的指针方便地访问当前项的学号和总分字段。此外，程序T7-1.ASM还使用了TYPE得到结构的字节数。

#### 例2:写一个求字符串长度的子程序。

子程序的调用说明如下：

```assembly
;子程序名：STRLEN
;功能：测字符串长度
;入口参数：字符串首地址的段值和偏移在堆栈顶
;出口参数：AX=字符串长度
;说明：(1)字符串以0结尾；字符串长度不包括结尾标志。;(2)本过程是一个远过程。
```

为了方便地表达堆栈参数，先定义如下结构：

```assembly
PARM 	STRUC
BPREG 	DW		?		;对应BP寄存器保存单元
RETADR	DD		?		;对应返回地址
STROFF	DW		?		;对应入口参数中的偏移
STRSEG 	DW 		?		;对应入口参数中的段值
PARM	ENDS
```

只定义上述结构PARM类型，但不定义结构变量。当子程序为了利用寄存器BP访问堆栈中的参数，而把寄存器BP压入堆栈后，可以认为一个这样的结构变量出现在当时的堆栈顶端。然后使寄存器BP指向此结构变量，于是就能方便地表达要存取的位于堆栈中的参数。子程序源代码如下：

```assembly
STRLEN 	PROC FAR
		PUSH 	BP
		MOV 	BP,SP
		PUSH 	DS
		PUSH 	SI
		MOV		DS,[BP].STRSEG		;取字符串首地址的段值
		MOV 	SI.[BP].STROFF		;取字符串首地址的偏移
		XOR 	AL,AL
STRLEN1:
		CMP		BYTEPTR[SI],AL
		JZ 		STRLEN2
		INC 	SI
		JMP 	STRLEN1
STRLEN2:
		MOV		AX,SI
		SUB 	AX,[BP].STROFF
		POP 	SI
		POP 	DS
		POP 	BP
		RET
STRLEN 	ENDP
```

该子程序与4.2.3节中的例5功能相同，实现算法也相同，所不同的是该子程序是远过程。请比较利用结构类型和结构字段前后代码的易读性和可修改性。

## 7.1.2记录

记录类型为按二进制位存取数据或信息提供了方便。

### 1.记录类型的说明

在描述记录型数据或使用记录型变量之前，需要说明记录类型。伪指令RECORD用于说明记录类型，一般格式如下：

> 记录名		RECORD		字段		[,字段...]

记录名标识说明的记录类型：字段表示构成记录的字段的名字、宽度和缺省初值。每一字段的格式如下：

> 字段名：宽度[=表达式]

字段名是记录中字段的名字。宽度表示相应的字段所占的位数，宽度必须是常数，宽度最大为16位。表达式的值将作为相应字段的缺省初值，如果缺省初值相对于它的宽度太大，则汇编时将产生错误提示信息。如果某个字段没有缺省初值，那么缺省初值被置为0

一个记录可以含有多个字段，字段间用逗号分隔。但在一般情况下各字段的宽度之和不超过16。例如：

> COLOR	RECORD BLINK:1,BACK:3,INTENSE:1,FORE:3

上述记录类型COLOR含四个字段（BLINK、BACK、INTENSE和FORE),各字段均没有赋缺省初值，它们的宽度分别是1、3、1和3。这四个字段所占总宽度正好是8位，所以也称为字节记录类型。这四个字段的具体意义请参见图5.14。

注意，在说明记录类型时，不实际分配存储单元。

如果一个记录中所有说明字段的总宽度大于8,那么汇编程序会给对应的记录变量分配两字节，否则仅给对应的记录变量分配一字节。第一个字段放在记录左边的较高有效位，随后说明的字段放在右边的一些后续位上，如果说明的字段总宽度不正好是8位或16位，那么向右对齐，记录高端未说明的位置为0。例如：

```assembly
ABCD RECORD AA:5=12,BB:3=6,CC:4=3
```

上述记录类型ABCD含三个字段，这三个字段所占各位如图7.1所示。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/root/image.2fz0gh1t5gbo.webp)

2.记录变量的定义

记录变量是其位分成一或多个字段的字节或字变量。定义记录变量的一般格式如下：

> [变量名]	记录名	<[字段值表]>

其中，变量名就是当前定义的记录变量的名称，记录变量名可以省略，如果省略，那么就不能直接通过符号名访问该记录变量。记录名是在说明记录类型时所用的名字。字段值表用来给记录变量的各字段赋初始值，各字段值的排列顺序及大小应与记录说明时的各字段相一致，中间以逗号分隔。如果某个字段采用在说明记录时所给定的缺省初值，那么可简单地用逗号表示；如果记录变量的所有字段均如此，那么可省去字段值表，但仍必须保留一对尖括号。

例如：设已定义了上述记录类型COLOR,那么可定义如下结构变量：

```assembly
WARNING COLOR <1,0,1,4>		;该字节的值是8CH
		COLOR <,3,,110B>;	;该字节的值是36H
COLORLST COLOR 32 DUP (<>)	;32个字节
```

当字段有7位宽时，可定义为一字符。
注意，如果字段值太大，则有汇编错误提示信息。

### 3.记录专用操作符

操作符WIDTH和MASK仅与记录一起使用，得到已说明记录的不同方面的常数

#### (1)操作符WIDTH

操作符WIDTH返回记录或记录中字段的以位为单位的宽度。一般格式如下：

```assembly
WIDTH	记录名
或者
WIDTH	记录字段名
```

设记录COLOR如上面的说明，那么：

```assembly
SUB 	AL,WIDTH COLOR		;SUB AL,8
MOV 	DH,WIDTH BACK		;MOV DH,3
ADD 	BH,WIDTH INTENSE 	;ADD BH.1
```

#### (2)操作符MASK

一般格式如下：

```assembly
MASK	记录名
或者 
MASK 	记录字段名
```

操作符MASK返回F8位或16位二进制数，这个二进制数中相应于指定字段的各位为1,其余各位为0。如果记录是字节记录类型，那么就是一个8位二进制数，如果记录是字记录类型，那么就是一个16位二进制数。设记录COLOR和ABCD如上面的说明，那么：

```assembly
MOV 	AL,MASK BLINK 		;MOV AL,10000000B
OR 		AH,MASK FORE 		;OR AH,00000111B
AND 	DX,MASK ABCD 		;AND DX,OFFFH
```

#### (3)记录字段

记录字段名作为一个特殊的操作符，它不带操作数，直接返回该字段移到所在记录的最右端所需移动的位数。设记录COLOR如上面的说明，那么：

```assembly
MOV CL.BLINK	;MOV CL,7
MOV CL,INTENSE	;MOV CL,3
```

4.记录及其字段的访问

由于8086/8088没有位操作指令，记录类型和记录操作符能够也只能够提供访问记录中字段的便利。

下面的程序片段说明如何访问记录及其字段：

```assembly
.....
COLOR 	RECORD 	BLINK:1,BACK:3,INTENSE:1,FORE:3	;说明记录类型
.....
CHAR 	DB 'A'
ATTR 	COLOR <0,0,1,7>							;定义记录变量
.....
		MOV 	BP,1 SHL WIDTH BACK 			;置循环计数器
NEXT:
		MOV		AH,9				;在当前光标位置显示字符
		MOV 	BH,0
		MOV		AL,CHAR
		MOV 	BL,ATTR
		MOV 	CX,1
		INT 	10H
		MOV		AL,ATTR				;取显示属性（记录变量）
		MOV 	AH,AL
		AND 	AL,NOT MASK BACK	;析出除背景的其他位
		MOV 	CL,BACK
		SHR 	AH,CL				;把背景字段移至有端
		INC		AH					;调整背景色
		SHL		AH,CL				;再向左移到原位
		AND 	AH,MASK BACK		;屏蔽除背景位的其他位
		OR 		AH,AL				;和其他位原值合并
		MOV 	ATTR,AH				;保存属性
		MOV 	AH,0
		INT 	16H
		DEC 	BP
		JNZ 	NEXT
		....
```

该程序片段在当前光标位置处循环显示各种不同背景的显示效果，按一次键，则换一种背景色

# 7.2宏

宏是宏汇编语言的主要特征之一。在汇编语言源程序中，若某程序片段需要多次使用，为了避免重复书写，那么可把它定义为一条宏指令。在写源程序时，程序员用宏指令来表示程序片段；在汇编时，汇编程序用对应的程序片段代替宏指令。

## 7.2.1 宏指令的定义和使用

宏指令在使用之前要先定义。宏定义的一般格式如下：

```assembly
宏指令名	MACRO	[形式参数表]
		....
			ENDM
```

其中，MACRO和ENDM是一对伪指令，在宏定义中，它们必须成对出现，表示宏义的开始和宏定义的结束。MACRO和ENDM之间的内容称为宏定义体，可以是由指令、伪指令和宏指令构成的程序片段。宏指令名由用户指定，适用一般标号命名规则。可选的形式参数表可由若干参数组成，各形式参数间用逗号分隔。

例如：我们把将AL寄存器内的低4位转换为对应十六进制数ASCII码的程序片段定义为一个宏：

```assembly
HTOASC 	MACRO
		AND 	AL,OFH
		ADD		AL,90H
		DAA
		ADC		AL,40H
		DAA
	ENDM
```

再如，我们把通过DOS的1功能调用从键盘读一个字符的程序片段定义为一个宏：

```assembly
GETTCH 	MACRO
		MOV		AH,1
		INT		21H
	ENDM
```

在定义宏指令后，就可使用宏指令来表示对应的程序片段，这称为宏调用。宏调用的一般格式如下：

```assembly
宏指令名	[实参数表]
```

其中，实参数表中的实参数应该与宏定义时的形式参数表中的形式参数相对应。例如，下面的程序片段调用了刚定义的两个宏：

```assembly
....
GETCH	
MOV 	AH,AL
SHR 	AL,1
SHR 	AL,1
SHR		AL,1
SHR 	AL,1
HTOASC
XCHG 	AH,AL
HTOASC
....
```

在对源程序汇编时，汇编程序把源程序中的宏指令替换成对应的宏定义体，这称为宏展开或宏扩展。上述程序片段在汇编时得到的指令如下：

```assembly
	....
	MOV		AH,
1	INT		21H
	MOV 	AH,AL
	SHR 	AL,1
	SHR 	AL,1
	SHR 	AL,1
	SHR		AL,1
1	AND		AL,OFH
1	ADD		AL,90H
1	DAA
1 	ADC 	AL,40H
1 	DAA
	XCHG AH,AL
1	AND		AL,OFH
1 	ADD 	AL,90H
1	DAA	
1	ADC AL,40H
	DAA
	......
```

注意，宏展开所得指令的行首标有符号“1”，以示区别。

## 7.2.2 宏指令的用途

### 1.缩短源代码

若在源程序中要多次使用到某个程序片段，那么就可以把此程序片段定义为一条宏指令。此后，在需要这个程序片段之处安排一条对应的宏指令就行了，由汇编程序在汇编时产生对应的代码。这不仅能有效地缩短源代码，而且能减少编辑汇编语言源程序过程中的错误。

例如，我们把使光标另起一行的程序片段写成如下的一个宏：

```assembly
CRLF 	MACRO
		XOR 	BH,BH
		MOV		AH,14
		MOV 	AL,ODH
		INT 	10H
		MOV		AL,OAH
		INT 	10H
	ENDM
```

### 2.扩充指令集

CPU的指令集是确定的，但利用宏能在汇编语言中在形式上对指令集进行扩充。扩充后的指令集是机器指令集与宏指令集的并集。这不仅能方便源程序的编写，而且便于阅读理解源程序。

在写子程序和中断处理程序时，为了保护现场常常需要把8个通用寄存器全部压入堆栈，但8086/8088指令集中没有把8个通用寄存器全部压栈的指令。为此，我们可定义一条宏指令PUSHA,由它实现把8个通用寄存器全部压入堆栈的功能:

```assembly
PUSHA 	MACRO
		PUSH AX
		PUSH CX
		PUSH DX
		PUSH BX
		PUSH SP
		PUSH BP
		PUSH SI
		PUSH DI
	ENDM
```

在定义了上述宏指令PUSHA后，每当需要把8个通用寄存器全部压栈时，就可以使用PUSHA指令了。与宏指令PUSHA相对应，我们还可定义宏指令POPA实现把由PUSHA压栈的8个通用寄存器依次退栈的功能。

### 3.改变某些指令助记符的意义

宏指令名可以与指令助记符或伪操作名相同，在这种情况下，宏指令的优先级最高，而同名的指令或伪操作就失效了。利用宏指令的这一特点，可以改变指令助记符的意义。例如，在定义如下宏指令后，助记符LODSB所表示指令的意义就变化了：

```assembly
LODSB 	MACRO
		MOV 	AH,[SI]
		INC 	SI
	ENDM
```

汇编程序TASM在遇到上述宏定义时，会发出一条警告信息；而汇编程序MASM则不给出任何警告信息。

## 7.2.3 宏指令中参数的使用

宏指令可以不带参数，如上面定义的宏指令GETCH和PUSHA等。但往往带参数的宏指令更具灵活性。下面介绍宏指令中参数的使用。

### 1.宏指令的参数很灵活

#### (1)宏指令的参数可以是常数、寄存器和存储单元，还可以是表达式。

##### 例1:在逻辑左移指令SHL的基础上定义一条宏指令SHLN,它能实现指定次数的左移。

```assembly
SHLN 	MACRO 	REG,NUM
		PUSH 	CX
		MOV 	CL,NUM
		SHL 	REG,CL
		POP 	CX
	ENDM
```

此后，可有如下格式的各种宏调用：

```assembly
SHLN 	BL,5
SHLN 	SI,9
SHLN 	AX,CL
```

在汇编时，宏指令“SHLNBL,5”扩展成如下的代码：

```assembly
1 PUSH 	CX
1 MOV 	CL,5
1 SHI 	BL,CI
1 POP 	CX
```

#### (2)宏指令的参数可以是操作码。

##### 例2:下面的宏指令MANDM有三个参数，第一个参数OPR作为操作符使用在宏体的指令中：

```assembly
MANDM 	MACRO OPR,X,Y
		MOV 	AX,X
		OPR 	AX,Y
		MOV 	X,AX
	ENDM
```

调用宏MANDM及其宏扩展如下所示：

```assembly
MANDM 	MOV 	[BX],[S1]
1 	MOV 	AX,[BX]
1 	MOV 	AX,[SI]
1 	MOV 	[BX],AX
MANDM		ADD	[BX],ES:[1234H]
1 	MOV 	AX,[BX]
1 	ADD 	AX,ES:[1234H]
1 	MOV 	[BX],AX
```

### 2.宏调用参数个数可以与定义时不一致

一般说来，宏调用时使用的实参个数应该与宏定义时的形参个数一致，但汇编程序并不要求它们必须相等。若实参个数多于形参个数，那么多余的实参被忽略；若实参的个数少于形参的个数，那么多余的形参用“空”代替。另外必须注意，宏展开后即实参取代形参后，所得的语句必须是有效的，否则汇编程序将会指示出错。

设已定义了上述的宏MANDM。宏调用“MANDM SUB,VAR1,VAR2,VAR3”展开后的语句如下：

```assembly
1 	MOV 	AX,VAR1
1 	SUB 	AX,VAR2
1 	MOV 	VAR1,AX
```

显然，多余的实参VAR3被忽略。宏调用`MANDM MOV,VAR1`展开后的语句如下：

```assembly
1 	MOV 	AX.VAR1
1 	MOV 	AX,
1 	MOV 	VAR1,AX
```

汇编程序将指示语句`MOV AX,`有错。另外，如果没有定义VAR1或VAR2,那么汇编程序也会给出相应的出错提示信息。

### 7.2.4特殊的宏运算符

为了方便宏的定义和调用，汇编程序还支持如表7.1所示的特殊运算符，它们适用于宏的定义或调用中，还适用于重复块。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/root/image.7eugkbg11vg0.webp)

### 1.强迫替换运算符&

在宏定义中，若参数在其它字符的紧前或紧后，或者参数出现在带引号的字符串中时，就必须使用该运算符，以区分参数。

#### 例1:在下面定义的宏指令JUMP中，参数CON作为操作码的部分，请注意分隔符“&”的使用。

```assembly
JUMP 	MACRO CON,LAB
J&CON 	LAB
	ENDM
;调用宏JUMP及其宏扩展如下所示：
JUMP 	NZ,HERE
1 		JNZ 	HERE
		JUMP	Z,THERE
1		JZ		THERE
```

例2:下面定义的宏MSGGEN中，两个参数合并成标号，一个参数用在字符串中。

```assembly
MSGGEN 	MACRO LAB,NUM,XYZ
		LAB&NUM DB	'HELLO MR. &XYZ',ODH,OAH, 24H
	ENDM
```

调用宏MSGGEN及其宏扩展如下所示：

```assembly
	MSGGEN 	MSG,1,TAYLOR
1 	MSG1	DB	'HELLO MR.TAYLOR',ODH,0AH,24H
```

### 2.字符串原样传递运算符<>

字符串原样传递运算符是一对尖括号，在宏调用、重复块和条件汇编中，由它括起的内容作为一个字符串。在宏调用时，若实参包含逗号或空格等间隔符，则必须使用该运算符，以保证实参的完整。若实参是某个有特殊意义的字符，为了使它只表示字符本身，也可使用该运算符，把它括起来。

例3:定义如下的宏：

```assembly
DFMESS 	MACRO	MESS
		DB 		'& MESS', ODH, OAH, 0	;定义的字符串以0结尾
		ENDM
```

调用宏DFMESS及宏扩展如下所示：

```assembly
	DFMESS <This is a example>
1 	DB 	'This is a example', ODH, OAH, 0
```

如果不使用该运算符，则情况如下所示：

```assembly
	DFMESS This is a example
1 	DB 	'This',ODH,0AH,0
```

### 3.文字字符运算符！

该运算符使其后的一个字符只作为一般字符。在宏调用时，如果实参中含有一些特殊字符，为了使这些特殊字符作为一般字符来处理，那么就必须在其前冠上该运算符。

例4:利用上述宏DFMESS定义字符串“Can not enter99”。由于字符串含有特殊符号“>”，为避免它与上述字符串原样传递运算符相混，则必须在其前冠该运算符。相应的宏调用及宏扩展如下所示：

```assembly
	DFMESS < Can not enter ! > 99>
1 	DB	'Can not enter>99*,0DH,0AH,0
```

### 4.表达式运算符%

在宏调用时，使用该运算符能把其后表达式的结果作为实参替换，而非表达式自身。

例5:调用上述宏DFMESS,宏调用和宏扩展如下所示：

```assembly
	DFMESS		%(12+3-4)		;使用表达式运算符%
1	DB 			'11',0DH,0AH,0
	DFMESS 		12+3-4			;未使用表达式运算符%
1	DB 			'12+3-4',0DH,0AH,0
```

### 5.宏注释

在宏定义中，如果注释以两个分号引导，那么宏扩展时该注释不出现。

7.2.5宏与子程序的区别

采用宏和子程序这两种方法均能达到简化源程序的目的。但是，这两者之间存在质的不同。我们从宏调用与子程序调用之间的差异来说明这两者的区别。

1. 宏调用是通过宏指令名进行的，在汇编时，由汇编程序把宏展开，有多少次宏调用，就有相应次的宏扩展，因此并不简化目标程序。子程序调用是在程序执行期间执行CALI,指令进行的，子程序的代码只在目标程序中出现一次，所以目标程序也得到相应的简化。
2. 宏调用时的参数由汇编程序通过实参替换形参的方式实现传递，所以参数很灵活。子程序调用时的参数须通过寄存器、堆栈或约定的内存单元传递。
3. 宏调用是在汇编时完成，所以不需要额外的时间开销。子程序调用和子程序返回均需要时间，且还涉及堆栈。

总之，当程序片段不长，速度是主要矛盾时，通常采用宏指令的方法简化源程序；当程序片段较长，额外操作所附加的时间就不明显，而节约存储空间是主要矛盾时，通常采用子程序的方法简化源程序和目标程序。

## 7.2.6与宏有关的伪指令

### 1.局部变量说明伪指令LOCAL

在宏定义体中可使用标号。例如：

```assembly
HTOASC 	MACRO
		AND 	AL,OFH				;屏蔽高4位
		CMP		AL,9
		JBE 	ISDECM				;不大于9时转
		ADD		AL,7
ISDECM: 
		ADD 	AL,30H
		ENDM
```

如果在程序中多次调用上述宏HTOASC,汇编时将出现标号重复定义错误。原因是每次展开宏HTOASC都得到一个标号ISDECM。为此，汇编程序提供伪指令LOCAL,供程序员说明宏的局部标号。

伪指令LOCAL的一般格式如下：

> LOCAL 标号表

标号表由标号构成，标号间用逗号分隔。汇编程序在每次展开宏时，总把由LOCAL伪指令说明的标号用唯一的符号（?? 0000 至 ?? FFFF)代替，从而避免标号重定义错误。为了允许程序多次调用宏HTOASC,应利用LOCAL说明标号ISDECM,如下：

```assembly
HTOASC 	MACRO
		LOCAL 	ISDECM
		AND		AL,OFH
		CMP		AL,9
		JBE 	ISDECM
		ADD		AL,7
ISDECM:
		ADD		AL,30H
		ENDM
```

如果在程序中有如下宏调用：

```assembly
......
HTOASC
......
HTOASC
......
```

那么，在汇编时就得到如下宏扩展：

```assembly
		......
		AND 	AL,OFH
1	 	CMP 	AL,9
1 		JBE 	?? 0000
1		ADD 	AL,7
1 ?0000:ADD		AL,30H
		......
1		AND 	AL,OFH
1 		CMP 	AL,9
1 		JBE 	?? 0001
1 		ADD 	AL.7
1 ?0001:ADD		AL,30H
		......
```

必须注意，LOCAL伪指令用在宏定义体内，而且它必须是宏定义伪指令MACRO后的第一条语句，在MACRO和LOCAL伪指令之间还不允许有注释和分号标志。

### 2.清除宏定义的伪指令PURGE

伪指令PURGE的作用是告诉汇编程序取消某些宏。其一般格式如下：

> PURGE 宏名表

宏名表由宏名构成，宏名之间用逗号分隔。汇编程序在遇到PURGE伪指令后，就取消由宏名表所列出的宏定义，此后不再扩展这些宏。

如果利用宏定义的方法改变了某些指令助记符的意义，那么在用PURGE伪指令取消有关宏后，就恢复了有关指令助记符的原始意义。

例如：我们先定义如下宏：

```assembly
LODSB 	MACRO
		MOV 	AH,[SI]
		INC 	SI
		ENDM
```

程序中使用如下指令：

```assembly
......
LODSB
PURGE 	LODSB
......
LODSB
```

那么，在汇编时就得如下实际代码：

```assembly
......
1	MOV AH,[SI]
1	INC SI
......
	LODSB
```

3.中止宏扩展的伪指令EXITM

伪指令EXITM通知汇编程序结束当前宏调用的扩展。一般格式如下：

```assembly
EXITM
```

当遇到伪指令EXITM时，汇编程序立即退出宏，在宏中剩下的语句不被扩展。如果在一嵌套的宏内遇到伪指令EXITM,则退出到外层宏。

伪指令EXITM通常与条件伪指令一起使用，以便在规定的条件下跳过宏内的最后的语句。

## 7.2.7宏定义的联套

宏定义的联套有两种情况：宏定义体中含宏调用；宏定义体中含宏定义。下面分别举例说明。

### 1.宏定义体中调用宏

宏汇编语言允许在宏定义体中使用宏调用，其限制条件仍是：必须先定义后调用。如下宏WHTOASC的定义体内就调用了宏HTOASC(见7.2.1):

```assembly
WHTOASC 	MACRO
		MOV 	AH,AL
		SHR		AL,1
		SHR 	AL,1
		SHR 	AL,1
		SHR 	AL,1
		HTOASC
		XCHG 	AH,AL
		HTOASC
		ENDM
```

如果在程序中调用了上述宏，那么在汇编时可得如下的宏展开：

```assembly
1 	MOV AH,AL
1 	SHR AL,1
1 	SHR AL,1
1 	SHR AL,1
1 	SHR AL,1
2 	AND AL,OFH
2 	ADD AL,90H
2 	DAA	
2 	ADC AL,40H
2 	DAA
1 	XCHG AH,AL
2 	AND AL,OFH
2 	ADD AL,90H
2 	DAA
2 	ADC AL,40H
2 	DAA
```

注意，指令前的符号表示该指令是宏展开的结果。

### 2.宏定义体中定义宏指令

宏定义体中还可含有宏定义，但只有在调用了外层的宏后，才能调用内层的宏。原因是只有在调用了外层的宏后，内层的宏定义才有效。

下面的宏DEFMAC含有一个宏定义，并且外层宏的参数MACNAME是内层的宏指令名：

```assembly
DEFMAC 	MACRO 	MACNAME,OPERATOR
		MACNAME MACRO X,Y,Z
		PUSH 	AX
		MOV 	AX,X
		OPERAT
		OR 		AX,Y
		MOV 	Z,AX
		POP		AX
		ENDM
		ENDM
```

下面的三次宏调用就生成三条宏指令ADDITION、SUBTRACT和LOGOR:

```assembly
DEFMAC 	ADDITION,ADD
;
DEFMAC 	SUBTRACT,SUB
;
DEFMAC 	LOGOR,OR
```

此后，就可调用这三个宏了。如下的宏调用：

```assembly
ADDITION VARI,VAR2,RESULT
```

可得以下宏扩展：

```assembly
1 	PUSH AX
1 	MOV AX,VAR1
1 	ADD AX,VAR2
1 	MOV RESULT,AX
1 	POP AX
```

# 7.3重复汇编

有时程序中会连续地重复完全相同或几平相同的一组语句，当出现这种情况时，可考虑用重复伪指令定义的重复块，以简化源程序。

重复块是允许建立重复语句块的宏的一种特殊形式。它们与宏的不同之处在于它们没有被命名，并因而不能被调用。但象宏一样，它们可以有参数，且在汇编过程中参数可被实自变量代替；宏运算符、用伪指令LOCAL说明的符号等可用在重复块中，重复块总是由伪指令ENDM结束。

本节介绍由伪指令REPT、IRP和IRPC定义的三种重复块，它们之间的不同点在于如何规定重复的次数。

## 7.3.1伪指令REPT

伪指令REPT用于创建重复块，重复块的重复次数由一数值表达式给定。一般格式如下：

```assembly
REPT	表达式
需重复的语句组
ENDM
```

宏汇编程序将把“需重复的语句组”连续地重复汇编由表达式值所决定的次数。表达式必须可求出数值常数（16位的无符号数）。任何有效的汇编程序语句均可以安排在“需重复的语句组”中。

例如：下面的语句实现把字符A到Z的ASCII码填入数组TABLE中：

```assembly
CHAR 	= 'A'
TABLE 	LABEL 	BYTE
		REPT 	26			;重复块开始，规定重复次数
		DB 		CHAR		;需重复的语句1
CHAR	=		CHAR+1		;需重复的语句2
		ENDM				;重复块结束
```

实际的汇编结果与对如下指令的汇编结果相同:

```assembly
TABLE 	LABEL 	BYTE
		DB 		'A'
		DB 		'A'+1
		DB 		'A'+2
		.....
		DB 		'A'+24
		DB 		'A'+25
```

7.3.2伪指令IRP

伪指令IRP用于创建重复块，重复次数和每次重复时使用的实参由实参数列表决定。一般格式如下：

```assembly
IRP	形式参数，<实参1,实参2.....实参n>
需重复的语句组
ENDM
```

实参的个数规定了重复的次数。宏汇编程序将把“需要重复的语句组”连续地重复汇编规定的次数，并在每次重复时依次用相应位置的实参数代替“需重复语句组”中的形式参数。实参数列表应放在一对尖括号内，若有多个实参数，则各实参数间用逗号分隔。

例如：下面的重复块实现把0~9的平方值存入数组QUART中：

```assembly
QUART 	LABEL 	BYTE
		IRP		X,<0,1,2,3,4,5,6,7,8,9>
		DB 		X*X
		ENDM
```

再如：下面的重复块实现把若干寄存器值压入堆栈：

```assembly
IRP		REG,<AX,BX,CX,DX>
PUSH 	REG
ENDM
```

实际的汇编结果与对如下指令的汇编结果相同：

```assembly
PUSH	AX
PUSH 	BX
PUSH 	CX
PUSH	DX
```



## 7.3.3伪指令IRPC

伪指令IRPC与伪指令IRP相似，但实参数列表是一个字符串，一般格式如下：

```assembly
IRPC 	形式参数,字符串
需重复的语句组
ENDM
```

字符串的长度规定了重复的次数。宏汇编程序将把“需要重复的语句组”连续地重复汇编规定的次数，并在每次重复时依次用“字符串”中的一个字符作为实参数代替“需重复语句组”中的形式参数。如果字符串含有空格、逗号等分隔符，那么字符串需用一对尖括号括起来。

例如：下面的重复块也实现把从2开始的10个偶数存入字数组TABLE中：

```assembly
TABLE 	LABEL 	BYTE
		IRPC	X,0123456789
		DW		(X+1)*2
		ENDM
```

再如，下面的重复块实现把AX、BX、CX和DX四个寄存器依次压入堆栈：

```assembly
IRPC 	REG, 	ABCD
		PUSH REG&X
		ENDM
```

注意，上述重复块中使用了分隔宏参数的强迫替换运算符“&。”。

# 7.4条件汇编条件

汇编语句提供根据某种条件决定是否汇编某段源程序的功能。在源程序中使用条件汇编语句的主要目的是：

1. 通过在汇编前或汇编时改变某种条件，从而方便地产生功能不同的程序；
2. 增强宏定义能力，使得宏的适用范围更广；
3. 改善汇编效率。

尽管条件汇编语句在形式上与高级语言中的条件语句相似，但本质上却是完全不同的。条件汇编语句是说明性语句，是由伪指令构成，它的功能由汇编程序实现；一般高级语言的条件语句是执行语句，它的功能由目标程序实现。

## 7.4.1条件汇编伪指令

条件汇编语句的一般格式如下：

```assembly
IFxxxx	条件表达式
	语句组1
[ ELSE 
	语句组2 ]
ENDIF
```

IFxxxx是条件伪指令助记符的一般形式，其中xxxx表示构成条件伪指令助记符的其他字符。完整的条件伪指令助记符如下：

```assembly
IF 	IFE 	IFDEF 	IFNDEF 	IF1 	IF2
IFB	IFNB	IFIDN	IFDIF
```

一定要在条件语句的最后安排伪指令ENDIF。条件表达式用于表示条件，不同的条件汇编指令，条件表达式的形式也有所不同。语句组可含有任意正确的语句，包括其他的条件语句。伪指令ELSE及语句组2是可选的。

条件汇编语句的一般意义为：如果条件伪指令要求的条件满足，那么汇编语句组1否则不汇编语句组1;在含有ELSE伪指令的情况下，如果条件不满足，则汇编语句组2。

由于在上述形式中的语句组1或语句组2可再含有条件汇编语句，所以就可能形成条件汇编语句的嵌套。汇编语言允许的嵌套层数足以满足一般应用需要。一个眼套的ELSE 伪指令总是与最近但又没有ELSE的IFxxxx伪指令相配。

### 1.伪指令IF和IFE

伪指令IF的一般格式如下：

```assembly
IF	表达式
```

如果表达式的值不等于0,则条件满足，即条件为真。表达式不能包含向前引用，其结果应为一常数值。

伪指令IFE的一般格式如下：

```assembly
IFE	表达式
```

如果表达式的值等于0,则条件满足，即条件为真。表达式不能包含向前引用，其结果应为一常数值。IFE伪指令的条件与IF伪指令的条件相反。

例1:在下面的条件语句中，如果MFLAG值不为0,即条件满足，那么就汇编语句组1,否则汇编语句组2:

```assembly
if 		MFLAG
MOV		AH,0			;语句组1
INT		16H				;当MFLAG值不为0时，汇编此语句组
else
MOV		AH,1			;语句组2
INT		21H				;当MFLAG值为0时，汇编此语句组
endif
```

为了便于表示条件，条件表达式有时是关系表达式或逻辑表达式。

例2:在下面的条件语句中，条件表达式是一个关系表达式，根据关系表达式的求值方法，如果PORT值为0,则该关系表达式的值为1,不为0,所以条件满足：

```assembly
if 	PORT 	EQ 0
PORTADDR	=	3F8H
IVECTN		=	OBH		;条件满足时汇编此语句组
IMASKV 		= 	11110111B
endif
```

例3:如下定义的宏SHIFTL使用了重复块和结束宏扩展伪指令EXITM:

```assembly
SHIFTL 	MACRO	OP,N
COUNT	=		0
		REPT 	N
		SHL 	OP,1
COUNT	=		COUNT+1
		if 		COUNT GE N
		EXITM
		endif
		ENDM
		INC		OP
		ENDM
```

调用该宏和扩展的情况如下：

```assembly
		SHIFTL 	AX,1
2 		SHL 	AX,1
1 		INC 	AX
		;
		SHIFTL 	BX,3
2 		SHL 	BX,1
2 		SHL 	BX,1
2 		SHI	 	BX,1
1 		INC 	BX
```

请注意伪指令EXITM的作用，它只中止一层扩展或重复。

### 2.伪指令IFDEF和IFNDEF

伪指令IFDEF的一般格式如下：

```assembly
IFDEF	符号
```

如果符号已定义或被说明成外部符号，则条件满足，即条件为真。

伪指令IFNDEF的一般格式如下：

```assembly
IFNDEF	符号
```

如果符号未定义或未被说明成外部符号，则条件满足，即条件为真。伪指令IFDEF的条件与伪指令IFNDEF的条件相反。

例如：在下面的条件语句中，如果已先定义符号MLARGE,则条件满足，那么过程AXINC被定义为远过程，否则过程AXINC被定义成近过程：

```assembly
		ifdef MLARGE
AXINC 	PROC FAR				;若已定义MLARGE则汇编此语句
		else
AXINCS 	PROC NEAR				;若未定义MLAGER则汇编此语句
		endif
		INC	AX					;不受影响
		RET 					;不受影响
AXINC 	ENDP
```

符号可以在源程序中定义，也可以在汇编命令行中定义。如何在汇编命令行中定义符号，请参见有关汇编器使用手册。

### 3.伪指令IF1和IF2

伪指令IF1的格式如下：

```assembly
IF1
```

若是第一趟扫描则条件为真。

伪指令IF2的格式如下：

```assembly
IF2
```

若是第二趟扫描则条件为真。

## 7.4.2条件汇编与宏结合

## 1.宏中使用条件汇编

条件汇编与宏相结合，能大大扩大宏的使用范围。

例如：如下定义的宏ADDNUM有两个参数，在对宏调用扩展时，能根据不同的参数扩展成不同的指令：

```assembly
ADDNUM 	MACRO REG,NUM				;宏定义
		if 	(NUM GT【>】 2) OR (NUM 【<=】LE 0)
		ADD REG,NUM
		else
		INC REG
		if NUM EQ 2
		INC REG
		endif
		endif
		ENDM
```

宏调用举例和相应的扩展情况如下：

```assembly
		ADDNUM		AX,4
1 		ADD 		AX,4
;
		ADDNUM 		AX,2
1		INC 		AX
1		INC			AX
;
1		ADDNUM 		AX,1
1 		INC 		AX
;
		ADDNUM 		AX,0
1 		ADD 		AX,0
```

汇编语言还提供了专门用于测试宏参数的条件汇编伪指令，下面就介绍这些伪指令。

### 1.伪指令IFB和IFNB

伪指令IFB一般使用在宏定义内，格式如下：

```assembly
IFB <参数>
```

如果在宏调用时没有使用实参来代替该形参，那么条件满足。注意，参数应该用尖扩号括起。

伪指令IFNB一般使用在宏定义内，格式如下：

```assembly
IFNB <参数>
```

如果在宏调用时使用实参来代替该形参，那么条件满足。注意，参数应该用尖扩号括起。伪指令IFNB的条件与伪指令IFB的条件相反。

例如：如下定义的宏PRINT,若指定显示信息时，则显示之，否则显示缺省信息：

```assembly
PRINT 	MACRO 	MSG
		ifb 	<MSG>
		MOV 	SI,DEFAULTMSG
		else
		MOV 	SI,MSG
		endif
		CALL 	SHOWIT
		ENDM
```

### 2.伪指令IFIDN和IFDIF

伪指令IFIDN一般使用在宏定义内，格式如下：

```assembly
IFIDN	<参数1>,<参数2>
IFIDNI	<参数1>,<参数2>
```

如果字符串参数1与字符串参数2相等，则条件满足。参数1或参数2可能是宏定义中的形参，如果是形参，比较之前先由相应的实参所代替。字符串是按字符逐个比较的式一对大小写有区别，格式二忽略大小写区别。注意，参数应用尖括号括起。伪指令IFDIF一般使用在宏定义内，格式如下：

```assembly
IFDIF	<参数1>,<参数2>
IFDIFI 	<参数1>,<参数2>
```

如果字符串参数1与字符串参数2不等，则条件满足。其他说明同上。例如：如下定义的宏RDWR的第二个参数就决定了读写方式：

```assembly
RDWR 	MACRO 	BUFF,RWMODE
		LEA 	DX,BUFF
		ifidni 	<RWMODE>,<READ>
		CALL 	READIT
		endif
		ifidni
		CALL 	WRITEIT
		endif
		ENDM
```

宏调用举例和相应的扩展如下：

```assembly
		RDWR 	MESS,Write
1 		LEA 	DX,MESS
1 		CALL 	WRITEIT
;
		RDWR 	MESS,Read
1 		LEA 	DX,MESS
1 		CALL READIT
```

例如：如下定义的宏GETCH就有加强的功能：

```assembly
GETCH 	MACRO 	CHAR
		MOV		AH,1
		INT 	21H
		ifnb 	<CHAR>
		ifdifi 	<CHAR>,<AL>
		MOV 	CHAR,AL
		endif
		endif
		ENDM
```

# 7.5源程序的结合

为了便于编辑源程序和对程序进行修改或维护，汇编程序允许把源程序存放在多个文本文件中，在汇编时结合到一起，同时参加汇编。本节介绍源程序的结合方法和应用。

## 7.5.1源程序的结合

存放在若干文本文件中的源程序的结合是利用伪指令INCLUDE完成的。它的一般格式如下：

```assembly
INCLUDE 文件名
```

伪指令INCLUDE指示汇编程序将指定的文本文件从本行起加入汇编，直到该文本文件的最后一行汇编完后，继续汇编随后的语句。

文件名可带有盘符和路径，采用DOS有关规则表示。若文件名没有盘符或路径，则首先在由汇编命令行参数/I所指定的目录中寻找该文件，然后再在当前目录中寻找该文件。对于MASM而言，最后还会在由环境变量INCLUDE所指定的目录中寻找该文件。对于TASM而言，若文件名没有扩展名，则假设扩展名为ASM

下列程序的功能是：接受一个字符串，然后按小写和大写形式重新显示字符串。整个源程序存放在三个文本文件中。

```assembly
;程序名：T7-2.ASM
;功能：(略）
INCLUDE DATA.ASM		;结合含数据部分的文件DATA.ASM
CSEG 	SEGMENT
		ASSUME CS:CSEG,DS:DSEG
START:
		MOV		AX,DSEG
		MOV 	DS,AX
		CLD
		MOV 	DX,OFFSET MESS1 	;显示提示信息
		MOV 	AH,9
		INT 	21H
		MOV 	AH,10				;接受字符串
		MOV 	DX,OFFSET BUFFER
		INT 	21H
		CALL 	NEWLINE				;另起一行
		MOV 	BL,BUFFER+1
		XOR 	BH,BH
		MOV 	BUFFER[BX+2],0		;把接收到的字符串标为0结尾
		MOV 	SI,OFFSET STRBEG
		CALL 	STRLWR				;转换成小写字符串
		MOV 	SI,OFFSET STRBEG
		CALL 	DISPMES				;显示之
		CALL 	NEWLINE				;另起一行
		MOV 	SI,OFFSET STRBEG
		CALL 	STRUPR				;转换成大写字符串
		MOV 	SI,OFFSET STRBEG
		CALL 	DISPMES				;显示之
		CALL 	NEWLINE				;另起一行
EXIT:
		MOV		AH,4CH				结束
		INT 	21H
		
NEWLINE PROC	
	;另起行，代码略。
NEWLINE ENDP

;子程序名：DISPMES
;功能：显示以0结尾的字符串
;入口参数：SI=字符申首地址偏移	
DISPMES 	PROC
DISPME1:
		LODSB
		OR 		AL,AL
		JZ 		DISPME2
		MOV 	DL,AL
		MOV 	AH,2
		INT 	21H
		JMP		DISPME1
DISPME2:
		RET
DISPMES ENDP
;
INCLUDE STRING.ASM 			;结合含子程序的文件STRING.ASM
CSEG 	ENDS
		END 	START
```

文本文件DATA.ASM含有数据部分，其内容如下所示：

```assembly
;文件名：DATA.ASM
;内容：程序T7-2.ASM的一部分
STRLEN	=	128
DSEG		SEGMENT
BUFFER	DB 	STRLEN,O,STRLEN DUP (0)
STRBEG	=	BUFFER+2
MESSI	DB	"Please input : $ "
DSEG		ENDS
```

文本文件STRING.ASM含有两个子程序STRLWR和STRUPR,它们分别实现把字符串转换成小写和大写，其内容如下所示：

```assembly
;文件名：STRING.ASM
;内容：程序T7-2.ASM的一部分
;子程序名：STRLWR
;功能：把字符串转换为小写
;入口参数：SI=字符串起始地址偏移
;其他说明信息略
STRLWR	PROC
		JMP 	STRLWR2
STRLWR1:
		SUB 	AL,'A'
		CMP 	AL,'Z'-'A'
		JA 		STRLWR2
		ADD 	AL,'a'
		MOV 	[SI-01],AL
STRLWR2:
		LODSB
		AND 	AL,AL
		JNZ 	STRLWR1
		RET
STRLWR 	ENDP
;
;子程序名：STRUPR
;功能：把字符串转换成大写
;入口参数：SI=字符串起始地址偏移
;其他说明信息略
STRUPR 	PROC
		JMP 	STRUPR2
STRUPR1: 
		SUB 	AL,'a'
		CMP 	AL,'z'-'a'
		JA 		STRUPR2
		ADD 	AL,'A'
		MOV		[SI-01],AL
STRUPR2: 
		LODSB
		AND 	AL,AL
		JNZ 	STRUPR1
		RET
STRUPR 	ENDP
```

## 7.5.2宏库的使用

通常程序员会把一组有价值和经常使用的宏定义集中存放在一个文本文件中，这样的文本文件称为宏库。有了宏库后，只要在源程序首安排结合宏库的伪指令INCLUDE,便能方便地调用宏库中的宏。这样做不仅节省编辑源程序的时间，而且能够减少错误。

例如：设已建立宏库DOSBIO.MAC,其内容如下：

```assembly
;接受一个字符串
GETSTR 	MACRO 	MBUFF
		MOV 	DX,MBUFF
		MOV 	AH,10
		INT 	21H
	ENDM:
	;
	;显示一个字符串
DISPSTR MACRO 	MBUFF
		MOV 	DX,MBUFF
		MOV 	AH,9
		INT		21H
		ENDM
	;取得一个字符
	;
GETCH 	MACRO 	CHAR
		MOV 	AH,1
		INT		21H
		IFNB 	<CHAR>
		IFDIF1 	<CHAR>,<AL>
		MOV 	CHAR,AL
		ENDIF
		ENDIF
		ENDM
		;
		;显示一个字符
ECHOCH 	MACRO 	CHAR
		IFNB 	<CHAR>
		IFDIFI 	<CHAR>,<DL>
		MOV 	DL,CHAR
		ENDIF
		ENDIF
		MOV		AH,2
		INT		21H
		ENDM
```

现在利用上述宏库DOSBIO.MAC改写程序T7-2.ASM如下：

```assembly
;程序名：T7-3.ASM
;功能：(略）
;
	INCLUDE		DOSBIO.MAC		;结合宏库DOSBIO.MAC
;
	INCLUDE 	DATA.ASM		;含数据部分的文件DATA.ASM
CSEG 	SEGMENT
		ASSUME CS:CSEG,DS:DSEG
START:
		MOV 	AX,DSEG
		MOV 	DS,AX
		CLD
		DISPSTR <OFFSET MESS1> 		;调用宏 DISPSTR
		GETSTR 	<OFFSET BUFFER>		;调用宏GETSTR
		CALL 	NEWLINE
		MOV 	BL,BUFFER+1
		;
		;与程序T7-2中相应内容相同
EXIT:
		MOV		AH,4CH
		INT		21H
NEWLINE PROC
		ECHOCH ODH					;调用宏ECHOCH
		ECHOCH OAH					;调用宏ECHOCH
		RET
NEWLINE	ENDP
;
DISPMES PROC
DISPME1:
		LODSB
		OR		AL,AL
		JZ 		DISPME2
		ECHOCH	AL					;调用宏ECHOCH
		JMP 	DISPME1
DISPME2:
		RET
DISPMES ENDP
;
INCLUDE STRING.ASM					;结合文件STRING.ASM
CSEG 	ENDS
		END		START
```

对上述程序有如下两点补充说明：

(1)由于MASM采用两遍扫描，并且只在第一遍扫描时登记宏定义，所以可使用如下条件汇编伪指令，通知汇编程序在第一遍扫描时加入宏库，而在第二遍扫描时，不加入宏库：

```assembly
IF1
INCLUDE DOSBIO.MAC
ENDIF
```

这样既加快了第二遍扫描的速度，又能避免在汇编列表清单中含有宏定义部分。但必须注意，如果结合的不是宏库，则不能只在第一遍扫描时结合，而在第二遍扫描时不结合。

(2)宏库中可含有多个宏定义，一个程序不一定调用宏库中定义的全部宏。例如，上述程序结合了宏库DOSBIO.MAC,但没有调用其中的宏GETCH。对于不使用的宏，可以用伪指令PURGE清除。清除操作如下所示：

```assembly
INCLUDE DOSBIO.MAC
PURGE 	GETCH
```

注意，上述清除操作对宏库没有影响。