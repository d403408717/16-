[TOC]

把一个程序分成具有多个明确任务的程序模块，分别编制、调试后再把它们连接在一起，形成一个完整的程序，这样的程序设计方法称为模块化程序设计。

模块化程序设计有如下优点：

1. 单个的程序模块易于编写、调试及修改；
2. 若于程序员可以并行工作，工作进度可加快；
3. 若干反复使用和验证过的程序模块可被利用
4. 程序的易读性好；
5. 程序的修改可局部化。

模块化程序设计的主要步骤是：

1. 正确地描述整个程序需要完成什么样的工作；
2. 把完整的任务划分成多个具有明确功能的程序模块，并明确各模块之间的相互关系；
3. 根据各模块的具体功能和地位，选择合适的程序设计语言，编写程序并初步调试；
4. 把各模块分别编译或汇编成目标模块，并连接到一起，经过调试形成一个完整的程序；
5. 整理文档资料。

虽然模块化程序设计的关键是模块的划分，但本章只介绍如何利用汇编语言编写符合要求的程序模块。

# 8.1段的完整定义

一个复杂的程序通常有若干模块组成。源模块可用汇编语言编写，也可用高级语言编写。每个源模块被单独汇编或编译成目标（OBJ)模块，最后由连接程序（LINKER)把各目标模块连接成一个完整的可执行的程序。

由于8086/8088采用分段的形式访问内存，所以一个模块往往又含有多个段。一个程序的若干模块之间存在的联系必定体现在模块间的段与段的联系上。

连接程序如何把若干模块的多个段恰当地组合到一起呢？如何沟通有关段之间的联系呢？实际上，汇编语言中的段定义伪指令等指示汇编程序把合适的连接信息写入到目标模块中，连接程序再根据目标模块中的连接信息进行连接操作

在新版的汇编语言中，有两种方法定义段：完整的段定义和简化的段定义。本节介绍段的完整定义。

## 8.1.1 完整的段定义

完整的段定义提供了彻底控制段的机制，该机制可使得各模块的各个段严格按要求组合和衔接。

### 1.一般格式

完整段定义的一般格式如下：

```assembly
段名 	SEGMENT [定位类型] [组合类型] ['类别']
	语句
......
段名	ENDS
```

段开始语句SEGMENT中的可选项“定位类型”、组合类型”和“类别””通知汇编程序和连接程序如何建立和组合段。应当按顺序说明这些可选项，但不需要给出所有可选项，如果不给出某个可选项，那么汇编程序使用该可选项的缺省值。在前面各章节所列程序中的段虽然均采用完整的定义，却都没有给出这些可选项。

段名可以是唯一的，也可以与程序中其他的段名相同。在同一模块中，如果已用相同的段名定义过段，那么当前这个段就被视为前一个同名段的继续，即同一个段。

对一个模块中的同名段而言，后续同名段的定义伪指令SEGMENT中的可选项取值应该与前一个同名段相同，或者不再给定可选项值而默认与前一个同名段相同。

#### 例1:如下程序T8-1.ASM中含有两个名为DSEG的段和两个名为CSEG的段：

```assembly
;程序名：T8-1.ASM
;功能：(略）
DSEG 	SEGMENT				;定义数据段DSEG
MESS 	DB 	'HEL'
DSEG 	ENDS
CSEG 	SEGMENT				;定义代码段CSEG
		ASSUME 	CS:CSEG,DS:DSEG
START:
		MOV 	AX,DSEG
		MOV 	DS,AX
		MOV 	DX,OFFSET MESS
		MOV 	AH,9
		INT		21H
CSEG 	ENDS
;
DSEG 	SEGMENT				;数据段DSEG的继续
		DB		'LO',0DH,0AH,'$'
DSEG	ENDS
;
CSEG 	SEGMENT				;代码段CSEG的继续
		MOV 	AX,4C00H
		INT		21H
CSEG 	ENDS
		END 	START
```

由于后一同名段被视为前一同名段的继续，所以汇编后只有DSEG和CSEG两个段，类似于程序T3-1.ASM的汇编结果。

下面介绍段定义伪指令中可选项的作用和所取值的意义。

### 2.定位类型

定位类型表示出当前段对起始地址的要求，从而指示连接程序如何衔接相邻两段。可选择的定位类型及所表示的起始地址列于表8.1。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/root/image.6pyxhabja3w0.webp)

一般情况下（80386以下）缺省的定位类型是PARA,即段起始地址位于可用的第一个节（每节为16个字节）的边界处。定位类型BYTE使得当前段紧接前一段，前后两段间没有空闲单元，所以是最节约的定位类型。定位类型WORD使得段从偶地址开始，不仅较为节约，而且有利于把数据单元定位在偶地址。定位类型DWORD常用于80386的32位段。一页等于256字节，所以定位类型PAGE可能导致最大的段间隔。

#### 例2:如下程序T8-2.ASM的两个段的定位类型均是PARA:

```assembly
;程序名：T8-2.ASM
;功能：(略）
DSEG 	SEGMENT 	PARA 	COMMON	;类型为PARA
MESS 	DB 		'HELLO!',0DH,0AH,'$'
DSEG 	ENDS
;
CSEG 	SEGMENT 	PARA 	PUBLIC	;定位类型为PARA
		ASSUME 	CS:CSEG,DS:DSEG
START:
		MOV		AX,DSEG
		MOV 	DS,AX
		MOV 	DX,OFFSET MESS
		MOV 	AH,9
		INT		21H
		MOV 	AX,4C00H
		INT		21H
CSEG 	ENDS
		END 	START
```

最后的可执行程序中两个段的衔接情况图8.1(a)所示。如果把段CSEG的定位类型改为WORD,衔接情况如图8.1(b)所示。如果把段CSEG的定位类型改为BYTE,衔接情况如图8.1(c)所示。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/root/image.4xc4kqzyse80.webp)

### 3.组合类型

不同模块的同名段的组合，为更有效更便利地使用存储器提供了方便。组合类型就是用于通知连接程序，如何把不同模块内段名相同的段组合到一起。有如下组合类型:**(1)PUBLIC**

组合类型PUBLIC表示当前段与其它模块中组合类型为PUBLIC的同名段组合成一个段。组合的先后顺序取决于启动LINK程序时目标模块名排列的次序。由于组合后受同一个段地址控制，所以组合时后续段的起始地址都作相应调整。但组合时仍遵照定位类型进行衔接，即同名段间可能有间隔。

**(2)COMMON**

组合类型COMMON表示当前段与其它模块中的同名段重叠，即起始地址相同。最终段的长度等于它们中最长的段的长度。由于段覆盖，所以前一同名段中的初始化数据可能被后续同名段中的初始化数据所覆盖。

**(3)STACK**

组合类型STACK表示当前段是堆栈段，组合情况与PUBLIC相同。

**(4)MEMORY**

组合类型MEMORY与组合类型PUBLIC相同，为兼容而设。

**(5)AT表达式**

它表示当前段应按绝对地址定位，其段地址即为表达式之值。一般AT段不包含代码和初始化数据，它仅用于表示已在内存中的代码或数据的地址样板，如显示缓冲区或其它由硬件定义的绝对存储单元。LINK程序不对AT段生成任何代码或数据。

**(6)PRIVATE**

组合类型PRIVATE表示不与其它段组合。宏汇编程序MASM不识别此关键字。

若段定义伪指令SEGMENT语句中没有给出组合类型，就表示不与其它段组合。

#### 例3:某个程序的第一个源程序模块是例2所给出的T8-2.ASM,第二个源程序模块如下所示：

```assembly
;程序（模块）名：T8-3.ASM
;功能：(略）
DSEG 	SEGMENT PARA COMMON
		DB 	'OK'
DSEG 	ENDSC
SEG 	SEGMENT PARA PUBLIC
		MOV		AH,4CH
		INT		21H
CSEG 	ENDS
		END
```

先把这两个源程序模块分别汇编，最后用LINK程序连接，命令行如下：

```assembly
LINK T8-2 + T8-3
```

模块2中的DSEG段和模块1中的DSEG段重叠，模块2中的CSEG段和模块1中的CSEG段合并成一个段。最后得到的可执行程序的内容如下所示：

```assembly
0000:0000	4F 4B 4C 4C 4F 0D 0A 24		;两个段重叠后的结果OKLLO..$
0000:0008	00 00 00 00 00 00 00 		;CSEG的定位类型是PARA导致的间隔
0001:0000 	MOV 	AX,26E2				;模块T8-2中的CSEG段
0001:0003 	MOV 	DS,AX
0001:0005 	MOV 	DX,0000
0001:0008 	MOV 	AH,09
0001:000A 	INT 	21
0001:000C 	MOV 	AX,4C00
0001:000F 	INT 	21
0001:0011 	00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 ;衔接间隔
0001:0020 	MOV 	AH,4C				;模块T8-3中的CSEG段
0001:0022	INT		21
```

### 4.类别

类别用于表示段的分类。LINK程序总是使类别相同的段相邻。实际上只有类别相同的同名段才根据组合类型进行组合。

类别是一个由程序员指定的字符串，但必须用单引号括起。如果一个段没有给出类别，那么这个段的类别就为空。例3中两个模块的四个段均没有给出类别，它们的类别均为空。

#### 例4:设某个程序的模块甲如下所示：

```assembly
;模块甲（MODULE1)
DSEG 	SEGMENT 	PARA 	PUBLIC 	'DATA'
		......
DSEG 	ENDS
;
CSEG 	SEGMENT 	PARA 	PUBLIC	'CODE'
START:
		....
CSEG 	ENDS
		END 	START
;再设该程序的模块乙如下所示：
;模块乙（名为MODULE2)
DSEG 	SEGMENT PARA PUBLIC	'XYZ'
		......
DSEG 	ENDS
；
ASEG 	SEGMENT PARA PUBLIC 'CODE'
		......
ASEG 	ENDS
		END
;再设该程序的模块丙如下所示：
;模块丙（名为MODELE3)
ESEG 	SEGMENT PARA PUBLIC 'DATA'
		......
ESEG 	ENDS
		END
;用如下命令连接它们的目标模块：
LINK 	MODULE1+MODULE2+MODULE3
```

在得到的可执行程序中，各段的排列次序是：模块甲的DSEG、模块丙的ESEG、模块甲的CSEG、模块乙的ASEG和模块乙的DSEG。

## 8.1.2 关于堆栈段的说明

一个完整的汇编语言源程序一般应该含有一个堆栈段，只有COM型程序例外。

当把某个段的组合类型指定为STACK时，这个段就被指定为堆栈段了。也就是说，组合类型STACK不仅是某种组合类型，而且能够表示当前段是堆栈段。当然，如果在程序的其他模块中也有组合类型为STACK的同名段，那么连接时将以接续的方式组合到一起，这样会构成一个存储空间更大的堆栈。

#### 例5:为程序T8-1.ASM增加一个大小为1024字节的堆栈段。

```assembly
;程序名：T8-1A.ASM
;功能：(略）
SSEG 	SEGMENT PARA STACK			;定义堆栈段
		DB 	1024 	DUP (?)		
SSEG 	ENDS
;
DSEG 	SEGMENT PARA COMMON
MESS 	DB	'HELLO',ODH,0AH,'$'
DSEG 	ENDS
;
CSEC 	SEGMENT PARA PUBLIC
		;内容与程序T8-1中的代码段内容相同
CSEG 	ENDS
		END 	START
```

LINK程序会把组合类型为STACK的段的有关信息写入可执行程序文件中。于是在执行该程序时，操作系统的装入程序就能根据这些信息自动设置寄存器SS和SP,从而构成物理堆栈。设置的SS值是组合类型为STACK的段的段值，设置的SP值是堆栈段的大小，即SS:SP指向堆栈尾。

如果在说明堆栈段时不指明组合类型STACK,虽然在主观上想让它用作堆栈段，但没有得到汇编程序和连接程序的承认，所以必须在代码段中安排传送指令来设置寄存器SS和SP。

例如：设源程序中有如下准备用于堆栈的段：

```assembly
SSEG 	SEGMENT 	PARA
		DB 	1024 	DUP (?)
STOP 	LABEL WORD
SSEG 	ENDS
```

那么在代码段中可用如下指令设置堆栈：

```assembly
CLI
MOV		AX,SSEG
MOV 	SS,AX
MOV 	SP,OFFSET STOP
STI
```

由于硬件中断和程序使用同一个堆栈，所以在切换堆栈时要关中断。

允许一个汇编语言源程序不含有堆栈段。如果LINK程序没有发现堆栈段，那么它会发出警告信息，并把第一段的段值作为堆栈的段值，堆栈空间设定为64KB。我们在先前章节中给出的程序几乎都没有说明堆栈段，所以这些程序都使用这个缺省的堆栈段。如果认为这个缺省的堆栈段是可行的话，那么在程序中可不必说明堆栈段，也不必理会LINK程序给出的警告信息。

无论在程序中是否说明堆栈段，只要需要，都可通过重置寄存器SS和SP来切换堆栈，从而建立合适的新堆栈。

## 8.1.3段组的说明和使用

先看下面的程序T8-4.ASM。它含有两个数据段和一个代码段。为了说明问题，把数据段和代码段都作了简化。

```assembly
;程序名：T8-4.ASM
;功能：(略）
DSEG1 	SEGMENT 	PUBLIC		;数据段1
VAR1 	DB 	?
DSEG1 	ENDS
DSEG2 	SEGMENT 	PUBLIC		;数据段2
VAR2 	DB 	?
DSEG2 	ENDS
;
CSEG 	SEGMENT PARA PUBLIC		;代码段
		ASSUME CS:CSEG,DS,DSEG1		;使CS对应段CSEG
									;使DS对应段DSEG1
START:
		MOV 	AX,DSEG1
		MOV 	DS,AX				;置DS寄存器
		MOV 	BL,VAR1
		;......
		ASSUME 	DS DSEG2			;使DS对应段DSEG2
		MOV 	AX,DSEG2
		MOV 	DS,AX				;重置DS寄存器
		MOV 	VAR2,BL
		;......
		MOV 	AH,4CH
		INT		21H
CSEG 	ENDS
		END 	START
```

在上述程序T8-4中，为了访问变量VAR1,设置了数据段寄存器DS,为了访问变量VAR2而重置了DS。如果要频繁地交叉访问段DSEG1和段DSEG2中的数据，那么不仅很麻烦，而且程序也会变得穴长。改进的方法是使附加段寄存器ES对应段DSEG2,并把DSEG2段的段值置入ES,通过段超越前缀“ES:”来实现对段DSEG2中数据的访问。但改进的方法仍会使目标代码有介余。

好的方法是把DSEG1和DSEG2段作为一个段来处理。段组就是为实现此目的服务的。若程序员要在源代码的各独立段中安排几种数据类型，且要在执行时能通过一个独立的、公用的段寄存器访问它们，就可使用段组。

伪指令GROUP用于把源程序模块中若干不同名的段集合成一个组，并赋予一个组名。它的一般格式如下：

```assembly
组名	GROUP	段名	[,段名....]
```

其中，段名与段名间用逗号间隔，段名也可用由表达式“SEG变量”或者表达式“SEG标号”代替。

#### 例6:利用段组，改写程序T8-4.ASM。改写的程序如下：

```assembly
;程序名：T8-4G.ASM
;功能：(略）
DSIS2 	GROUP 	DSEG1,DSEG2			;说明段组
;
DSEG1 	SEGMENT PUBLIC				;数据段1
VAR1	DB		?
DSEG1 	ENDS
;
DSEG2 	SEGMENT PUBLIC				;数据段2
VAR2 	DB 		?
DSEG2 	ENDS
;
CSEG 	SEGMENT PARA PUBLIC 		;代码段
		ASSUME CS:CSEG,DS:DSIS2		;使DS对应组DS1S2
START:
		MOV		AX,DSIS2
		MOV 	DS,AX				;置DS寄存器
		MOV 	BL,VAR1
		;......
		MOV 	VAR2,BL
		;......
		MOV 	AH,4CH
		INT		21H
CSEG 	ENDS
		END 	START
```

组名表示组，也代表组的起始地址。组名的使用与段名的使用类似。上述程序中的指令“MOV AX,DS1S2”是把组DSIS2的起始地址的段值送AX寄存器。段组名也可用在ASSUME语句中，表示使某个段寄存器与某个组相对应。

在定义段组后，段组内各段所定义的所有标号和变量除与定义它们的段起始点相关外还与组的起始点相关。如果在ASSUME伪指令中使段寄存器与段组对应，那么有关标号或变量的地址就相对于段组起点计算；如果在ASSUME伪指令中使段寄存器与组内某个段对应，那么有关标号或变量就相对于该段的起点计算。所以在使用段组后，程序员要谨慎地使用ASSUME伪指令，并保证具体置入段寄存器的值与之相适应。

#### 例7:如下程序T8-5.ASM说明了如何把变量作为组的成员访问和把变量仅作为段内的成员访问。

```assembly
;程序名：T8-5.ASM
;功能：(略）
DGROUP 	GROUP 	CSEG,DSEG			;说明段组
;
CSEG 	SEGMENT
		ASSUME CS:CSEG,DS:DGROUP	;使DS与组对应
START:
		MOV 	AX,DGROUP
		MOV 	DS,AX
		MOV 	BL,VAR1				;作为组内成员访问
		MOV 	VAR2,BL
		;
		ASSUME 	DS:DSEG				;使DS与段DSEG对应
		MOV 	AX,DSEG
		MOV 	DS,AX
		MOV 	BH,VAR1				;仅作为段内成员访问
		MOV 	VAR2,BH
		MOV 	AH,4CH
		INT 	21H
CSEG 	ENDS
;
DSEG 	SEGMENT						;数据段DSEG
VAR1	DB	'A'
VAR2 	DB	'B'
DSEG 	ENDS
		END 	START
```

在把连接后所得的可执行程序T8-5装入内存时的映象如下（设开始段值为26E2H):

```assembly
26E2:	0000	MOV		AX,26E2			;CSEG段
		0003 	MOV 	DS,AX
		0005 	MOV 	BL,[0020]		;MOV AL,VAR1
		0009	MOV		[0021],BL		;MOV VAR2,AL
		000D	MOV		AX,26E4
		0010 	MOV 	DS,AX
		0012 	MOV 	BH,[0000]		;MOV BH,VAR1
		0016 	MOV 	[0001],BH 		;MOV VAR2,BH
		001A 	MOV 	AH,4C
		001C	INT		21
		001D	00 00 00				;段之间隔
26E2:	0020	41 42					;DSEG段（即26E4:0)
```

如果要用运算符OFFSET得到在段组内某个段中定义的标号或变量相对于段起始点的偏移，那么必须在标号或变量前再加上组名。例如：

```assembly
MOV DX,OFFSET DGROUP VAR1
```

否则，只能得到相对于所在段起始点的偏移。

段组并不直接影响连接时段的次序，组内各段不必连续，不属于这组的某个段可以夹在组内的两个段之间。由于要通过一个段寄存器访问组内各段，所以连接后组内所有段仍必须保证在64KB以内。

在纯汇编语言程序中，尽管程序员可以按自己的愿望使用段组，但使用段组并没有太多必要

# 8.2段的简化定义

完整的段定义使得程序员可以完全控制段，但较为复杂。新版汇编语言提供了段的简化定义方法，从而使程序员能方便地定义段。无论是编写独立的汇编语言程序，还是编写供高级语言程序调用的函数，简化的段定义伪指令几乎总使程序设计更容易。本节介绍段的简化定义。

## 8.2.1存储模型说明伪指令

在程序中使用段简化定义伪指令之前，必须先使用存储模型说明伪指令描述程序采用的存储模型。存储模型说明伪指令的简单格式如下：

```assembly
MODEL	存储模型
```

注意该伪指令以符号点“.”引导。例如，为了说明采用SMALL存储模型，只要在源程序首使用如下伪指令：

```assembly
.MODEL SMALL
```

常用的存储模型有：

#### (1)SMALL

全部数据限制在单个64KB段内；全部代码也限制在单个64KB段内。这是独立的汇编语言程序最常用的模型。在这种存储模型下，数据段寄存器可保持不变，所有转移均认为是段内转移。

#### (2)MEDIUM

全部数据限制在单个64KB段内；但代码可大于64KB。在这种存储模型下，数据段寄存器可保持不变，但会出现段间转移的情形。

#### (3)COMPACT

全部代码限制在单个64KB段内；数据总量可大于64KB,但一个数组不能大于64KB。

#### (4)LARGE

代码可超过64KB;数据也可超过64KB,但一个数组不能大于64KB。

#### (5)HUGE

代码可超过64KB;数据也可超过64KB,并且一个数组也能大于64KB。

独立的汇编语言程序可选用任一种存储模型，对大多数完全用汇编语言编写的程序来说，小（SMALL)模型就足够了。

## 8.2.2简化的段定义伪指令

### 1.简化的段定义伪指令

简化的段定义伪指令均以符号点引导。下面介绍常用的简化段定义伪指令：

#### (1)定义代码段的伪指令

定义代码段的伪指令如下，它表示一个代码段的开始：

```assembly
.CODE
```

##### 例1:写一个使系统喇叭发出“嘟”一声的程序。

```assembly
;程序名：T8-6.ASM
;功能：(略）
		.MODEL		SMALL	;说明采用小模型
		.CODE				;说明代码段开始
START:
		MOV		DL,7
		MOV		AH,2
		INT		21H
		MOV 	AX,4C00H
		INT 	21H
		END START			;结束代码段
```

上述程序就一个代码段，没有数据段和堆栈段。伪指令.CODE说明代码段的开始，伪指令END说明段结束。

简化的段定义伪指令说明一个段的开始，同时也表示上一个段的结束。伪指令END说明最后一个段的结束。

#### (2)定义堆栈段的伪指令

定义堆栈段的伪指令一般格式如下，它表示一个堆栈段的开始

```assembly
.STACK	[大小]
```

可选的“大小”说明堆栈的字节数，若没有指定堆栈大小，则采用缺省值1024。如下的伪指令就表示定义一个2KB的堆栈。

```assembly
.STACK 2048
```

通常只有在编写纯粹的汇编语言程序时才需要定义堆栈。

#### (3)定义数据段的伪指令

定义（初始化）数据段的伪指令如下，它表示数据段的开始：

```assembly
.DATA
```

例如：如下伪指令定义了一个数据段：

```assembly
		.DATA
VAR3	DB	5
IARRAY 	DW 	50 	DUP(0)
MESS 	DB	'HELLO',ODH,OAH,'$'
```

##### 例2:利用简化的段定义伪指令改写8.1.2节的程序T8-1A.ASM。

```assembly
;程序名：T8-1B.ASM
;功能：(略）
.MODEL 	SMALL		;说明存储模型
.STACK 	1024		;定义堆栈段
.DATA				;说明数据段开始
MESS 	DB 	'HELLO', ODH, OAH,'$'
		.CODE				;说明代码段开始
START:
		MOV 	AX,DGROUP	;把段组的起始段值
		MOV 	DS,AX		;置入数据段寄存器DS
		MOV 	DX,OFFSET MESS
		MOV		AH,9
		INT		21H
		MOV 	AX,4C00H
		INT		21H
		END START
```

在一个源程序模块中可定义多个由伪指令.DATA开始的数据段，这如同在一个源程序模块中定义多个同名的数据段。

此外，还有伪指令.DATA?和伪指令.CONST,它们分别用于说明未初始化数据段的开始和常数数据段的开始。在编写纯粹的汇编语言程序时，一般不使用这两条伪指令，因为在由伪指令.DATA说明的数据段中也可以定义未初始化的数据和常数数据。除非为了遵守高级语言的约定，可能使用这两条伪指令。

宏汇编程序自动把可能存在的由.DATA说明的数据段、由.CONST说明的常数数据段、由.DATA?说明的未初始化数据段和由.STACK说明的堆栈段集合成一个段组。那么如何定义一个不属于这个段组的独立数据段呢？如何定义一个较大的数据段呢？这可利用伪指令，FARDATA来实现。

#### (4)定义远程（独立）数据段的伪指令

定义独立数据段伪指令的一般格式如下，它表示一个独立数据段的开始：

```assembly
.FARDATA	[名字]
```

“名字”是可选的，如果使用，则就成为该数据段的段名。例如：如下伪指令定义了一个独立的数据段：

```assembly
		.FARDATA
NEWPTR	DD	0
BUFF 	DB 	1024 DUP(?)
```

此外，还有伪指令.FARDATA?用于说明未初始化的独立数据段。在编写纯粹汇编语言程序时，无需使用伪指令.FARDATA?,因为在由伪指令.FARDATA说明的独立数据段中也可定义未初始化数据。

### 2.缺省段名

在使用简化的段定义伪指令说明各段后，程序员一般不需要知道这些段的段名和它们的定位类型、组合类型等。但如果想把简化的段定义伪指令与标准的段定义伪指令混合使用，那么就需要知道这些内容了。表8.2列出了在小（SMALL)内存模型情况下，各段的段名等信息

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/root/image.fn17mbuaa3c.webp)

如果在中型、大型或巨型模式下，由伪指令.CODE说明的代码段的段名在字符串"_TEXT"之前还会加上模块名（源文件名）。例如，设模块名为ABC,则代码段名就成为ABC.TEXT。因此，如果使用中大型内存模式，那么模块源文件名不要以数字开头。

如果在使用伪指令.FARDATA说明一个独立的数据段时加了名字，那么此名字就成为该独立数据段的段名。

## 8.2.3 存储模型说明伪指令的隐含动作

### 1.隐含的段组和段设定

存储模型说明伪指令，.MODEL,除了说明程序采用的存储模型外，还起着相当于如下语句的作用：

DGROUPGROUP DATA, CONST, BSS. STACKASSUME CS:TEXT.DS:DGROUP,SS:DGROUP

它指示汇编程序把可能有的段_DATA、段CONST、段.BSS和段STACK集合成--个名为DGROUP的段组，同时指示汇编程序把数据段寄存器DS和堆栈段寄存器SS与段组DGROUP对应，使代码段寄存器CS与代码段对应。注意，在中型、大型模式下，代码段的段名不再是.TEXT。

由于伪指令.MODEL的上述隐含动作，所以在使用伪指令.MODEL后，可以直接引用段组DGROUP.而且多数情况下也可以不使用伪指令ASSUME,上面的程序T8-1B.ASM就是一例。

但在少数情况下，程序仍需要安排ASSUME语句来指示段寄存器与段的对应关系。例如：下列代码设置DS,使它依次对应.DATA段、CODE段、FARDATA段，最后又对应.DATA段：

```assembly
......
.DATA
......
.FARDATA
......
.CODE
MOV 	AX,@DATA
MOV 	DS.AX
ASSUME 	DS:@DATA
......
MOV 	AX,@CODE
MOV 	DS,AX
ASSUME 	DS:@CODE
.....
MOV 	AX,@FARDATA
MOV 	DS,AX
ASSUME 	DS:@FARDATA
.....
MOV 	AX,@DATA
MOV 	DS,AX
ASSUME 	CS:@DATA
.....
```



### 2.有关的预定义符

在上述程序片段中使用的符号@CODE等是汇编程序提供的若干预定义符。它们类似于用伪指令EQU所定义的符号。与简化的段定义伪指令相关的一些预定义符号有：

1. 符号@CODE表示代码段的段名。
2. 符号@DATA表示由.DATA段和.STACK段等集合而成段组的组名。
3. 符号@FARDATA表示独立数据段的段名。

# 8.3模块间的通信

一个程序的若干模块在功能上是有联系的，不仅程序的运行次序可能要从一个模块转到另一个模块，而且程序处理数据和变量也会涉及不同的模块。如何实现这种联系呢？具体地说，模块甲如何调用模块乙内的过程？模块乙如何访问模块甲内的数据？本节介绍这方面的内容。

## 8.3.1 伪指令PUBLIC和伪指令EXTRN

由于各模块被单独汇编，所以，如果模块甲要按符号名调用或访问在其他模块内定义的某个过程或变量，那么，模块甲必须告诉汇编程序此指定符号名（标识符）在别的模块内定义，否则，在汇编模块甲时，汇编程序会给出“符号未定义”这类汇编出错信息。另一方面，如果在模块乙内定义的过程或变量准备供其他模块调用或访问，那么，模块乙也必须通知汇编程序，否则，汇编程序不会把相应的标识符保存到目标程序中去，最终导致连接失败。

伪指令EXTRN和伪指令PUBLIC就是分别用于通知汇编程序上述两种信息。

### 1.伪指令PUBLIC

伪指令PUBLIC用于声明在当前模块内定义的某些标识符是公共标识符，即可供其他模块使用的标识符。它的一般格式如下所示：

```assembly
PUBLIC	标识符[,标识符，...]
```

上述语句中位于助记符PUBLIC之后的“标识符”就是要声明的公共标识符。一条PUBLIC语句可声明多个这样的标识符，标识符间用逗号分隔。一个源程序模块内可使用多条PUBLIC语句。数据变量名和程序标号（包括过程名）均可声明为公用标识符。

#### 例1:如下源程序模块中声明VAR1、VAR2和DELAY为可供其它模块使用的公共标识符：

```assembly
		.MODEL 	SMALL
		PUBLIC 	VAR1,VAR2		;声明VAR1和VAR2是公共标识符
		PUBLIC 	DELAY			;声明DELAY为公共标识符
		.DATA					;数据段开始
VAR1 	DW 	?
VAR2	DB	?
VAR3 	DB 	5 	DUP (0)
		.CODE					;代码段开始
DELAY 	PROC
LAB1:
		RET
DELAY 	ENDP
		END
```

由于没有声明VAR3和LAB1为公共标识符，所以其它模块不能使用这两个标识符。

### 2.伪指令EXTRN

伪指令EXTRN用于声明当前模块使用的哪些标识符在其他模块内定义。它的一般格式如下所示：

```assembly
EXTRN 标识符：类型 [,标识符：类型，...]
```

上述语句中位于助记符EXTRN后的每一项“标识符：类型”声明一个在其他模块内定义的标识符。汇编程序为了产生合适的代码或保留恰当的存储单元，要求在声明标识符的同时指出其类型属性，“标识符”和“类型”之间用冒号分隔。类型可以是NEAR、FAR或者BYTE、WORD、DWORD等标识符类型属性。

一条EXTRN语句可声明多个这样的标识符，每项之间用逗号分隔。一个源程序模块内可使用多条EXTRN语句。

#### 例2:下面的语句声明VAR1、VAR2和DELAY为在其它模块定义的标识符：

```assembly
EXTRN 	DELAY:NEAR
EXTRN 	VAR1:WORD,VAR2:BYTE
```

注意：把EXTRN伪指令安排在段的里面与段的外面是有区别的。如果EXTRN伪指令出现在某个段内，表示所声明的标识符虽在其它模块内，但却在同一个段内。如果EXTRN伪指令出现在段外，那么表示不知道所声明的标识符在哪一个段内被定义。

### 3.声明一致性

各模块内的PUBLIC语句和EXTRN语句必须互相呼应，互相一致。凡是由PUBLIC语句声明的标识符，应该是其它模块的EXTRN语句中用到的标识符；反之，凡是由EXTRN语句声明的标识符必须在将要连接在一起的其它模块的PUBLIC语句中找到，而且所指明的类型必须一致。如果不遵守这些原则，就不能正确连接成功。

## 8.3.2模块间的转移

模块间的转移是指从一个模块的某个代码段转移到另一个模块的某个代码段。这种转移通常是以过程调用及返回的形式出现，例如：模块甲调用定义在模块乙内的某个过程，但有时这种转移也直接采用转移指令的形式。

若两个模块的涉及转移的代码段在连接后不能组合为一个代码段，那么发生在这两个代码段之间的转移必须是段间转移，所以模块间的转移就成为远调用或远转移；否则模块间的转移可以是近调用或近转移，当然仍采用远调用或远转移也完全是可以的。

由于近调用或近转移的效率比远调用或远转移的效率高，所以一般总是乐意采用近调用或近转移。但是，这并非总做得到，因为分布在不同源程序模块中的两个代码段在连接时能被组合为一个段是有条件的，那就是它们的段名及其类别必须相同，而且段组合类型也应为PUBLIC。在实际编程时，不同的模块往往由不同的人员完成，所以很难做到段同名。为了避免考虑不同模块中的代码是否能组合成一个段，反而常常采用远调用或远转移。

#### 例3:演示程序T8-7.ASM有如下三个模块组成，主模块中代码段的段名是CSEG,而两个从模块中代码段的段名是TEXT:

```assembly
;程序名：T8-7.ASM
;功能：演示模块间的转移
		EXTRN SUBI:FAR					;声明SUB1是在其它模块内定义
CSEG 	SEGMENT PARA PUBLIC	'CODE'
		ASSUME CS:CSEG
START: 
		CALL 	FAR PTR SUB1			;调用在T8-7MA中定义的过程SUB1
		MOV 	AX,4C00H
		INT 	21H
CSEG 	ENDS
		END 	START
;
;模块名：T8-7MA.ASM
;功能：作为程序T8-7的一个模块
		PUBLIC 	SUB1					;声明SUB1是公共标识符
		EXTRN 	SUB2:NEAR				;声明SUB2是在其它模块内定义
TEXT 	SEGMENT PARA PUBLIC 'CODE'
		ASSUME 	CS:TEXT
SUBI 	PROC 	FAR						;定义远过程SUB1
		MOV 	DL,'*'
		MOV 	AH,2					;显示符号'*'
		INT 	21H
		CALL 	SUB2					;调用在T8-MA中定义的过程SUB2
		RET
SUB1	ENDP
TEXT 	ENDS
		END
;
;模块名：T8-7MB.ASM
;功能：作为程序T8-7的一个模块
PUBLIC 	SUB2							;声明SUB2是公共标识符
TEXT 	SEGMENT PARA PUBLIC 'CODE'
		ASSUME 	CS:TEXT
SUB2 	PROC 	NEAR					;定义近过程SUB2
		MOV 	DL,'+'
		MOV 	AH,2					;显示符号”+”
		INT 	21H
		RET
SUB2 	ENDP
TEXT 	ENDS
		END
```

由于模块T8-7MA中的代码段与主模块T8-7中的代码段不同名，所以连接时不能组合成一个段，因此过程SUB2被定义成远过程，在主模块T8-7中也相应地声明为FAR类型。由于，模块T8-7MA和模块T8-7MB中的两个代码段的段名和段类别相同，而且组合类型为PUBLIC,所以，在连接时它们能被组合成一个段。再由于，只有过程SUB1调用过程SUB2,即只有段内调用，因此过程SUB2才被定义为近过程，在模块T8-7MA中也相应地声明为NEAR类型。可用如下命令把它们的三个目标模块连接到一起：

```assembly
TLINK T8-7+T8-7MA+T8-7MB
```

如果主模块也要调用过程SUB2,那么应该把SUB2也定义为远过程。

采用简化的段定义可避免考虑段名是否相同，把有关的问题留给汇编程序解决。

### 例4:利用简化的段定义改写程序T8-7.ASM的三个模块。

```assembly
;程序名：T8-7A.ASM
;功能：(略）
		EXTRN 	SUB1:NEAR
		.MODEL 	SMALL
		.CODE
START: 
		CALL 	SUB1
		MOV 	AX,4C00H
		INT		21H
		END 	START
;程序名：T8-7AMA.ASMP
		UBLIC 	SUB1
		EXTRN 	SUB2:NEAR
		.MODEL 	SMALL
		.CODE
SUB1 	PROC
		......				;其它代码略
		CALL 	SUB2
		RET
SUB1 	ENDP
		END
;模块名：T8-7AMB.ASM
		PUBLIC 	SUB2
		.MODEL 	SMALL
		.CODE
SUB2 	PROC
		......				;代码略
SUB2 	ENDP
		END
```

由于三个模块均是SMALL模型，所以连接后的代码在一个段内，因此SUB1和SUB2均被作为近过程对待。

## 8.3.3模块间的信息传递

模块间的信息传递主要表现为模块间过程调用时的参数传递。在第4章介绍的过程调用参数传递原则和方法依然有效。少量参数可利用寄存器传递或利用堆栈传递，大量参数可先组织在一个缓冲区中，然后利用寄存器或堆栈传递相应的指针。

如果要利用约定的存储单元传递参数，情形稍稍复杂些，需要把它们声明为公共标识符。

#### 例5:写一个显示DOS版本号的程序。

为了展示模块间信息的传递，我们把程序分成两个模块。主模块有一个数据段和一个代码段，从模块只含有两个子程序。源程序如下所示：

```assembly
;程序名：T8-8.ASM
;功能：演示模块间的利用寄存器和约定存储单元传递信息
DSEG 	SEGMENT 	PUBLIC 	'DATA'
MESS 	DB 		'DOS Version is'
MESS1 	DB 		?
		DB		','
MESS2 	DB 		2 DUP 	(?)
		DB 		ODH,0AH,'$'
VERM 	DB 		0
VERN	DB		0
DSEG 	ENDS
		;声明VERM和VERN是公共标识符
		PUBLIC 	VERM,VERN
		;声明GETVER和TODASC在其它模块定义
		EXTRN 	GETVER:FAR,TODASC:FAR
		;
CSEG 	SEGMENT PUBLIC 'CODE'
		ASSUME	CS:CSEG,DS:DSEG
START:
		MOV		AX,DSEG
		MOV 	DS,AX
		CALL 	GETVER					;取得DOS版本号；
		MOV 	AL,VERM
		MOV 	BX,LENGTH MESS1
		MOV 	SI,OFFSET MESS1
		CALL 	TODASC					;把主版本号转换成可显示形式
		MOV 	AL,VERN
		MOV 	BX,LENGTH MESS
		MOV 	SI,OFFSET MESS2
		CALL 	TODASC					;把次版本号转换成可显示形式
		MOV 	DX,OFFSET MESS
		MOV 	AH,9
		INT 	21H						;显示版本信息
		MOV 	AX,4C00H
		INT 	21H
CSEG 	ENDS
		END 	START
		
;模块名：T8-8MA.ASM
;功能：作为程序T8-8.ASM的模块
		;声明GETVER和TODASC为公共标识符
		PUBLIC 	GETVER,TODASC
		;声明VERM和VERN在其它模块定义
		EXTRN 	VERM:BYTE,VERN:BYTE
FUNC 	SEGMENT PUBLIC 'CODE' 			;定义代码段
		ASSUME 	CS:FUNC
;子程序名：GETVER
;功能：获取DOS版本号
;入口参数：无
;出口参数：在其它模块的VERM单元中存放主版本号
;		在其它模块的VERN单元中存放次版本号
;说明：远过程
GETVER 	PROC 	FAR
		MOV		AH,30H					;30HDOS功能调用是
		INT		21H						;取DOS版本号
		MOV 	VERM,AL					;AL含主版本号
		MOV 	VERN,AH					;AH含次版本号
		RET
GETVER 	ENDP	
;
;子程序名：TODASC
;功能：把一个8位二进制数转换成相应十进制数的ASCII码审
;入口参数：AL=欲转换二进制数；BX=十进制数的最少位数
;		DS:SI=存放ASCII码串的缓冲区首地址
;出口参数：ASCII码串在相应缓冲区中
;说明：远过程
TODASC 	PROC 	FAR
		MOV 	CL,10
TOASC1:
		XOR 	AH,AH
		DIV		CL
		ADD		AH,30H
		MOV		[SI+BX-1],AH
		DEC		BX
		JNZ		TOASC1
		RET
TODASC 	ENDP
FUNC	ENDS
		END
```

子程序GETVER把DOS的版本号直接填入在主模块中约定的单元VERM和VERN中。子程序TODASC的入口参数由寄存器传递，转换得到的十进制数ASCII码串直接写到主模块的指定缓冲区中。这两个子程序均被定义为远过程。

正确设置数据段或附加段寄存器是模块间正确传递信息的保证。在访问定义在其他模块的变量前，必须保证已设置好相应的段寄存器。例如：在调用GETVER之前必须正确设置数据段寄存器DS,因为子程序GETVER在访问约定的变量时，认为数据段寄存器已设置好。如有必要还可动态地改变段寄存器内容。

模块间传递信息的另一个方法是利用段覆盖，这个方法只适用于模块间传递信息。具体方法是：在两个模块中都定义一个同名同类别数据段，规定段组合类型是COMMON;把要传递的数据（变量）安排在这两个数据段的相同位置上。由于这两个在不同模块中的数据段同名同类别，且组合类型是COMMON,所以连接时它们就发生重叠。

#### 例6:写一个显示当前系统日期的程序。

为了简单化，显示的日期只含月和日。主模块有一个代码段和一个数据段，从模块也有一个代码段和数据段。源程序如下

```assembly
;程序名：T8-9.ASM
;功能：演示利用段覆盖方法在模块间传递信息
EXTRN	GETDATE:FAR					;声明GETDATE在其它模块定义
;
DSEG 	SEGMENT 	COMMON			;定义一个具有COMMON类型的数据段
MESS 	DB 			'Current date is'
MESS1	DB	2		DUP (?)
		DB	?
MESS2	DB	2		DUP	(?)
		DB 	ODH,0AH,24H
DSEG 	ENDS
;
CSEG 	SEGMENT 	PUBLIC			;代码段
		ASSUME CS:CSEG,DS:DSEG
START: 
		MOV 	AX, DSEG
		MOV 	DS,AX				;置数据段寄存器
		CALL 	GETDATE				;调用GETDATE取日期
		MOV 	DX,OFFSET MESS
		MOV 	AH,9				;显示日期信息
		INT		21H
		MOV 	AX,4C00H
		INT		21H
CSEG	ENDS
		END 	START
;
;模块名：T8-9MA.ASM
;功能：作为T8-9.ASM的一部分
PUBLIC 	GETDATE						;声明GETDATE为公共标识符
;
DSEG 	SEGMENT COMMON				;定义一个具有COMMON类型的数据段
MESS 	DB 	'Current date is'
MESS1 	DB 	2 DUP (?)
		DB	'-'
MESS2	DB	2	DUP(?)				;这部分数据与模块T8-8.ASM中完全相同
		DB 	ODH,0AH,24H
YEAR 	DW 	?						;这部分变量是另外加上的
MONTH 	DB 	?
DAY 	DB 	?
DSEG 	ENDS
;
CSEG 	SEGMENT BYTE PUBLIC 		;定义代码段
		ASSUME CS:CSEG,DS:DSEG
;子程序名：GETDATE
;功能：取得系统当前日期并把月日数转换成相应的十进制数ASCII串
;入口参数：无
;出口参数：ASCH申填入约定缓冲区
;说明：远过程
GETDATE PROC 	FAR
		MOV 	AH,2AH				;2AH号DOS功能调用是
		INT		21H					;取系统当前日期
		MOV 	YEAR,CX				;保存年数
		MOV 	MONTH,DH			;保存月数
		MOV 	DAY,DL				;保存日数
		MOV 	AL,MONTH
		MOV 	BX,LENGTH MESS1		;把月数转换成十进制数ASCII串
		MOV 	SI,OFFSET MESS1
		CALL	TODASC
		MOV 	AL,DAY
		MOV 	BX,LENGTH MESS2		;把日数转换成十进制数ASCII串
		MOV 	SI,OFFSET MESS2
		CALL 	TODASC
		RET
GETDATE ENDP
;
TODASC 	PROC 	NEAR				
	;由于该子程序仅供当前段调用，所以规定为NEAR类型;
	;其他部分与T8-8MA.ASM中所列相同
TODASC 	ENDP
CSEG 	ENDS
		END
```

模块T8-9MA中的数据段比模块T8-9.ASM中的数据段多了若干变量，在段覆盖时，以最长的段为段的最后实际长度。但必须注意，要传递的数据变量必须安排在相同的位置。由于模块T8-9MA中含有要访问的数据段，所以过程GETDATE能够随便地访问想要访问的对象。

# 8.4子程序库

子程序库能帮助程序员快速地编写出正确的程序，本节介绍如何建立子程序库和利用子程序库。

## 8.4.1子程序库

把频繁使用的一组子程序的源代码集中存放在某个文件中，再通过INCLUDE伪指令把它与完成某个任务的源程序相结合，这样就能方便地利用这些子程序，而无需重新编写或编辑它们。这种方法能提高编写程序的效率，但有以下不足：其一，当前源程序中的标号或变量名等可能与被结合的子程序文件中的标号等发生冲突（即符号重新定义）;其二，由于是源程序结合，所以每次汇编都包括对子程序文件的汇编，增加了汇编时间。

采用模块化程序设计方法，把包含常用子程序的源程序文件改写成一个源程序模块，然后单独汇编它，于是就可形成一个常用子程序目标文件。完成某个具体任务的程序只要把所需调用子程序声明为在其他模块内定义，那么就能通过汇编，最后再与这个常用子程序目标文件相连接就能得到可执行程序。这个方法能克服上述源程序结合方法的不足，从而进一步提高编写程序的效率。由于被连接的每一目标文件的全部代码都会成为最终可执行程序的一部分，所以这个方法也有一个缺点：当前未使用到的但却属于常用子程序目标文件的子程序都会出现在最终的可执行程序中。库能克服这个缺点。

子程序库是子程序模块的集合。库文件中存放着子程序的名称，子程序的目标代码以及连接过程所必需的重定位信息。当目标文件与库相连接时，LINK程序只把目标文件所需要的子程序从库中找出来，并嵌入到最终的可执行程序中去，而不是把库内的全部子程序统统嵌入到可执行程序。所以，库与目标文件不同，子程序库能克服子程序目标文件的缺点。

## 8.4.2建立子程序库

为了给调用者提供方便，库中的子程序应该提供统一的调用方法，所以需要遵守如下约定：

1. 参数传递方法保持统一。
2. 过程类型保持相同，即都为远过程或都为近过程。请特别注意，如果过程类型选择NEAR,那么必须保证在连接时调用者所在段能与子程序所在段组合成一个段，为此调用者所在段的段名和类别应该与子程序所在段的段名和类别相同，且组合类型同为PUBLIC。
3. 采用一致的寄存器保护措施和可能需要的堆栈平衡措施。
4. 子程序名称规范。

建立子程序库的一般步骤如下：

1. 确定库所含子程序的范围，即库准备包含哪些子程序。
2. 确定参数传递方法。
3. 确定子程序类型，还确定子程序所在段的段名、定位类型、组合类型和类别。
4. 确定寄存器保护措施等其他内容。
5. 利用专门的库管理工具程序，把经过调试的子程序目标模块逐一加入到库中。

下面我们来建立一个子程序库，它包含若干数制转换子程序目标模块。为了方便地使库中各子程序目标模块所在的段相同，在编写各子程序模块源程序时，采用简化的段定义，并把存储模型定为SMALL;但子程序类型规定为FAR,即均为远过程，于是，调用模块只要把欲调用的子程序声明为在其它模块内定义，且类型为FAR即可，而无需考虑是否能与被调用的子程序模块组合成同一个段。此外，还作如下约定：采用寄存器传递出入口参数；各子程序除了可能破坏AX、BX、CX和DX四个寄存器的内容外，保护其他寄存器的内容。

#### 例1:编写一个把二进制数转换为对应十进制数ASCII码串的子程序，并把它添加到名为BDHL.LIB的库中。

源程序如下所示：

```assembly
;源文件名：T8L1.ASM
;功能：(略）
			PUBLIC 	BDASCS		;声明BDASCS为公共标识符
			.MODEL 	SMALL
			;
			.CODE				;代码段开始
;子程序名：BDASCS
;功能：(略）
;入口参数：AX=欲转换的二进制数
;		DS:DX=缓冲区首地址
;出口参数：(略）说明：
;			(1)远过程
;			(2)缓冲区至少长5个字节
BDASCS 	PROC 	FAR
		PUSH 	SI
		MOV 	SI,DX
		MOV 	CX,5
		MOV 	BX,10
@@1: 	XOR 	DX,DX
		DIV 	BX
		ADD		DL,30H
		MOV 	[SI+4],DL
		DEC 	SI
		LOOP	@@1
		POP		SI
		RET
BDASCS 	ENDP
		END
```

设源程序存放在文件T8L1.ASM中，可利用如下命令汇编成目标模块且添加到库BDHL.LIB中去：

```assembly
TASM 	T8L1
TLIB	BDHL.LIB+T8L1.OBJ
```

#### 例2:编写一个把二进制数转换为对应十六进制数ASCII码串的子程序，并把它添加到名为BDHL.LIB的库中。

源程序如下所示：

```assembly
;源文件名：T8L2.ASM
;功能：(略）
PUBLIC 	BHASCS				;声明BHASCS是公共标识符
		.MODEL SMALL
		.CODE				;代码段开始
;先定义一个内部使用的过程（子程序说明信息等略）
HTOASC 	PROC 	NEAR
		AND 	AL,OFH
		ADD		AL,90H
		DAA
		ADC 	AL,40H
		DAA
		RET
HTOASC 	ENDP
;
;子程序名：BHASCS功能：(略）
;入门参数：AX=欲转换的二进制数DS:DX=缓冲区首地址
;出口参数：(略）
;说明：(1)远过程
;		(2)缓冲区至少长4个字节
BHASCS 	PROC 	FAR
		PUSH	DI
		PUSH 	ES
		CLD
		PUSH 	DS
		POP		ES
		MOV		DI,DX
		MOV 	CX,404H
@@1:
		ROL 	AX,CL
		MOV 	DX,AX
		CALL 	HTOASC
		STOSB	
		MOV 	AX,DX
		DEC 	CH
		JNZ		@@1
		POP		ES
		POP		DI
		RET
BHASCS 	ENDP
		END
```

子程序BHASCS调用HTOASC实现把一位十六进制数转换为对应ASCII码，但由于没有把标识符HTOASC声明为公共标识符，所以它不能供其它程序调用。由于子程序HTOASC与子程序BHASCS在同一个模块，且只供BHASCS调用，所以采用类型NEAR。子程序HTOASC的代码始终伴随着子程序BHASCS,当某个程序需要连入子程序BHASCS时，子程序HTOASC也被连入。

设源程序存放在文件T8L2.ASM中，可利用如下命令把它汇编成目标模块且添加到库中去：

```assembly
TASM	T8L2
TLIB 	BDHL+T8L2
```

## 8.4.3使用举例

下面我们举例说明如何利用子程序库。

#### 例3:写一个显示16H号中断向量的程序。

使用简单的段定义方式，存储模型定为SMALL。实现算法是：取有关中断向量；再调用已建立的库BDHL.LIB中的子程序BHASCS把向量值转换为对应十六进制数的ASCII码串；最后显示ASCII码串。源程序如下所示：

```assembly
;程序名：T8-10.ASM
;功能：(略）
VECTOR	=	16H
		.MODEI	SMALL
		.STACK 	1024
		.DATA
MESS 	LABEL 	BYTE
MESSI 	DB 	4 DUP (0)
		DB	':'
MESS2 	DB 	4 DUP (0)
		DB 	ODH,0AH,24H
		.CODE
		EXTRN 	BHASCS:FAR		;类型为FAR,但认为在相同段内
START:	
		MOV 	AX,@DATA
		MOV		DS,AX			;设置数据段寄存器
		MOV		AH,35H
		MOV		AL,VECTOR
		INT		21H				;返回中断向量于ES,BX中
		PUSH 	BX				;保存中断向量中的偏移部分
		MOV		AX,ES			;先转换中断向量的段值部分
		MOV		DX,OFFSET MESS1
		CALL 	FAR PTR BHASCS	;转换
		POP		AX				;中断向量的偏移部分送AX
		MOV		DX,OFFSET MESS2
		CALL 	FAR PTR BHASCS	;转换
		MOV		DX,OFFSET MESS
		MOV		AH,9			;显示中断向量
		INT		21H
		MOV		AX,4C00H
		INT		21H
		END		START
```

#### 例4:写一个显示系统常规内存量的程序。

系统常规内存量存放在内存单元40:13H的字单元中，以KB为单位。

实现算法是：先从40:13H单元中取得内存量；再调用库BDHL.LIB中的子程序BDASCS把以二进制数形式表示的内存量转换为对应十进制数的ASCII码串；最后显示之。程序采用完整的段定义方式。源程序如下所示：

```assembly
;程序名：T8-11.ASM
;功能：(略）
		EXTRN 	BDASCS:FAR			;认为BDASCS不在相同段内
SSEG 	SEGMENT STACK	'STACK'		;定义堆栈段
		DB	400H	DUP	(0)
SSEG 	ENDS
CSEG 	SEGMENT 	PUBLIC			;数据代码合为一段
MESS 	DB 		'Total ='
MESS1 	DB 	5 	DUP 	(0)
		DB	'KB',0DH,0AH,24H
		ASSUME CS:CSEG,DS:CSEG		;段寄存器使用设定
START:
		PUSH	CS
		POP		DS
		MOV		AX,40H
		MOV		ES,AX
		MOV		AX,ES:[13H]			;取常规内存量
		MOV		DX,OFFSET MESS1
		CALL	BDASCS				;转换
		MOV		DX,OFFSET MESS
		MOV		AH,9				;显示
		INT		21H
		MOV		AX,4C00H
		INT		21H
CSEG 	ENDS
		END		START
```

# 8.5编写供TurboC调用的函数

将C语言和汇编语言混合使用的传统方式是，先用C语言和汇编语言编写出独立的模块，然后编译C语言模块并汇编汇编语言模块，最后再将得到的目标模块连接到一起。普通情况下，这种汇编语言模块由若干被频累调用而左右程序运行效率的子程序所组成。

在C语言中，习惯上把子程序称为函数，所以，我们说这种汇编语言模块由若干汇编函数组成。为了使C模块能够调用到汇编模块中的函数，在编写汇编模块时，必须注意两个方面的内容。第一，汇编模块必须能够恰当地与C模块连接到一起，并且其中的汇编函数名等要符合C语言的约定；第二，汇编函数必须能恰当地处理C风格的函数调用，包括访问传递过来的参数、返回值及遵守C函数所要求的寄存器保护规则。

## 8.5.1汇编格式的编译结果

为了能够编写出可供Turbo C调用的函数，应了解Turbo C模块与汇编模块的接口机制，而从以汇编形式给出的编译结果中可方便地了解这种机制。设有如下C程序：

```c
/*程序名：TC8.C*/
int 	Sum(int,int,int)	;/*说明函数Sum的调用格式*/
int 	xxx = 5				;/*已初始化的变量*/
int 	yyy					;/*初始化的变量*/
main()						/*主函数*/
{
	yyy=Sum(1,xxx,3);
	printf("%d\n",yyy);
}
int"Sum(int i,int j,intm)	/*函数Sum*/
{
	return(i+j+m);
}
```

用下面的命令要求TurboC按SMALI.模式编译TC8.C,并以汇编格式输出编译结果：

```assembly
TCC -ms -S TC8.C
```

以汇编格式输出的编译结果保存在文件TC8.ASM中，尽管该文件比较究长，但阅读和理解它对编写供Turbo C调用的汇编函数是有帮助的，而且对学习C语言也是有益的。TC8.ASM的主要内容如下所示（已删去空段和注释等次要内容）:

```assembly
-TEXT 	segment byte public 'CODE'				;代码段
DGROUP 	group 	_DATA, _BSS
		assume cs:_TEXT,ds:_DGROUP,ss:DGROUP
_TEXT 	ends

_DATA 	segment word public 'DATA'		;已初始化的数据段
_xxx 	label 	word
		dw 		5
_DATA 	ends
_TEXT 	segment byte public 'CODE'		;代码段
_main 	proc 	near
		mov 	ax,3
		push 	ax						;为调用Sum压入第三个参数
		push 	word ptr DGROUP:_xxx		;压入第二个参数
		mov 	ax.1
		push 	ax						;压入第一个参数
		call 	near ptr _Sum			;调用Sum
		add 	sp,6					;废除压入堆栈的三个参数
		mov 	word ptr DGROUP:_yyy,ax ;把和送变量yyy
		push 	word ptr DGROUP:_yyy	;为调用printf压入第二个参数
		mov 	ax,offset DGROUP:s@
		push	ax						;压入第一个参数
		call 	near ptr _printf		;调用printf
		pop 	cx						;废除压入堆栈的两个参数
		pop 	cx
@1:		
		ret
_main 	endp

_Sum 	proc	near					;函数Sum
		push 	bp
		mov 	bp,sp
		mov 	ax,word ptr [bp+4]		;访问第一个参数
		add 	ax,word ptr [bp+6]		;访问第二个参数
		add		ax,word ptr [bp+8]		;访问第三个参数
		jmp 	short	@2
@2:
		pop 	bp

_Sum 	endp
_TEXT 	ends

_BSS 	segment word public 'BSS'		;未初始化数据段
_yyy 	label 	word
		db 		2 	dup (?)
_BSS 	ends;数据段

DATA 	segment word public 'DATA'	;数据段
8@ 		
		label 	byte				;也即字符串"%d\n"
		db 		37
		db 		100
		db 		10
		db 		0
DATA 	ends

TEXT 	segment byte public 'CODE'	;代码段
		extrn	_printf:near		;printf是TurboC的库函数
TEXT 	ends						
		public 	_yyy					;声明公共标识符
		public	_xxx
		public _main
		public _Sum
		end
```

从上面以汇编格式给出的编译结果中，可以看到函数Sum除包含一条多余的跳转指令外，已足够精练。但为了方便地说明如何编写供Turbo C调用的函数，我们仍然假设希望把上述C函数Sum改写成汇编函数。相应地，C程序TC8.C改写如下：

```c
/*程序名：CA8.C*/
extern Sum(int,int,int)		/*声明函数Sum在其他模块内定义*/,
    int xxx=5;				/*已初始化的变量*/
    int yyy;				/*初始化的变量*/
main()						/*主函数*/
{
    yyy=Sum(1,xxx,3);
    printf("%d\n",yyy);
}
```

程序CA8.C与TC8.C的区别是不再定义函数Sum,而声明它在其他模块内定义下面就介绍如何编写包含函数Sum的汇编模块。

## 8.5.2 汇编模块应该遵守的约定

### 1.关于内存模式和段的约定

为了使得汇编模块能够恰当地与TurboC模块连接到一起，汇编模块必须采用与Turbo C模块一致的内存模式，同时必须遵守与Turbo C兼容的段命名约定。

如果采用完整的段定义形式，那么汇编模块中所需段的定义应该按照TC8.ASM中相应段的定义形式来编写。这似乎有点麻烦。

幸运的是，利用简化的段定义方式，能轻松地实现内存模式的一致和兼容的段命名。因为采用简化的段定义方式后，汇编程序就自动完成这方面的全部工作。

根据表8.1所列内容，只要用伪指令.MODEL说明内存模式为SMALL,然后用伪指令.CODE定义的代码段以及用伪指令.DATA定义的数据段等均与TC8.ASM中相应段的定义一致。换句话说，在汇编模块中安排伪指令“.MODEL SMALL”，那么汇编后所得到的目标模块就能够与Turbo C按SMALL模式编译后所得的目标模块有效地连接。事实上，不仅仅在SMALL模式下如此，在其他内存模式下也如此。

伪指令.MODEL通知汇编程序，用简化的段定义伪指令创建的段与选定的内存模式兼容，并控制用PROC伪指令创建的过程的隐含类型。另一方面，由伪指令.MODEL定义的内存模式与具有同样类型的TurboC模式是相互兼容的。所以，简化的段定义伪指令.CODE、DATA、DATA?、FARDATA、FARDATA?及.CONST等产生的段与Turbo C 相应的段兼容

### 2.关于函数名的约定

一般情况下，Turbo C 希望所有的外部标号均以下划线“-"开头。Turbo C自动地给函数名及全局变量名（包括外部变量名）加上下划线，在上述TC8.ASM中可清楚地看至这一点。所以，如果汇编模块中定义的函数准备提供给Turbo C调用，那么函数名必须以下划线开头。顺便指出，如果汇编模块中定义的变量也准备供Turbo C访问，那么也需以下划线开头。

通常情况下，在处理符号名时，汇编程序对字母的大小写并不敏感，所以不区别对待大写字母和小写字母，而皆以大写字母对待。因为C语言区别对待大小写字母，所以在编写准备与C模块相连接的汇编模块时，应该注意符号名的大小写，以便保持一致。而且，要通知汇编程序对大小写区别对待，至少对于C模块和汇编模块所共享的那些符号而言应该如此。汇编程序的命令行可选项/ml和/mx可以做到这一点。

汇编程序的命令行可选项/ml使得汇编对所有符号均按大小写区别对待。命令行可选项/mx使得汇编只对公共标识符和外部标识符等按大小写区别对待。

至此，我们可编写出如下格式的汇编模块：

```assembly
.MODEL SMALL
.CODE
PUBLIC	_Sum
_Sum 	PROC
......
......
_Sum 	ENDP
		END
```

## 8.5.3参数传递和寄存器保护

下面介绍用汇编语言编写的函数如何与TurboC交流信息，这包括三个方面的内容：获取由调用者提供的入口参数、把可能的处理结果值返回给调用者以及寄存器的保护。换一个角度看，这三个方面的内容也就是Turbo C调用函数的一般方法。

### 1.获取入口参数

我们在4.2.3节中介绍了如何利用堆栈传递参数及访问堆栈中的参数。在知道了参数类型（占用堆栈空间的字节数）和次序后，就能够通过BP寄存器方便地访问堆栈中白参数。

Turbo C 通过堆栈将参数传递给函数。调用函数之前，Turbo C先将要传给函数的参数压入堆栈，最先压入最右边的参数，最后压入最左边的参数。

在TC8.C中的C语句`yyy=Sum(1,xxx,3);`被编译成如下汇编指令：

```assembly
mov 	ax,3					;L1
push 	ax						;L2(先压入最右边的参数）
push 	word ptr DGROUP:_XXX	;L3(然后压入变量xxx的值）
mov 	ax,1					;L4
push 	ax						;L5(最压入最左边的参数）
call 	near ptr _Sum			;L6
add 	sp,6					;L7(废除压入堆栈的三个参数）
mov 	word ptr DGROUP:_yyy,ax ; L8
```

从中可清楚地看到，先压入最右边的参数3,再压入变量xxx的值，最后压入最左边的参数1。

从函数返回时，先前压入堆栈的参数仍然保留在堆栈中，但这些参数已没有任何用途。所以，在每次调用函数之后，TurboC立即调整堆栈指针，使之指向压入参数前所指的位置，这样就放弃了堆栈中的参数。在上面的例子中，3个双字节的参数共占6个字节的堆栈空间，所以在调用Sum之后，第L7条指令将堆栈指针加6以删除这些参数。这里很重要的一点就是，堆栈中的参数由调用者负责删除.

那么，如何访间堆栈中的参数呢？先来看看TC8.C中的函数Sum是如何访间堆栈中的参数的：

```assembly
push 	bp
mov 	bp,sp
moy 	ax,word ptr[bp+4]		;S3(访问第一个参数）
add 	ax,word ptr [bp+6]		;S4(访问第二个参数）
add		ax,word ptr [bp+8]		;S5(访问第三个参数）
```

在执行完上面的第L6条指令后，堆栈顶如图8.2(a)所示，在把参数依次压入堆栈后进行函数调用时，返回地址也被压入堆栈。为了通过BP寄存器访问堆栈中的参数，先保护BP寄存器。在执行完上面的第S1和第S2条指令后，堆栈顶如图8.2(b)所示。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/root/image.15g1hu00qq8w.webp)

直接用汇编语言编写的函数Sum当然也能够按此方法访问堆栈中的参数。

但并非所有情形都如此简单。首先，相对于BP以常偏移量访问参数的做法并不理想，这不仅仅因为不易于阅读和理解，而且容易搞错偏移量，特别是如果增加要传递的参数，那么偏移量又得重新调整。更为严重的是：如果采用远调用，那么保存的返回地址要占用4个字节的堆栈空间；此外，压入堆栈的参数所占用堆栈空间的字节数与参数类型有关，例如：如果参数是一个长整数或长指针，则将占用4字节的堆栈空间。为此，TASM还提供了一条伪指令ARG来帮助程序员处理好访问堆栈中参数的问题。

### 2.返回值

象普通C函数一样，供Turbo C调用的汇编函数也可利用寄存器返回值。通常情况下，8或16位的值通过AX寄存器返回，32位的值通过DX:AX寄存器对返回，其中高16位在DX寄存器中。所以，可通过AX返回短指针，通过DX:AX返回长指针。在上述TC8.ASM中，可清楚地看到函数Sum的返回值由寄存器AX传递出来。

现在，我们可编写出如下的含有函数Sum的汇编模块：

```assembly
;模块名：A8.ASM
;内容：含一个供CA8.C调用的函数Sum
		.MODEL SMALL
		.CODE
		PUBLIC _Sum
PARM1 	EQU [BP+4]
PARM2 	EQU [BP+6]
PARM3 	EQU [BP+8]
_Sum 	PROC
		PUSH 	BP
		MOV 	BP,SP
		MOV		AX,PARM1				;访问第一个参数
		ADD		AX,PARM2				;访访问第二个参数
		ADD 	AX,PARM3 				;访问第三个参数
		POP 	BP
		RET								;由AX寄存器返回结果
_Sum 	ENDP
		END
```

通过下面的命令能够完成对CA8.C的编译、对A8.ASM的汇编，最后再连接到一起：

```assembly
TCC	-ms CA8 A8.ASM
```

通过下面的三条命令能够分别编译、汇编和连接：

```assembly
TCC	-ms	-c	CA8
TASM	/ml	A8T
LINK cOs CA8 A8, CA8, .CS
```

上述最后连接命令中的cOs和cs分别是TurboC的SMALL模式下的启动代码目标模块文件和函数库文件。

必须注意，上面的汇编模块A8.ASM只能与按SMALL模式编译的C模块相连接。

### 3.保护寄存器

Turbo C要求供它调用的汇编语言函数必须保护好寄存器BP、SP、CS、DS和SS的内容。尽管在汇编语言函数中可以改变这些寄存器的内容，但当返回时，它们的值必须与调用前相同。可以随意地改变寄存器AX、BX、CX、DX和ES及标志寄存器的内容。

寄存器SI和DI是特殊情况，因为TurboC将其用作寄存器变量。如果在调用汇编语言函数的C模块中启动了寄存器变量，那么在汇编语言函数中就必须保护寄存器SI和DI;但若没有启动寄存器变量，就不必保护这两个寄存器。一个稳妥的做法是，象保护寄存器BP它们那样，总是保护寄存器SI和DI。

### 8.5.4举例

#### 例1:写一个求若干16位有符号数之和的汇编语言函数。

它有两个入口参数，其一是数组的元素个数，其二是指向数组的指针。它的返回值是一个32位有符号数。在C模块中说明的原型格式是：

```c
extern long niadd(int*,int);
```

适用于SMALL模式编译的C模块相连的汇编模块如下所示：

```assembly
;模块名：A86.ASM
;功能：(略）
PARM	STRUC
REGBP	DW 	?
RETADDR DW 	?
POINTER DW 	?
COUNT	DW 	?
PARM	ENDS
		.MODEL SMALL
		.CODE
PUBLIC 	_niadd
_niadd 	PROC
		PUSH 	BP
		MOV 	BP,SP
		PUSH 	SI
		PUSH 	DI
		CLD		
		MOV 	SI,[BP].POINTER
		MOV 	CX,[BP].COUNT
		XOR 	BX,BX
		MOV 	DI,BX
@@1:
		LODSW
		CWD
		ADD 	BX,AX
		ADC		DI,DX
		LOOP	@@1
		MOV 	DX,DI
		MOV 	AX,BX
		POP 	DI
		POP 	SI
		POP 	BP
		RET
_niadd 	ENDP
		END
```

源程序中说明了一个结构数据类型，它能够反映在保护寄存器BP之后堆栈顶的内容，但并没有真正地分配结构变量，在获取入口参数时，可以设想在堆栈顶分配了这样的一个结构变量，这样可方便表达堆栈中的参数。另外，作为入口参数给出的缓冲区首地址只含有偏移，而没有段值，这是因为这个函数只准备提供给按SMALL模式编译的C模块调用，所以认为在调用该函数之前数据段寄存器已设置好，而无需由函数再设置。调用上述函数的一个TurboC程序如下所示：

```c
extern	long niadd (int * ,int );
int buffer[6]=(12345,10000,-20000,23456,-2345,-56};
main()
{
	long x;
    x=niadd(buffer,6);
    printf("x=%ld\n",x);
}
```

#### 例2:改写上面的A86.ASM,使其具有良好的通用性，并把所得模块添加到相应的TurboC函数库中去。

汇编模块A86.ASM中的函数niadd只能供以SMALL模式编译的C模块调用，其原因除了伪指令，MODEL说明采用SMALL模式外，更主要的还在于只考虑了近调用和短指针，例如：在LARGE模式下，C模块将以远调用方式调用函数niadd,并且传递给函数的指针也是长指针。

改写后的模块如下所示：

```assembly
;程序名：A86A.ASM
;功能：(略）
		.MODEL SMALL
		.CODE
		PUBLIC	_niadd
PARM	STRUC
REGBP	DW 	?
		IF @CODESIZE EQ 0
RETADDR	DW	?
		ELSE
RETADDR DD 	?
		ENDIF
		IF 	@DATASIZE EQ 0
POINTER DW 	?
		ELSE
POINTER	DD	?
		ENDIF
COUNT	DW 	?
PARM	ENDS
;
_niadd 	PROC
		PUSH 	BP
		MOV 	BP,SP
		PUSH 	SI
		PUSH 	DI
		CLD
		IF 		@DATASIZE EQ 0
		MOV 	SI,[BP].POINTER
		ELSE
		PUSH 	DS
		LDS 	SI,[BP].POINTER
		ENDIF
		MOV 	CX,[BP].COUNT
		XOR 	BX,BX
		MOV		DI,BX
@@1:
		LODSW
		CWD
		ADD 	BX,AX
		ADC 	DI,DX
		LOOP	@@1
		MOV 	DX,DI
		MOV 	AX,BX
		IF 		@DATASIZE NE 0
		POP		DS
		ENDIF
		POP		DI
		POP		SI
		POP 	BP
		RET
_niadd 	ENDP
		END
```

上述汇编模块中使用了预定义符号@CODESIZE和@DATASIZE,它们的值决定了条件汇编语句中的条件是否满足。当利用.MODEL伪指令把内存模式设定为SMALL或COMPACT时，预定义符号@CODESIZE的值为0;当内存模式为MEDIUM、LARGE或HUGE时，预定义符号@CODESIZE的值为1。当内存模式为SMALL或MEDIUM时，预定义符号@DATASIZE的值为0;当内存模式为COMPACT或LARGE时，@DATASIZE的值为1;当内存模式为HUGE时，@DATASIZE的值为2。

另外，在使用.MODEL伪指令设定内存模式后，缺省的过程类型会根据内存模式的不同而改变，在中、大或巨模式下，缺省过程类型是FAR,而不再一直NEAR。

汇编程序模块A86A.ASM考虑了各种内存模式的情况，所以较为通用。如果要使它与按某种模式编译的C模块相连接，那么只要把由伪指令，MODEL指定的内存模式修改为这种希望的内存模式，再重新汇编即可。同样在重新汇编后，也就可把它添加到相应模式的库函数中。

利用库管理工具TLIB,可以直接把汇编后所得的目标模块添加到Turbo C的相应函数库中去。如指定的内存模式为SMALL,那么可通过如下命令把目标模块添加到SMALL模式的库CS.LIB中：

```assembly
TASM	/mx	A86A
TLIB 	CS.LIB+A86A.OBJ
```

如果欲把它添加到LARGE模式的库CL.LIB中，则可先把伪指令.MODEL指定的模式修改为LARGE,然后重新汇编它，最后发出如下命令：

```assembly
TLIB CL.LIB+A86A.OBJ
```

