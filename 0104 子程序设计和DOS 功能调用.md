[toc]

本章先介绍知何设计汇编语言子程序，然后把DOS功能调用视为子程序，介绍如何利用D)S系统功能调用。最后介绍子程序的递归和重入概念。

# 4.1 子程序设计 

如果某个程序片段将反复在程序中出现，就把它设计成子程序。这样能有效地缩短程序长度、节约存储空间。如果某个程序片段具有通用性，可供许多程序共享，就把它设计成子程序。这样能大大减轻程序设计的工作量，例如标准函数程序，此外，当某个程序片段的功能相对独立时，也可把它设计成子程序，这样便于模块化，也便于程序的阅读、调试和修改。

在80x86系列汇编语言中，子程序常常以过程的形式出现。

## 4.1.1 过程调用和返回指令

过程调用指令和过程返回指令属于程序控制指令这一组。通常，过程调用指令用于由主程序转子程序，过程返回指令用于由子程序返回主程序。==由于程序的代码可分为多个段，所以，像无条件转移指令一样，过程调用指令有段内调用和段间调用之分，与之相对应，过程返回指令也有段内返回和段间返回之分。把段内调用和段内返回称为近调用和近返回，把段间调用和段间返回称为远调用和远返回。==在汇编语言中，过程也有远近类型之分。

### 1.过程调用指令

过程调用指令首先把子程序的返回地址（即CAIL指令的下一条指令的地址）压入堆栈，以便执行完子程序后返回调用程序（主程序）继续往下执行。然后转移到子程序的入口地址去执行子程序。按照转移目标是否在同一段来分，调用指令分为段内调用和段间调用；按照获得转移目标地址的方式来分，调用指令分为直接调用和间接调用。下面介绍这四种调用指令，在汇编语言中，均用指令助记符CALL表示。

过程调用指令不影响标志。

#### (1)==段内直接调用==

==段内直接调用指令用于调用当前段内的子程序==，格式如下：

> CALL 过程名

例如：

```assembly
CALL	SUB1	;SUB1是近过程
CALL	TOASCII	;TOASCII是近过程
```

该指令进行的具体操作分解如下：

```assembly
SP 		<- SP-2
[SP]	<- IP
IP		<- IP+disp
```

==段内直接调用指令只把返回地址的偏移部分压入堆栈保存==，堆栈变化如图4.1所示。实际上，转移发生在同一段内，代码段寄存器CS的内容不发生变化。==然后把返回地址与子程序入口地址的差值(disp)加到指令指针IP上，使IP之内容为目标地址偏移，从而达到转移的目的。与无条件段内直接转移指令相似，段内直接调用指令的转移是相对转移，指令由操作码和地址差构成。地址差(disp)等于目标地址到CAIL指令下一条指令开始地址的差，汇编程序在汇编时计算出disp。==在段内直接调用指令中，总用一个字表示disp,所以转移范围达一32768+32767。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/root/image.650sl9izmgc0.webp)

#### (2)==段内间接调用==

==段内间接调用指令也用于调用当前段内的子程序==，格式如下：

```assembly
CALL  OPRD
```

==PRD是16位通用寄存器或字存储器操作数==。

该指令进行的具体操作分解如下：

```
SP		<-	SP-2
[SP] 	<-	IP
IP 		<-	(OPRD)
```

该指令只把返回地址的偏移部分压入堆找保存，堆栈变化如图4.1所示。如OPRD是16位通用寄存器操作数，则把寄存器之内容送IP;如OPRD是字存储器操作数，则把字存储单元之内容送IP。

例如：

```assembly
CALL	BX
CALL	WORD PTR BX
CALL	VARW			;VARW是字变鼠
```

#### (3)==段间直接调用==

==段间直接调用指令用于调用其它代码段中的子程==序。格式如下：

```assembly
CALL 	过程名
```

==该指令先把返回地址的段值压入堆栈，再把返回地址的偏移压入堆栈，达到保存返回地址的目的==，堆栈的变化如图4.2所示。==然后把过程的入口地址的段值和偏移分别送入CS和IP,达到转移的目的==。该指令进行的具体操作分解如下：

```assembly
SP 		<-	SP-2
[SP] 	<-	CS
SP 		<-	SP-2
[SP] 	<-	IP
IP 		<-	过程入口地址的偏移
CS		<-	过程入口地址的段值
```

段间直接调用指令与无条件段间直接转移指令相似，机器指令中含有转移目标地址。
例如：

```assembly
CALL	FAR PTR SUBRO 	;设SUBRO是远过程
CALL	SUBF			;设SUBF是远过程
```

==汇编程序MASM能根据过程名所指定的被调用过程的类型决定采用段内直接调用指令还是段间直接调用指令。如果先调用后定义，那么为了调用远过程须在过程名前叫上类型说明符号`FAR PTR`。==
汇编程序TASM根据由过程名所指定的被调用过程与调用指令是否在同一段内决定采用段内直接调用指令还是段间直接调用指令。

#### (4)==段间间接调用==

==段间间接调用指令也用于调用其它代码段中的子程序==。格式如下：

```assembly
CALL OPRD
```

==OPRD是双字存储器操作数==。该指令进行的具体操作可分解如下：

```assembly
SP 		<-	SP-2
[SP] 	<-	CS
SP 		<-	SP-2
[SP] 	<-	IP
IP 		<-	OPRD之低字值
CS		<-	OPRD之高字值
```

==该指令把返回地址的段值和偏移分别压入堆栈保存（堆栈变化如图4.2所示）,然后把双字存储器操作数的低字送IP,把双字存储器操作数的高字送CS,从而实现远转移。==

例如：

```assembly
CALL	DWORD PTR [BX]
CALL	VARD			;设VARD是双字变量
```

==对于间接调用，如果操作数是16位操作数，则汇编成段内间接调用，如果操作数是32位操作数，则汇编成段间间接调用。如果发生调用指令语句在先，有关变量定义伪指语句在后的情况，则需要在调用语句中用PTR等操作符加以说明==。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/root/image.lguz4ninh1c.webp)

### 2.过程返回指令

过程返回指令把子程序的返回地址从堆栈弹出到IP或CS和IP,从而返回调用程序(主程序）继续往下执行。弹出的子程序返回地址一般应该就是由调用指令压入堆栈的返回地址。

过程返回指令不影响标志。

#### (1)段内返回指令

==段内返回指令用于近过程的返回==，格式如下：

```
RET
```

该指令完成的具体操作如下所示：

```
IP		<-	[SP]
SP		<- 	SP+2
```

该指令只从堆栈弹出一个字，送到指令指针IP。堆栈变化过程如图4.1(b)到图4.1(a)所示。==它与段内调用指令相对应，使用在近过程中。实际上，段内调用指令调用近过程时，仅把返回地址的偏移压入堆栈==。

#### (2)段间返回指令

段间返回指令用于远过程的返回，格式如下：

```assembly
RET
```

该指令完成的具体操作如下所示：

```assembly
IP 		<-	[SP]
SP		<- 	SP+2
CS 		<-	[SP]
SP 		<-	SP+2
```

==该指令从堆栈弹出两个字，分别送到指令指针IP和代码段寄存器CS。==堆栈变化过程如图4.2(b)到图4.2(a)所示。==它与段间调用指令相对应，使用在远过程中。实际上，段间调用指令调用远过程时，把返回地址的段值和偏移都压入堆栈。==

==尽管段内返回指令的助记符与段间返回指令的助记符是相同的，但它们的机器指令码是不同的。汇编程序MASM能根据RET指令所在过程的类型决定采用段内返回指令还是采用段间返回指令，只有当返回指令语句出现在远类型过程中时，MASM才把它汇编成段间返回指令。==

汇编程序TASM除了具有上述功能外，还提供了段间返回指令的专门助记符RETF。例如：

```assembly
RETF
```

无论RETF出现在远过程中还是近过程中，汇编程序TASM总把它汇编成段间返回指令。

#### (3)段内带立即数返回指令

段内带立即数返回指令的格式如下：

```assembly
RET		表达式
```

==汇编程序把表达式的结果data取整==。

该指令完成的具体操作如下所示：

```assembly
IP		<-	SP
SP		<-	SP+2
SP 		<-	SP+data
```

==先从堆栈弹出一个字作为返回地址，再额外根据data修改堆栈指针。所以该指令不仅能实现段内返回，而且能同时再修改堆栈指针。由于对堆栈的正常操作均是以字为单位，所以，表达式的结果一般应是偶数==。例如：

```assembly
RET		4
```

设在执行上面的指令前SP=B67EH,在执行后SP=B684H。

#### (4)段间带立即数返回指令

段间带立即数返回指令的格式如下：

```assembly
RET		表达式
```

==该指令先从堆栈弹出两个字，分别送到指令指针IP和代码段寄存器CS,再额外修改堆栈指针，即把表达式的结果加到SP。所以该指令不仅能实现段间返回，而且能同时再修改堆栈指针。==例如：

```
RETF		4
```

设在执行上面的指令前SP=B67EH,则在执行后SP=B686H。

## 4.1.2过程定义语句

==利用过程定义伪指令语句，可把程序片段说明为具有近类型或远类型的过程，并且能给过程起一个名字。==过程定义语句的格式如下：

```assembly
过程名			PROC [NEAR | FAR]
...
...
过程名			ENDP
```

==过程的类型在过程定义开始语句PROC中指定，过程可以被指定为近（NEAR)类型，也可以被指定为远（FAR)类型。如果不指定，则通常默认为近类型。==如前所述，使用段内调用指令还是段间调用指令调用一个过程，由被调用过程的类型决定。==因此，如果一个过程要被别的程序段调用，就应该把它说明为FAR类型。如果某个过程具有NEAR类型，那么该过程仅能被其所在段调用。==

定义一个过程的开始语句PROC和结束语句ENDP前的过程名称必须一致，从而保持配对。过程名称的命名与普通标号的命名方法相同。==像普通标号一样，过程名具有段值、偏移和类型这三个属性。过程名的段值和偏移是对应过程入口（过程定义开始伪指令语句后的指令语句）的段值和偏移，过程名的类型就是过程的类型。==

把一位十六进制数转换为对应ASCII码的过程可定义如下：

```assembly
;设欲转换的十六进制数码在AL的低4位
	;转换得到的ASCII码在AL中
	HTOASC PROC NEAR
		AND		AL,0FH
		ADD		AL,30H
		CMP 	AL,39H
		JBE 	TOASC1
		ADD		AL,7H
	TOASC1:		RET
	HTOASC 		ENDP
```

为了能返回调用程序，==一般说来，在一个过程中至少要有一条返回指令，也可含多条返回指令。返回指令语句是过程的出口。==

但返回指令语句不一定非要安排在过程的最后。为了说明在一个过程中使用多条RET指令，现将上述的“把一位十六进制数转换为对应ASCII码的过程”改写如下：

```assembly
HTOASC		PROC
		AND 	AL,OFH
		CMP		AL,9
		JBE		HTOASC1
		ADD 	AL,37H
		RET
HTOASC1:ADD		AL,30H
		RET
HTOASC	ENDP
;还可把上述过程作如下改变，其中RET语句不在过程的最后：
HTOASC	PROC
		AND		AL,OFH
		CMP		AL,9
		JBE 	HTOASC1
		ADD		AL,37H
HTOASC2:RET
HTOASC1:ADD		AL,30H
		JMP 	HTOASC2
HTOASC	ENDP
```

## 4.1.3子程序举例

#### 例1:写一个把用ASCII码表示的两位十进制数转换为对应二进制数的子程序

转换算法是：设x是十位数，y是个位数，计算10x+y。

子程序源代码如下：

```assembly
;入口参数：DH='1'(十位数ASCII码，非压缩)，DL='2'(个位数ASCII码，非压缩)
;出口参数：AL=对应二进制数
SUBR	PROC
		MOV AL,DH	;'1'，挪到al中
		AND AL,OFH	;清空高4位
		MOV AH,10	;10挪到ah中，作为乘数
		MUL AH		;al * ah (1 * 10) = 10
		MOV AH,DL	;'2'，挪到AH中
		AND AH,OFH	;清空高4位
		ADD AL,AH	;乘积加值
		RET
SUBR	ENDP
```

上述子程序SUBR没有判别作为入口参数提供的DH和DL寄存器中的内容是否是十进制数的ASCII码。

#### 例2:写一个把16位二进制数转换为4位十六进制数ASCII码的子程序。

转换方法是：把16位二进制数向左循环移位四次，使高4位成为低四位，析出低四位，调用子程序HTOASC转换出1位十六进制数的ASCII码，重复四次便完成转换。子程序源代码如下：

```assembly
;入口参数:	DX=欲转换的二进制数
;		   DS:BX=存放转换所得ASCII码串的缓冲区首地址
;出口参数：十六进制数ASCII码串按高位到低位依次存放在指定的缓冲区中
HTASCS		PROC
			MOV 	CX,4
HTASCS1:	ROL		DX,1	;循环左移4位
			ROL 	DX,1	;高四位成为低四位
			ROL 	DX,1
			ROL 	DX,1
			MOV 	AL,DL	;复制出低四位
			CALL 	HTOASC	;转换得ASCII码
			MOV 	[BX],AL	;保存
			INC 	BX		;调整缓冲区指针
			LOOP 	HTASCS1	;重复四次
			RET
HTASCS ENDP
```

利用上述子程序HTASCS按十六进制数形式显示地址为F000:0000H的字单元内容的一个程序如下：

```assembly
;程序名：T4-1.ASM功能：(略）
DSEG 		SEGMENT;数据段
BUFF 		DB 4 DUP (0)	;存放4位十六进制数的ASCII码
			DB	'H', ODH, OAH,'$'	;形成以$结尾的串
DSEG ENDS
;
CSEG 		SEGMENT		;代码段
			ASSUME CS : CSEG , DS : DSEG
START: 		MOV AX,DSEG
			MOV DS,AX
			;
			MOV AX,0F000H
			MOV ES,AX
			MOV DX,ES:[0]		;取指定内存单元的内容
			MOV BX,OFFSET BUFF	;准备入口参数
			CALL HTASCS			;转换为4位十六进制数的ASCII码
			;
			MOV DX,OFFSET BUFF
			MOV AH,9			;调用9H号功能显示之
			INT	21H
			;
			MOV AX,4C00H		;程序正常结束
			INT	21H
			;
HTASCS	PROC
;内容略
HTASCS	ENDP
;
HTOASC	PROC
;内容略
HTOASC	ENDP
CSEG	ENDS
		END START
```

#### 例3:写一个把16位二进制数转换为5位十进制数ASCII码的子程序

为了简单，设二进制数是无符号的。

第三章中的程序T3-11.ASM实现“把16位二进制数转换为5位十进制的BCD码”，把BCD码转换为对应ASCII码是容易的，所以可把T3-11.ASM改写成一个子程序。但我们采用另一个算法把16位二进制数转换为5位十进制数的BCD码。

该算法是：==把16位二进制数除以10,余数是“个位”数的BCD码；把商再除以10,余数就是“十位”数的BCD码==；

如此，可依次得“百位”、“千位”和“万位”数的BCD码。这可利用一个循环来控制。

```assembly
;入门参数：AX=欲转换的二进制数
;		 DS:BX=存放转换所得ASCII码串的缓冲区首地址
;出口参数：十进制数ASCII码串按万位到个位的序依次存放在指定的缓冲区中
BTOASC		PROC
			MOV SI,4		;置循环次数
			MOV CX,10		;置除数10
BTOASC1:	XOR	DX,DX		;把被除数扩展成32位
			DIV CX			;除操作
			ADD DL,30H		;余数为BCD码，转换为ASCII码
			DEC SI			;调整循环计数器
			MOV	[BX][SI],DL	;保存所得ASCII码
			OR SI,SI		;判是否转换结束
			JNZ BTOASC1		;否，继续
			RET
BTOASC	ENDP
```

在上面的子程序中，寄存器SI既作计数器使用又作变址指针使用。

## 4.1.4子程序说明信息

为了能正确地使用子程序，在给出子程序代码时还要给出子程序的说明信息。子程序说明信息一般由如下几部分组成，每一部分内容应简明确切：

> (1)子程序名。
>
> (2)功能描述。
>
> (3)人口和出口参数。
>
> (4)所用的寄存器和存储单元。
>
> (5)使用的算法和重要的性能指标。
>
> (6)其他调用注意事项和说明信息。
>
> (7)调用实例。

子程序说明信息至少应该包含前三部分内容。例如：

```assembly
;子程序名：	AHTOASC
;功能：把8位二进制数转换为2位十六进制数的ASCII码
;入口参数：	AL=欲转换的8位二进制数
;出口参数：	AH=十六进制数高位的ASCH码
;			AL=十六进制数低位的ASCH码
;其他说明：
;		(1)近过程
;		(2)除AX寄存器外，不影响其他寄存器
;		(3)调用了HTOASC实现十六进制数到ASCII码的转换
;
```

在看了上述关于子程序AHTOASC的说明信息后，即使不熟悉或不了解这个子程序
本身，也就能够方便地调用它了。
子程序AHTOASC的源代码如下：

```assembly
AHTOASC PROC
		MOV AH,AL
		SHR AL,1
		SHR AL,1
		SHR AL,1
		SHR AL,1
		CALL HTOASC
		XCHG AH,AL
		CALL HTOASC
		RET
AHTOASC ENDP
```

## 4.1.5寄存器的保护与恢复

子程序为了完成其功能，通常要使用一些寄存器存放内容，有时还要使用一些存储单元存放内容。也就是说，在子程序运行时通常会破坏一些寄存器或存储单元的原有内容。所以，如果不采取措施，那么在调用子程序后，主程序就无法再使用存放在这些寄存器或存储单元中的原有内容了，这常常会导致主程序的错误。为此，要对有关寄存器或存储单元的内容进行保护与恢复。

寄存器的保护与恢复有两种方法：

1. 把需要保护的寄存器的内容，在主程序中压入堆栈和弹出堆栈。这种方法的优点是，在每次调用子程序时，只要把主程序所关心的寄存器压入堆栈。但缺点是：在主程序中使用压入和弹出堆栈的操作会使主程序不易理解；如果要多次调用子程序时，会很累赞，而且常常会忘了把某个寄存器压入堆栈。

2. ==在子程序一开始就把在子程序中要改变的寄存器内容压入堆栈，而在返回之前再恢复这些寄存器的内容。这种方法的优点是：在主程序中可方便地调用子程序，而无需考虑要把哪些寄存器压入堆栈；只需要在子程序中写一次压入和弹出堆栈指令组即可。这种方法是常用的方法。==

下面是重写的BTOASC子程序，它保护了所有被改变的寄存器内容，主程序在调用它时，不必担心各寄存器的内容受影响。

```assembly
;子程序说明信息略
BTOASC	PROC
		PUSH AX
		PUSH CX
		PUSH DX
		PUSH SI
		MOV SI,5		;置循环次数
		MOV CX,10		;置除数10
BTOASC1:XOR DX,DX		;把被除数扩展成32位
		DIV CX			;除操作
		ADD	DL,30H		;余数为BCD码，转换为ASC11码
		DEC SI			;调整循环计数器
		MOV [BX][SI],DI	;保存所得ASCII码
		OR SI,SI		;判是否转换结束
		JNZ BTOASC1		;否，继续
		POP SI
		POP DX
		POP CX
		POP AX
		RET
BTOASC	ENDP
```

还有几点说明，请注意：

> (1)上述子程序BTOASC实际上还破坏了标志寄存器中的部分标志，可用PUSHF票令和POPF指令保护和恢复标志寄存器。但一般不在子程序中保护和恢复标志寄存器。
>
> (2)在利用堆栈进行寄存器的保护和恢复时，要注意堆栈的先进后出特性。
>
> (3)有时为了简单，并不保护含有入口参数的寄存器。是否要保护入口参数寄存器，可以根据实际情况事先约定。在BTOASC中，含有入口参数的AX寄存器得到了保护。可像寄存器的保护和恢复那样，保护和恢复有关存储单元的内容，即在子程序开始时把有关存储单元的内容压入堆栈，在子程序返回前恢复它们。在子程序中应尽量避免把普通存储单元作为临时变量使用，可以利用堆栈元素作为临时变量使用。

# 4.2主程序与子程序间的参数传递

主程序在调用子程序时，往往要向子程序传递一些参数；同样，子程序运行后也经常要把一些结果参数传回给主程序。主程序与子程序之间的这种信息传递称为参数传递。我们把由主程序传给子程序的参数称为子程序的入口参数，把由子程序传给主程序的参数称为子程序的出口参数。一般子程序既有入口参数，又有出口参数。但有的子程序只有入口参数，而没有出口参数；有的子程序只有出口参数，而没有入口参数。

==有多种参数传递的方法：寄存器传递法、约定内存单元传递法、堆栈传递法和CALL后续区传递法==等。主程序与子程序间传递参数的方法是根据具体情况而事先约定好的。有时可能同时采用多种方法。

## 4.2.1利用寄存器传递参数

==利用寄存器传递参数就是把参数放在约定的寄存器中。这种方法的优点是实现简单和调用方便。但由于寄存器的个数是有限的，且寄存器往往还要存放其他数据，所以只适用于要传递的参数较少的情况。==

在4.1节中的子程序HTOASC和子程序AHTOASC等就是利用寄存器来传递参数的，现再举几例。

#### 例1:写一个把大写字母改为小写字母的子程序。

在ASCII码表中，大写字母的ASCII码比对应小写字母的ASCII码小20H(即'a'-'A'),所以在确定字符是大写字母后，把它转换为对应的小写字母是容易的。

```assembly
;子程序名：UPTOLW
;功能：把大写字母转换为小写字母
;入口参数：AL=字符ASCII码
;出口参数：AL=字符ASCII码
;说明：如果字符是大写字母，则转换为小写字母，其他字符不变
UPTOLW			PROC
	PUSHF		;保护各标志
		CMP AL,'A'
		JB 	UPTOLW1
		CMP AL,'Z'
		JA 	UPTOLW1
		ADD AL,'a'-'A'
UPTOLW1:POPF		;收复各标志
		RET
UPTOLW 	ENDP
```

上面的子程序还保护了标志寄存器，从而使得在执行该子程序后，各标志不受影响是否要保护标志可视具体情况而事先约定。有时还利用某些标志作为出入口参数。进位标志CF常常用作为出口参数。

#### 例2:写一个判别字符是否为数字符的子程序。并利用该子程序把一个字符串中的所有数字符删除。

子程序ISDECM及其说明如下：

```assembly
;子程序名：ISDECM
;功能：判别一个字符是否为数字符
;入门参数：AL=字符
;出口参数：CF为0表示字符是数字符，否则字符是非数字符
ISDECM		PROC
		CMP AL,'0'
		JB 	ISDECM1
		CMP AL,'9'
		JA 	ISDECM1
		CLC
		RET
ISDECM1:STC
		RET
ISDECM ENDP
```

对上述子程序可作些优化工作，改写过的ISDECM如下所示：

```assembly
ISDECM 		PROC
		CMP AL,'0'
		JB 	ISDECM1
		CMP AL,'9'+1
		CMC				;把CF标志取反
ISDECM1:RET
		ISDECM ENDP
```

改写过的子程序ISDECM利用了CMP指令对标志的影响。使用该子程序把字符串中的数字符删除的源程序如下，子程序位于主程序之前：

```assembly
;程序名：T4-2.ASM
;功能：(略）
DSEG 		SEGMENT
STRING		DB		'AB=C950=asd',0		;假设的字符串
DSEGE		NDS
;
CSEG		SEGMENT
			ASSUME CS : CSEG , DS : DSEG
ISDECM		PROC
;(略）
ISDECM 		ENDP
START: 		MOV AX,DSEG
			MOV DS,AX
			MOV SI,OFFSET STRING;置取指针
			MOV	DI,SI			;置存指针
NEXT:		MOV	AL,[SI]			;取一个字符
			INC SI
			OR 	AL,AL			;是否到字符串尾？
			JZ 	OK				;是，转
			CALL ISDECM			;否，判是否为数字符
			JNC	NEXT			;是，不保存而处理下一个字符
			MOV [DI],AL			;否，保存
			INC DI
			JMP NEXT			;处理下一个字符
OK:			MOV[DI],AL
			MOV AH,4CH
			INT 21H
CSEG		ENDS
			END START
```

## 4.2.2利用约定存储单元传递参数

==在传递参数较多的情况下，可利用约定的内存变量来传递参数。这种方法的优点是子程序要处理的数据或送出的结果都有独立的存储单元，编写程序时不易出错。==但这种方要占用一定的存储单元，此外还有其他缺点，我们在下文中说明。

#### 例3:写一个实现32位数相加的子程序。

```assembly
;子程序名：MADD
;功能：32位数相加
;入口参数：DATA1和DATA2缓冲区中分别存放要相加的32数
;出口参数：DATA3缓冲区中存放结果
;说明：
;(1)32位数据的存放次序采用“高高低低”的原则
;(2)可能产生的进位存放在DATA3开始的第5字节中
MADD 	PROC
		PUSH AX
		PUSH CX		;保护寄存器
		PUSH SI
		MOV CX,2
		XOR SI,SI
MADD1: 	MOV AX,WORD PTR DATA1[SI]
		ADC AX,WORD PTR DATA2[SI]	;16位相加
		MOV WORD PTR DATA3[SI],AX
		INC SI
		INC	SI
		LOOP MADD1
		MOV AL,0			;处理进位位
		ADC AL,0
		MOV	BYTE PTR DATA3+4,AL
		POP SI
		POP CX				;恢复寄存器POP AXRET
MADD	ENDP
```

==利用约定的存储单元传递参数，通用性较差。上述子程序MADD的通用性就较差。为了传递较多的参数，又要保持良好的通用性，通常把参数组织成一张参数表，存放在某个存储区，然后把这个存储区的首地址传送给子程序。既可利用寄存器传递首地址，也可利用堆栈方法传递首地址==。

#### 例4:设计一个把以ASCII码表示的十进制数字串转换为二进制数的子程序。

设表示的十进制数不大于65535。

假设要转换的十进制数字串存放在由DS:BX所指的缓冲区中，该缓冲区的第一字节含有十进制数字串的长度。缓冲区结构如图4.3所示。设十进制数字串中各位对应的BCD码是dn、dn-1..d2、d,那么它所表示的二进制数可由下式计算出：
$$
Y =((((0*10+d_n)*10+d_{n-1})*10+...)*10+d_2)*10+d_1
$$
可通过迭代的方法进行上式的计算，迭代公式如下，Y的初值为0:
$$
Y = Y * 10 + d_i \qquad(i=n,n-1,...1)
$$
所以，当十进制数字串中数字的个数为n时，那么只需进行n次选代计算。

由于假设数字串表示的十进制数不超过65535,所以用16位的AX寄存器存放迭市式中的Y,也用AX寄存器返回转换后得到的二进制数。子程序源代码如下所示：

```assembly
;子程序名：DTOBIN
;功能：把用ASCII码表示的十进制数字串转换为二进制数
;人口参数：DS:BX=缓冲区首地址（缓冲区结构如图4.3)
;出口参数：AX=转换得到的二进制数
DTOBIN		PROC
		PUSH 	BX
		PUSH 	CX
		PUSH 	DX
		XOR 	AX,AX		;设置初值0
		MOV 	CL,[BX]
		INC 	BX
		XOR 	CH,CH		;CX=n
		JCXZ 	DTOBIN2
DTOBIN1:MOV		DX,10
		MUL		DX			;Y*10
		MOV 	DL,[BX]		;取下一个数字符
		INC 	BX
		AND 	DL,OFH		;转成BCD码
		XOR 	DH,DH
		ADD 	AX,DX		;Y*10+di
		LOOP 	DTOBIN1
DTOBIN2:POP		DX
		POP 	CX
		POP		BX
		RET
DTOBIN	ENDP
```

这个子程序有两点不足：

(1)没有检查数字串中是否有非十进制数字符存在，

(2)不适用于数字串表示的十进制数超过值65535的情况。

## 4.2.3利用堆栈传递参数

==如果使用堆栈传递入口参数，那么主程序在调用子程序之前，把需要传递的参数依压入堆栈，子程序从堆栈中取入口参数；如果使用堆栈传递出口参数，那么子程序在返回前，把需要返回的参数存入堆栈，主程序在堆栈中取出口参数。==

利用堆栈传递参数可以不占用寄存器，也无需使用额外的存储单元。==但由于参数和子程序的返回地址混杂在一起，有时还要考虑保护寄存器，所以较为复杂。通常利用堆栈传递入口参数，而利用寄存器传递出口参数。==

#### 例5:写一个测量字符串长度的子程序。

设字符串以0为结束标志。利用堆栈来传递入口参数：字符串的起始地址（设包括段值和偏移）;利用寄存器传递出口参数：字符串的长度。设子程序STRLEN是一个近过程，那么主程序在调用它时，只把返回地址的偏移压入堆栈。

```assembly
;子程序名：STRLEN
;功能：测量字符串的长度
;入门参数：字符串起始地址的段值和偏移在堆栈中，见图4.4(a)
;出口参数：AX=字符串长度
STRLEN		PROC
		PUSH 	BP
		MOV 	BP,SP			;为取参数作准备
		PUSH 	DS				;保护寄存器
		PUSH 	SI				;执行此指令后的堆栈如图4.4(c)
		MOV 	DS,[BP+6]		;取入口参数
		MOV 	SI,[BP+4]
		MOV 	AL,0			;置要找的结束标志值
STRLEN1:CMP		AL,[SI]			;判是否到字符串尾
		JZ 		STRLEN2			;到尾转
		INC 	SI
		JMP 	STRLEN1			;继续
STRLEN2:MOV		AX,SI
		SUB 	AX,[BP+4]		;计算字符串长度
		POP 	SI
		POP 	DS				;收复寄存器
		POP 	BP
		RET
STRLEN ENDP
```

子程序借助指针寄存器BP,采用寄存器相对寻址方式取得入口参数，位移量与返回地址占用的字节数、为保护寄存器BP而使用的字节数、入口参数压入堆栈的次序有关。==另外，请注意当BP寄存器作为指针使用时，隐含使用的段寄存器是SS,这正是堆栈段。==

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/root/image.45gz4oludl20.webp)

主程序调用这个子程序的代码片段如下所示，它说明主程序与子程序一定要事先公定，密切配合：

```assembly
;······
MOV		AX,SEG STRMESS		;把入口参数压入堆栈
PUSH 	AX
MOV 	AX,OFFSET STRMESS
PUSH 	AX
CALL	STRLEN				;调用子程序
							;执行调用指令后堆找如图4.4(b)
ADD		SP,4				;从子程序返回后堆栈如图4.4(d);
							;执行此指令后堆栈如图4.4(e)
MOV		LEN,AX				;保存字符串长度值
;......
```

主程序在调用子程序STRLEN之前把要测量长度的字符串起始地址的段值和偏移作为入口参数压入堆栈，然后就马上调用子程序STRLEN,这样堆栈就如图4.4(b)所示从子程序返回后堆栈如图4.4(d)所示，==所以需要废除仍在堆栈中的入口参数，本例中利用了指令`ADD SP,4`来平衡堆栈，==使堆栈恢复到如图4.5(e)所示。

==另一种废除堆栈中入口参数的方法是使用带立即数返回指令。这可在子程序返回时就自动平衡堆栈，主程序就不需要再进行堆栈的平衡工作。对于上述子程序，可采用如下的返回指令：==

```assembly
RET 	4 	;此立即数与入口参数量有关
;等于
ADD SP,4
```

如果这样的话，上面主程序调用子程序的片段就不需要调整堆栈指针的指令`ADD SP,4`。

## 4.2.4利用CALL后续区传递参数

==CALL后续区是指位于CALL指令后的存储区域。主程序在调用子程序之前，把入口参数存入CALL指令后的存储单元中，子程序根据保存在堆栈中的返回地址找到入口参数，这种传递参数的方法称为CALL后续区传递参数法。==利用CALL后续区传递参数的子程序必须修改返回地址。由于这种方法把数据和代码混在一起，所以在x86系列汇编语言程序中使用得不多。

图4.5是有关代码段、数据段和堆栈段的示意图，其中

- (a)是调用之前的情形，
- (b)是调用和保护有关寄存器后的情形，
- (c)是修改返回地址后的情形。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/root/image.1v353gvqbudc.webp)

#### 例6:写一个把字符串中的大写字母改为小写字母的子程序（近过程）

设字符串以0为结束标志。用CALL后续区传递字符串起始地址的段值和偏移。

```assembly
;子程序名：STRLWR
;功能：把字符申中的所有大写字母改为小写字母
;入口参数：字符串起始地址的段值和偏移在CALL后续区（见图4.5(a))。
;出口参数：无
STRLWR		PROC
		PUSH 	BP
		MOV 	BP,SP
		PUSH 	AX
		PUSH 	SI			;保护寄存器
		PUSH 	DS
		MOV 	SI,[BP+2]	;从堆栈中取得返回地址，见图4.5(b)
		MOV 	DS,CS:[SI+2];取入口参数（字符串段值）
		MOV 	SI,CS:[SI]	;取入口参数（字符串偏移）
STRLWR1:MOV		AL,[SI]
		CMP 	AL,0
		JZ		STRLWR3
		CMP 	AL,'A'
		JB 		STRLWR2		;把字符串中的大写字母改为小写
		CMP 	AL,'Z'
		JA 		STRLWR2
		ADD 	AL,'a'-'A'	;ADD AL,20H
		MOV		[S1],AL
STRLWR2:INC		SI
		JMP 	STRLWR1
STRLWR3:ADD		WORD	PTR[BP+2],4	;修改返回地址，见图4.5(c)
		POP 	DS		;恢复寄存器
		POP 	SI
		POP		AX
		POP 	BP
		RET
STRLWR	ENDP
```

调用上述子程序的源程序片段如下所示：

```assembly
;......
CALL STRLWR
DW	OFFSET STRMESS
DW 	SEG DSEG
CONT:......
```

# 4.3DOS功能调用及应用

尽管DOS正在逐步消亡，但目前在DOS平台上进行x86汇编语言程序设计的实习还是最有效的途径。了解DOS功能调用，不仅有益于程序设计锻炼，也便于在DOS平，上进行汇编语言程序设计的实习。

## 4.3.1DOS功能调用概述

### 1.什么是DOS功能调用

MS-DOS(PC-DOS)内包含了许多涉及设备驱动和文件管理等方面的子程序，DOS的各种命令就是通过适当地调用这些子程序实现的。为了方便程序员使用，把这些子程序编写成相对独立的程序模块而且编上号。程序员利用汇编语言可方便地调用这些子程序。这些子程序被精心编写，而且经过了大量的各种应用范围的实践考验。程序员调用这些子程序可减少对系统硬件环境的考虑和依赖，从而一方面可大大精简应用程序的编写，另一方面可使程序有良好的通用性。这些编了号的可由程序员调用的子程序就称为DOS的功能调用或称为系统调用。一般认为DOS的各种命令是操作员与DOS的接口，而功能调用则是程序员与DOS的接口。

DOS功能调用主要包括三方面的子程序：设备驱动（基本I/O)、文件管理和其他（括内存管理、置取时间、置取中断向量、终止程序等）。随着DOS版本的升级，这种称为DOS 功能调用的子程序数量也不断增加，功能更加完备，使用也更加方便。

### 2.调用方法

可按如下方法调用DOS功能调用：

1. 根据需调用的功能调用准备入口参数。有部分功能调用是不需要入口参数的，但大部分功能调用需要入口参数，在调用前应按要求准备好入口参数。

2. 把功能调用号送AH寄存器。

3. 发软中断指令`INT 21H`。


程序员不必关心有关子程序在何处，也不必关心它是如何实现其功能的。

例如：调用2号功能调用，使喇叭发出“哪”的一声。2号功能调用的功能是在屏幕上显示一个字符，入口参数是DL寄存器为要显示字符的ASCII码。当要显示字符的ASCII码为07H时，并不在屏幕上显示字符，而是使喇叭发出“嘟”的一声。程序片段如下：

```assembly
MOV		DL,07H		;准备入口参数
MOV		AH,2		;置功能调用号
INT 	21H			;实施调用
```

大部分功能调用都有出口参数，在调用后，可根据有关功能调用的说明取得出口参数。部分功能调用没有出口参数，如2号功能调用，调用它后，只是在屏幕上显示相应的字符，或发出“嘟”的一声。

还有个别功能调用很特殊，调用它后就不再返回。例如4CH号功能调用，其功能就是结束程序的运行而返回DOS。我们已在多个程序中使用了这个功能调用。4CH号功能调用有一个存放在AL寄存器中的入口参数，该入口参数是程序的结束码，其值的大小不影响程序的结束。例如：

```assembly
MOV		AL,0		;置退出码
MOV		AH,4CH		;置功能调用号
INT		21H			;实施调用
```

## 4.3.2基本I/O功能调用

### 1.==带回显键盘输人（1号功能调用）==

功能：==从标准输入设备上读一字符，并将该字符回显在标准输出设备上。通常情况下，标准输入设备就是键盘，标准输出设备就是屏幕。如果键盘无字符可读，则一直等待到有字符可读（即按键）。==

入口参数：无。

出口参数：==AL=读到字符的代码（ASCII码）。==

说明：如果读到的字符是Ctrl+C或Ctrl+Break,则结束程序。

### 2.==不带回显键盘输入（8号功能调用）==

==除读到的输入字符不在屏幕上显示外，同1号功能调用==。

### 3.==直接键盘输入（7号功能调用）==

功能：从标准输入上读一字符。通常情况下，标准输入就是键盘。如果键盘无字符可读，则一直等待到有字符可读（即按键）

入口参数：无。

出口参数：==AL=读到字符的代码。==

说明：

==(1)不检查读到的字符是否是Ctrl+C或Ctrl+Break。==

==(2)不回显读到的字符。==

### 4.==显示输出（2号功能调用）==

功能：向标准输出设备写一字符。通常情况下，标准输出设备就是屏幕。

入口参数：==DL=要输出的字符（ASCII码）。==

出口参数：无。

说明：==在显示输出时检查是否按Ctrl+C或Ctrl+Break键，如是则结束程序==。

### 5.==直接控制台输入输出（6号功能调用）==

功能：直接控制台输入输出。通常情况下，控制台输入就是键盘输入，控制台输出就是屏幕输出。

入口参数：若DL=OFFH,表示输入；否则表示输出，DL=输出字符代码。

出口参数：输入时，ZF=1表示无字符可读；ZF=0表示读到字符，AL=输入字符代码；输出时，无。
说明：

1. 在输入时，如无字符可读，并不等待。

2. 不检查Ctrl+C或Ctrl+Break键。

3. 在读到字符时也不回显。

4. 在输入时，如果AL=0,表示用户曾按过一个扩展键，在下一次调用该功能时返回扩展键的扫描码。

5. 在输出时，不解释制表符等特殊控制符。


### 6.==显示字符串（9号功能调用）==

功能：在标准输出上显示一个字符串。通常情况下，标准输出就是屏幕。

入口参数：DS:DX=需要输出字符串的首地址，字符串以字符'$'为结束标志。

出口参数：无。

说明：在显示输出时检查是否按Ctrl+C或Ctrl+Break键，如是则结束程序。

### 7.==输入字符串（0AH号功能调用）==

功能：从标准输入上读一个字符串。通常情况下，标准输入就是键盘。

入口参数：==DS:DX=缓冲区首地址==。

出口参数：接收到的输入字符串在缓冲区中。

说明：

(1)==缓冲区第一字节置为缓冲区最大容量，可认为是入口参数；缓冲区第二字节存放实际读入的字符数（不包括回车符）,可认为是出口参数的一部分；第三字节开始存放接受的字符串。==

(2)==字符串以回车键结束，回车符是接受到的字符串的最后一个字符==。

(3==)如果输入的字符数超过缓冲区所能容纳的最大字符数，则随后的输入字符被丢弃并且响铃，直到遇回车键为止。==

(4)==如果在输入时按Ctrl+C或Ctrl+Break键，则结束程序。==

### 8.==取键盘输入状态（0BH号功能调用）==

功能：判别在标准输入设备上是否有字符可读。

入口参数：无。

出口参数：AL=0,表示无字符可读；AL=OFFH,表示有字符可读。

说明；检查是否按Ctrl+C或Ctrl+Break键，如遇这种键，则程序结束。

### 9.==清除输入缓冲区后再输人（0CH号功能调用）==

功能：清除输入缓冲区，然后再执行某个输入功能。

入口参数：AL=清除输入缓冲区后要执行的功能号。

出口参数：决定于清除输入缓冲区后执行的功能。

说明：清除缓冲区后执行的功能应是01H、06H、07H、08H或0AH,如不是这样，则不输入。例如：若AL=0,则在清除输入缓冲区后，没有进一步的处理。

### 10.==打印输出（5号功能调用）==

功能：向连接在第一个并行口上的打印机输出一字符。

入口参数：DL=要打印的字符（ASCII码）。

出口参数：无。

说明：打印机可能不立即打印出指定的字符。

## 4.3.3应用举例例

#### 例1:写一个程序，它用二进制数形式显示所按键的ASCII码。

首先利用1号功能调用接受一个字符，然后通过移位的方法从高到低依次把其ASCII码值的各位析出，再转换成ASCII码，利用2号功能调用显示输出。源程序如下所示：它还含有一个形成回车换行（光标移到下一行首）的子程序。

```assembly
;程序名：T4-3.ASM
;功能：(略）
CSEG		SEGMENT
		ASSUME CS : CSEG , DS : CSEG
START:		MOV		AH,1		;读一个键
			INT		21H
			CALL 	NEWLINE		;回车换行
			MOV 	BL,AL
			MOV 	CX,8		;8位
NEXT:		SHL		BL,1		;依次析出高位
			MOV 	DL,30H
			ADC 	DL,0		;转换得ASCII码
			MOV 	AH,2
			INT 	21H			;显示之
			LOOP 	NEXT		;循环8次
			MOV 	DL,'B'
			MOV 	AH,2		;显示二进制数表示符
			INT 	21H
			MOV 	AH,4CH		;正常结束
			INT		21H
;子程序名：NEWLINE
;功能：形成回车和换行（光标移到下一行首）
;入口参数：无
;出口参数：无
;说明：通过显示回车符形成回车，通过显示换行符形成换行
NEWLINE 	PROC
			PUSH 	AX
			PUSH 	DX
			MOV 	DL,OAH		;回车符的ASCII码是ODH
			MOV 	AH,2		;显示回车符
			INT		21H
			MOV 	DL,OAH		;换行符的ASCII码是0AH
			MOV 	AH,2		;显示换行符
			INT 	21H
			POP 	DX
			POP 	AX
			RET
NEWLINE ENDP
CSEG	ENDS
		END		START
```

#### 例2:写一个程序，它先接受一个字符串，然后显示其中数字符的个数、英文字母的个数和字符串的长度。

先利用0AH号功能调用接受一个字符串，然后分别统计其中数字符的个数和英文字母的个数，最后用十进制数的形式显示它们。整个字符串的长度可从0AH号功能调用的出口参数中取得。源程序如下所示：

```assembly
;程序名：T4-4.ASM
;功能：(略）
MLENGTH = 128		;缓冲区长度
;
DSEG 		SEGMENT			;数据段
BUFF		DB MLENGTH		;符合0AH号功能调用所需的缓冲区
			DB ?			;实际键入的字符数
			DB MLENGTH DUP (0)
MESSO DB 	'Please input : $ '
MESS1 DB	'Length = $'
MESS2 DB	'X = $'
MESS3 DB	'Y = $'
DSEG 		ENDS
;
CSEG 		SEGMENT				;代码段
		ASSUME CS:CSEG,DS:DSEG
START: 		MOV 	AX,DSEG
			MOV 	DS,AX			;置DS
			MOV 	DX,OFFSET MESSO	;显示提示信息
			CALL 	DISPMESS
			MOV  	DX,OFFSET BUFF
			MOV 	AH,10			;接受一个字符串
			INT		21H
			CALL 	NEWLINE
			MOV 	BH,0			;清数字符计数器
			MOV 	BL,0			;清字母符计数器
			MOV 	CL,BUFF+1		;取字符串长度
			MOV 	CH,0
			JCXZ 	COK				;若字符串长度等于0,不统计
			MOV 	SI,OFFSET BUFF+2;指向字符申首
AGAIN: 		MOV 	AL,[SI]			;取一个字符
			INC 	SI
			CMP 	AL,'0'			;判是否是数字符
			JB		NEXT
			CMP 	AL,'9'
			JA 		NODEC
			INC 	BH				;数字符计数加1
			JMP 	SHORT NEXT
NODEC:		OR		AL,20H			;转小写
			CMP 	AL,'a'			;判是否是字母符
			JB 		NEXT
			CMP 	AL,'z'
			JA 		NEXT
			INC 	BL				;字母符计数加1
NEXT:		LOOP	AGAIN			;下一个
;
COK:		MOV 	DX,OFFSET MESS1
			CALL 	DISPMESS
			MOV 	AL,BUFF+1		;取字符串长度
			XOR	 	AH,AH
			CALL 	DISPAL			;显示字符单长度
			CALL 	NEWLINE
			MOV 	DX,OFFSET MESS2
			CALL 	DISPMESS
			MOV 	AL,BH
			XOR 	AH,AH
			CALL 	DISPAL			;显示数字符个数
			CALL 	NEWLINE
			MOV 	DX,OFFSET MESS3
			CALL 	DISPMESS
			MOV 	AL,BL
			XOR 	AH,AH
			CALL 	DISPAL			;显示字母符个数
			CALL 	NEWLINE
			;
			MOV		AX,4C00H		;程序正常结束
			INT 	21H				
;
;子程序名：DISPAL
;功能：用十进制数的形式显示8位二进制数
;入口参数：AL=8位二进制数
;出门参数：无
DISPAL 		PROC
		MOV 	CX,3			;8位二进制数最多转换成3位十进制数
		MOV 	DL,10
DISP1:	DIV		DL
		XCHG 	AH,AI			;使AL=余数，AH=商
		ADD 	AL,'0'			;得ASCII码
		PUSH 	AX				;压入堆栈
		XCHG 	AH,AL
		MOV		AH,0
		LOOP 	DISP1			;继续
		MOV 	CX,3
DISP2:	POP		DX				;弹出一位
		CALL 	ECHOCH			;显示之
		LOOP 	DISP2			;继续
		RET
DISPAL ENDP
;
;显示由DX所指的提示信息，其他子程序说明信息略
DISPMESS PROC
		MOV		AH,9
		INT 	21H
		RET
DISPMESS ENDP
;
;显示D1.中的字符，其他子程序说明信息略
ECHOCH PROC
		MOV 	AH,2
		INT		21H
		RET
ECHOCH ENDP
;
;略去子程序NEWLINE,该子程序列于源程序T4-3.ASM中
CSEG	ENDS
		END 	START
```

#### 例3:写一个显示指定内存单元内容的程序。具体要求是：允许用户按十六进制数的形式输入指定内存单元的段值和偏移，然后用十六进制数形式显示指定字节单元的内容。

该程序可分成如下几步：

(1)接收段值和偏移；

(2)把指定字节单元中的内容转换成2位十六进制数的ASCII码，边转换边显示。

为了接收段值和偏移，设计子程序GETADR。子程序GETADR接收用户输入的十性六进制数串，并转换为二进制数。根据功能划分，又派生出子程序GETSTR和子程HTOBIN。子程序GETSTR接收一个最大长度为4的十六进制数串；子程序HTOBIN负责把这个数串转换为二进制数。所以，子程序GETADR调用GETSTR和HTOBIN实现其功能。在两次调用GETADR后，可分别得段值和偏移。子程序GETADR没有入口参数，只有在寄存器中提供的出口参数。

子程序GETSTR较为复杂。它并没有调用0AH号功能接收字符串，而是循环调用号功能接收单个字符。这样处理后，如果用户按错键，就可使喇叭发出“哪”的一声，作为提示。为此，它要检查用户按的键是否是十六进制数码键，要处理退格键，还要控制接收的字符数不超过规定等。子程序GETSTR的实现流程如图4.6所示。它通过约定的缓冲区传递参数。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/root/image.4685gv4p86a0.webp)

子程序HTOBIN实现把4位十六进制数的ASCII码转换为一个二进制数。它采用`X=16*X+Yi`的迭代公式，进行四次迭代计算。其中X的初值为0,Yi是根据十六制数符的ASCII码转换得出的值。

```assembly
;程序名：T4-5.ASM功能：用十六进制数的形式显示指定内存字节单元的内容
;符号常量定义
CR = ODH		;回车符
LF = 0AH		;换行符
BACKSPACE = 08H	;退格符
BELLCH = 07H	;响铃符
;数据段
DSEG 	SEGMENT
;存放指定单元的段值和偏移
SEGOFF 	DD ?		MESS1 
		DB 'SEGMENT:$'
MESS2 	DB 'OFFSET:$'
BUFFER 	DB 	5 	DUP (0)		;缓冲区
DSEG 	ENDS
;代码段
CSEG SEGMENT
	ASSUME CS : CSEG , DS : DSEG
START: 		MOV 	AX,DSEG
			MOV 	DS,AX		;设置数据段寄存器
			MOV 	DX,OFFSET MESS1
			CALL 	DISPMESS	;显示提示信息
			CALL 	GETADR		;接收段值
			MOV 	WORD PTR SEGOFF+2,AX	;保存
			MOV 	DX,OFFSET MESS2
			CALL 	DISPMESS	;显示提示信息
			CALL 	GETADR		;接收偏移
			MOV		WORD PTR SEGOFF,AX	;保存
			LES 	DI,SEGOFF		;把段值和偏移送有关寄存器
			MOV 	AL,ES:[DI]		;取字节值
			CALL 	SHOWAL			;转换并且显示
			MOV 	AX,4C00H		;程序结束
			INT 	21H				
;子程序说明信息略
SHOWAL		PROC
			PUSH 	AX
			MOV		CL,4
			ROL		AL,CL
			CALL 	HTOASC
			CALL 	PUTCH
			POP		AX
			CALL 	HTOASC
			CALL 	PUTCH
			RET
			SHOWAL 	ENDP
;子程序说明信息略
GETADR 		PROC
GETADR1:	CALL	GETSTR			;接收一个字符串
			CMP 	BUFFER,CR		;字符串是否空
			JNZ		GETADR2			;不空，转
			CALL 	BELL			;字符串无内容则响铃
			JMP 	GETADR1			;重新接收
GETADR2: 	MOV 	DX,OFFSET BUFFER
			CALL 	HTOBIN			;转换出段值
			CALL 	NEWLINE			;另起一行
			RET
GETADR ENDP
;
;子程序说明信息略
GETSTR 		PROC
			MOV 	DI,OFFSET BUFFER	;置缓冲区首地址
			MOV 	BX,0				;清接收字符数计数器
GETSTR1:	CALL	GETCH				;得一个字符
			CMP		AL,CR				;是否是回车
			JZ 		GETSTR5				;是，转
			CMP		AL,BACKSPACE		;否，是否是退格键
			JNZ		GETSTR4				;否，转
			CMP 	BX,0				;是，判是否有字符可擦除
			JZ 		GETSTR2				;没有字符可擦除，响铃
			DEC 	BX					;有字符可擦，计数减
			CALL 	PUTCH				;光标回移
			MOV		AL,20H
			CALL 	PUTCH				;显示空格擦原字符
			MOV 	AL,BACKSPACE
			CALL 	PUTCH				;光标再回移
			JMP 	GETSTR1;继续接收
GETSTR2:	CALL	BELL
			JMP		GETSTR1
GETSTR4:	CMP		BX,4				;一般键处理
			JZ 		GETSTR2				;如已接收四个字符，响铃
			CALL 	ISHEX				;判是否为十六进制数码符
			JC 		GETSTR2				;否，响铃
			MOV		[BX][DI],AL			;是，依次保存
			INC 	BX					;计数加
			CALL 	PUTCH				;显示之
			JMP 	GETSTR1				;继续接收
GETSTR5:	MOV		[BX][DI],AL			;保存回车符
			RET
GETSTR ENDP
;子程序说明信息略
HTOBIN 		PROC
			PUSH 	CX
			PUSH 	DX					;保护寄存器
			PUSH 	SI
			MOV 	SI,DX				;置指针
			XOR 	DX,DX				;值清0
			MOV 	CH,4				;置循环计数初值
			MOV 	CL,4				;置移位位数
HTOBINI: 	MOV 	AL,[SI]				;取一位十六进制数
			INC 	SI
			CMP 	AL,CR				;是否是回车符
			JZ		HTOBIN2				;是，转返回
			CALL 	ATOBIN				;十六进制数码符转换成值
			SHL 	DX,CL				;X*16+Yi
			OR 		DL,AL
			DEC 	CH					;循环控制
			JNZ		HTOBIN1
HTOBIN2: 	MOV 	AX,DX				;置出口参数
			POP 	SI
			POP 	DX					;恢复寄存器
			POP 	CX
			RET
HTOBIN ENDP
;子程序说明信息略
ISHEX 		PROC
			CMP		AL,'0'
			JB 		ISHEX2
			CMP 	AL,'9'+1
			JB 		ISHEX1
			CMP		AL,'A'
			JB 		ISHEX2
			CMP		AL,'F'+1
			JB 		ISHEX1
			CMP		AL,'a'
			JB		ISHEX2
			CMP 	AL,'f'+1
ISHEX1: 	CMC
ISHEX2: 	RET
ISHEX 		ENDP
;子程序说明信息略
ATOBIN		PROC
			SUB		AL,30H
			CMP		AL,9
			JBE 	ATOBIN1
			SUB		AL,7
			CMP		AL,15
			JBE 	ATOBIN1
			SUB		AL,20H
ATOBIN1:	RET
ATOBIN 		ENDP
;子程序说明信息略
PUTCH 		PROC
			PUSH 	DX
			MOV 	DL,AL
			MOV		AH,2
			INT		21H
			POP		DX
			RET		
PUTCH ENDP
;
;子程序说明信息略
BELL 		PROC
			MOV 	AL,BELLCH
			CALL 	PUTCH
			RET
BELL 		ENDP
;子程序说明信息略
GETCH 		PROC
			MOV 	AH,8			;接受一个字符但不显示
			INT		21H
			RET
GETCH 		ENDP
;
;略去子程序HTOASC,该子程序列于4.1节中
;略去子程序DISPMESS,该子程序列于源程序T4-4.ASM中
;略去子程序NEWLINE,该子程序列于源程序T4-3.ASM中
CSEG 		ENDS
			END 	START
```

# 4.5子程序的递归和重入

子程序是否可递归和是否可重入是子程序的两个重要特性，下面简单介绍这两个基本概念。

## 4.5.1递归子程序

如果一个子程序直接调用它自身，这种调用称为直接递归调用；如果一个子程序间接调用它自身，这种调用称为间接递归调用。具有递归调用的子程序就称为递归子程序。递归是嵌套的特殊情形。

递归子程序必须采用寄存器或堆栈传递参数。递归的深度受堆栈空间的限制。

下面的子程序FACT采用递归算法实现求阶乘，是递归子程序。

```assembly
;子程序名：FACT
;功能：计算n!
;入门参数：(AX)=n
;出口参数：(AX)=n!
;说明：(1)采用递归算法实现求阶乘；
;(2)n不能超过8。
FACT 	PROC
		PUSH 	DX
		MOV 	DX,AX
		CMP 	AX,0		;n为0?
		JZ 		DONE		;是，转
		DEC 	AX			;否，则n-1
		CALL 	FACT		;求（n-1)!
		MUL		DX			;n*(n-1)1
		POP		DX
		RET
DONE: 	MOV 	AX,1		;0!=1
		POP		DX
		RET
FACT 	ENDP
```

该子程序限制入口参数n大小的主要原因，是只采用16位表示阶乘值。

## 4.5.2可重人子程序

==子程序的重入是指子程序在中断后被重新调用==。子程序的重入不同于子程序的递归，重入是被动行为，而递归是主动行为，重入前的调用和重入调用往往是不相干的，而递归调用前后却是密切相关的。

我们把能够重入的子程序称为可重入子程序。在设计可重入子程序时，必须注意如下几点：

==(1)不能利用约定存储单元传递参数==。

下面的子程序NRENT的功能与子程序HTOASC相同，实现把一位16进制数转换为对应的ASCII码，但利用存储单元传递参数。

```assembly
;入口参数：欲转换的一位16进制数在变量HVAR中
;出口参数：对应的ASCII码在变量RESULT中
NRENT 	PROC
		PUSH 	AX 			;(1)
		MOV 	AI,HVAR		;(2)
		AND		AL,OFH		;(3)
		ADD		AL,30H 		;(4)
		CMP 	AL,39H 		;(5)
		JBE		HOK 		;(6)
		ADD 	AL,7 		;(7)
HOK:	MOV 	RESULT,AL 	;(8)
		POP		AX			;(9)
		RET					;(10)
NRENT 	ENDP
```

如果在执行该子程序的第（1)或（2)条指令前被中断，并且中断处理程序重新调用该子程序，那么入口参数就会被破坏，所以原调用的返回就不正确。如果在执行该子程序的第（8)条指令后被中断，并且中断处理程序重新调用该子程序，那么第一次调用的出口参数就会被破坏。所以上述子程序是不可重入的。

==(2)不能使用约定的存储单元保存中间值。==

原因是约定存储单元保存的中间值在重入时可能被破坏。如果子程序要使用临时变量保存中间值，那么临时变量须安排在堆栈中。下面的例子说明如何把临时变量安排在堆栈中。

```assembly
;子程序名：SLEN
;功能：测字符串的长度入口参数：(DS:SI)=字符串首地址出口参数：(AX)=字符串长度
;说明：字符串以0结尾
SLEN 	PROC 	NEAR
		PUSH 	BP
		MOV 	BP,SP
		SUB 	SP,2
		MOV 	WORD PTR [BP-2],0
SLEN1:	MOV		AL,[SI]
		INC 	SI
		OR 		AL,AL
		JZ 		SLEN2
		INC		WORD PTR[BP-2]
		JMP 	SLEN1
SLEN2:	MOV		AX,[BP-2]
		MOV		SP,BP
		POP		BP
		RET
SLEN 	ENDP
```

调用上述子程序后的堆栈如图4.8所示，BP-2所指单元为计数器使用的临时单元。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/root/image.25stpajw6wv4.webp)

# 4.4磁盘文件管理及应用

DOS磁盘文件管理功能调用是DOS功能调用的重要组成部分，不仅有助于汇编语言程序设计练习，也有助于对磁盘文件管理系统的理解。

## 4.4.1DOS磁盘文件管理功能调用

==在下面介绍的DOS磁盘文件管理功能调用中，==用于表示文件名的ASCII字符串必须以ASCII码值0结尾（不是数字符号0),这样的字符串通常称为ASCIIZ串。文件名可以是包含盘符和路径的文件标识。如没有盘符，那么认为是当前盘，如路径不是从根目录开始，那么就认为从当前目录开始。

这些功能调用均利用标志CF表示调用是否成功，如果不成功，那么AX含有错误代码。常见的错误代码有：

> 01		无效的功能号
>
> 02		文件未找到
>
> 03		路径未找到
>
> 04		同时打开文件太多
>
> 05		拒绝存取
>
> 06		无效的文件号（柄）

### 1==.建立文件（3CH号功能调用）==

```assembly
;功能：建立文件（创建新的，或刷新老的文件）。
;入口参数：DS:DX=代表文件名的字符串的首地址。
;CX=文件属性。
;出口参数：CF=0表示成功，AX=文件号（柄）。
;CF=1表示失败，AX=错误代码。
;说明：(1)可指定的文件属性如下：
;00H普通
;01H只读
;02H隐含
;04H系统
;(2)创建文件成功后，文件长度定为0。
```

### 2.==打开文件（3DH号功能调用）==

```assembly
;功能：打开文件。
;入口参数：DS:DX=代表文件名的字符串的首地址。
;AL=存取方式。
;出口参数：CF=0表示成功，AX=文件号（柄）。
;CF=1表示失败，AX=错误代码。
;说明：(1)存取方式规定如下：
;00H 只读方式
;01H只写方式
;02H读写方式
;(2)打开文件成功后，文件指针定位于开始的第一个字节（偏移0)处。
```

### 3.==读文件（3FH号功能调用）==

```assembly
;功能：读文件。
;入口参数：BX=文件号（柄）。
;CX=读入字节数。
;DS:DX=准备存放所读数据的缓冲区的首地址。
;出口参数：CF=0表示成功，AX=实际读到的字节数。
;CF=1表示失败，AX=错误代码。
;说明：(1)通常情况下，实际读到的字节数与欲读入的字节数相同，除非不够读。
;(2)缓冲区应保证能容下所读到的数据。
;(3)文件应以读或读写方式打开。
;(4)读文件后，文件指针将定位到读出字节之后的第一个字节处。
```

### 4.==写文件（40H号功能调用）==

```assembly
;功能：写文件。
;入口参数：BX=文件号（柄）;
;CX=写盘字节数；
;DS:DX=存放写数据的缓冲区的首地址。
;出口参数：CF=0表示成功，AX=实际写出的字节数。
;CF=1表示失败；AX=错误代码。
;说明：(1)通常情况下，实际写出的字节数与欲写盘的字节数相同，除非磁盘满。
;(2)文件应以写或读写方式打开。
;(3)写文件后，文件指针将定位到写入字节之后的第一个字节处。
```

### 5.==关闭文件（3EH号功能调用）==

```assembly
;功能：关闭文件。
;入口参数：BX=文件号（柄）。
;出口参数：CF=0表示成功。
;CF=1表示失败。
;说明：文件号是打开该文件时系统所给定的文件号。
```

### 6.==移动文件读写指针（42H号功能调用）==

```assembly
;功能：移动文件（读写）指针。
;入口参数：BX=文件号（柄）。
;CX:DX=移动位移量。
;AL=移动方式。
;出口参数：CF=0表示成功，此时，DX:AX=移动后文件指针值。
;CF=1表示失败，此时，(AX)=1表示无效的移动方式，(AX)=6表示无效的文件号。
;说明：(1)文件指针值（双字）是以文件首字节为0计算的。
;(2)移动方式和表示的意义如下：
;00H 移动后文件指针值=0(文件头）+移动位移量
;01H =当前文件指针值+移动位移量
;02H =文件长（文件尾）+移动位移量
;(3)在第一种移动方式中，移动位移量总是正的。
;(4)在后两种移动方式中，移动位移量可正可负。
;(5)该子功能不考虑文件指针是否超出文件范围。
```

### 7.==删除文件（41H号功能调用）==

```assembly
;功能：删除文件。
;入口参数：DS:DX=代表文件名的字符串首地址。
;出口参数：CF=0表示成功；
;CF=1表示失败，AX=错误代码。
;说明：只能删除一个普通文件。
```

## 4.4.2应用举例

#### 例1:写一个显示文本文件内容的程序。文本文件固定为当前目录下的TEST.TXT文件

具体算法是：先打开文件；然后顺序读文件，每次读一个字符，把所读字符在屏幕上显示出来，如此循环直到文件结束；最后关闭文件。图4.7是流程图。考虑到TEST.TXT是文本文件，所以认为ASCII码值为1AH的字符就是文件结束符。也就是说，在读到ASCII码值为1AH的字符，就认为文件结束。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/root/image.1y4twacwg69s.webp)

设计一个子程序READCH,它每次从文件中顺序读一个字符。这个子程序通过进位标志CF来反映是否正确地读到字符，如果读时发生错误，则CF置位，否则CF被清。调用它的程序应通过CF判别读文件操作是否成功。考虑到万一文本文件没有文件结束符的情况，所以该子程序还判别是否的确已读到文件尾（如果实际读到的字符数为0就意味着文件结束）,当这种情况发生时，就返回一个文件结束符。源程序如下：

```assembly
;程序名：T4-6.ASM
;功能：显示当前目录下的文本文件TEST.TXT内容
;符号常量定义
EOF	=	1AH		;文件结束符ASCII码
;数据段
DSEG 	SEGMENT
FNAME 	DB		'TEST.TXT',0			;文件名
ERROR1 	DB		'File not foun',07H,0	;提示信息
ERROR2 	DB		'Reading error',07H,0
BUFFER 	DB	?							;1字节的缓冲区
DSEG 	ENDS
;代码段
CSEG 	SEGMENT
ASSUME CS : CSEG , DS : DSEG
START: 
		MOV 	AX, DSEG
		MOV 	DS,AX			;置数据段寄存器
		;
		MOV 	DX,OFFSET FNAME
		MOV 	AX,3D00H		;为读打开指定文件
		INT 	21H
		JNC 	OPEN_OK			;打开成功，转
		;
		MOV 	SI,OFFSET ERROR1
		CALL 	DMESS			;显示打开不成功提示信息
		JMP 	OVER
		;
OPEN_OK:
		MOV		BX,AX			;保存文件柄
CONT:
		CALL	READCH			;从文件中读一个字符
		JC 		READERR			;如读出错，则转
		CMP		AL,EOF			;读到文件结束符吗？
		JZ 		TYPE_OK			;是，转
		CALL 	PUTCH			;显示所读字符
		JMP 	CONT			;继续
		;
READERR:
		MOV 	SI,OFFSET ERROR2
		CALL 	DMESS			;显示读出错提示信息
		;
TYPE_OK:
		MOV		AH.3EH			;关闭文件
		INT		21H
OVER: 
		MOV 	AH, 4CH			;程序结束
		INT		21H
		;
;子程序说明信息略
READCH 	PROC
		MOV 	CX,1			;置读字节数
		MOV 	DX,OFFSET BUFFER;置读缓冲区地址M
		OV 		AH,3FH			;置功能调用号
		INT		21H				;读
		JC 		READCH2			;读出错，转
		CMP 	AX,CX			;判文件是否结束
		MOV 	AL,EOF			;设文件已结束，置文件结束符
		JB 		READCH1			;文件确已结束，转
		MOV 	AL,BUFFER		;文件未结束，取所读字符
READCH1:
		CLC		
READCH2:
		RET
READCH 	ENDP
;子程序名：DMESS
;功能：显示一个以0为结束符的字符串
;入口参数：SI=字符申首地址
;出口参数：无
DMESS 	PROC
DMESS1: 
		MOV 	DL,[SI]
		INC 	SI
		OR 		DL,DL
		JZ 		DMESS2
		MOV		AH,2
		INT 	21H
		JMP 	DMESS1
DMESS2:
		RET
DMESS 	ENDP
PUTCH 	PROC
;同T4-5.ASM中的子程序PUTCH
PUTCH 	ENDP
CSEG 	ENDS
		END		START
```

上述程序每次只从文件中读一个字节数据，读者可把它改为每次从文件中读若干字节数据。如果文件TEST.TXT的中间带有文件结束符（1AH),则在结束符后的数据就不会在屏幕上显示出来，请读者修改上述程序，使其不考虑文件结束符。

#### 例2:写一个能把键盘上输入的全部字符（直到CTRL+Z键，值1AH)存入某个文件的程序。

为简单起见，文件固定为当前盘根目录下的TEST.TXT,如果它已存在，则更新它。

具体算法是：先建立指定文件；然后读键盘，把所读字符顺序写入文件，如此循环直到读到文件结束符（1AH);关闭文件。源程序如下：

```assembly
;程序名：T4-7.ASM
;功能：把键盘上输入的字符全部存入文件TEST.TXT
;常量定义
EOF 	= 	1AH			;文件结束符的ASCII码
;数据段
DSEG		SEGMENT
FNAME 		DB	'\TEST.TXT',0		;文件名
ERRMESS1 	DB	'Can not create file',07H,'$'
ERRMESS2 	DB	'Writing error',07H,'$'	;提示信息
BUFFER 		DB 	?						;1字节缓冲区
DSEG		ENDS
;代码段
CSEG 		SEGMENT
ASSUME CS:CSEG,DS:DSEG
START:	MOV		AX,DSEG
		MOV 	DS,AX			;置数据段寄存器
		MOV	 	DX,OFFSET FNAME	;建立文件
		MOV 	CX,0			;普通文件
		MOV 	AH,3CH
		INT 	21H
		JNC 	CREA_OK			;建立成功，转
		MOV 	DX,OFFSET ERRMESS1	;显示不能建立提示信息
		CALL 	DISPMESS
		JMP 	OVER
CREA_OK:
		MOV 	BX,AX			;保存文件柄
CONT:
		CALL	GETCHAR			;接收一个键
		PUSH 	AX
		CALL 	WRITECH			;向文件写所读字符
		POP		AX
		JC 		WERROR			;写出错，转
		CMP 	AL,EOF			;读到文件结束符吗？
		JNZ 	CONT			;不是，继续
		JMP 	CLOSEF			;遇文件结束符，转结束
WERROR:
		MOV		DX,OFFSET	ERRMESS2	;显示写出错提示信息
		CALL 	DISPMESS
CLOSEF: 
		MOV 	AH, 3EH			;关闭文件
		INT 	21H
OVER:	
		MOV		AX,4C00H		;程序结束
		INT 	21H				
;子程序说明信息略
WRITECH PROC
		MOV 	BUFFER,AL		;把要写的一字节送入缓冲区
		MOV 	DX,OFFSET BUFFER;置缓冲区地址
		MOV 	CX,1			;置写的字节数
		MOV 	AH,40H			;置功能号
		INT 	21H				;写
		RET
WRITECH ENDP
;子程序说明信息略
GETCHAR PROC
		MOV		AH,1
		INT		21H
		RET
GETCHAR ENDP
;
DISPMESS PROC
;同T4-4.ASM中的DISPMESS
DISPMESS ENDP
CSEG 	ENDS
		END 	START
```

上述程序每次只向文件写一个字节，读者可把它改成每次向文件写若干字节。它通过1号功能调用读键盘，然后就把所读的字符写入文件，如果实际运行这个程序，也许读者会发现在按退格键和回车键时有些特殊异常，如何解决就作为练习留给读者。

#### 例3:写一个程序把文件2拼接到文件1上。文件1固定为当前目录下的TEST1,文件2固定为当前目录下的TEST2。

具体算法是：为写打开文件TEST1,为读打开文件TEST2;把文件TEST1的读写指针移到尾；读TEST2的一块到缓冲区，写这一个块到TEST1,如此循环，直到TEST2结束；最后关闭两个文件。源程序如下：

```assembly
;程序名：T4-8.ASM
;功能：把文件TEST2拼接到文件TEST1之后
;符号常量定义
BUFFLEN	=	512		;数据段
DSEG 	SEGMENT
FNAME1 		DB	'TEST1',0		;文件名1
FNAME2 		DB	'TEST2',0		;文件名2
HANDLEI 	DW 	0				;存放文件1的文件柄
HANDLE2 	DW 	0				;存放文件2的文件柄
ERRMESS1 	DB 'Can not open file',07H,'$
ERRMESS2 	DB 'Reading error',07h,'$'
ERRMESS3 	DB	'Writing error',07H,'$'
BUFFER 		DB BUFFLEN DUP (0);缓冲区
DSEG 		ENDS
;代码段
CSEG 	SEGMENT
ASSUME CS:CSEG,DS:DSEG
START:
		MOV		AX,DSEG
		MOV 	DS,AX			;置数据段寄存器
		MOV 	DX,OFFSET FNAME1;为写打开文件1
		MOV 	AX,3D01H
		INT		21H
		JNC		OPENOK1			;成功，转
		;
ERR1: 
		MOV 	DX,OFFSET ERRMESS1;显示打开文件不成功提示信息
		CALL 	DISPMESS
		JMP 	OVER				;转结束
OPENOKI:
		MOV		HANDLE1,AX			;保存文件1的柄
		MOV 	DX,OFFSET FNAME2	;为读打开文件2
		MOV 	AX,3D00H
		INT 	21H
		JNC 	OPENOK2				;成功，转
		MOV 	BX,HANDLE1			;如文件2打开不成功
		MOV 	AH,3EH				;则关闭文件1
		INT 	21H
		JMP 	ERR1				;再显示提示信息
OPENOK2:
		MOV		HANDLE2,AX			;保存文件2的柄
		MOV 	BX,HANDLE1
		XOR 	CX,CX
		XOR 	DX,DX
		MOV 	AX,4202H			;移动文件1的指针到文件尾
		INT 	21H
CONT:
		MOV 	DX,OFFSET BUFFER	;读文件2
		MOV 	CX,BUFFLEN
		MOV 	BX,HANDLE2
		MOV 	AH,3FH
		INT 	21H
		JC		RERR				;读出错，转
		OR 		AX,AX				;文件2读完了？
		JZ 		COPYOK				;是，转结束
		MOV 	CX,AX				;写到文件2的长度等于读出的长度
		MOV 	BX,HANDLE1
		MOV 	AH,40H				;写到文件2
		INT 	21H
		JNC 	CONT				;写正确，继续
WERR: 
		MOV 	DX,OFFSET ERRMESS3
		CALL 	DISPMESS			;显示写出错提示信息
		JMP 	SHORT COPYOK
		;
RERR: 	MOV 	DX,OFFSET ERRMESS2
		CALL 	DISPMESS			;显示读出错提示信息;
COPYOK:
		MOV		BX,HANDLE1			;关闭文件
		MOV 	AH,3EH
		INT		21H
		MOV 	BX,HANDLE2
		MOV 	AH,3EH
		INT		21H
		;
OVER: 	MOV 	AH,4CH				;程序结束
		INT		21H					;
DISPMESS PROC
;同T4-4.ASM中的DISPMESS
DISPMESS ENDP
CSEG 	ENDS
		END 	START
```

在上述程序中，文件名是固定的，读者可把它改成文件名由用户输入。读者也可把它扩展为把两个文件合并成第三个文件。这些都留给读者作为练习。
