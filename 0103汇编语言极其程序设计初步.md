[toc]

汇编语言不仅仅是由汇编格式指令构成的指令语句，它还包括丰富的伪指令语句及其他内容。本章先简单介绍汇编语言，然后介绍如何进行顺序、分支和循环等程序设计。

# 3.1 汇编语言的语句

汇编语言源程序由汇编语言语句组成。尽管与高级语言的语句相比，汇编语言语句比较简单，但它有两类完全不同的语句。本节介绍语句格式及其主要组成部分——表达式的表示。

## 3.1.1 语句种类和格式

### 1.语句的种类

==汇编语言有两种类型的语句，一种是指令语句，另一种是伪指令语句==。这两种语句截然不同。汇编程序在对源程序进行汇编时,把指令语句翻译成机器指令,也就是说,==指令语句有着与其对应的机器指令==。==伪指令语句没有与其对应的机器指令，只是指示汇编程序如何汇编源程序，包括符号的定义、变量的定义、段的定义等==。

在宏汇编语言中，还有一种特殊的语句，称为宏指令语句。利用宏定义伪指令，可以把一个程序片段定义为一宏指令。当宏指令作为语句出现时,该语句就称为宏指令语句。所以,在宏汇编语宣中，==除了指令语句和伪指令语句外，还有宏指令语句。==我们在第7章中再介绍宏指令语句。

### 2.语句的格式

指令语句和伪指令语句的格式是相似的，都由四部分组成。

指令语句的格式如下：

> \[标号：] 指令助记符 	\[操作数 \[, 操作数]] 	[注释]
>

我们在2.4.1中已对指令语句的格式作过简要说明。其中操作数可以是常数(数值表达式）操作数、寄存器操作数（寄存器名）或者存储器操作数（地址表达式）。

伪指令语句的格式如下：

> [名字]  	伪指令定义符  	[参数……参数]  	[;注释]

伪指令定义符规定了伪指令的功能。一般伪指令语句都有参数，用于说明伪指令的操作对象，参数的类型和个数随着伪指令的不同而不同。有时参数是常数（数值表达式），有时参数是一般的符号，有时是具有特殊意义的符号。伪指令语句中的名字有时是必需的，有时是可省的,这也与具体的伪指令有关。==在汇编语言源程序中，名字与标号很容易区分，名字后没有冒号，而标号后一定有冒号。==

汇编程序忽略由分号开始至行尾的注释。为了阅读和理解程序的方便，程序员要恰当地使用注释，通过注释来说明语句或程序的功能。有时整行都可作为注释，只要该行以分号引导。

通常一个语句写一行。语句的各组成部分间要有分隔符。标号后的冒号是现成的分隔符，注释引导符分号也是现成的分隔符。此外，空格和制表符是最常用的分隔符，且多个空格或多个制表符的作用与一个空格或制表符的作用相同。空格和制表符被作为分隔符（除非作为字符串中的字符）而被忽略,所以常通过在语句行中加入空格和制表符的方法使上下语句行的各部分对齐，以方便阅读。尽管对齐不是必需的，但肯定有助于阅读。参数之间常用逗号作分隔符，但有时也用空格或制表符作分隔符。

==标号和名字一般最多由 31 个字母、数字及规定的特殊字符(?@ $）等组成，并且不能用数字开头==。一般情况下,汇编程序不区分标号和名字中的字母的大小写，除非要求汇编程序进行区分。值得指出的是，标号和名字要尽量起得有意义,这会大大有助于程序的阅读和理解。另外，标号和名字不能是汇编语言的保留字。==汇编语言中的保留字主要是指令助记符、伪指令定义符和寄存器名==，还有一些其他的特殊保留字。顺便说一下,汇编程序也不区分保留学中字母的大小写。

## 3.1.2 数值表达式

在汇编语言中，不仅有各种类型的运算符，还有许多操作符。==通过运算符、操作符及括号把常数和符号连起来，就得到表达式。表达式又分为数值表达式和地址表达式。==上述指令语句中的操作数和伪指令语句中的参数在许多场合下只是数值表达式。所谓数值表达式是指在汇编过程中能够由汇编程序计算出数值的表达式。所以组成数值表达式的各部分必须在汇编时就能完全确定。

标号和变量可作为数值表达式中的符号，由符号说明伪指令语句或符号定义伪指令语句说明或定义的符号，也可成为数值表达式中的符号。下面先介绍常数和运算符。

### 1.常数

常数有多种类型和表示方式，常用的类型和表示方式如下：

#### （1）十进制常数

==由若干个0到9的数字组成的序列，可以用字母D结尾==。例如：1024.2048D。通==常情况下，常数用十进制表示，所以在表示十进制常数时一般不加后缀字母D==。但汇编语言提供改变基数的伪指令.RADIX。

#### （2）十六进制常数

==由若干个0至９的数字或字母A至F所组成的序列，必须以字母H结尾。为了避免与普通符号（如标号、名字和保留字)相混淆，十六进制数必须以数字开头。所以，凡是以字母A至F开头的十六进制数，必须在前面加一个0==。在汇编语言中，十六进制数用得较普遍。例如：

```assembly
OR 		AX,8080H
AND 	BL,0F0H
```

#### （3）二进制常数

==由若干个0和1组成的序列，必须以字母B结尾==。在汇编语言程序设计中，有时用二进制数较方便。例如：

```assembly
TEST	BL.00110100B
OR		AL.11001010B
```

#### （4）八进制常数

由若干个0至7的数字组成的序列，必须以字母Q结尾。例如：127Q,377Q。

#### （5）字符串常数（串常数）

==一个字符串常数是用引号括起来的一个或多个字符。串常数的值是包括在引号中的字符的ASCII代码值==。例如，A’的值是41H,而'ab'的值是6162H。==因此，串常数与整常数有时可以交替使用==。例如：

```assembly
CMP		AL,'A'
MOV		VARW.'ab'
CMP		AX,"AB"
```

### 2.算数运算符

==算术运算符包括正(+）、负（-）加（+）减(-）乘（x）、除(/)和模(MOD）==,这些算术运算符的意义与高级语言中同样运算符的意义相似。例如：

```assembly
ADD 	AX,100 * 4+2
SUB		CX,100H/2
MOV		AL,-3
```

### 3.关系运算符

==关系运算符包括相等（EQ）、不等(NE）、小于(LT）、大于（GT）、小于或等于(LE）、大于或等于(GE）==。==运算结果总是一个数字值。若关系不成立，则结果为0,若关系成立，则结果为0FFFFH。==例如：

```assembly
MOV		AX,1234H GT 1024H
MOV		BX,1234H+5 LT 1024H
```

汇编后，目标程序中对应上述语句的指令如下：

```assembly
MOV		AX.OFFFFH
MOV		BX.0
```

### 4.逻辑运算符

==逻辑运算符包括按位操作的“与”（AND)、“或”(OR）、“异或”(XOR)和“非”(NOT)，另外，还有左移位(SHL）和右移位（SHR）。逻辑运算的结果是数值==。例如：

```assembly
MOV		AX,1 SHL 3
ADD		CX,1024 SHR 4
OR		AL,3 AND 47H
AND		BL NOT(7 OR 54H)
```

汇编后。目标程序中对应上述语句的指令如下：

```assembly
MOV		AX,8
ADD		CX,40H
OR		AL,3
AND		BL,0A8H
```

请注意逻辑运算符与指令助记符的区别，==表达式中的逻辑运算是由汇编程序在汇编时完成的==。

### 5.在数值表达式中产生的操作符

汇编语言中还有如下操作符可用在数值表达式中：HIGH,LOW,LENGTH,SIZE,OFFSET,SEG,TYPE,WIDTH和MASK等。下面先介绍HIGH和LOW,其他的操作符在以后的章节中再作介绍。

#### （1）HIGH

使用格式如下：

```assembly
HIGH 数值表达式
```

==结果是数值表达式值的高8位==。

#### （2）LOW

使用格式如下：

```assembly
LOW 表达式
```

==结果是数值表达式值的低8位==。

例如：

```assembly
MOV		AX,HIGH （1234H+5）
MOV 	AX,HIGH 11234H+5
MOV 	AX,LOW 1234H-3
```

汇编后，目标程序中对应上述语句的指令如下：

```assembly
MOV 	AX,12H
MOV 	AX,17H 	;HIGH优先级高于加(+)
MOV		AX,31H
```

### 6.运算符和操作符的优先级

汇编语言中各种运算符和操作符的优先级按高到低排列如下：

> （1）圆括号，尖括号，方括号，圆点符,LENGTH,SIZE,WIDTH,MASK。其中，尖括号使用于记录中，圆点符使用于结构中。
>
> （2）PTR,OFFSET,SEG,TYPE,THIS,冒号。其中，方括号号用于表示段超越前缀。
>
> （3）*,/,MOD,SHL,SHR。
>
> （4）HIGH.LOW。
>
> （5）+,-。
>
> （6）EQ,NE,LT,LE,GT.GE。
>
> （7）NOT。
>
> （8）AND。
>
> （9）OR,XOR。
>
> （10）SHORT。

## 3.1.3 地址表达式

==地址表达式所表示的是存储器操作数的地址。==单个的标号、变量(对应直接寻址方式)和有方括号括起的基址或变址寄存器（对应寄存器间接寻址）是地址表达式的特例。在2.3 节中介绍的寄存器相对寻址、基址加变址寻址和相对基址加变址寻址等寻址方式的各种表示均属于地址表达式。

==在一个存储器地址上加或减一个数字量，结果仍为存储器地址==。例如：

```assembly
MOV 	AX,VARW+1
```

==如VARW是变量,那么“VARW+4”表示以变量VARW的偏移加4为偏移的存储单元，而不是变量VARW的内容加4==。实际上，在汇编时无法确定变量VARW的值。

在表示变址寻址方式时，下面的表示方法是等价的（其中VARW是变量或是符号常量）：

```assembly
[VARW+BX]
VARW[BX]
```

在表示基址加变址寻址方式时，下面的表示方法是等价的（其中VARW是变量或是符号常量）：

```assembly
VARW[BX+DI]
[VARW+BX+DI]
VARW[BX][DI]
VARW[D1][BX]
```

# 3.2 变量和标量

==变量和标号分别代表存储单元。变量表示的存储单元中存放数值；标号表示的存储单元中存放指令代码。==标号的定义很简单。本节介绍如何定义变量，以及变量和标号的属性。

## 3.2.1 数据定义语句

==通过数据定义语句可为数据项分配存储单元，并根据需要设置其初值。还可用符号代表数据项，此时符号就与分配的存储单元相联系。==代表数据项的符号本身称为变量名，与之相对应的存储单元用于存放变量，==所以常常就把这样的存储单元称为变量==。

### 1.数据定义语句

数据定义语句是最常用的伪指令语句。一般格式如下：

> [变量名] 数据定义符 表达式[表达式……,表达式] ;注释

例如：

```assembly
VARB	DB	3
VARW	DW	-12345
		DB	1
```

==变量名是可选的，如果使用变量名，那么它就直接代表该语句所定义若于数据项中的第一个数据项。==各表达式间用逗号分隔。例如：

```assembly
BUFF	DB100, 3+4，5*6
```

#### （1）定义字节数据项

==每一字节数据项只占用一个字节存储单元。定义字节数据项的数据定义符是DB。==例如：

```assembly
COUNT 	DB	100
		DB	0DH,0AH,'$'
TABLE 	DB 	0,1,4.9,16
```

上面的数据定义语句被汇编后所对应的存储区域分配情况如图3.1(a)所示，图中的数字值用16进制表示。从图3.1(a）可见，由引号括起的字符对应其ASCI码值。下面是存取上述有关变量或数据项的指令举例：

```assembly
DEC		COUNT
MOV 	AL,TABLE
MOV 	TABLE+2,BL		;TABLE+2是从TABLE开始的第3个字节
```

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20221209/image.2cwmucp9qadc.webp)



#### （2）定义字数据项

==每一字数据项占用两个宇节存储单元。定义字数据项的数据定义符是DW==。例如：

```assembly
FLAG	DW	2FCDH,1024,-1
VECT 	DW 	0
DW		2047
```

上面的数据定义语句被汇编后所对应的存储区域分配情况如图3.1(b)所示，图中的数字值用16进制表示。-1用补码表示为OFFFFH。下面是存取上述有关变量或数据项的指令举例：

```assembly
MOV		BX,VECT
TEST 	FLAG,1234H
OR		AX,FLAG+2		;FLAG+2是从 FLAG开始的第二个字变量
```

#### （3）定义双字数据项

==每一双字数据项要占用四个字节存储单元。定义双字数据项的数据定义符是DD。==例如：

```assembly
VECTOR	DD	4
FARPTR	DD	12345678H,0
```

数据定义语句中的表达式一般是数值表达式，汇编程序在计算出数值后就作为对应数据项的初值。所以,结果数值的大小必须适合对应变量或数据项的范围。

#### （4）定义没有初值的数据项

==如果数据定义语句中的表达式单单是一个问号(?）,那么表示不预置对应变量的初值，而仅仅是给变量分配存储单元==。例如：

```assembly
INBUFF 	DB	5,2,2,8,?
VARW 	DW	？
OLDV	DD	2
```

#### （5）定义字符串

==定义字节数据伪指令DB也可方便地用于定义字符串。字符串要用引号括起来，单引号和双引号皆可，只要配对==。DB伪指令把字符串中的各个字符的ASCII码值依次存放在相应的字节存储单元中。例如；

```assembly
MESS1 	DB	'HELLO:'
```

上述语句与如下语句起相同的作用：

```assembly
MESS1 	DB	'H','E','L','L','O','!'
```

显然,用一对引号把字符串括起，要比把每一个字符用引号括起方便得多。再如：

```assembly
MESS2 	DB 	"How are you ?",0DH,0AH,24H
```

图3.2给出了某个程序中的下列数据定义语句所定义的变量或数据项使用的存储单元和存放格式。

```assembly
VARW	DW	5678H
VARB	DB	2,3,-2
VARD	DD	0,12345678H
MESS	DB	'OK!'
FLAG	DW	'Ab'
```

从图3.2可见，字变量FLAG的初值'Ab’被解释为4162H,所以，其高8位41H存放在高地址字节中。请注意“高高低低”原则。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20221209/image.4q2b7g4b8hw0.webp)

#### （6）定义其他类型数据项

利用数据定义语句还可定义8字节数据项和10字节数据项。==定义8字节数据项的数据定义符是DQ,定义10字节数据项的数据定义符是DT。==例如：

```assembly
DT	0
DQ	?
```

### 2.重复操作符DUP

有时需要定义数组，有时还需要定义数据缓冲区。为此，汇编语言提供了在数据定义语句中使用的重复操作符DUP。例如：

```assembly
BUFFER	DB 		8 DUP(0)
```

==上述伪指令语句就定义了由8 个字节组成的缓冲区,每个字节的初值为0。这样的缓冲区也可理解成由8项构成的数组，每项一个字节，初值为 0==。上述数据定义语句与如下的数据定义语句起到相同的作用：

```assembly
BUFFER 	DB		0,0,0,0,0,0,0,0
```

重复操作符DUP的一般使用格式如下：

```assembly
count	DUP （表达式[表达式...）
```

上式作为特殊的表达式只能使用在数据定义语句中。==其中，count是重复次数，要重复的内容含在括号内，如有多个表达式，则表达式间用逗号分隔==。表达式中还可再使用重复操作符DUP,但有一定的嵌套层次限制。例如：

```assembly
BUFFER1		DB	5,0,5 DUP（?）
BUFFER2		Dw	1024 DUP（0）
BUFFER3 	DB	256 DUP（'ABCDE’)
DATA		DW	1,5 DUP（1，2.4 DUP(O))
```

## 3.2.2 变量和标号

### 1.变量和标号的属性

==变量表示存储单元，这种存储单元中存放数值；标号也表示存储单元，这种存储单元中存放机器指令代码。==所以,变量和标号均表示存储器操作数，都具有如下三种属性：

- ==段值==，变量或标号对应存储单元所在段的段值。
- ==偏移==，变量或标号对应存储单元的起始地址的段内偏移。
- ==类型，变量的类型主要是字节(BYTE)、字(WORD)和双字(DWORD）；标号的类型主要是近（NEAR）和远（FAR）近表示段内标号，远表示段间标号。==

在汇编语言程序设计中,变量和标号的这三个属性很重要，为此，==汇编语言提供专门的析值操作符和类型操作符，以便于对变量和标号的这三个属性进行有关操作处理==。下面就简单介绍这些操作符。

### 2.析值操作符

==析值操作符也称为数值回送操作符，原因是这些操作符把一些特征或存储器地址的一部分作为数值回送==。五个析值操作符的简单使用格式如下：

```assembly
SEG		变量名或标号
OFFSET	变量名或标号
TYPE	变量名或标号
LENGTH	变量名
SIZE	变量名
```

这些操作符都使用在数值表达式中。为了说明这些操作符的作用，我们设在某个程序中有如下数据定义片段：

```assembly
VARW	DW	1234H,5678H
VARB	DB	3,4
VARD	DD	12345678H
BUFF	DB	10 DUP (?)
MESS	DB	'HELLO’
```

设变量VARW 从偏移100H开始，对应的存储情况如图3.3所示。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20221209/image.2saow5u65uc0.webp)

**（1）==操作符SEG能返回变量所在段的段值==，例如：**

```assembly
MOV		AX,SEG VARW		;把变量VARW所在段的段值送AX
MOV		DS,AX			;再送到数据段寄存器DS
```

（2）==操作符OFFSET返回变量或者标号的偏移==，例如：

```assembly
MOV		BX,OFFSET VARW		;把VARW的偏移(100H)送BX
ADD		DI,OFFSET VARW+2 	;在汇编时计算出OFFSET VARW +2 = 102H
MOV		SI,OFFSET VARB		;把VARB的偏移(104H)送SI
```

请注意上一条指令与如下指令有质的不同：

```assembly
LEA		SI,VARB
```

利用操作符（OFFSET只能取得用数据定义伪指令定义的变量的有效地址，而不能取得一般操作数的有效地址。实际上，OFFSET只是汇编语言提供的操作符，它的返回值是在汇编时由汇编程序计算出来的。

**（3）==操作符TYPE返回变量或标号的类型，类型用数值表示==。常见类型和对应的数值规定如下：**

```assembly
字节(BYTE)变量		1
字(WORD)变量		 2
双字(DWORD)变量		4
近(NEAR)标号		-1
远(FAR)标号		-2
```

由上述表示关系可见,变量的类型值是对应类型的变量项所占用的字节数，而标号的类型值却没有实际的物理意义。

**（4）==操作符LENGTH返回利用DUP定义的数组中元素的个数，即重复操作符DUP前的count值。==如果变量定义语句中没有使用DUP,则总返回1。如果嵌套使用了DUP，则只返回最外层的重复数。例如：**

```assembly
MOV		CX,LENGTH 	VARW 		;1送CX
MOV		CX,LENGTH	BUFF		;10送CX
MOV		CX,LENGTH 	MESS		;1送CX
```

**（5）==操作符SIZE返回用DUP定义的数组占用的字节数==，可按下式计算：**

> ==SIZE变量=(LENGTH 变量)*(TYPE 变量)==

例如：

```assembly
MOV		CX,SIZE		VARW	;2送CX
MOV		CX,SIZE 	BUFF	;10送CX
MOV		CX,SIZE		MESS	;1送CX
```

### 3.属性操作符

==为了提高访问变量、标号和一般存储器操作数的灵活性，汇编语言还提供了属性操作符PTR和THIS==,以达到按指定属性访问的目的。

#### （1）操作符PTR

我们先看一个例子。汇编程序在汇编指令`MOV [SI],1`时,将发出警告提示信息或出错提示信息，其==原因是汇编程序不能确定指针寄存器所指的存储器操作数的类型==，==即要访问的存储器操作数是字节类型还是字类型==。

==程序员要在源程序中明确指明，要访问的存储器操作数是字节类型还是字类型。==这可利用PTR操作符来指明，例如：

```assembly
MOV 	WORD PTR [SI],1			;指明字类型
MOV 	BYTE PTR [SI],1			;指明字节类型
```

==PTR是最常用的合成操作符，用在地址表达式前，用于指定或临时改变变量和标号的类型。==一般格式如下：

> 类型 PTR 地址表达式

其中,类型可以是BYTE,WORD,DWORD,NEAR和FAR。==它指示汇编程序无论地址表达式所表示的单元类型是什么,当前均以PTR前面的类型为准。请注意，PTR操作符并不分配存储单元，而只是临时性地强制指定变量或标号的类型。==

于是，==利用PTR便可访问一个字变量的高字节和低字节，也可把两个字节变量当作一个字变量来访问==，例如：

```assembly
VARW		DW		1234H
VARB		DB		1
			DB		3
VARD		DD		12345678H
			...
			MOV		AX,WORD PTR VARB	;访间由VARB开始的一个字
			MOV		AL,BYTE PTR VARW	;访间字变量VARW的低字节
			MOV		BYTE PTR VARW+1,AL	;访间字变量VARW的高字节
			MOV		DX,WORD PTR VARD	;访问双字变量VARD的低字
			MOV		WORD PTR VARD+2,ES	;访问双字变量VARD的高字
			MOV		WORD PTR ES:[DI+2],0
```

再如：

```assembly
JMP		FAR PTR OK			;OK是标号
JMP		DWORD PTR OLDVECT	;OLDVECT是变量
```

#### （2）操作符THIS

操作符THIS 的一般格式如下：

```assembly
THIS 类型
```

==其中类型可以是BYTE,WORD,DWORD,NEAR和FAR 等。它返回一个具有指定类型的存储器操作数，但决不为该存储器操作数分配存储单元，所返回存储器操作数地址的段值和偏移就是下一个将分配的存储单元的段值和偏移。==

==与操作符PTR相比,有相似之处，即都能指定操作数类型。所不同的是,操作符THIS并不直接作用于其他的变量或标号，而操作符PTR则不然。==

==操作符THIS一般使用在符号定义语句中，从而定义一个具有类型、段值和偏移三属性的表示存储器操作数的符号。==例如：

```assembly
MY_BYTE	EQU	THIS BYTE	;EQU是符号定义语句的定义符
MY_WORD DW	?
```

如果在源程序中安排上述两条伪指令语句,那么==符号MY_BYTE 就表示一个字节变量，它的段值和偏移与紧随其后的字变量MY_WORD相同。所以，对字节变量MY_BYTE 的访间实际上就是对字变量MY_WORD 低字节的访问。==

# 3.3 常用伪指令语句和源程序组织

本节介绍常用的伪指令语句，并给出汇编语言源程序的组织形式。其他伪指令语句在以后各章节需要处给出。

## 3.3.1 符号定义语句

通过符号定义语句，可把常数、表达式等用符号来表示。恰当地使用符号定义语句,不仅可大大方便程序的书写和阅读，对程序的调试和修改也很有利。

### 1.等价语句 EQU

等价语句的一般格式如下：

> 符号名 EQU 表达式

**（1）==用符号来代表常数或数值表达式==。**

**在这种情况下，汇编程序计算出表达式的值，符号就代表计算结果。**例如：

```assembly
COUNT		EQU		100		;符号COUNT就代表常数 100
BUFF_LEN 	EQU		4*COUNT	;COUNT是已定义的符号常数
LTX			EQU		1
RDX			EQU		LTX + 50
...
INBUFFER	DBCOUNT,?,COUNT DUP（?）
...
```

**（2）==用符号表示一个字符串==。**

**可用一简短的符号表示一复杂的字符串，以后当汇编程序遇到所定义的符号时，就用字符串代替之。**例如：

```assembly
HELLO EQU	"How are you !"
```

**（3）==重新定义关键字或指令助记符==。**

**也即给汇编语言的关键字或指令助记符起一个别的名称。**例如：

```assembly
MOVE	EQU	MOV
COUNT 	EQU	CX
```

在安排了上述语句后，就可用MOVE代替指令助记符MOV,用COUNT代表寄存器CX。当然指令助记符MOV和寄存器名CX还照可使用。例如：

```assembly
MOVE	AX,CX		;MOV AX,CX
MOV		COUNT,100	;MOV CX.100
```

**（4）==定义存储器操作数符号==。**

**所定义的存储器操作数符号具有类型、段值和偏移属性。例如：**

```assembly
VARW	EQU		THIS WORD	 ;VARW 的类型是字，段值和偏移
VARB	DB		2 DUP（O）	;接的下一单元VARB相同。
FLAG	DW		？
FLAG1	EQU		BYTE PTR FLAG
FLAG2	EQU		BYTE PTR FLAG +1
```

在这之后，就可使用这些符号，例如：

```assembly
MOV		AX,VARW
MOV		AL,FLAG1		;相当于 MOV AL,BYTE PTR FLAG
MOV		FLAG2,AL		;相当于MOV BYTE PTR FLAG+1,AL.
```

==需要注意：第一,等价语句不另外给符号分配存储单元；第二,等价语句定义的符号不能与其它符号相同，也不能被重新定义,否则汇编程序会认为出现符号重新定义错误。==

### 2.等号语句 =

==汇编语言还专门提供等号语句来定义符号常数，即用符号表示一个常数==。等号语句的一般格式如下：

> 符号名 = 数值表达式

例如：

```assembly
XX = 10
YY = 20+300/4
```

数值表达式应该可以计算出数值，所以表达式中一般不能含有向前引用的符号名称。用等号语句定义的符号可被重新定义。例如：

```assembly
ABCD = 1
ABCD = 100
ABCD = 2*ABCD+1
```



### 3.定义符号名语句 LABEL

定义符号名语句的一般格式如下：

> 符名 LABEL 类型

其中类型可以是BYTE,WORD,DWORD,NEAR和FAR 等。==该语句的功能是定义有符号名指定的符号，使该符号的段属性和偏移属性与下一个紧接着的存储单元的段属性和偏移属性相同，使该符号的类型为参数所规定的类型。==例如：

```assembly
BUFFER 	LABEL	WORD
BUFF	DB		100 DUP(0)
```

==BUFFER 的类型是WORD，段属性和偏移属性与BUFF相同==。再如：

```assembly
QUIT	LABEL 	FAR
EXIT:	MOV		AH,4CH
```

这样指令`MOV	AH,4CH`就有了两个标号QUIT和EXIT,但它们的类型不同。

## 3.3.2 段定义语句

为了与存储器的分段结构相对应，汇编语言的源程序也由若干个段组成。段定义语句就是用来按段组织程序和利用存储器的。

### 1.段开始和结束语句

汇编语言源程序中的段以段开始语句开始，以段结束语句结束。段定义的一般格式如下：

> 段名 SEGMENT\[定位类型]\[组合类型]\['类别']
>
> 
>
> 段名 ENDS

段开始语句的定义符是SEGMENT,其中的定位类型、组合类型和类别都是可省的，暂不介绍它们的用法和用途（请见第8章）。段结束语句的定义符是ENDS.段开始语句中的段名与段结束语句中的段名要相同，从而保持配对。==段名的命名方法与一般符号的命名方法相同==。

一个简单的数据段如下所示：

```assembly
DSEG 	SEGMENT
MESS	DB'HELLO',0DH,0AH,'$'
DSEG	ENDS
```

一个简单的代码段如下所示：

```assembly
CSEG	SEGMENT
		MOV		AX,DSEG		;把数据段DSEG的段值送AX
		MOV		DS,AX		;再送DS寄存器
		MOV		DX,OFFSET MESS
		MOV		AH,9
		INT		21H			;DOS系统功能调用
		MOV		AH,4CH
		INT		21H			;DOS系统功能调用
CSEG	ENDS
```

当段名作为符号被引用时,表示对应段的段值。

==指令“INT  21H”是软中断指令，在上述代码中起调用DOS系统功能的作用。==由于指令语句和数据定义语句都要占用存储单元，所以它们一定要安排在某个程序段内。

### 2.段使用设定语句

汇编程序根据段开始语句和段结束语句判断出源程序的段划分，为了有效地产生目标代码，汇编程序还要了解各程序段与段寄存器间的对应关系。段寄存器与程序段的对应关系由段使用设定语句说明。

段使用设定语句的简单格式如下：

> ASSUME 段寄存器名:段名[段寄存器名:段名……]

==段寄存器名可以是CS,DS,SS和ES.段名就是段开始语句和段结束语句中规定的段名。==例如，下面的ASSUME语句告诉汇编程序，从现在开始CS寄存器对应CSEG段,DS寄存器对应DSEG段。

```assembly
ASSUME CS:CSEG,DS:DSEG
```

==ASSUME 伪指令中的段名域也可以是一个特别的关键字NOTHING,它表示某个段寄存器不再与任何段有对应关系。==

==在一条 ASSUME语句中可建立多个段寄存器与段的关系，只要用逗号分隔。在源程序中可使用多条 ASSUME语句，通常在代码段的一开始就使用 ASSUME语句，确定段寄存器与段的对应关系，以后可根据需要再使用ASSUME语句改变已建立的对应关系。==

例如：

```assembly
DSEG1	SEGMENT			;定义一个数据段，段名为DSEG1
VARW	DW		12		
		...
DSEG1	ENDS
DSEG2	SEGMENT			;定义另一个数据段，段名为DSEG2
XXX		DW		0
YYY		DW		0
		...
DSEG2	ENDS
;
CSEG	SEGMENT				;定义一个代码段
		ASSUME 	CS:CSEG,DS:DSEG1,ES:DSEG2
		MOV		AX,DSEG1
		MOV		DS,AX
		MOV		AX,DSEG2
		MOV		ES,AX
		...
		MOV		AX,VARW
		MOV		XXX,AX
		...
		ASSUME	DS:DSEG2,ES:NOTHING
		MOV		AX,DSEG2
		MOV		DS,AX
		...
		MOV		AX,XXX
		MOV		YYY,AX
		...
CSEG	ENDS
```

==段使用设定语句是伪指令语句，它不能设置段寄存器的值，==所以在上述程序中还需要通过指令语句来给数据段寄存器和附加段寄存器赋值。

变量 VARW在DSEG1 段中定义,变量 XXX在 DSEG2 段中定义，而第一条ASSUME语句表示数据段寄存器DS对应DSEG1,附加段寄存器ES 对应DSEG2,因此汇编程序在汇编`MOV XXX,AX`时，将自动加上段超越前缀,即成为`MOV	ES:XXX.AX`。由于第二条ASSUME语句把DS与DSEG2对应，而变量XXX和YYY均在DSEG2段中，所以汇编程序在汇编`MOV AX,XXX`和`MOV YYY,AX`时就不再加上段超越前级。假设没有第二条ASSUME 语句,那么汇编程序也要为上述两条指令加上段超越前缀 ES。如果在第二条 ASSUME语句后，安排指令`MOV	AX,VARW`, 汇编程序将发出无法访问变量VARW的出错提示信息，原因是第二条ASSUME语句解除了DS寄存器与DSEG1段的对应关系,DSEG1段不再与任何段寄存器对应。再如果把第二条 ASSUME语句改变为`ASSUME	DS:DSEG2,ES:DSEG1`,结果会是什么?

==我们可以在有关的指令中明确加上段超越前缀，从而改变 ASSUME语句对有关指令的影响。==例如；

```assembly
MOV		AX,ES:XXX
MOV		DS:YYY,AX
```

### 3.ORG语句

==汇编程序在对源程序汇编的过程中,使用地址计数器来保存当前正在汇编的指令或者变量的地址偏移。通常地址计数器的值逐步递增，但程序员可利用ORG 语句调整地址计数器的当前值。==

ORG语句的一般格式如下：

> ORG 数值表达式

==汇编程序在汇编到该伪指令语句后，使地址计数器的值调整成数值表达式的结果值。==如数值表达式的值是n,那么ORG伪指令语句使下一个字节地址成为n。例如：

```assembly
TESTSEG		SEGMENT
			ORG		100H
BEGIN:		MOV		AX,1234H
			...
			ORG		500H
VAR			DW		1234
			...
TESTS		ENDS
```

标号 BEGIN的偏移等于100H,变量VAR的偏移等于500H。

另外,==汇编语言用符号“\$”表示地址计数器的值。允许程序员在指令和伪指令中直接用符号\$引用地址计数器的当前值。==例如下面的语句表示跳过8个字节的存储区：

```assembly
ORG  	$+8
```

==当\$用在指令中时，它表示本条指令第一字节的地址偏移==。例如：下面的指令表示转移到距当前指令第一字节后6字节处：

```assembly
JMP		$+6
```

==当\$用在伪指令的参数中时，它表示的是地址计数器的当前值==。例如：

```assembly
ARRAY 	DW1,2,$+4,3,4,$+4
```

设在汇编时 ARRAY分配的地址偏移是100H,那么汇编后相应的存储情况如图3.4所示。从图 3.4中可见,由于\$的值在不断变化，所以数组中两个数值表达式\$ +4得到的结果是不同的。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20221209/image.1vmcb9mrflds.webp)

## 3.3.3 汇编语言源程序的组织

### 1.一个完整的源程序

我们先看一个简单又完整的源程序。

```assembly
;程序名:	T3-1.ASM
;功 能:显示信息“HELLO”
;
SSEG	SEGMENT PARA STACK 	;堆栈段
		DW		256 DUP(?)
SSEG	ENDS
;
DSEG	SEGMENT				;数据段
MESS	DB		'HELLO',0DH,0AH,'$'
DSEG	ENDS
;
CSEG	SEGMENT				;代码段
		ASSUME CS:CSEG,DS:DSEG
START:	MOV		AX,DSEG
		MOV		DS,AX		;设置数据段寄存器
		MOV		DX,OFFSET MESS
		MOV		AH,9		;显示信息HELLO		
		MOV		AH,4CH
		INT		21H			;返回DOS
CSEG	ENDS
		END		START
```

源程序T3-1.ASM含有三个段,即堆栈段、数据段和代码段。数据段含有程要使用到的数据，代码段一般含有程序的代码。堆栈段作为堆栈使用，它由堆栈段定义语句说明，在第8章节中再对此作解释。

在经过汇编和连接处理后，可得到一个可执行程序。它运行时,在屏幕上显示字符串信息“HELLO”操作系统(DOS)在把它装入运行时,将给上述逻辑上的三个段,分配三个相应的物理段。在DOS把控制权转到该程序时，将设置妥代码段寄存器CS和指令指针寄存器IP。

### 2.源程序的组织

==汇编语言源程序的主体是若干个段，少到一个段，多至几十个段，一般格式如下==：

```assembly
NAME1 	SEGMENT；
		...
		...
NAME1 	ENDS
;
NAME2 	SEGMENT
		...
		...
NAME2 	ENDS
		...
		...
NAMEn 	SEGMENT
		...
		...
NAMEn 	ENDS		
		END		标号
```

通常情况下代码和数据分别在代码段和数据段中，但有时代码和数据可以合并在同一个段中。==一个完整的汇编语言源程序至少含有一个代码段，但一个汇编语言源程序模块却可以只有数据段。==目前，我们总把数据段安排在程序的前面，把代码段安排在程序的后面，但不是非要这样安排的。

此外，一个完整的程序还应该带有自己的堆栈段。但操作系统(DOS)在装载没有堆栈段的程序时，会指定一个堆栈段。由于堆栈段的安排比较固定，而且我们在全教程中所举的例子程序均较小，总可利用DOS安排的堆栈,所以为了简单，在以后所举各例子中均省去堆栈段。尽管连接程序在连接这种没有堆栈段的目标模块时，会发出一条警告信息，但可忽略它。

指令语句和数据定义伪指令语句应安排在段内。部分伪指令语句可安排在段外，例如，符号定义语句一般安排在源程序的开始处。在源程序的最后还要有源程序结束语句。

### 3.源程序结束语句

==源程序结束语句的一般格式如下：END [标号]该语句告诉汇编程序，源程序到此为止。汇编程序在遇到该语句后，就不再对其后的任何语句进行汇编，所以，源程序结束语句往往是源程序的最后一条语句。==

==END语句可带有一个已在程序中定义过的标号,这表示程序要从标号所对应的指令开始执行,也就是说，标号给定了程序的启动地址。==

如果源程序是一个独立的程序,那么END语句应带有标号,从而指定程序的启动地址。如果源程序仅是一个模块，且不是主模块，则END语句不应带有标号。我们在第8章中介绍模块和主模块的概念。

# 3.4顺序程序设计

==CPU在执行顺序程序片段时，按照指令的先后次序执行指令，因此在顺序程序片段中，指令的先后次序是至关重要的==。在具体的顺序程序片段中，有些指令语句可以前后颠倒，有些则不行。此外，还要注意顺序的优化，做到充分利用前面的处理结果，尽量避免重复操作。本节介绍顺序程序设计的基本方法。

## 3.4.1 顺序程序举例

### 例1：设X和Y均为16位无符号数，写一个求表达式 16X+Y值的程序。

由于表达式中的X和Y是16位数，表达式的结果可能要超出16 位，所以定义两个字变量用于保存X和Y，另外用一个 32 位的双字变量来保存结果。数据段可定义如下：

```assembly
;程序名：T3-2.ASM
;功 能：计算16X+Y
DSEG		SEGMENT
XXX		DW		1234H		;设X为1234H
YYY		DW		5678H		;设Y为5678H
ZZZ		DD		？			;用于保存结果
DSEG	ENDS
```

==CPU对寄存器操作数的运算操作要比对存储器操作数的运算操作快得多,所以应尽量利用寄存器进行运算操作。==用DX和AX寄存器保存中间结果,DX 保存高16位，AX保存低16位。结合上述数据段，程序的代码段如下：

```assembly
CSEG		SEGMENT
			ASSUME 	CS:CSEG,DS:DSEG
START:		MOV		AX,DSEG4		;(1)设置数据段寄存器值
			MOV		DS,AX
			MOV		AX,XXX			;(2)把X送AX并扩展到32位
			XOR		DX,DX
			ADD		AX,AX			;(3)计算X*16
			ADC		DX,DX
			ADD		AX,AX			;X*4
			ADC		DX,DX
			ADD		AX,AX			;X*8
			ADC		DX,DX
			ADD		AX,AX			;X*16
			ADC		DX,DX
			ADD		AX,YYY			;(4)在X*16的结果上再加Y
			ADC		DX,0
			MOV		WORD PTR ZZZ,AX ;(5)保存结果
			MOV		WORD PTR ZZZ+2,DX
			MOV		AH,4CH			;(6)返回DOS
			INT		21H
CSEG		ENDS
			END		START
```

上面的代码分为六步，必须依次执行。第（2）步和第（5）步中的两条指令语句可以分别颠倒，其它各步中的指令语句均不能颠倒。

第（3）步是计算X*16,采用了四次自身相加的方法达到乘16 的目的。也可采用移位的方法达到乘 16 的目的，这样就不能简单地把16位扩展到32位，第（2）步和第(3）步可合并如下：

```assembly
MOV		AX,XXX		;X送AX
MOV		DX,AX		;X送DX
MOV		CL,4
SHL		AX,CL		;AX左移4位等价于乘16
MOV		CL,12
SHR		DX.CL		;等价于得AX的高4位
```

还有一个更简单的方法计算Xx16,那就是采用乘法指令。对应于第（2）步和第(3）步的代码可简化如下：

```assembly
MOV		AX,XXX
MOV		DX,16
MUL		DX			;AX*DX->DX: AX
```

显然,代码长度越来越短，但这并不意味着执行速度越来越快，因为执行一条乘除法指令所花的时间较多。当乘数是２的倍数时，往往可用移位指令实现乘运算；当乘数较小时,往往可采用相加的方法实现乘运算;在对执行速度要求不大的情况下,可直接采用乘法指令实现乘运算。

### 例2：写一个把压缩存放的 BCD码，转换为对应十进制数字ASCII码的程序。

==所谓压缩存放是指一个字节中存放两个BCD码,即低4位存放一个BCD码，高4位存放一个BCD 码。如果仅仅是低4位存放一个BCD码，就称为非压缩存放==。BCD码与对应十进制数字ASCII码的关系很简单，在非压缩BCD码上加 30H,就得对应十进制数字的ASCII码。源程序如下：

```assembly
;程序名：T3-3.ASM
;功 能：压缩BCD码转换成ASCII码
DSEG	SEGMENT
BCD		DB	86H			;假设的压缩BCD码
ASCII	DB	2		DUP （O）;存放 ASCII码
DSEG	ENDS
;
CSEG	SEGMENT
		ASSUME CS:CSEG, DS:DSEG
START:	MOV		AX,DSEG			;（1）设置数据段寄存器值
		MOV		DS,AX			;(2）把存放在低4位的BCD码转换为对应十进制数字的 ASCII码
		MOV		AL,BCD
		AND		AL,OFH
		ADD		AL,30H
		MOV		ASCII+1,AL		;（3）把存放在高4位的BCD码转换为对应十进制数字的 ASCII码
		MOV		AL,BCD
		MOV		CL,4
		SHR		AL,CL
		ADD		AL,30H
		MOV		ASCII,AL
		MOV		AH,4CH			;（4）返回DOS
		INT		21H
CSEG	ENDS
		END		START
```

上述代码段中的第(2）步和第(3）步可以颠倒，它们分别独立地把存放在低位和高位的BCD码转换为对应十进制数的 ASCII码。

## 3.4.2 简单查表法代码转换

在汇编语言程序设计中,代码转换是经常的事。上面的把BCD码转换成 ASCII码就是一例。对于各种不同代码之间的转换，往往要采用各种不同的方法，以便获得最佳效率。查表是实现代码转换的一种方法，下面介绍的==简单查表方法是一种计算查表方法，适用于代码集合较小且转换关系复杂的场合。==

### 例3：写一个把16进制数字码转换为对应七段代码的程序。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20221209/image.56qn9nkbexg0.webp)

利用如图3.5所示的七段显示数码管，能较好地显示16进制数字(0……,9,A,b,C,d,E,F)。七段数码管的每一段对应一个二进制位，如果我们设0表示对应段亮,1表示对应段暗，那么数字码0对应以二进制形式表示的代码1000000,数字码1对应以二进形式表示的代码1111001,如此，数字码F对应以二进制形式表示的代码 0001110。这种用于表示七段数码管亮暗的代码就称为七段代码。

显然.16进制数字码与七段代码间的关系难以表示成一个简单的算术表达式,所以，利用表的方法实现代码转换较合适。源程序如下：

```assembly
;程序名：T3-4.ASM
;功 能：16进制数字码到七段代码的转换
DSEG	SEGMENT
TAB		DB 1000000B,1111001B,0100100B,0110000B	;七段代码表
		DB 0011001B,0010010B,0000010B,1111000B
		DB 0000000B,0010000B,0001000B,0000011B
		DB 1000110B,0100001B,0000110B,0001110B
XCXDE	DB 8									;假设的 16 进制数字码
YCODE	DB ?									;存放对应的七段代码
DSEG	ENDS
;
CSEG	SEGMENT
		ASSUME CS:CSEG,DS:DSEG
START:	MOV		AX,DSEG
		MOV		DS.AX
		MOV		BL,XCODE						;取16进制数字码
		AND		BL,OFH							;保证在0至F之间
		XOR		BH,BH							;表内偏移用16位表示以便寻址
		MOV		AL,TAB[BX]						;取得对应的代码
		MOV		YCODE,AL						;保存
		MOV		AH,4CH
		INT		21H
CSEG	ENDS
		END		START
```

利用查表的方法实现代码转换的关键是表的组织。上述程序中按16 进制数字码的大小组织七段代码表,这就便于查找。可以说，实际上没有进行真正的查找，只根据16进制数字码，确定对应代码在表中的位置而已。这种代码转换方法简明快捷。

8086/8088CPU还专门提供一条查表指令XLAT(也称为换码指令）,以方便实现上述这种类型的查表。查表指令 XLAT的格式如下：

```assembly
XLAT
```

该指令把寄存器BX 的内容作为表(每项一字节）的首地址，把寄存器AL的值作为下标，取出表项内容再送AL,寄存器。也就是把寄存器AL中的内容转换成表中对应项的值，此即所谓的换码。使用此指令前，应先把表的首地址送BX寄存器。表最大为 256项。

此指令属于数据传送指令组。利用XLAT指令，上述程序T3-4.ASM的转换部分的代码可改写如下：

```assembly
MOV		BX,OFFSET TAB
MOV		AL,XCODE
AND		AL,0FH
XLAT
```

## 3.4.3 查表法求函数值

==有许多数学函数的求值计算用汇编语言实现较为困难，除非利用数学协处理器。然而，上述这种表的组织形式和查表的方法，能够适用于直接获得某些数学函数的值。==

### 例4：设X是一个1～10之间的整数，写一个求函数Y=LG(X)值的程序。

把1～10这10个数的对数值组织成一张表,那么程序运行时的计算工作就大大简化,甚至可以说没有具体的计算。由于1～10的以10为底的对数在0～1的范围之间，为了表示的方便和考虑一定的精度,所以把这些对数值放大 10000倍，这样每个对数值就用一个字表示。源程序如下：

```assembly
;程序名：T3-5.ASM
;功 能：求1到10的对数值
DSEG	SEGMENT
VALUE	DB 4				;假设的X
ANSWER	DW ?				;存放X的对数值
TAB		DW 0,3010,4771,6021,6990,7782,8451,9031,9542,10000	;对数表
DSEG	ENDS
;
CSEG	SEGMENT
		ASSUME	CS:CSEG, DS:DSEG
START:	MOV		AX,DSEG
		MOV		DS,AX
		MOV		AL,VLAUE	;取X值
		XOR		AH,AH		;把X值用16位表示
		DEC		AX			;得表项号（从1开始）
		ADD		AX,AX		;得表项在表内的地址
		MOV		BX,AX		;表内地址送基址寄存器
		MOV		AX,TAB[BX]	;获得对数
		MOV		ANSWER,AX	;保存
		MOV		AH,4CH
		INT		21H
CSEG	ENDS
		END		START
```

由于1的对数值是第一项，所以首先要自变量值X减1；又由于每个对数值用一个字表示，所以在计算表项在表内的地址时要乘 2。上述程序没有考虑X超出范围的情况。

使用查表法求函数值有两个优点：(1）程序比较容易；(2）能够得到十进制或十六进制（或任何其他）格式的高精度函数值。其缺点也许不那么明显：（1）函数值必须事先安排好，因而有许多限制；（2）函数值的精度和准确性由程序员控制，而不是由数学函数决定，当数据表的项较多时，难免有误差。

# 3.5 分支程序设计

几乎所有的程序都不是从头顺序地执行到尾，而是在处理中经常存在着判断，并根据某种条件的判定结果而转向不同的处理。这样程序就不再是简单地顺序执行，而是分成两个或多个分支。本节介绍分支程序设计的基本方法。

## 3.5.1 分支程序举例

程序分支的两种基本结构如图3.6所示，这两种结构分别对应高级语言中的if语句和 if-else 语句。在汇编语言中，一般利用条件测试指令和条件转移指令等实现简单的分支。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20221209/image.3k2zjjo5ai00.webp)

### 例1：设有三个单字节无符号数存放在BUFFER开始的缓冲区中,写一个能将它们按大到小重新排列的程序。

设数据段就只有三个要排序是数据,定义如下：

```assembly
;程序名：T3-6.ASM
;功 能：实现三个无符号数的由大到小的排序
DATAS	SEGMENT
BUFFER 	DB 87,234,123
DATAS	ENDS
;有多种方法可实现三个数的排序,我们采用交换法，先得到三个数中的最大数，然后再得到剩下两个数的最大数。为了方便，先把要排序的三个数取到三个寄存器中。源程序代码段如下所示：
CODES		SEGMENT
			ASSUME	CS:CODES,DS:DATA
SSTART:		MOV		AX,DATAS
			MOV		DS.AX
			MOV		SI,OFFSET BUFFER
			MOV		AL,[SI]			;(1）把三个数取到寄存器中
			MOV		BL,[S1+1]
			MOV		CL,[SI+2]
			CMP		AL,BL			;(2)排序
			JAE		NEXT1
			XCHG	AL,BL
	NEXT1:	CMP		AL,CL
			JAE		NEXT2
			XCHG	AL,CL
	NEXT2:	CMP		BL,CL
			JAE		NEXT3
			XCHG	BL,CL
	NEXT3:	MOV		[SI],AL			;（4）按大到小依次存回缓冲区
			MOV		[S1+1],BL
			MOV		[S1+2],CL
			MOV		AH,4CH
			INT		21H
	CODES 	ENDS
			END		START
```

在上述的排序片段中，含有三个分支,每个分支都是图3.6(a)的结构。另外，上述程序先把三个要排序的数取到三个寄存器中。当然也可以不这样做，而是直接在内存中交换排序。请把下面的程序片段与上述程序中的（2）、（3）和（4）步作一比较，孰优孰劣

```assembly
		MOV		SI,OFFSET BUFFER
		MOV		AL,[SI]
		CMP		AL,[SI+1]
		JAE		NEXT1
		XCHG	AL,[SI+1]
		MOV		[SI],AL
NEXT1:	CMP		AL,[SI+2]
		JAE		NEXT2
		XCHG	AL,[S1+2]
		MOV		[SI],AL
NEXT2:	MOV		AL,[SI+1]
		CMP		AL,[SI+2]
		JAE		NEXT3
		XCHG	AL,[S1+2]
		MOV		[SI+1],AL
NEXT3:
```

### 例2：写一个实现把一位十六进制数转换为对应ASCII码的程序。

十六进制数码与对应ASCII码的关系如下所示：

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20221209/image.2e0wmkvpiw00.webp)

这种对应关系可表示为一个分段函数：
$$
Y =\begin{cases}
X+30H \quad (0 \le X \le 9) \\
\\
X+37H \quad (0AH \le X \le 0FH)
\end{cases}
$$


所以,程序要根据十六进制数码值是否超过9而进行分支。源程序如下：

```assembly
;程序名：T3-7.ASM
;功 能：十六进制数到ASCII码的转换
DATA		SEGMENT
XX		DB 	4			;假设的十六进制数码
ASCII	DB 	?			;存放对应的 ASCII码
DATA	ENDS
;
CODE	SEGMENT
		ASSUME CS:CODE,DS:DATA
START:	MOV		AX,DATA
		MOV		DS,AX
		MOV		AL,XX
		AND		AL,OFH		;确保在0至F之间
		CMP		AL,9		;（1）
		JA		LAB1		;（2）超过9转移
		ADD		AL,30H		;（3）
		JMP		LAB2		;（4）
LAB1:	ADD		AL,37H		;（5）
LAB2:	MOV		ASCI1,AL	;（6）
		MOV		AH,4CH
		INT		21H
CODE	END
		SEND		START
```

上述程序中进行转换工作的指令(1)-（6）符合图3.6(b)的结构，指令(3）的无条件转移语句（指令）很重要，如果没有这条指令，分支的结构就简化成为图 3.6(a)。由于上述程序分支的一边稍作变形后，可包含分支的另一边，所以进行代码转换工作可作如下优化、从而使处理既简单又高效：

```assembly
	ADD		AL,30H
	CMP		AL,39H
	JBE		LAB2
	ADD		AL,7
LAB2:
```

一般情况下，如果分支结构同图3.6(b），且有一边很简单时,可考虑把它改变为图3.6(a)的结构。具体的方法是，在判断之前先假设是简单的情况。

### 例3：写一个实现把一位十六进制数所对应的 ASCII码转换为十六进制数的程序。

如果要转换的ASCII码没有对应的十六进制数码，则转换为特殊值负1。

考虑到A--F也能用小写字母表示，转换关系可用如下函数描述：
$$
Y = \begin{cases}
-1 \qquad\qquad\qquad (X \lt '0')\\
X-'0' \qquad\qquad ('0' \le X \le '9')\\
-1 	\qquad\qquad\qquad ('9' < X < 'A')\\
X-'A'+10 \quad\quad ('A' \le X \le 'F')\\
-1 \qquad\qquad\qquad ('F' < X < 'a')\\
X - 'a' + 10 \quad\quad('a' \le X \le 'f')\\
-1 \qquad\qquad\qquad (X>'f')
\end{cases}
$$
实现转换功能的源程序如下，其中对分支结构作了些优化处理；

```assembly
;程序名：T3-8.ASM
;功 能：ASCII码转换为十六进制数
DATA	SEGMENT
XX		DB 		?		;存放十六进制数
ASCII	DB		'a'		;假设的 ASCII码
DATA	ENDS
;
CODE	SEGMENT
		ASSUME CS:CODE, DS:DATA
START:	MOV		AX,DATA
		MOV		DS,AX
		MOV		AL,ASCII
		CMP		AL,'0'
LAB:	JB		LAB5			;小于’0'转范围外处理
		MOV		AH,AL			;设在0’一’9*之间
		SUB		AH,'0'			;转换处理
		CMP		AL,'9'
		JBE		LAB6			;确在’0’-’9'之间转保存处
		CMP		AL,'A'
		JB		LAB5			;小于’A’转范围外处理
		MOV		AH,AL			;设在'A’一’F’之间
		SUB		AH,'A’-10		
		CMP		AL,'F'
		JBE		LAB6			;确在’A'-"F'之间转保存处
		CMP		AL,'a'
		JB		LAB5			;小于a'转范围外处理
		MOV		AH,AL			;设在'a’一'f'之间
		SUB		AH,'a'-10
		CMP		AL,'f'
		JBE		LAB6			;确在'a'-中之间转保存处。
LAB5:	MOV		AH,-1			;范围外处理
LAB6:	MOV		XX,AH			;保存转换结果
		MOV		AH,4CH
		INT		21H
CODE	ENDS
		END		START
```

由于大写字母的 ASCII码值与对应小写字母的ASCII码值间相差 20H.或者说大写字母ASCII码的位5为0,而小写字母ASCII码的位5为1,所以指令“AND AL,0DFH”能把A1,中的小写字母的ASCII码值转换为对应大写字母的 ASCII码,因此上述程序中进行代码转换的工作可进一步优化为：

```assembly
	CMP		AL,'O'
LAB:JB		LAB5		;小于’0’转范围外处理
	MOV		AH,AL		;设在’0’-”9’之间
	SUB		AH,'0'		;转换处理
	CMP		AL,'9'
	JBE		LAB6		;确在’0’--’9’之间转保存处
	AND		AL,11011111B;如为小写字母，则转换为大写字母
	CMP		AL,'A'
	JB		LABS		;小于’A'转范围外处理
	MOV		AH,AL		;设在’A’-'F'之间
	SUB		AH,'A'-10
	CMP		AL,'F'
	JBE		LAB6		;确在’A’-’F'之间转保存处
LAB5: ...
```

*如果不考虑待转换的字符可能不是十六进制数码的情况，则程序要简单得多。通过指令“OR  AL,20H”把AL寄存器中的大写字母ASCII码值转换为对应的小写字母的ASCII码值，如果AL中是数字符的ASCII码，也不会受影响。相应的代码如下：

```assembly
	OR		AL,20H
	SUB		AL,'0'
	CMP		AL,9
	JB		LAB4
	SUB		AL,’a’-'0'-10
LAB4:MOV	XX,AL
```

## 3.5.2 利用地址表实现多向分支

==当要根据某个变量的值，进行多种不同处理时，就产生了多向分支==。多向分支的结构如图3.7所示。在高级语言中，常用switch语句等实现多向分支。在汇编语言中，如何实现多向分支呢?

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20221209/image.2roxihz96di0.webp)

任何复杂的多向分支总可分解成多个简单分支。图3.8给出了根据X的值是否为1～4,而进行5种不同处理的流程图片段。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20221209/image.3h32jp6rfpq0.webp)

用汇编语言实现这种多向分支的源程序结构如下所示：

```assembly
...
		CMP			AH,1		;设X在AH寄存器中
		JZ			YES_1		;(*)
		JMP			NOT_1
YES_1:	...
		...
		JMP			ОK
NOT_1:	CMP			AH,2
		JZ			YES_2		;(*)
		JMP			NOT_2
YES_2:	...
		...
		JMP			OK
NOT_2:	CMP			AH,3
		JNZ			NOT_3		;(**)
YES_3:	...
		...
		JMP			ОK
NOT_3:	CMP			AH,4
		JNZ			NOT_4		;(**)
YES_4:	...
		...
		JMP			SHORT OK		;(***)
NOT_4:	...
		...
OK:		...
		...
```

在上述说明多向分支程序结构的(\*)语句处，无条件转移指令和条件转移指令配合实现分支（远距离条件转移）在（\* \*)语句处，假设地址差能用一字节表示，故没有使用无条件转移指令；在(\* \* \*)处也假设地址差能用一字节表示，故使用了 SHORT。在实际的程序中，要根据具体情况选择使用合适的指令。

这种程序结构显得繁琐，如果要用它实现5路以上的多向分支，则更加复杂。==在汇编语言中，可使用地址表实现多向分支。当多向分支在5路以上时，用地址表实现起来既方便又高效。==

设程序MBRANCH每次只接收一个单健命令"A"至"H”,然后根据命令进行相应的处理。如果接受到的输人不是规定的命令字母，则不处理。

==为了利用入口地址表(也称散转表)实现多向分支，事先必须安排一张入口地址表。如果各处理程序均在同一代码段内，则入口地址只要用偏移表示,所以人口地址表的每一项只用一个字==。MBRANCH的地址表可如下组织：

```assembly
DSEG		SEGMENT
						;定义的其他数据
COMTAB		DW		COMA,COMB,COMC,COMD
			DW 		COME,COMF,COMG,COMH		;人口地址表
			... 							;定义的其他数据
DSEG		ENDS
```

有了上述地址表后，多向分支的实现是方便的。采用地址表实现多向分支的MBRANCH源程序的有关代码如下所示：

```assembly
...
		MOV		AH,1
		INT		21H			;接收键盘合令，命令代码在AL中
		AND		AL,11011111B;小写转大写
		CMP		AL,'A'		;判是否起"A"--"H"
		JB		OK
		CMP		AL,'H'
		JA		0K
		;
		SUB		AL,'A'		;* *把命令字母德转换成序号（从0开始）
		XOR		AH,AH		;* * 
		ADD		AX,AX		;* *计算入口地址表内的地址
		MOV		BX,AX		;* *
		JMP		COMTAB[BX]	;转对应命令处理程序。
OK:		MOV		AH,4CH		;结束
		INT		21H
		;
COMA:	...					;命令A 处理程序
		...
		JMP		OK
		;
COMB:	...					;命令B处理程序
		...
		JMP		OK
		;
COMC:	...					;命令C处理程序
		...
		JMP		OK
		;
		...					;其他命令处理程序。
...
COMH:	...					;命令H 处理程序；
		...
		JMP		OK
```

上述代码中真正实现多向分支的部分是注释行带（\* \*)的指令。通过地址表实现多向分支的关键是。根据分支各路的条件确定对应处理程序的入口地址在地址表中的位置,或者说对应处理程序的编号。在程序MBRANCH中,代表命令的字母是连续的，所以很容易由命令字母得出对应处理程序的编号。如果命令字母不连续，则稍稍复杂些。

# 3.6 循环程序设计

当要重复某些操作时,就应考虑使用循环。循环通常由四部分组成：(1）初始化部分；（2）循环体部分；（3)调整部分；4）控制部分。各部分之间的关系如图3.9所示。图3.9(a）是先执行后判断的结构，图3.9(b）是先判断后执行的结构。有时这四部分可以简化，形成互相包含交叉的情况，不一定能明确分成四部分。有多种方法可实现循环的控制，常用的有计数控制法和条件控制法等。本节介绍循环程序设计的基本方法。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20221209/image.3edvx9s7ziq0.webp)

## 3.6.1 循环程序举例

在第2章中介绍的若干数据求和等程序片段均采用了循环结构，下面再举几例，说明实现循环的基本方法。

### 例1：求内存中从地址0040：0000H开始的1024个字的字检验和。

==所谓字检验和是指，结果只用字表示，忽略可能产生的进位。==在数据传输时，为了保证数据传输的正确性，一般要对传输的数据进行某种检查，常用的简单检查方法是检查数据的按字节或字累计的和。

程序采用如图3.9(a)所示的循环结构，这个循环的四部分俱全。在初始化部分设置存储器指针和循环计数器等；循环体部分只要实现累加求和，并不要考虑进位，存储器指针的调整作为循环调整部分的内容;采用计数法控制循环。源程序如下：

```assembly
;程序名：T3-9.ASM
;功 能：说明根据计数法控制循环
DSEG	SEGMENT
SUM		DW		？			;存放检验和
DSEG	ENDS
CSEG	SEGMENT
		ASSUME CS:CSEG
		;（1）初始化部分
START:	MOV		AX,40H
		MOV		DS,AX		;设置数据段寄存器值为0040H
		MOV		SI,0		;设置偏移为0
		MOV		CX,1024		;设置循环计数器
		XOR		AX,AX		;检验和清0
		;（2）循环体部分
AGAIN:	ADD		AX,[SI]		;求和
		;（3）调整部分
		INC		SI			;修改存储器地址指针
		INC		SI			
		;(4）循环控制部分
		LOOP	AGAIN		;根据CX倒计数
		;
		ASSUME 	DS:DSEG
		MOV		BX,DSEG
		MOV		DS,BX
		MOV		SUM,AX		;保存检验和
		MOV		AH,4CH		;返回DOS
		INT		21H
CSEG	ENDS
		END		START
```

在初始化部分设置数据段寄存器DS之值为 40H,这与先前的程序稍有不同，实际上,根据要求被处理的数据已明确在 40H段。在得到检验和之后，重新设置数据段寄存器DS 之值为程序中定义的 DSEG 段的段值，以便保存检验和。

### 例2：不利用乘法指令实现乘法运算。

为了简单,设乘数和被乘数均是单字节无符号整数。举本例的目的是为了说明设计循环程序的基本方法，并非提倡不利用乘法指令。另一方面确有不能使用乘法指令的场合，例如Z80的指令集中无乘法指令,那么只有用其他方法实现乘法运算了。

下面的程序采用累加的方法实现乘法，也就是把被乘数累加乘数次得到积。由于设被乘数和乘数均是单字节无符号整数，所以积只用一个字表示。源程序如下：

```assembly
;程序名：T3-10.ASM
;功 能：（略）
DSEG		SEGMENT
XXX		DB			234				;假设的被乘数
YYY		DB			125				;假设的乘数
ZZZ		DW			?				;存放积
DSEG		ENDS
CSEG		SEGMENT
	ASSUME CS:CSEG,DS:DSEG
START:		MOV		AX,DSEG
			MOV		DS,AX			
			;（1）初始化部分
			MOV		AL.XXX
			XOR		AH,AH			;为了累加方便，用16 位表示被乘数
			MOV		CL.YYY
			XOR		CH,CH			;用CX存放循环计数（乘数）
			XOR		DX,DX			;清累加器
			JCXZ	OK				;如果乘数为0,则不必循环
			;（2）循环体部分
MULTI:		ADD		DX.AX
			;（3）循环控制部分
			LOOP	MULTI
			;
OK:			MOV		ZZZ,DX			;保存积
			MOV		AH,4CH			;返回DOS
			INT		21H
CSEG		ENDS
			END		START
```

在上述程序的循环中，只有循环的三部分，而缺少循环调整部分。为了使循环体简单，所以把被乘数扩展成16位，从而可直接采用16位的累加。如果只采用8位累加，则循环体可修改如下：

```assembly
ADD		DL,AL
ADC		DH,0
```

采用累加的方法实现乘法运算虽然简单，但当乘数稍大时将花较多的时间。一般可采用移位相加的方法实现乘法运算，移位相加的方法类似于手算。图3.10是一种移位相加法实现乘法运算的流程图，其中被乘数左移，乘数右移。图3.10符合图3.9(a)所示的循环结构。

实现图3.10所示流程图的源程序片段如下所示：

```assembly
		...
		MOV		AL.XXX
		XOR		AH,AH			;取被乘数，并扩展到16位
		MOV		BL,YYY			;取乘数
		XOR		DX,DX			;积清0
		MOV		CX,8			;设置循环计数器（单字节无符号，故 8次循环）
		;
MULTI:	SHR		BL,1			;乘数右移1位
		JNC		NEXT			;判是否要把被乘数加到积
		ADD		DX,AX			;加到积上
NEXT:	ADD		AX,AX			;被乘数左移1位
		LOOP	MULTI			;循环控制
		;
		MOV		ZZZ,DX			;保存积
		...
```

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20221209/image.14h34h5w2pcw.webp)

### 例3：把16位二进制数转换为5 位十进制数。为了简单，设二进制数是无符号的，采用BCD码表示十进制数。

16 位二进制数能表示的最大十进制数只有5位，即最高位是“万位”。有多种转换方法。这里采用的方法是:先把二进制数除以10000,得到的商即为十进制数的“万位”;再用余数除以1000.得到的商为十进制数的“千位”按照这样的顺序，分别用每次的余数除以100、10、1得到的商分别为十进制数的“百位”“十位”和“个位”。每次除后得到的商就是8421BCD码。

我们用一个循环次数确定为5的循环来实现转换。在每次循环中，先获取新的除数；然后进行除法；保存商，由于使用16位除数，还要把除后的余数调整为 32 位作为新的被除数。源程序如下：

```assembly
;程序名：T3-11.ASM
;功 能：（略）
DSEG		SEGMENT
DATA		DW			23456		;假设的二进制数
BUFFER 		DB			5 DUP (0)	;准备存放十进制数
JM			DW			10000,1000,100,10,1		;5个除数
DSEG		ENDS
;
CSEG		SEGMENT
			ASSUME CS:CSEG， DS:DSEG	
START:		MOV		AX,SEG DATA
			MOV		DS,AX
			MOV		DI,OFFSET JM		;置指向除数的指针初值
			MOV		SI,OFFSET BUFFER 	;置缓冲区指针初值
			MOV		CX,5				;置循环次数
			MOV		AX,DATA				;取二进制数
			XOR		DX,DX				;扩展为32位
NEXT:		MOV		BX,[D1]				;取除数
			ADD		DI,2
			DIV		BX					;进行除
			MOV		[SI],AL				;保存商
			INC		SI
			MOV		AX,DX				;调整余数为新的被除数
			XOR		DX,DX
			LOOP	NEXT				;计数循环控制
			MOV		AX,4C00H			;返回DOS
			INT		21H
CSEG		ENDS
			END		START
```

如果要把各位十进制数字用对应的 ASCII码表示,那么只需在除法指令和保存商指令之间加入指令“ADDAL,30H”,该指令实现把AL,寄存器中的BCD码转换为对应的ASCII 码。

### 例4：写一个把字符串中的所有大写字母改为小写的程序。设字符串以0结尾。

图3.11是实现的流程图，具有图3.9(b）所示的结构。它的主体是一个循环次数不确定（字符串长不确定）,根据是否到达字符串尾这个条件来控制的循环。源程序如下，数据段中的字符串起示例作用。

```assembly
;程序名：T3-12.ASM1
;功 能：说明根据某个条件控制的循环
DSEG		SEGMENT
STRING 		DB		"HOW are yoU ! ',0	;假设的字符串
DSEG		ENDS
CSEG		SEGMENT
;
ASSUME 		CS:CSEG, DS:DSEG
START:		MOV		AX,DSEG
			MOV		DS,AX
			MOV		SI,OFFSET	STRING	;取字符串开始地址
AGAIN:		MOV		AL,[SI]				;取一字符
			OR		AL,AL				;是否到字符串尾?
			JZ		0K					;到字符串尾，转
			CMP		AL,'A’				;否则,判是否为大写字母
			JB		NEXT				;否，转继续
			CMP		AL,'Z'
			JA		NEXT				;否、转继续
			OR		AL,20H				;是大写字母,改为小写字母
			MOV		[SI],AL				;送回到字符串中
NEXT:		INC		SI					;调整指针
			JMP		AGAIN				;继续
OK:			MOV		AX,4C00H			;结束
			INT		21H
CSEG		ENDS
			END		START
```

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20221209/image.9157e8nqa0w.webp)

上述的循环采用先判后执行的结构。此外,为了更有效，可作如下变形：

```assembly
		MOV		SI,OFFSET	STRING		;取字符串开始地址
AGAIN:	MOV		AL,[SI]					;取一字符
		INC		SI						;调整指针
		OR		AL,AL 					;是否到字符串尾？
		JZ		OK						;到字符串尾，转
		CMP		AL.'A'					;否则,判是否为大写字母
		JB		AGAIN					;(x)
		CMP		AL,'Z'
		JA		AGAIN					;(*)
		ADD		AL,20H					;是大写字母，改为小写字母
		MOV		[SI-1],AL;				送回到字符串中
		JMP		AGAIN					;继续
```

### 例5：写一个程序判定从地址 0040；0000H开始的 2048个内存字节单元中是否有字符’A’。如有则把第一个(按地址由小到大为序）含此指定字符的存储单元的地址偏移送到0000:03FEH单元中;如没有则把特征值OFFFFH送上述指定单元。

图3.12是流程图，含有一个由计数和条件双重控制的一个循环,也就是最多循环N次，在N次循环过程中，如特定条件满足，则提前结束循环。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20221209/image.1q0vujiaf04g.webp)

实现上述流程的源程序不需要数据段。为了使程序便于阅读和修改，在程序开始定义了若干符号常量。

```assembly
;程序名：T3-13.ASM
;功 能：说明由计数和条件双重控制的循环
;常量定义
SEGADDR = 	40H			;开始地址段值
OFFADDR = 	0			;开始地址偏移
COUNT 	= 	2048		;长度（计数）
KEYCHAR	=	'A'			;指定字符
SEGRESU	= 	0			;结果保存单元段值
OFFRESU	= 	3FEH		;结果保存单元偏移
;代码段
CSEG		SEGMENT
			ASSUME CS:CSEG
START:		MOV		AX,SEGADDR		;初始化
			MOV		DS,AX
			
			MOV		SI,OFFADDR
			MOV		CX,COUNT
			MOV		AL,KEYCHAR
NEXT:		CMP		AL,[S1]			;找
			JZ		0K				;找到转
			INC		SI				;调整指针
			LOOP	NEXT			;继续下一次
			MOV		SI,OFFFFH		;没有找到时的特征值
OK:			MOV		AX,SEGRESU
			MOV		DS,AX
			MOV		BX,OFFRESU
			MOV		[BX],SI			;结果送指定单元
			MOV		AH,4CH
			INT		21H
CSEG		ENDS
			END		START
```

### 例6：设缓冲区DATA中有一组单字节有符号数，以0为结束标志。写一个程序实现如下功能:把前5个正数依次送入缓冲区 PDATA,把前5个负数依次送入缓冲区MDATA:如正数或负数不足5个，则用0补足。

在把正数和负数送入对应缓冲区前，用0填正数和负数缓冲区,从而实现“如正数或负数不足5个。则用0补足”的要求。这通过一个循环次数确定的循环完成。然后，获取前5个正数和负数，这也用一个循环结构来实现。第二个循环的结束条件是：遇到结束标志，或者获取的正数个数和负数个数均已为5。图3.13给出了流程图。

我们用变址寄存器SI和DI作为已获取的正数和负数的计数器，这样做不仅可方便地把获得的正数和负数送入相应的缓冲区，而且有利于统计已取得的正数和负数的个数。源程序如下所示。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20221209/image.7ddl22ajphw0.webp)

```assembly
;程序名：T3-14.ASM;功 能：（略）
MAX_COUNT		=	5
DSEG		SEGMENT
DATA	DB 3,—4.5,6,-7.8,-9,-10,-1,-32,-123,27,58,44,-12.0
PDATA 	DB MAX_COUNTDUP（?）			;存放正数
MDATA 	DB MAX_COUNTDUP（?）			;存放负数
DSEG	ENDS
;
CSEG	SEGMENT
		ASSUME		CS:CSEG,DS:DSEG
START:	MOV		AX,DSEG
		MOV		DS,AX
		MOV		CX,MAX_COUNT			;清存放正数和负数的缓冲区
		MOV		S1,OFFSET PDATA
		MOV		DI,OFFSET MDATA
		MOV		AL.0
NEXT1:	MOV		[SI],AL
		MOY		[DI],AL
		INC		SI
		INC		DI
		LOOP	NEXT1
		;获取前若干个正数和负数
		MOV		BX.OFFSET DATA ;置指针初值
		XOR		SI,SI
		XOR		DI,DI
NEXT2:	MOV		AL,[BX]			;取一个数据
		INC		BX
		CMP		AL,0			;与0比较
		JZ		OVER			;是结束标志，就结束
		JG		PLUS			;为正数，则转
		CMP		DI,MAX_COUNT	;负数处理
		JAE		CONT
		MOV		MDATA[DI],AL
		INC		DI
		JMP		SHORT_CONT
PLUS:	CMP		SI,MAX_COUNT		;正数处理
		JAE		CONT
		MOV		PDATA[SI],AL
		INC		SI
CONT:	MOV		AX,SI				;判是否已获得足够的正数和负数
		ADD		AX,DI
		CMP		AX，MAX_COUNT+MAX——COUNT
		JB		NEXT2
OVER:	MOV		AH,4CH
		INT		21H
CSEG	ENDS
		END		START
```

## 3.6.2 多重循环程序举例

所谓多重循环就是循环之中还有循环。

### 例7：设BUFFER缓冲区中有10个单字节无符号整数，写一个程序将它们由小到大排序。

有各种各样的排序算法，这里为了方便地说明二重循环,采用“简单选择”法，图3.14是流程图。源程序如下，其中SI相当于外层循环控制变量1,DI相当于内存循环控制变量J,为了使I从1开始递增，排序数组开始地址先减1后再存入BX寄存器。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20221209/image.d8dvcuwggxs.webp)

```assembly
;程序名：T3-15.ASM
;功 能：说明二重循环的实现
DSEG		SEGMENT
BUFFER 	DB 	23,12,45,32,127,3,9,58,81,72	;假设的10个数据
N	EQU		10								;定义符号N为常数10
DSEG		ENDS
;
CSEG		SEGMENT
			ASSUME	CS:CSEG,DS:DSEG
START:		MOV		AX,DSEG
			MOV		DS,AX
			MOV		BX,OFFSET BUFFER–1		;设置缓冲区开始地址
			MOV		SI,1					;I=1
FORI:		MOV		DI,SI
			INC		DI						;J=I+1
FORJ:		MOV		AL,[BX+SI]
			CMP		AL,[BX+DI]				;A[I]与A[J]比较
			JBE		NEXTJ					;A[I]小于等于A[D]转
			XCHG	AL,[BX+DI]				;A[I]与A[J]交换
			MOV		[BX+SI],AL
NEXTJ:		INC		DI						;J=J+1
			CMP		DI,N
			JBE		FORJ					;J<=N时转
NEXTI:		INC		SI						;I=I+1
			CMP		SI,N - 1
			JBE		FORI					;I<=N-1时转
			;
			MOV		AH,4CH
			INT		21H
CSEG		ENDS
			END		START
```

读者能否在保持算法的前提下,对上述程序作些优化工作。

### 例8：设字符串1在数据段1中，字符串2在数据段2中，写一程序判别字符串2是否是字符串1的子字符串。如是子字符串，则把数据段2中的FLAG单元置1.否则将其清0。设字符串以0结尾。

判别一个字符串是否是另一字符串的子字符串的方法很多,我们选取实现较简单的一种算法，源程序如下。

```assembly
;程序名：T3-16.ASM
;功 能:（略）
DSEG1		SEGMENT
STRM		DB "THIS IS A STRING1",0	;假设的在数据段1中的字符率
DSEG1		ENDS
;
DSEG2		SEGMENT
STRS		DB 'STRING'.0				;假设的在数据段 2中字符串
FLAG		DB ?
DSEG2		ENDS
;
CODE		SEGMENT
		ASSUME		CS:CODE, DS:DSEG1， ES:DSEG2
START:		MOV		AX,DSEG1
			MOV		DS,AX			;数据段1段值置 DS
			MOV		AX,DSEG2
			MOV		ES,AX			;数据段2段值ES
			;
			MOV		DI,OFFSET STRS	;测字符串2的长度
			MOV		BX,DI			;保存字符串2首地址
			XOR		CX,CX			;清计数器
			DEC		DI
WHILE1:		INC		DI				;调整指针
			INC		CX				;计数先加1
			CMP		BYTE PTR ES:[DI];0字符串2是否结束
			JNZ		WHILE1
			DEC		CX				;得字符串2长度
			MOV		DX,CX			;保存
			;
			MOV		SI,OFFSET STRM	;取字符串1首地址
			MOV		BP,SI
FORI:		MOV		CX,DX			;置要比较的字符个数
			MOV		DI,BX			;置首地址
FORJ:		MOV		AL,ES:[DI]
			CMP		[SI],AL			;比较一字节
			JNZ		NEXTI			;不等，从字符串1的下一个字符开始
NEXTJ:		INC		DI
			INC		SI
			LOOP	FORJ			;继续下一字符的比较
			MOV		FLAG,1			;置是子字符串标志
			JMP		OVER
NEXTI:		CMP		BYTE PTR [SI],0	;判字符串1是否结束
			JZ		NOTF			;是，转结束
			INC		BP
			MOV		SI,BP
			JMP		FORI
NOTF:		MOV		FLAG,0			;置非子字符申标志
			;
OVER:		MOV		AH,4CH			;程序结束
			INT		21H
CODE		ENDS
			END		START
```

另请注意上面程序中ASSUME语句和给FLAG单元赋值的语句。
